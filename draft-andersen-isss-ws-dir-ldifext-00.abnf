ldif-file            = ldif-content / ldif-changes
ldif-content         = ["total" 1*SEP] heading ldif-attrval-record
                         *(1*SEP ldif-attrval-record)
ldif-changes         = ["incremental" 1*SEP] heading
                         ldif-change-record
                         *(1*SEP ldif-change-record)
heading              = 0,1*(version-spec 1*SEP)
                         ["agreement-id:" *SPACE agreement-id 1*SEP]
                         [charset: *SPACE charset-name 1*SEP]
agreement-id         = 1*safe-initval
charset-name         = a character set name, as registered
                         with IANA [9]
ldif-attrval-record  = dn-spec SEP 1*(attrval-spec SEP)
                         ["key" SEP 1*(attrval-spec SEP)]
ldif-change-record   = dn-spec SEP 1*(changerecord SEP)
                         ["key" SEP 1*(attrval-spec SEP)]

version-spec         = "version:" *SPACE version-number
version-number       = 1*DIGIT ; version-number MUST be "1" for the
                                 ; LDIFext format described in this
                                 ; document if operating in LDIF mode.

dn-spec              = ("dn:" *SPACE dn) /
                         ("dn::" *SPACE base64-dn) /
                         ("dn:" *SPACE 1*DIGIT rdn
                         *(("," / ";") *SPACE rdn)) /
                         ("dn::" *SPACE 1*DIGIT base64-rdn
                         *(("," / ";") *SPACE base64-rdn )) /
                         ("s:" [*SPACE dn]) / ("s::" *SPACE base64-dn)
dn                   = <a distinguished name, as defined in
                         RFC 1779 [3]>
base64-dn            = <a dn which has been base-64 encoded, as
                         defined in RFC 1521 [5]>
rdn                  = <a relative distinguished name, as defined in
                         RFC 1779 [3]>
base64-rdn           = <an rdn which has been base-64 encoded, as
                         defined in RFC 1521 [5]>

attrval-spec         = attribute-description ((":") /
                         (":" *SPACE value *(*SPACE "\" *SPACE value)) /
                         ("::" *SPACE base64-value
                         *(*SPACE "\" *SPACE base64-value)) /
                         (":<" *SPACE url*(*SPACE "\" *SPACE url)))
url                  = <a Uniform Resource Locator, as defined in
                         [19]>  ; (See Note 6, below)
attribute-description = <an attribute description, as defined in [4].
                         An attribute description MAY NOT contain a
                         colon ":">
value                = 1*safe-initval *safe
safe                 = <any value except CR or LF>
safe-initval         = <any value except CR, LF, colon (":", ASCII 58
                         decimal), SPACE, and less-than ("<" , ASCII 60
                         decimal)>
base64-value         = <base-64-encoded value, as defined in
                         RFC 1521 [5]>
changerecord         = change-add / change-delete / change-modify /
                         change-moddn
change-add           = ["changetype:" *SPACE "add" SEP] attrval-spec
                         *(SEP attrval-spec)
change-delete        = "changetype:" *SPACE "delete"
change-moddn         = "changetype:" *SPACE ("modrdn" / "moddn") SEP
                         ("newrdn:" *SPACE rdn /
                         "newrdn::" *SPACE base-64-rdn) SEP
                         "deleteoldrdn:" *SPACE ("0" / "1")
                         0,1*(SEP (("newsuperior:" *SPACE dn) /
                         ("newsuperior:: *SPACE base-64-dn)))
change-modify        = "changetype:" *SPACE "modify" 1*(SEP mod-spec)
mod-spec             = mod-add-spec / mod-delete-spec /
                         mod-replace-spec
mod-add-spec         = "add:" *(*SPACE attribute-description)
                         1*(SEP attrval-spec) SEP "-"
mod-delete-spec      = "delete:" *(*SPACE attribute-description)
                         *(SEP attrval-spec) SEP "-"
mod-replace-spec     = "replace:" *(*SPACE attribute-description)
                         *(SEP attrval-spec) SEP "-"
SPACE                = <ASCII SP, space>
SEP                  = (CR LF / LF)
CR                   = <ASCII CR, carriage return>
LF                   = <ASCII LF, line feed>
DIGIT                = <any ASCII decimal digit (60 - 71 decimal)>


