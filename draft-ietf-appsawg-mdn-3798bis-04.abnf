mdn-request-header = "Disposition-Notification-To" ":" [FWS]
         mailbox *("," [FWS] mailbox)

Disposition-Notification-Options =
          "Disposition-Notification-Options" ":" [FWS]
                         disposition-notification-parameter-list
disposition-notification-parameter-list =
          disposition-notification-parameter
          *([FWS] ";" [FWS] disposition-notification-parameter)
disposition-notification-parameter = attribute [FWS] "="
          [FWS] importance [FWS] "," [FWS] value *([FWS] "," [FWS] value)
importance = "required" / "optional"

   An importance of "required" indicates that interpretation of the
   disposition-notification-parameter is necessary for proper generation
   of an MDN in response to this request.  An importance of "optional"
   indicates that an MUA that does not understand the meaning of this
   disposition-notification-parameter MAY generate an MDN in response
   anyway, ignoring the value of the disposition-notification-parameter.

   No disposition-notification-parameter attribute names are defined in
   this specification.  Attribute names may be defined in the future by
   later revisions or extensions to this specification.  Disposition-
   notification-parameter attribute names beginning with "X-" will never
   be defined as standard names; such names are reserved for
   experimental use.  disposition-notification-parameter attribute names
   not beginning with "X-" MUST be registered with the Internet Assigned
   Numbers Authority (IANA) and described in a standards-track RFC or an
   experimental RFC approved by the IESG.  [[ more work needed here ]]
   (See Section 10 for a registration form.)

original-recipient-header =
          "Original-Recipient" ":" [FWS] address-type [FWS] ";" [FWS] generic-address

   The address-type and generic-address token are as specified in the
   description of the Original-Recipient field in Section 3.2.3.

   The purpose of carrying the original recipient information and
   returning it in the MDN is to permit automatic correlation of MDNs
   with the original message on a per-recipient basis.

disposition-notification-content = [ reporting-ua-field CRLF ]
             [ mdn-gateway-field CRLF ]
             [ original-recipient-field CRLF ]
             final-recipient-field CRLF
             [ original-message-id-field CRLF ]
             disposition-field CRLF
             *( failure-field CRLF )
             *( error-field CRLF )
             *( extension-field CRLF )
extension-field = extension-field-name ":" *([FWS] text)
extension-field-name = field-name

address-type = atom
atom = <The version from RFC 5321 (not from RFC 5322) is used in this document.>


   b.  An "MTA-name-type" specifies the format of a mail transfer agent
       name.  For example, for an SMTP server on an Internet host, the
       MTA name is the domain name of that host, and the "dns" MTA-name-
       type is used.

mta-name-type = atom

reporting-ua-field = "Reporting-UA" ":" OWS ua-name OWS [ ";" OWS ua-product OWS ]
ua-name = *text-no-semi
ua-product = *([FWS] text)
text-no-semi = %d1-9 /         ; "text" characters excluding NUL, CR,
               %d11 / %d12 / %d14-58 / %d60-127  ; LF, or semi-colon
OWS      = *WSP             ; generators
           / [CFWS]         ; parsers
           ; Optional whitespace.
           ; MDN generators SHOULD use "*WSP"
           ; (typically a single space or nothing.
           ; It SHOULD be nothing at the end of a field),
           ; unless an RFC 5322 "comment" is required.
           ;
           ; MDN parsers MUST parse it as "[CFWS]".

   The Reporting-UA field is defined as follows:

   An MDN describes the disposition of a message after it has been
   delivered to a recipient.  In all cases, the Reporting-UA is the MUA
   that performed the disposition described in the MDN.  This field is
   optional, but recommended.  For Internet Mail user agents, it is
   recommended that this field contain both: the DNS name of the
   particular instance of the MUA that generated the MDN, and the name
   of the product.  For example,

   Reporting-UA:  pc.example.com; Foomail 97.1

   If the reporting MUA consists of more than one component (e.g., a
   base program and plug-ins), this may be indicated by including a list
   of product names.

mdn-gateway-field = "MDN-Gateway" ":" OWS mta-name-type OWS ";" OWS mta-name OWS
mta-name = *text

   For gateways into Internet Mail, the MTA-name-type will normally be
   "smtp", and the mta-name will be the Internet domain name of the
   gateway.
original-recipient-field =
          "Original-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS
generic-address = *text

   The address-type field indicates the type of the original recipient
   address.  If the message originated within the Internet, the address-
   type field will normally be "rfc822", and the address will be
   according to the syntax specified in RFC-MSGFMT [2].  The value
   "unknown" should be used if the Reporting MUA cannot determine the
   type of the original recipient address from the message envelope.
   This address is the same as that provided by the sender and can be
   used to automatically correlate MDN reports with original messages on
   a per recipient basis.

final-recipient-field =
          "Final-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS

   The generic-address subfield of the Final-Recipient field MUST
   contain the mailbox address of the recipient (from the From header
   field of the MDN) as it was when the MDN was generated by the MUA.

   The Final-Recipient address may differ from the address originally
   provided by the sender, because it may have been transformed during
   forwarding and gatewaying into a totally unrecognizable mess.
   However, in the absence of the optional Original-Recipient field, the
   Final-Recipient field and any returned content may be the only
   information available with which to correlate the MDN with a
   particular message recipient.

   The address-type subfield indicates the type of address expected by
   the reporting MTA in that context.  Recipient addresses obtained via
   SMTP will normally be of address-type "rfc822".

   Since mailbox addresses (including those used in the Internet) may be
   case sensitive, the case of alphabetic characters in the address MUST
   be preserved.

original-message-id-field =
             "Original-Message-ID" ":" OWS msg-id OWS

disposition-field =
             "Disposition" ":" OWS disposition-mode OWS ";"
             OWS disposition-type
             [ OWS "/" OWS disposition-modifier
             *( OWS "," OWS disposition-modifier ) ] OWS
disposition-mode = action-mode OWS "/" OWS sending-mode
action-mode = "manual-action" / "automatic-action"
sending-mode = "MDN-sent-manually" / "MDN-sent-automatically"
disposition-type = "displayed" / "deleted" / "dispatched" /
             "processed"
disposition-modifier = "error" / disposition-modifier-extension
disposition-modifier-extension = atom

failure-field = "Failure" ":" *([FWS] text)
error-field = "Error" ":" *([FWS] text)

mdn-request-header =
          "Disposition-Notification-To" ":" [FWS]
                    mailbox *("," [FWS] mailbox)
Disposition-Notification-Options =
          "Disposition-Notification-Options" ":" [FWS]
                    disposition-notification-parameter-list
disposition-notification-parameter-list =
                    disposition-notification-parameter
                    *([FWS] ";" [FWS] disposition-notification-parameter)
disposition-notification-parameter = attribute [FWS] "=" [FWS]
                    importance [FWS] "," [FWS] value *([FWS] "," [FWS] value)
importance = "required" / "optional"
original-recipient-header =
          "Original-Recipient" ":" [FWS] address-type [FWS] ";" [FWS] generic-address

disposition-notification-content =
          [ reporting-ua-field CRLF ]
          [ mdn-gateway-field CRLF ]
          [ original-recipient-field CRLF ]
          final-recipient-field CRLF
          [ original-message-id-field CRLF ]
          disposition-field CRLF
          *( failure-field CRLF )
          *( error-field CRLF )
          *( extension-field CRLF )
OWS          = *WSP             ; generators
                  / [CFWS]         ; parsers
                  ; Optional whitespace.
                  ; MDN generators SHOULD use "*WSP"
                  ; (typically a single space or nothing.
                  ; It SHOULD be nothing at the end of a field),
                  ; unless an RFC 5322 "comment" is required.
                  ;
                  ; MDN parsers MUST parse it as "[CFWS]".
address-type = atom
mta-name-type = atom
reporting-ua-field = "Reporting-UA" ":" OWS ua-name OWS [ ";" OWS ua-product OWS ]
ua-name = *text-no-semi
ua-product = *([FWS] text)
text-no-semi = %d1-9 /        ; "text" characters excluding NUL, CR,
           %d11 / %d12 / %d14-58 / %d60-127      ; LF, or semi-colon
mdn-gateway-field = "MDN-Gateway" ":" OWS mta-name-type OWS ";" OWS mta-name
mta-name = *text
original-recipient-field =
          "Original-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS
generic-address = *text
final-recipient-field =
          "Final-Recipient" ":" OWS address-type OWS ";" OWS generic-address OWS
original-message-id-field = "Original-Message-ID" ":" OWS msg-id OWS
disposition-field =
          "Disposition" ":" OWS disposition-mode OWS ";"
          OWS disposition-type
          [ OWS "/" OWS disposition-modifier
          *( OWS "," OWS disposition-modifier ) ] OWS
disposition-mode = action-mode OWS "/" OWS sending-mode
action-mode = "manual-action" / "automatic-action"
sending-mode = "MDN-sent-manually" / "MDN-sent-automatically"
disposition-type = "displayed" / "deleted" / "dispatched" /
           "processed"
disposition-modifier = "error" / disposition-modifier-extension
disposition-modifier-extension = atom
failure-field = "Failure" ":" *([FWS] text)
error-field = "Error" ":" *([FWS] text)
extension-field = extension-field-name ":" *([FWS] text)
extension-field-name = field-name

boundary="RAA14128.773615765/example.com"

