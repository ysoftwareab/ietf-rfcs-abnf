language-range = language-tag / "*"
language-tag   = 1*8[alphanum] *["-" 1*8alphanum]
alphanum       = ALPHA / DIGIT

extended-language-range  = range ; a range
                 / privateuse       ; a private-use range
                 / grandfathered    ; a grandfathered registration

range         = (language
                    ["-" script]
                    ["-" region]
                    *("-" variant)
                    *("-" extension)
                    ["-" privateuse])

language      = (2*3ALPHA [ extlang ]) ; shortest ISO 639 code
                 / 4ALPHA                 ; reserved for future use
                 / 5*8ALPHA               ; registered language subtag
                 / "*"                    ; or wildcard

extlang       = *2("-" 3ALPHA) ("-" ( 3ALPHA / "*"))
                                          ; reserved for future use
                                          ; wildcard can only appear
                                          ;   at the end

script        = 4ALPHA                 ; ISO 15924 code
                 / "*"                    ; or wildcard

region        = 2ALPHA                 ; ISO 3166 code
                 / 3DIGIT                 ; UN M.49 code
                 / "*"                    ; or wildcard

variant       = 5*8alphanum            ; registered variants
                 / (DIGIT 3alphanum)      ;
                 / "*"                    ; or wildcard

extension     = singleton *("-" (2*8alphanum)) [ "-*" ]
                                          ; extension subtags
                                          ; wildcard can only appear
                                          ;   at the end

singleton     = %x41-57 / %x59-5A / %x61-77 / %x79-7A / DIGIT
                 ; single letters (except for "x") or digits

privateuse    = "x" 1*("-" (1*8alphanum))

grandfathered = 1*3ALPHA 1*2("-" (2*8alphanum))
                   ; grandfathered registration
                   ; Note: I is the only singleton
                   ; that starts a grandfathered tag

alphanum      = (ALPHA / DIGIT)       ; letters and numbers
