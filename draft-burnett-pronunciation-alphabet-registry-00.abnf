registry = file-date record-separator records

record-separator = "%%" CRLF

records = record *(record-separator record)

record = (alphabet-record / alias-record)
kv-separator = *SP ":" *SP

text = *(ASCCHAR/LWSP)

ASCCHAR = %x21-25 / %x27-7E / UNICHAR

UNICHAR = AMPERSAND "#x" 2*6HEXDIG ";"

AMPERSAND = %x26

date = 4DIGIT "-" 2DIGIT "-" 2DIGIT


file-date = "File-Date" kv-separator date CRLF

alphabet-record = alphabet-type tag 1*description added reference
                      [deprecated] *comments

alias-record = alias-type tag *description added alias
                      [deprecated] *comments

alphabet-type = type kv-separator "alphabet" CRLF

alias-type = type kv-separator "alias" CRLF

type = "Type"

tag = "Tag" kv-separator tag-format CRLF

description = "Description" kv-separator text CRLF

added = "Added" kv-separator date CRLF

reference = "Reference" kv-separator text CRLF

alias = "Alias" kv-separator tag-format CRLF

deprecated = "Deprecated" kv-separator date CRLF

comments = "Comments" kv-separator text CRLF

                      Figure 1: Registry Format ABNF

tag-format = 2ALPHA *5(["."] 1*20(ALPHA / DIGIT))

                       Figure 3: Tag and Alias ABNF
