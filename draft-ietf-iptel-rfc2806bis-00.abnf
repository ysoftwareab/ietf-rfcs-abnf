telephone-uri         =  "tel:" subscriber                          |
subscriber            =  global-number / local-number               |
global-number         =  global-number-digits *par                  |
local-number          =  local-number-digits *par context *par      |
par                   =  parameter / extension / isdn-subaddress    |
isdn-subaddress       =  ";isub=" 1*uric                            |
extension             =  ";ext=" 1*phonedigit                       |
context               =  ";phone-context=" descriptor               |
descriptor            =  domainname / global-number-digits          |
global-number-digits  =  "+" 1*phonedigit                           |
local-number-digits   =  1*phonedigit-hex                           |
domainname            =  *( domainlabel "." ) toplabel [ "." ]      |
domainlabel           =  alphanum                                   |
                                 / alphanum *( alphanum / "-" ) alphanum    |
toplabel              =  ALPHA / ALPHA *( alphanum / "-" ) alphanum |
parameter             =  ";" pname ["=" pvalue ]                    |
pname                 =  1*( alphanum / "-" )                       |
pvalue                =  1*paramchar                                |
paramchar             =  param-unreserved / unreserved / escaped    |
unreserved            =  alphanum / mark                            |
escaped               =  "%" HEXDIG HEXDIG                          |
param-unreserved      =  "[" / "]" / "/" / ":" / "&" / "+" / "$"    |
phonedigit            =  DIGIT [ visual-separator ]                 |
phonedigit-hex        =  HEXDIG [ visual-separator ]                |
visual-separator      =  "-" / "." / "(" / ")"                      |
alphanum              =  ALPHA / DIGIT                              |


