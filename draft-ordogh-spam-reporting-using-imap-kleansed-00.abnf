directive           = "SET" / "CLEAR" / directive-ext
directive-ext       = atom
                         ; New directives MUST begin with "X" or be
                         ; registered with IANA as standard or
                         ; standards-track
abuse-type          = "AT" SP abuse-type-id
abuse-type-id       = 1*DIGIT
                         ; New abuse types MUST be registered with
                         ; IANA as standard or standards-track
reference           = reference-type SP reference-value
reference-type      = "UID" / "SEQ" / "URLAUTH" / reference-type-ext
reference-type-ext  = atom
                         ; New reference types MUST begin with "X"
                         ; or be registered with IANA as standard or
                         ; standards-track
reference-value     = uniqueid /           ; see [IMAP]
                         sequence-set /       ; see [IMAP]
                         authorized-url /     ; see [URLAUTH]
                         reference-value-ext
authorized-url      = authimapurlfull /    ; see [URLAUTH]
                         authimapurlrump      ; see [URLAUTH]
reference-value-ext = atom
                         ; New reference values MUST correspond to
                         ; reference-type-ext
part-id-list        = "(" part-id *(SP part-id) ")"
part-id             = header-id / body-id
header-id           = "header." header-fld-name
                         ; see header-fld-name in [IMAP]
body-id             = "body" *(. 1*DIGIT)
request-action      = "DO" SP req-action SP destination-box
req-action          = "KEYWORD" /
                         "RELOCATE" /
                         "DELETE"
destination-box     = mailbox / nil
resp-text-code      = resp-spam-actions    ; responses specific to
                                              ; this command, extending
                                              ; the resp-text-code
                                              ; defined in [IMAP]
resp-spam-actions   = "KEYWORD" /
                         "RELOCATE" /
                         "RELOCATED" /
                         "DELETE" /
                         "DELETED"
