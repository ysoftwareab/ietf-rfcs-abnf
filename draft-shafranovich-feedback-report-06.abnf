feedback-report = *( feedback-type / user-agent / version )
                     [ dkim-failure ]
                     opt-fields-once
                     dkim-fields-once
                     *( opt-fields-many )

feedback-type = "Feedback-Type:" [CFWS] token [CFWS] CRLF
       ; the "token" must be a registered feedback type as
       ; described elsewhere in this document

user-agent = "User-Agent:" [CFWS] product [CFWS] CRLF

version = "Version:" [CFWS] token [CFWS] CRLF
       ; as described above

dkim-failure = "DKIM-Failure:" [CFWS] token [CFWS] CRLF
       ; the "token" must be a registered DKIM failure type
       ; as described elsewhere in this document

opt-fields-once = [ arrival-date ]
                     [ dkim-failure ]
                     [ incidents ]
                     [ original-envelope-id ]
                     [ original-mail-from ]
                     [ reporting-mta ]
                     [ source-ip ]

arrival-date = "Arrival-Date:" [CFWS] date-time [CFWS] CRLF

incidents = "Incidents:" [CFWS] 1*DIGIT [CFWS] CRLF

original-envelope-id = "Original-Envelope-Id:" [CFWS]
                          envelope-id [CFWS] CRLF

original-mail-from = "Original-Mail-From:" [CFWS]
                        reverse-path [CFWS] CRLF

reporting-mta = "Reporting-MTA:" [CFWS] mta-name [CFWS] CRLF

source-ip = "Source-IP:" [CFWS]
               ( IPv4-address-literal /
                 IPv6-address-literal ) [CFWS] CRLF

dkim-fields-once = [ dkim-canon-body ]
                      [ dkim-canon-header ]
                      [ dkim-domain ]
                      [ dkim-identity ]
                      [ dkim-selector ]
dkim-canon-body = "DKIM-Canonicalized-Body:" [CFWS]
                     base64string [CFWS] CRLF

dkim-canon-header = "DKIM-Canonicalized-Header:" [CFWS]
                       base64string [CFWS] CRLF

dkim-domain = "DKIM-Domain:" [CFWS] domain-name [CFWS] CRLF

dkim-identity = "DKIM-Domain:" [CFWS] [ local-part ] "@"
                   domain-name [CFWS] CRLF

dkim-selector = "DKIM-Selector:" [CFWS] selector [CFWS] CRLF

opt-fields-many = [ authres-header ]
                     [ original-rcpt-to ]
                     [ removal-recipient ]
                     [ reported-domain ]
                     [ reported-uri ]

original-rcpt-to = "Original-Rcpt-To:" [CFWS]
                      forward-path [CFWS] CRLF

removal-recipient = "Removal-Recipient:" [CFWS]
                       mailbox [CFWS] CRLF

reported-domain = "Reported-Domain:" [CFWS]
                     domain-name [CFWS] CRLF

reported-uri = "Reported-Domain:" [CFWS] URI [CFWS] CRLF

boundary="part1_13d.2e68ed54_boundary"

boundary="part1_13d.2e68ed54_boundary"

boundary="part1_13d.2e68ed54_boundary"

spf=fail
