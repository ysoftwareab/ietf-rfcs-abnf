MSA = Mail Submission Agent
MDA = Mail Delivery Agent

dmarc-uri       = URI
                       ; "URI" is imported from [RFC3986]; commas (ASCII
                       ; 0x2C) and exclamation points (ASCII 0x21)
                       ; MUST be encoded

dmarc-record    = dmarc-version dmarc-sep *(dmarc-tag dmarc-sep)

dmarc-tag       = dmarc-request /
                       dmarc-srequest /
                       dmarc-adkim /
                       dmarc-aspf /
                       dmarc-auri /
                       dmarc-furi /
                       dmarc-fo /
                       dmarc-rfmt
                       ; components other than dmarc-version and
                       ; dmarc-request may appear in any order

dmarc-version   = "v" *WSP "=" *WSP %x44 %x4d %x41 %x52 %x43 %x31

dmarc-sep       = *WSP %x3b *WSP

dmarc-request   = "p" *WSP "=" *WSP
                       ( "none" / "quarantine" / "reject" )

dmarc-srequest  = "sp" *WSP "=" *WSP
                       ( "none" / "quarantine" / "reject" )

dmarc-adkim     = "adkim" *WSP "=" *WSP ( "r" / "s" )

dmarc-aspf      = "aspf" *WSP "=" *WSP ( "r" / "s" )

dmarc-auri      = "rua" *WSP "=" *WSP
                       dmarc-uri *(*WSP "," *WSP dmarc-uri)

dmarc-furi      = "ruf" *WSP "=" *WSP
                       dmarc-uri *(*WSP "," *WSP dmarc-uri)

dmarc-fo        = "fo" *WSP "=" *WSP
                       ( "0" / "1" / ( "d" / "s" / "d:s" / "s:d" ) )

dmarc-rfmt      = "rf"  *WSP "=" *WSP Keyword *(*WSP ":" Keyword)
                       ; registered reporting formats only

selected = true
selected = false

ruf=mailto:auth-reports@example.com"

ruf=mailto:auth-reports@thirdparty.example.net"

rua=mailto:dmarc-feedback@example.com"
