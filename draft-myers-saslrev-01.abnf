sasl-mech             = 1*20mech-char
mech-char             = %x41-5A / DIGIT / "-" / "_"
                  ; mech names restricted to uppercase letters,
                  ; digits, "-" and "_"
initial-response = *UTF8
UTF8           = %x01-7F / UTF8-2 / UTF8-3 / UTF8-4
UTF8-loworder  = %x80-BF
UTF8-2         = %xC1-DF UTF8-loworder
                  ; Disallow overlong sequences beginning with 0xC0.
UTF8-3         = (%xE0 %xA0-BF UTF8-loworder) /
                    (%xE1-EC 2UTF8-loworder) /
                    (%xED %x80-9F UTF8-loworder) /
                    (%xEE 2UTF8-loworder) /
                    (%xEF %x80-BE UTF8-loworder) /
                    (%xEF %xBF %x80-BD)
                  ; Disallow overlong sequences beginning with 0xE0,
                  ; disallow encoded surrogate characters, and
                  ; disallow U+FFFE, U+FFFF.
UTF8-4         = (%xF0 %x90-BF 2UTF8-loworder) /
                    (%xF1-F7 3UTF8-loworder)
                  ; Disallow overlong sequences beginning with 0xF0.

