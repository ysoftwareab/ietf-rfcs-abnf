filter         = "(" filtercomp ")"
filtercomp     = and / or / not / item
and            = "&" filterlist
or             = "|" filterlist
not            = "!" filter
filterlist     = 1*filter
item           = simple / present / substring / extensible
simple         = attr filtertype assertionvalue
filtertype     = equal / approx / greater / less
equal          = "="
approx         = "~="
greater        = ">="
less           = "<="
extensible     = attr [":dn"] [":" matchingrule] ":=" assertionvalue
                       / [":dn"] ":" matchingrule ":=" assertionvalue
                       / ":=" assertionvalue
present        = attr "=*"
substring      = attr "=" [initial] any [final]
initial        = assertionvalue
any            = "*" *(assertionvalue "*")
final          = assertionvalue
attr           = AttributeDescription
                         ; The <AttributeDescription> rule is defined in
                         ; Section 4.1.5 of [Protocol].
matchingrule   = oid
                         ; The <oid> rule is defined in Section 4.1
                         ; of [Syntaxes] and is used to encode a
                         ; matching rule OBJECT IDENTIFIER.
assertionvalue = valueencoding
                         ; The <valueencoding> rule is used to encode an
                         ; <AssertionValue> from Section 4.1.7 of [Protocol].
valueencoding  = 0*(normal / escaped)
normal         = %x01-27 / %x2b-5b / %x5d-7f
escaped        = "\" hex hex
hex            = %x30-39 / %x41-46 / %x61-66


