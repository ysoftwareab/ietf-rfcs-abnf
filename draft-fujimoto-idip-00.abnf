OCTET          = <any 8-bit sequence of data>

CHAR           = <any US-ASCII character (octets 0 - 127)>

UPALPHA        = <any US-ASCII uppercase letter "A".."Z">

LOALPHA        = <any US-ASCII lowercase letter "a".."z">

ALPHA          = UPALPHA | LOALPHA

DIGIT          = <any US-ASCII digit "0".."9">

CTL            = <any US-ASCII control character
                    (octets 0 - 31) and DEL (127)>

CR             = <US-ASCII CR, carriage return (13)>

LF             = <US-ASCII LF, linefeed (10)>

SP             = <US-ASCII SP, space (32)>

HT             = <US-ASCII HT, horizontal-tab (9)>

CRLF           = CR LF

TEXT           = <any OCTET except CTLs>

HEX            = "A" | "B" | "C" | "D" | "E" | "F"
                  | "a" | "b" | "c" | "d" | "e" | "f" | DIGIT

word           = token | quoted-string

token          = 1*<any CHAR except CTLs or tspecials>

tspecials      = "(" | ")" | "<" | ">" | "@"
                  | "," | ";" | ":" | "
                  | "/" | "[" | "]" | "?" | "="
                  | "{" | "}"

quoted-string  = ( <"> *(qdtext) <"> )

qdtext         = <any CHAR except <"> and CTLs>

IDIP-request    = IDIP-command IDIP-parameters CRLF IDIP-data

IDIP-parameters = * (IDIP-parameter CRLF)

IDIP-command    = start-command
                   | end-command
                   | list-command
                   | call-command
                   | kill-command
                   | add-command
                   | delete-command
                   | ping-command
IDIP-response = IDIP-status IDIP-data

IDIP-status   = status-line IDIP-parameters

status-line   = status-code SP status-description CRLF

status-code   = success       ; 1xx
                 | client-error  ; 2xx
                 | server-error  ; 3xx
                 | generic-error ; 4xx

success           = "100" ; OK

client-error      = "201" ; Authentication Error
                     | "202" ; Request Denied

server-error      = "301" ; Invalid Callee
                     | "302" ; Server Internal Error
                     | "303" ; No Function Available
                     | "304" ; Cannot Provide Acceptable Data
                     | "305" ; Server Timeout
                     | "306" ; IDO Moved
                     | "307" ; Function Busy

generic-error     = "401" ; Unknown Error

status-description  = 1*TEXT

IDIP-data       = * <any OCTET>

IDIP-parameter   = IDIP-version
                    | IDO-To
                    | IDO-From
                    | content-type
                    | content-length
                    | accept-type
                    | IDIP-authenticate
                    | keyword-parameter
                    | location-parameter

IDIP-Version = "IDIP-Version:" SP "IDIP" "/" 1*DIGIT "." 1*DIGIT

IDO-To         = "To:" SP IDO-Address

IDO-From       = "From:" SP IDO-Address

IDO-Address    = identity_name "@" host

identity_name  = 1 * TEXT

host           = <A legal Internet host domain name
                    or IP address (in dotted-decimal form),
                    as defined by Section 2.1 of RFC 1123>

content-type   = "Content-Type:" SP media-type

media-type     = type "/" subtype *( ";" parameter )

type           = token

subtype        = token

parameter      = attribute "=" value

attribute      = token

value          = token | quoted-string

accept-type       = "Accept-Type:" SP media-type

IDIP-authenticate = "IDIP-Authenticate:" SP auth-options

auth-options      = option *(; option )

option            = authenticate-style

authenticate-style = "style" "=" value

keyword-parameter  = "Keywords:" SP keywords

keywords           = keyword * ("," keywords)

keyword            = token | quoted-string

location-parameter = "Location:" SP location

location           = IDO-Address
start-command     = "START" CRLF

end-command       = "END" CRLF

list-command      = "LIST" CRLF

call-command      = "CALL" SP function-name CRLF

function-name     = word


kill-command      = "KILL" SP function-name CRLF

add-command       = "ADD" CRLF


delete-command    = "DELETE" function-name CRLF

ping-command      = "PING" function-name CRLF

IDI-function   = "<idi-function>" *property "</idi-function>"

property       = name-prop     ; Name Property
                  | accept-prop   ; Accept Property
                  | spec-prop     ; Specification Property
                  | keywords-prop ; Keywords Property
                  | location-prop ; Location Property

name-prop      = "<name>" function-name "</name>"

spec-prop      = "<bag>" *( "<spec>" standard-name "</spec>" ) "</bag>"
                  | "<spec>" standard-name "</spec>"
standard-name  = token

accept-prop   = "<bag>" *( "<accept>" media-type "</accept>" ) "</bag>"
                 | "<accept>" media-type "</accept>"

keywords-prop = "<bag>" *( "<keyword>" keyword "</keyword>" ) "</bag>"
                 | "<keyword>" keyword "</keyword>"

location-prop = "<location>" url "</location>"

url           = <any URL>

v=0
c=IN IP4 133.164.59.101
m=application 20450 udp 0

v=0
u=ftp://guest1:openSesame@133.164.59.101/incoming/X080711.data

v=0
u=ftp://guest1:openSesame@133.164.59.101/incoming/X080711.data

