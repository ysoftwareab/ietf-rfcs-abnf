capability       =/ "URLAUTH=BINARY"

      ; Command parameters; see Section 3.1

urlfetch         =  "URLFETCH" 1*(SP url-fetch-arg)

url-fetch-arg    =  url-fetch-simple / url-fetch-ext

url-fetch-simple =  url-full
      ; Unextended URLFETCH.

url-fetch-ext    =  "(" url-full *(SP url-fetch-param) ")"
      ; If no url-fetch-param present, as unextended.

url-fetch-param  =  "BINARY" / "BODYPARTSTRUCTURE" / atom
      ; TODO: Should this be iana-token?

      ; Response; see Section 3.2

urlfetch-data    =  "*" SP "URLFETCH" 1*(SP url-full [url-metadata]
                       SP (nstring / literal8))
      ; MUST use nstring and MUST NOT use SP url-metadata when client
      ; issues url-fetch-simple or no url-fetch-param.

url-metadata     =  1*(SP "(" url-metadata-el ")")

url-metadata-el  =  "BODYPARTSTRUCTURE" SP body



