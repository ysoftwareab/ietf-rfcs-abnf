delta-seconds  = 1*DIGIT


corrected_received_age = max(now - date_value, age_value)

corrected_initial_age = corrected_received_age
                            + (now - request_time)

apparent_age = max(0, response_time - date_value);
corrected_received_age = max(apparent_age, age_value);
response_delay = response_time - request_time;
corrected_initial_age = corrected_received_age + response_delay;
resident_time = now - response_time;
current_age   = corrected_initial_age + resident_time;

freshness_lifetime = max_age_value

freshness_lifetime = expires_value - date_value

response_is_fresh = (freshness_lifetime > current_age)

Age = "Age" ":" age-value
age-value = delta-seconds

Cache-Control   = "Cache-Control" ":" 1#cache-directive

cache-directive = cache-request-directive
           | cache-response-directive

cache-request-directive =
             "no-cache"                          ; Section 3.2.1
           | "no-store"                          ; Section 3.2.2
           | "max-age" "=" delta-seconds         ; Section 3.2.3, 3.2.4
           | "max-stale" [ "=" delta-seconds ]   ; Section 3.2.3
           | "min-fresh" "=" delta-seconds       ; Section 3.2.3
           | "no-transform"                      ; Section 3.2.5
           | "only-if-cached"                    ; Section 3.2.4
           | cache-extension                     ; Section 3.2.6

cache-response-directive =
             "public"                               ; Section 3.2.1
           | "private" [ "=" <"> 1#field-name <"> ] ; Section 3.2.1
           | "no-cache" [ "=" <"> 1#field-name <"> ]; Section 3.2.1
           | "no-store"                             ; Section 3.2.2
           | "no-transform"                         ; Section 3.2.5
           | "must-revalidate"                      ; Section 3.2.4
           | "proxy-revalidate"                     ; Section 3.2.4
           | "max-age" "=" delta-seconds            ; Section 3.2.3
           | "s-maxage" "=" delta-seconds           ; Section 3.2.3
           | cache-extension                        ; Section 3.2.6

cache-extension = token [ "=" ( token | quoted-string ) ]

Expires = "Expires" ":" HTTP-date

Pragma            = "Pragma" ":" 1#pragma-directive
pragma-directive  = "no-cache" | extension-pragma
extension-pragma  = token [ "=" ( token | quoted-string ) ]

Vary  = "Vary" ":" ( "*" | 1#field-name )
Warning    = "Warning" ":" 1#warning-value

warning-value = warn-code SP warn-agent SP warn-text
                                             [SP warn-date]

warn-code  = 3DIGIT
warn-agent = ( host [ ":" port ] ) | pseudonym
                       ; the name or pseudonym of the server adding
                       ; the Warning header, for use in debugging
warn-text  = quoted-string
warn-date  = <"> HTTP-date <">

