format=flowed" as specified in [RFC2646] and SHOULD preserve flowed
fields          =/  *( newsgroups /
                          path /
                          followup-to /
                          expires /
                          control /
                          distribution /
                          summary /
                          approved /
                          organization /
                          xref /
                          supersedes )

newsgroups      =  "Newsgroups:" newsgroup-list CRLF

newsgroup-list  =  [FWS] newsgroup-name
                      *( "," [FWS] newsgroup-name ) [FWS]

newsgroup-name  =  component *( "." component ) ; 71 character max

component       =  plain-component / encoded-comp

plain-component =  component-start *29component-rest

component-start =  lowercase / DIGIT

lowercase       =  %x61-7A ;  a-z

component-rest  =  component-start / "+" / "-" / "_"

encoded-comp    =  ace-prefix 1*26ldh

ace-prefix      =  "xn--"

ldh             =  lowercase / DIGIT / "-"

path            =  "Path:" [FWS]
                      *( path-host [FWS] path-delimiter [FWS] )
                      path-host [FWS] CRLF

path-host       =  ( ALPHA / DIGIT )
                      *( ALPHA / DIGIT / "-" / "." / ":" / "_" )

path-delimiter  =  "/" / "?" / "%" / "," / "!"

followup-to     =  "Followup-To:" ( newsgroup-list / poster-text )
                      CRLF

poster-text     =  [FWS] %x70.6F.73.74.65.72 [FWS]
                      ; "poster" in lower-case

expires         =  "Expires:" date-time CRLF

control         =  "Control:" verb *( FWS argument ) CRLF

distribution    =  "Distribution:" dist-name *( "," dist-name ) CRLF

dist-name       =  [FWS] ALPHA *( ALPHA / "+" / "-" / "_" ) [FWS]

summary         =  "Summary:" unstructured CRLF

approved        =  "Approved:" mailbox-list CRLF

organization    =  "Organization:" unstructured CRLF
xref            =  "Xref:" [CFWS] path-host
                      1*( CFWS location ) [CFWS]

location        =  newsgroup-name ":" utext

supersedes      =  "Supersedes:" 1*msg-id CRLF

