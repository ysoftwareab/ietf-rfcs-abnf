getquota           =  "GETQUOTA" SP quota-root-name

getquotaroot       =  "GETQUOTAROOT" SP mailbox

quota-list         =  "(" quota-resource *(SP quota-resource) ")"

quota-resource     =  resource-name SP resource-usage SP resource-
                       limit

quota-response     =  "QUOTA" SP quota-root-name SP quota-list

quotaroot-response =  "QUOTAROOT" SP mailbox *(SP quota-root-name)

setquota           =  "SETQUOTA" SP quota-root-name SP setquota-list

setquota-list      =  "(" [setquota-resource *(SP setquota-resource)]
                       ")"

setquota-resource  =  resource-name SP resource-limit

quota-root-name    =  astring

resource-limit     =  number

resource-name      =  "STORAGE" | "MESSAGE" | "MAILBOXES" |
                       resource-name-priv | resource-name-vnd |
                       resource-name-ext

resource-name-priv =  "X-" atom
                       ;; Private use

resource-name-vnd  =  "V-" atom
                       ;; Vendor specific, must be registered with IANA
resource-name-ext  =  atom
                       ;; Not starting with either X- or V- and defined
                       ;; in a Standard Track or Experimental RFC

resource-names =    "(" [resource-name *(SP resource-name)] ")"

resource-usage     =  number
                       ;; must be less than corresponding resource-limit

capability-quota   =  capa-quota-res

capa-quota-res     =  "QUOTA=RES-" resource-name

status-att         =/  "DELETED-MESSAGES" | "DELETED-STORAGE"

                       [[anchor20: Should this be optional unless the
                       server implements MESSAGE/STORAGE?]]

resp-text-code     =/  "OVERQUOTA" [SP tag]


