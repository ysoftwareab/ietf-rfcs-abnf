sort            = ["UID" SP] "SORT" SP
                     "(" sort-criterion *(SP sort-criterion) ")"
                     SP charset 1*(SP search-key)

sort-criterion  = ["REVERSE" SP] sort-key

sort-key        = "ARRIVAL" / "CC" / "DATE" / "FROM" / "SIZE" /
                     "SUBJECT" / "TO"
thread            = ["UID" SP] "THREAD" SP thread-algorithm
                       SP charset 1*(SP search-key)

thread-algorithm  = "ORDEREDSUBJECT" / "REFERENCES" / atom

charset           = astring
                       ; CHARSET argument to MUST be registered with IANA

sort-data       = "SORT" *(SP nz-number)

thread-data       = "THREAD" [SP 1*thread-list]

thread-list       = "(" thread-members / thread-nested ")"

thread-members    = nz-number *(SP nz-number) [SP thread-nested]

thread-nested     = 2*thread-list



subject         = *subj-leader [subj-middle] *subj-trailer

subj-refwd      = ("re" / ("fw" ["d"])) *WSP [subj-blob] ":"

subj-blob       = "[" *BLOBCHAR "]" *WSP

subj-fwd        = subj-fwd-hdr subject subj-fwd-trl

subj-fwd-hdr    = "[fwd:"

subj-fwd-trl    = "]"

subj-leader     = (*subj-blob subj-refwd) / WSP
subj-middle     = *subj-blob (subj-base / subj-fwd)
                   ; last subj-blob is subj-base if subj-base would
                   ; otherwise be empty

subj-trailer    = "(fwd)" / WSP

subj-base       = NONWSP *([*WSP] NONWSP)
                   ; can be a subj-blob

BLOBCHAR        = %x01-5a / %x5c / %x5e-7f
                   ; any CHAR except '[' and ']'

NONWSP          = %x01-08 / %x0a-1f / %x21-7f
                   ; any CHAR other than WSP


