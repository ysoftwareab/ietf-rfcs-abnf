capability          =/ "SUBMIT" / "SUBMIT=DSN"
                         ;; This extension also reserves all further
                         ;; capabilities starting with the "SUBMIT="
                         ;; prefix for future extensions related to the
                         ;; message submission over IMAP
   
uid                 =/ "UID" SP sendmail
   
sendmail            = "SUBMIT" SP uniqueid [SP submission-options]
   
submission-options  = "(" submission-option *( SP submission-option ) ")"
   
submission-option   = sub-option-from / sub-option-recipient
                         / sub-option-dsn-ret / sub-option-dsn-envid
   
sub-option-from     = "FROM" SP one-email-addr
                         ;; MUST NOT be present more than once
   
sub-option-recipient= "RECIPIENT" SP one-email-addr [SP sub-option-rcpt-dsn]
                         ;; MAY be present more than once
   
sub-option-rcpt-dsn = "DSN" SP ( "NONE" / dsn-spec )
                         ;; MUST NOT be present more than once
   
dsn-spec            = "(" dsn-spec-item *( SP dsn-spec-item ) ")"
                         ;; an individual dsn-spec-item MUST NOT
                         ;; be present more than once
   
dsn-spec-item       = "DELAY" / "FAILURE" / "SUCCESS"
   
sub-option-dsn-ret  = "DSN-RET" SP ( "FULL" / "HDRS" )
   
sub-option-dsn-envid= "DSN-ENVID" SP xtext
                         ;; <xtext> is defined in [RFC3461], section 4.
                         ;; The allowed syntax is further limited by
                         ;; its section 4.4.
   
one-email-addr      = string
                         ;; valid e-mail address as per [RFC5321]

