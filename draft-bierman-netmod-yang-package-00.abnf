pkg-uri             = yang-pkg-uri parameter-list
parameter-list      = "?" pkg-parameter "&" rev-parameter
pkg-parameter       = "name=" package-name
rev-parameter       = "rev=" revision-date

package-stmt     = optsep package-keyword sep identifier-arg-str
                           optsep
                           "{" stmtsep
                               [yang-package-version-stmt]
                               meta-stmts
                               [status-stmt stmtsep]
                               req-revision-stmts
                               package-body-stmts
                           "}" optsep

yang-package-version-stmt   = yang-package-version-keyword sep
                                  yang-package-version-arg-str
                                  optsep stmtend

yang-package-version-arg-str = < a string that matches the rule
                                   yang-package-version-arg >

yang-package-version-arg    = "1"

req-revision-stmts = 1*(revision-stmt stmtsep)

uses-revision-stmt = revision-keyword sep date-arg-str stmtend

package-body-stmts  = *((uses-package-stmt /
                                 uses-module-stmt /
                                 imports-module-stmt /
                                 uses-capability-stmt) stmtsep)
uses-package-stmt = uses-package-keyword sep
                          identifier-arg-str optsep
                          (";" /
                           "{" stmtsep
                               ;; these stmts can appear in any order
                               uses-revision-stmt stmtsep
                               [description-stmt stmtsep]
                               [reference-stmt stmtsep]
                           "}")

uses-module-stmt = uses-module-keyword sep
                          identifier-arg-str optsep
                          (";" /
                           "{" stmtsep
                               ;; these stmts can appear in any order
                               uses-revision-stmt stmtsep
                               [description-stmt stmtsep]
                               [reference-stmt stmtsep]
                               *(uses-feature-stmt stmtsep)
                           "}")

uses-feature-stmt = uses-feature-keyword sep
                          identifier-arg-str optsep
                          (";" /
                           "{" stmtsep
                               ;; these stmts can appear in any order
                               [description-stmt stmtsep]
                               [reference-stmt stmtsep]
                           "}")

imports-module-stmt = imports-module-keyword sep
                          identifier-arg-str optsep
                          (";" /
                           "{" stmtsep
                               ;; these stmts can appear in any order
                               uses-revision-stmt stmtsep
                               [description-stmt stmtsep]
                               [reference-stmt stmtsep]
                           "}")

uses-capability-stmt = uses-capability-keyword sep
                        uri-str optsep
                        (";" /
                         "{" stmtsep
                             ;; these stmts can appear in any order
                             [description-stmt stmtsep]
                             [reference-stmt stmtsep]
                             *(uses-parameter-stmt stmtsep)
                         "}")

uses-parameter-stmt = uses-parameter-keyword sep
                          identifier-arg-str optsep
                          (";" /
                           "{" stmtsep
                               ;; these stmts can appear in any order
                               [description-stmt stmtsep]
                               [reference-stmt stmtsep]
                               *(uses-value-stmt stmtsep)
                           "}")

uses-value-stmt = uses-value-keyword sep
                          value-arg-str optsep
                          (";" /
                           "{" stmtsep
                               ;; these stmts can appear in any order
                               [description-stmt stmtsep]
                               [reference-stmt stmtsep]
                           "}")

value-arg-str = <string containing parameter value>


imports-module-keyword   = 'imports-module'
package-keyword          = 'package'
uses-revision-keyword    = 'uses-revision'
uses-capability-keyword  = 'uses-capability'
uses-feature-keyword     = 'uses-feature'
uses-module-keyword      = 'uses-module'
uses-parameter-keyword   = 'uses-parameter'
uses-package-keyword     = 'uses-package'
uses-value-keyword       = 'uses-value'
yang-package-version-keyword = 'yang-package-version'

