uMailbox = uLocal-part "@" uDomain
                   ; Replace Mailbox in RFC 2821, section 4.1.2

uLocal-part = uDot-string / uQuoted-string
           ; MAY be case-sensitive
                   ; Replace Local-part in RFC 2821, section 4.1.2

uDot-string = uAtom *("." uAtom)
                   ; Replace Dot-string in RFC 2821, section 4.1.2

uAtom = 1*ucharacter
               ; Replace Atom in RFC 2821, section 4.1.2

ucharacter = atext / Non-ASCII
                   ; Replace character in RFC 2821, section 4.1.2
                   ; atext is defined in RFC 2822
                   ; UTF8-2, UTF8-3 and UTF8-4 are defined in RFC 3629

uQuoted-string = DQUOTE *uqcontent DQUOTE
                   ; Replace Quoted-string in RFC 2821, section 4.1.2
                   ; DQUOTE is Double Quote defined in RFC 4234

uqcontent = qcontent / Non-ASCII
                   ; qcontent is defined in RFC 2822, section 3.2.5

uDomain = (sub-udomain 1*("." sub-udomain)) / address-literal
                   ; Replace Domain in RFC 2821, section 4.1.2
                   ; address-literal is defined in RFC2821 section 4.1.2

sub-udomain = uLet-dig [uLdh-str]
                   ; Replace sub-domain in RFC 2821, section 4.1.2

uLet-dig = Let-dig / Non-ASCII
                   ; Let-dig in the right of '=' is defined in RFC 2822

uLdh-str = *( ALPHA / DIGIT / "-" / Non-ASCII) uLet-dig
                   ; Replace Ldh-str in RFC 2821, section 4.1.2

Non-ASCII = UTF8-2 / UTF8-3 / UTF8-4



uReverse-path = uPath
               ; Replace Reverse-path in RFC 2821, section 4.1.2

uForward-path = uPath
               ; Replace Forward-path in RFC 2821, section 4.1.2

uPath = "<" [ uA-d-l ":" ] uMailbox ">"
               ; Replace Path in RFC 2821, section 4.1.2

uA-d-l = uAt-domain *( "," uA-d-l )
               ; Replace A-d-l in RFC 2821, section 4.1.2

uAt-domain = "@" udomain
               ; Replace At-domain in RFC 2821, section 4.1.2
                   ; udomain is defined in section 2.3 of this document

ALT-ADDRESS-esmtp-value=Mailbox
           ; Mailbox is defined in RFC 2821, section 4.1.2

uReturn-path-line = "Return-Path:" FWS uReverse-path <CRLF>
           ; Replaces Return-path-line in the section 4.4 of [RFC2821]
       ; uReverse-path is defined in Section 2.3

uTime-stamp-line = "Received:" FWS uStamp <CRLF>
           ; Replaces Time-stamp-line in the section 4.4 of [RFC2821]

uStamp = From-domain By-domain uOpt-info ";"  FWS date-time
           ; Replaces Stamp in the section 4.4 of [RFC2821]

uOpt-info = [Via] [With] [ID] [uFor]
           ; Replaces Opt-info in the section 4.4 of [RFC2821]
           ; [With]'s protocl value will allow UTF8SMTP value

uFor = "FOR" FWS 1*( Path / uMailbox ) CFWS
           ; Replaces For in the section 4.4 of [RFC2821]
       ; uReverse-path is defined in Section 2.4


