distinguishedName = [name]
                          ; may be empty

name              = name-component *(COMMA name-component)

name-component    = attributeTypeAndValue *(PLUS attributeTypeAndValue)

attributeType     = keyword / oid

keyword           = ALPHA 1*keychar

keychar           = ALPHA / DIGIT / MINUS

oid               = number *(DOT number)

number            = ( LDIGIT *DIGIT ) / DIGIT

attributeValue    = string / hexstring

string            = *( stringchar / pair )
                          ; the string MUST NOT start with SHARP or SP
                          ; and MUST NOT end with SP

stringchar        = <any UTF-8 character (can be multiple octets)
                          except one of escaped or ESC>

pair              = ESC ( ESC / special / hexpair )

special           = escaped / SHARP / EQUALS / SP

escaped           = COMMA / PLUS / %x22 / %x3C / %x3E / %3B
                          ; "," / "+" / """ / "<" /  ">" / ";"

hexstring         = SHARP 1*hexpair

hexpair           = HEX HEX

HEX               = DIGIT / %x41-46 / %x61-66
                           ; 0-9 / A-F / a-f

ALPHA             = %x41-5A / %x61-7A
                           ; A-Z / a-z
LDIGIT            = %x31-39
                           ; 1-9

DIGIT             = %x30 / LDIGIT
                           ; 0-9

SP                = %x20 ; space (" ")
SHARP             = %x23 ; sharp sign ("#")
PLUS              = %x2B ; plus sign ("+")
COMMA             = %x2C ; comma (",")
MINUS             = %x2D ; minus sign ("-")
DOT               = %x2E ; period (".")
EQUALS            = %x3D ; equals sign ("=")
ESC               = %x5C ; backslash ("\")

UID=jsmith,DC=example,DC=net

OU=Sales+CN=J. Smith,DC=example,DC=net

CN=John Smith\, III,DC=example,DC=net

CN=Before\0dAfter,DC=example,DC=net

CN=Lu\C4\8Di\C4\87


FOO=BAR is ambiguous.  That is, FOO could be recognized as the
