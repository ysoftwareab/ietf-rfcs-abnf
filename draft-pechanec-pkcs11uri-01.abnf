semicolon               = "\%x3B"
backslash               = "\\"
hexa                    = 2*2( %x30-39 / %x41-46 / %x61-66 )

UTF8-octets-with-escapes= *( UTF8-char-with-escapes )
UTF8-char-with-escapes  = UTF8-1-with-escapes / UTF8-2 / UTF8-3 /
                           UTF8-4
UTF8-1-with-escapes     = %x00-3A / semicolon / %x3C-5B /
                           backslash / %x5D-7F
UTF8-2                  = %xC2-DF UTF8-tail
UTF8-3                  = %xE0 %xA0-BF UTF8-tail /
                           %xE1-EC 2( UTF8-tail ) /
                           %xED %x80-9F UTF8-tail /
                           %xEE-EF 2( UTF8-tail )
UTF8-4                  = %xF0 %x90-BF 2( UTF8-tail ) /
                           %xF1-F3 3( UTF8-tail ) /
                           %xF4 %x80-8F 2( UTF8-tail )
UTF8-tail               = %x80-BF

pk11-URI                = "pkcs11" ":" pk11-identifier
pk11-identifier         = *1( pk11-attr *( "%x3B" pk11-attr ) )
pk11-attr               = pk11-token / pk11-manufacturer /
                           pk11-serial / pk11-model /
                           pk11-object / pk11-objecttype /
                           pk11-id / pk11-passphrasedialog
pk11-ck-char            = %x41-5A / %x61-7A / %x30-39 / %x20-23 /
                           %x25-2F / %x3A / semicolon / %x3C-3F /
                           %x5B / backslash / %x5D-5F / %x7B-7E
pk11-token              = "token" "=" UTF8-octets-with-escapes
pk11-manufacturer       = "manufacturer" "=" UTF8-octets-with-escapes
pk11-serial             = "serial" "=" *( pk11-ck-char )
pk11-model              = "model" "=" UTF8-octets-with-escapes
pk11-object             = "object" "=" UTF8-octets-with-escapes
pk11-objecttype         = "objecttype" "=" ( "public" / "private" /
                           "cert" / "secretkey" / "data" )
pk11-id                 = "id" "=" *1( hexa *( ":" hexa ) )
pk11-passphrasedialog   = "passphrasedialog" "=" "builtin" / exec
exec                    = "exec" ":" filename
filename                = UTF8-octets-with-escapes

   While the PKCS#11 specification limits the length of some fields, eg.
   the manufacturer label can be up to thirty-two characters long, the
   PKCS#11 URI does not impose such limitations.  It is up to the
   consumer of the PKCS#11 URI to perform any necessary sanity checks.

   The "UTF8-octets-with-escapes" rule for the UTF8 string with escaped
   ";" and "\" characters is based on the "UTF8-octets" rule defined in
   [RFC3629].

   The attribute "token" represents a token label, the attribute
   "manufacturer" represents a manufacturer ID, the attribute "serial"
   represents a token serial number, the attribute "model" represents a
   token model, the attribute "object" represents a PKCS#11 object
   label, the attribute "objecttype" represents the type of the object,
   the attribute "id" represents the object ID, and the attribute
   "passphrasedialog" specifies how the application or library should
   ask for the token PIN, if needed.  The "builtin" value suggests that
   the application or the library should use whatever means of reading
   the PIN it can provide, and the "exec:<filename>" value specifies an
   external application whose standard output should be used as the
   token PIN.


manufacturer=Snake Oil, Inc.;
serial=;
model=1.0;
object=my-certificate;
objecttype=cert;
id=69:95:3e:5c:f4:bd:ec:91;
passphrasedialog=exec:/bin/askpass.sh

manufacturer=Snake Oil, Inc.;
passphrasedialog=exec:/bin/askpass.sh

object=my-certificate;
objecttype=cert;
id=69:95:3E:5C:F4:BD:EC:91;
passphrasedialog=exec:/bin/askpass.sh

object=my-certificate;
objecttype=cert;
passphrasedialog=exec:/bin/askpass.sh


