body                   = *line (permission-field eol) (signature-field eol) *line

line                   = *1(field / comment) eol

eol                    = *WSP [CR] LF

field                  = acknowledgments-field /
                         contact-field /
                         encryption-field /
                         hiring-field /
                         policy-field /
                         ext-field

fs                     = ":"

comment                = "#" *(WSP / VCHAR / %xA0-E007F)

acknowledgments-field  = "Acknowledgments" fs SP uri

contact-field          = "Contact" fs SP (email / uri / phone)

email                  = <Email address as per {{RFC5322}}>

phone                  = "+" *1(DIGIT / "-" / "(" / ")" / SP)

uri                    = <URI as per {{RFC3986}}>

encryption-field       = "Encryption" fs SP uri

hiring-field           = "Hiring" fs SP uri

permission-field       = "Permission" fs SP "none"

policy-field           = "Policy" fs SP uri

signature-field        = "Signature" fs SP uri

ext-field              = field-name fs SP unstructured

field-name             = <as per section 3.6.8 of {{RFC5322}}>

unstructured           = <as per section 3.2.5 of {{RFC5322}}>

   "ext-field" refers to extension fields, which are discussed in
   Section 4.4
