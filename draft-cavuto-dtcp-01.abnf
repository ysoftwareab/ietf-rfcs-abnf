ipad = the byte 0x36 repeated B times 
opad = the byte 0x5C repeated B times. 
    
COL            =  *(WSP) ":" *(WSP) ;  used in parameter-value pair 
NPCHAR         =  "\" 3DIGIT        ;  used to express ctrl-chars 
DSTRING        =  *(VCHAR / NPCHAR) ;  no embedded whitespace      
WC             =  "*"            ;  wildcard character for matching 
NOT            =  "!"            ;  invert character for matching 
N64BITNUM         =  1*20DIGIT 
N32BITNUM         =  1*10DIGIT 
N16BITNUM         =  1*5DIGIT  
N8BITNUM       =  1*3DIGIT  
DAYSEC         =  1*5DIGIT 
IPv4address    =  1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT 
TEXT           =  1*(1*(VCHAR) WSP) ; includes whitespace 
DTCP-Time      =  4DIGIT "-" 2DIGIT "-" 2DIGIT SP 2DIGIT ":"  
                     2DIGIT ":" 2DIGIT "." 3DIGIT  
                     ; This is ISO date/time: YYYY-MM-DD sp HH:MM:SS.TTT 
    
DTCP-Message   =  Request / Response / Notification 
    
Request        =  Request-Line 
                     ( Add-Req-Parameters 
                  /  Delete-Req-Parameters 
 
 
 
 
 
                  /  Refresh-Req-Parameters 
                  /  List-Req-Parameters 
                  /  Noop-Req-Parameters) 
                     *(extension-parameter) 
                     Csource-ID 
                     Seq 
                     Authentication-Info 
                     CRLF 
    
Response       =  Status-Line 
                     ( ( Add-Resp-Parameters 
                  /  Delete-Resp-Parameters 
                  /  Refresh-Resp-Parameters 
                  /  List-Resp-Parameters 
                  /  Noop-Resp-Parameters) / Error-Parameters ) 
                     *(extension-parameter)   
                     Timestamp 
                     Seq                  ; note absence of Csource-ID 
                     Authentication-Info 
                     CRLF 
    
Notification   =  Status-Line 
                     ( Restart-Notif-Parameters 
                  /  Rollover-Notif-Parameters 
                  /  Noop-Notif-Parameters 
                  /  Timeout-Notif-Parameters 
                  /  Congestion-Notif-Parameters 
                  /  CongDel-Notif-Parameters ) 
                     *(extension-parameter)   
                     Timestamp 
                     Authentication-Info  ;  note absence of Seq 
                     CRLF 
    
DTCP-Version   =  "DTCP" "/" 1*DIGIT "." 1*DIGIT 
    
Status-Line    =  DTCP-Version SP Status-Code SP Reason-Phrase CRLF 
Request-Line   =  Method SP DTCP-Version CRLF 
    
Method         =  "ADD" / "DELETE" / "REFRESH" / "LIST" / "NOOP"  
                     / extension-method 
    
extension-method  =  DSTRING 
    
Status-Code    =  Provisional 
                  /  Success 
                  /  Redirection  
                  /  Request-Failure  
                  /  Server-Failure  
 
 
 
 
 
                  /  Global-Failure  
                  /  extension-code 
    
Reason-Phrase  =  TEXT  ; differs from SIP 
extension-code =  3DIGIT 
    
Provisional       =  "130" ;  Sequence Number Rollover (Notif) 
                     /  "131" ;  NoOp Notification (Notif) 
Success           =  "200" ;  OK  
Redirection       =  "390" ;  Criterion Timeout Delete (Notif) 
Request-Failure   =  "400" ;  Bad Request 
                     /  "430" ;  Unknown Content Destination 
                     /  "431" ;  Unknown Criteria ID 
                     /  "432" ;  Improper Filter Specification 
                     /  "433" ;  Improper Timeout Specification 
                     /  "497" ;  Invalid Authentication 
                              ;  (never sent to client) 
                     /  "498" ;  Invalid Sequence Number 
                              ;  (never sent to client) 
                     /  "499" ;  Unknown Control Source Identifier 
                              ;  (never sent to client) 
Server-Failure    =  "500" ;  Server Internal Error 
                     /  "501" ;  Not Implemented 
                     /  "505" ;  DTCP Version not supported 
                     /  "580" ;  Congestion (Notif) 
                     /  "598" ;  Duplicate Packets Dropped (Notif) 
                     /  "599" ;  Server Restart (Notif) 
Global-Failure    =  "680" ;  Criterion Congestion Delete (Notif) 
    
Error-Parameters  =  Cdest-ID  
                     /  Criteria-ID  
                     /  Filter-Block 
                     /  Timeout-Block 
    
Add-Req-Parameters   =  Filter-Block Timeout-Block [Action]  
                           Option-Block [Flags] Cdest-ID 
    
Filter-Block               =  *(Filter-Element)  
Timeout-Block              =  *(Timeout-Element) 
TRemaining-Block           =  *(TRemaining-Element) 
Option-Block               =  *(Option-Element) 
Timeout-Required-Block     =  1*(Timeout-Element) 
TRemaining-Required-Block  =  1*(TRemaining-Element) 
    
    
Filter-Element    =  Source-Address 
                     /  Dest-Address 
                     /  Protocol 
 
 
 
 
 
                     /  Source-Port 
                     /  Dest-Port 
                     /  ICMP-Type 
                     /  ICMP-Code 
    
Timeout-Element   =  Timeout-Idle 
                     /  Timeout-Total 
                     /  Timeout-Packets 
                     /  Timeout-Bytes 
    
TRemaining-Element   =  Remaining-Idle 
                        /  Remaining-Total 
                        /  Remaining-Packets 
                        /  Remaining-Bytes 
    
Option-Element    =  Priority 
    
Add-Resp-Parameters  =  Criteria-ID 
    
Delete-Req-Parameters   =  ( (Criteria-ID / Criteria-ID-Filter)  
                              Cdest-ID ) [Flags] 
Delete-Resp-Parameters  =  Criteria-Count 
    
Refresh-Req-Parameters  =  ( (Criteria-ID / Criteria-ID-Filter)  
                              Cdest-ID ) Timeout-Required-Block 
Refresh-Resp-Parameters =  Criteria-Count 
    
List-Req-Parameters  =  [ ( (Criteria-ID / Criteria-ID-Filter)  
                              Cdest-ID ) ] [Flags] 
    
List-Resp-Parameters =  *(List-Resp-Entry CRLF) 
    
List-Resp-Entry   =  Criteria-Count Criteria-Num Main-List  
                        [Criteria-List] [Stats-List] 
 
Main-List            =  Csource-ID Csource-Address Cdest-ID  
                           Criteria-ID Timestamp 
Criteria-List        =  *(Filter-Element) *(Timeout-Element) [Flags] 
Stats-List           =  TRemaining-Block Stats-Block 
    
Stats-Block       =  Average-Bandwidth Matching-Packets  
                        Matching-Bytes Num-Refresh Last-Refresh 
    
Noop-Req-Parameters  =  [Flags] 
Noop-Resp-Parameters =  []    ;  no parameters 
    
Restart-Notif-Parameters      =  Alert-Info 
Rollover-Notif-Parameters     =  []       ; no parameters 
 
 
 
 
 
Noop-Notif-Parameters         =  []       ; no parameters 
Timeout-Notif-Parameters      =  Criteria-ID 
                                 /  Timeout-Required-Block 
                                 /  TRemaining-Required-Block 
Congestion-Notif-Parameters   =  Cdest-ID Average-Bandwidth 
                                    Alert-Bandwidth Max-Bandwidth 
CongDel-Notif-Parameters      =  Cdest-ID Criteria-ID-Filter 
    
extension-parameter  =  "X-" DSTRING COL DSTRING CRLF 
    
Csource-ID           =  "Csource-ID" COL DSTRING CRLF 
Seq                  =  "Seq" COL N64BITNUM CRLF 
Authentication-Info  =  "Authentication-Info" COL 40HEXDIG CRLF 
Cdest-ID             =  "Cdest-ID" COL DSTRING CRLF 
Source-Address       =  "Source-Address" COL IPFilter CRLF 
Dest-Address         =  "Dest-Address" COL IPFilter CRLF 
Protocol             =  "Protocol" COL ProtFilter CRLF 
Source-Port          =  "Source-Port" COL PortFilter CRLF 
Dest-Port            =  "Dest-Port" COL PortFilter CRLF 
ICMP-Type            =  "ICMP-Type" COL ICMPFilter CRLF 
ICMP-Code            =  "ICMP-Code" COL ICMPFilter CRLF 
Timeout-Idle         =  "Timeout-Idle" COL DAYSEC CRLF 
Timeout-Total        =  "Timeout-Total" COL DAYSEC CRLF 
Timeout-Packets      =  "Timeout-Packets" COL N32BITNUM CRLF 
Timeout-Bytes        =  "Timeout-Bytes" COL N64BITNUM CRLF 
Priority             =  "Priority" COL N8BITNUM CRLF 
Criteria-ID          =  "Criteria-ID" COL N32BITNUM CRLF 
Criteria-ID-Filter   =  "Criteria-ID" COL CritFilter CRLF 
Criteria-Count       =  "Criteria-Count" COL N32BITNUM CRLF 
Criteria-Num         =  "Criteria-Num" COL N32BITNUM CRLF 
Csource-Address      =  "Csource-Address" COL IPv4address CRLF 
Timestamp            =  "Timestamp" COL DTCP-Time CRLF 
Remaining-Idle       =  "Remaining-Idle" COL DAYSEC CRLF 
Remaining-Total      =  "Remaining-Total" COL DAYSEC CRLF  
Remaining-Packets    =  "Remaining-Packets" COL N32BITNUM CRLF 
Remaining-Bytes      =  "Remaining-Bytes" COL N64BITNUM CRLF 
Average-Bandwidth    =  "Average-Bandwidth" COL N64BITNUM CRLF 
Matching-Packets     =  "Matching-Packets" COL N64BITNUM CRLF 
Matching-Bytes       =  "Matching-Bytes" COL N64BITNUM CRLF 
Num-Refresh          =  "Num-Refresh" COL N32BITNUM CRLF 
Last-Refresh         =  "Last-Refresh" COL DTCP-Time CRLF 
Alert-Info           =  "Alert-Info" COL TEXT CRLF 
Alert-Bandwidth      =  "Alert-Bandwidth" COL N64BITNUM CRLF 
Max-Bandwidth        =  "Max-Bandwidth" COL N64BITNUM CRLF 
Action               =  "Action" COL ActionEntry CRLF 
    
ActionEntry          =  "Copy" 
                        /  "Block" 
 
 
 
 
 
                        /  "Redirect"  
                        /  extension-action 
    
extension-action     =  DSTRING 
    
Flags                = "Flags" COL FlagEntry *("," FlagEntry) CRLF 
    
FlagEntry            =  "Static"  
                        /  "SendAsync"  
                        /  "Stats"  
                        /  "Criteria"  
                        /  "Both"  
    
IPFilter    =  [NOT] IPEntry *("," [WSP] [NOT] IPEntry) 
    
ProtFilter  =  [NOT] ProtEntry *("," [WSP] [NOT] ProtEntry) 
    
PortFilter  =  [NOT] PortEntry *("," [WSP] [NOT] PortEntry) 
    
ICMPFilter  =  [NOT] ICMPEntry *("," [WSP] [NOT] ICMPEntry) 
    
CritFilter  =  [NOT] CritEntry *("," [WSP] [NOT] CritEntry) 
    
IPEntry     =  IPv4address                   ;  Single Entry 
               /  IPv4address "/" N8BITNUM      ;  Address/mask 
               /  IPv4address "-" IPv4address   ;  Range 
               /  IPv4address "-" WC            ;  Range to UBOUND 
               /  WC "-" IPv4address            ;  LBOUND to Range 
               /  WC                            ;  Pure Wildcard 
    
PortEntry   =  N16BITNUM                        ;  Single Entry 
               /  N16BITNUM "-" N16BITNUM          ;  Range 
               /  N16BITNUM "-" WC                 ;  Range to UBOUND 
               /  WC "-" N16BITNUM                 ;  LBOUND to Range 
               /  WC                            ;  Pure Wildcard 
    
ProtEntry   =  N8BITNUM                      ;  Single Entry 
               /  N8BITNUM "-" N8BITNUM         ;  Range 
               /  N8BITNUM "-" WC               ;  Range to UBOUND 
               /  WC "-" N8BITNUM               ;  LBOUND to Range 
               /  WC                            ;  Pure Wildcard 
    
ICMPEntry   =  N8BITNUM                      ;  Single Entry 
               /  N8BITNUM "-" N8BITNUM         ;  Range 
               /  N8BITNUM "-" WC               ;  Range to UBOUND 
               /  WC "-" N8BITNUM               ;  LBOUND to Range 
               /  WC                            ;  Pure Wildcard 
    
 
 
 
 
 
CritEntry   =  N64BITNUM                        ;  Single Entry 
               /  N64BITNUM "-" N64BITNUM          ;  Range 
               /  N64BITNUM "-" WC                 ;  Range to UBOUND 
               /  WC "-" N64BITNUM                 ;  LBOUND to Range 
               /  WC                            ;  Pure Wildcard 
    
    
