smb_URL       = ( SMB_absURL | SMB_relURL )
smb_absURL    = "smb:" smb_srv_type
smb_relURL    = ( smb_srv_type | smb_abs_path
                                     | smb_rel_path ) [ "?" query ]

smb_srv_type  = "//" [ ( workgroup | smb_net_path ) ]

workgroup     = nbtname
smb_net_path  = smb_server [ smb_abs_path [ "#" fragment ] ]
smb_abs_path  = abs_path [ "#" fragment ]
smb_rel_path  = rel_path [ "#" fragment ]

smb_server    = [ [ smb_userinfo "@" ] smb_srv_name [ ":" port ] ]

smb_srv_name  = nbtname | hostname | IPv4address
nbtname       = netbiosname scope
netbiosname   = 1*( netbiosnamec ) *( netbiosnamec | "*" )
netbiosnamec  = ( alphanum | escaped  | ":" | "=" | "+" | "$" |
                        "," | "-" | "_" | "!" | "~" | "'" | "(" | ")" )
scope         = *( "." domainlabel ) [ "." ]

smb_userinfo  = [ ntdomain ";" ] username [ ":" password ]
ntdomain      = *( unreserved | escaped |
                         "&" | "=" | "+" | "$" | "," )
username      = *( unreserved | escaped |
                         "&" | "=" | "+" | "$" | "," )
password      = *( unreserved | escaped |
                         "&" | "=" | "+" | "$" | "," )

