presence-id     = word-pres ":" local-part "@" domain
im-id           = word-im ":" local-part "@" domain
local-part      = dotted-string / quoted-string
ip4-address     = "::" 1*1decimal-byte 3*3("." 1*1decimal-byte)
ip6-address     = 1*4hex-char 7*7(":" 1*4hex-char)
hex-char        = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
domain          = 1*domain-label *("." 1*domain-label)
domain-label    = (ALPHA / DIGIT)
                          [*(ALPHA / DIGIT / "-") (ALPHA / DIGIT)]
dotted-string   = 1*atext *("." 1*atext)
quoted-string   = DQUOTE *qcontent DQUOTE
qcontent        = qtext / quoted-pair
quoted-pair     = "\" (%x1-9 / %xB-C / %xE-7E)
qtext           = %x1-8 / %xB-C / %xD-1F / %x20-21 / %x23-5B
                        / %x5D-7E
atext           = "!" / "#" / "$" / "%" / "&" / "'" / "*"
                        / "+" / "-" / "/" / "=" / "?" /"^" / "_"
                        / "`" / "{" / "|" / "}" / "~" / ALPHA / DIGIT
                        / %x1-8 / %xB-C / %xD-1F / %x7F
scheme          = presence-scheme / im-scheme
word-pres       = %x70.72.65.73
word-im         = %x69.6D
decimal-byte    = 1*3DIGIT

PRIM-command = request / response

generic-command = start-line
                             *command-header
                             CRLF
                             [ command-body ]

start-line = request-line / response-line

request-line = method
                            SP version
                            SP request-identifier
                            SP content-length
                            CRLF

method = "LOGIN"               ; Section 11.1
                          / "STARTTLS"            ; Section 11.2
                          / "LOGOUT"              ; Section 11.3
                          / "PING"                ; Section 11.4
                          / "VERIFYSERVER"        ; Section 11.5
                          / "SETACL"              ; Section 11.6.1
                          / "GETACL"              ; Section 11.6.2
                          / "SUBSCRIBE"           ; Section 12.1.1
                          / "UNSUBSCRIBE"         ; Section 12.1.2
                          / "CANCELSUBSCRIPTION"  ; Section 12.1.3
                          / "FETCH"               ; Section 12.1.4
                          / "PUBLISH"             ; Section 12.2.1
                          / "REMOVE"              ; Section 12.2.2
                          / "NOTIFY"              ; Section 12.3
                          / "SETCLASSTABLE"       ; Section 12.4.2
                          / "GETCLASSTABLE"       ; Section 12.4.3
                          / "STARTWATCHERNOTIFY"  ; Section 12.4.4
                          / "STOPWATCHERNOTIFY"   ; Section 12.4.5
                          / "WATCHERNOTIFY"       ; Section 12.4.6
                          / "LISTEN"              ; Section 13.1.1
                          / "SILENCE"             ; Section 13.1.2
                          / "SEND"                ; Section 13.2

version = "PP/1.0" / "IMP/1.0"

request-identifier = 1*[ALPHA / DIGIT] / "-"

content-length = 1*DIGIT

response-line = version
                            SP request-identifier
                            SP content-length
                            SP status-code
                            SP response-phrase
                            CRLF

command-header = (from-header
                              / to-header
                              / user-agent-id-header
                              / class-header
                              / tuple-id-header
                              / duration-header
                              / auth-state-header
                              / SASL-mechanism-header
                              / redirect-header
                              / pi-type-header
                              / content-type-header
                              / server-address-header
                              / astrength-header
                              / message-id-header
                              / conversation-id-header
                              / reply-to-header
                              / watcher-type-header
                              / max-content-length-header
                              )
                            CRLF

from-header = "From: " ( presence-id / im-id )

to-header = "To: " ( presence-id / im-id )

user-agent-id-header = "User-Agent-ID: " 1*atext

class-header = "Class: " class [ SP class ]
class = 1*atext

tuple-id-header = "Tuple-ID: " 1*atext

duration-header = "Duration: " 1*DIGIT

auth-state-header = "Auth-State: "
                               ( "init"
                               / "continue"
                               / "abort" )

SASL-mechanism-header = "SASL-Mech: " mechanisms
mechanisms = mechanism [ *(SP mechanism) ]
mechanism = "CRAM-MD5" / "PLAIN" / "EXTERNAL"

redirect-header = "Redirect: " address SP port
address = domain / ip4-address / ip6-address
port = 1*DIGIT

pi-type-header = "PI-Type: "
                              ( "leased"
                              / "permanent"
                              / "renew"
                              / "revert")

charset=UTF-8" for Instant Messaging commands, and "application/octet-
server-address-header = "Server-Address: "
                             ( ip4-address / ip6-address )

astrength-header = "AStrength: " astrength
astrength = "strong" / "medium" / "weak" / "none"

message-id-header = "Message-ID" 1*(DIGIT / ALPHA) ": " im-id

conversation-id-header = "Conversation-ID: " 1*atext

reply-to-header = "Reply-To: " im-id

watcher-type-header = "Watcher-Type: " ("fetch" / "subscribe")

max-content-length-header = "Max-Content-Length: " 1*DIGIT

date-time = [day ","] date time
day = "Mon" / "Tue" / "Wed" / "Thu" /  "Fri"
                / "Sat" / "Sun"
date = 1*2DIGIT month 4DIGIT
month = "Jan" / "Feb" / "Mar" / "Apr" / "May"
                / "Jun" / "Jul" / "Aug" / "Sep" / "Oct"
                / "Nov" / "Dec"
time = hour zone
hour = 2DIGIT ":" 2DIGIT [":" 2DIGIT]
zone = ("+" / "-") 4DIGIT ; hours+min (HHMM)

date="Thu, 5 Sep 2000 15:45:45 -0500">
date="Thu, 5 Sep 2000 16:30:28 -0500">
