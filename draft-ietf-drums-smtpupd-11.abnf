ehlo            = "EHLO" SP Domain CRLF
helo            = "HELO" SP Domain CRLF

ehlo-ok-rsp  =    ( "250"    domain [ SP ehlo-greet ] CRLF )
                / (    "250-"   domain [ SP ehlo-greet ] CRLF
                    *( "250-"   ehlo-line                CRLF )
                       "250"    SP ehlo-line             CRLF  )

ehlo-greet   = 1*(%d0-9 / %d11-12 / %d14-127)
                ; string of any characters other than CR or LF

ehlo-line    = ehlo-keyword *( SP ehlo-param )

ehlo-keyword = (ALPHA / DIGIT) *(ALPHA / DIGIT / "-")
                ; additional syntax of ehlo-params depends on
				; ehlo-keyword

ehlo-param   = 1*(%d33-127)
                ; any CHAR excluding <SP> and all
                ; control characters (US-ASCII 0-31 inclusive)

Reverse-path = Path
Forward-path = Path
Path = "<" [ A-d-l ":" ] Mailbox ">"
A-d-l = At-domain *( "," A-d-l )
         ; Note that this form, the so-called "source route",
         ; MUST BE accepted, SHOULD NOT be generated, and SHOULD be
At-domain = "@" domain
Mail-parameters = esmtp-param *(SP esmtp-param)
Rcpt-parameters = esmtp-param *(SP esmtp-param)
esmtp-param     = esmtp-keyword ["=" esmtp-value]
esmtp-keyword   = (ALPHA / DIGIT) *(ALPHA / DIGIT / "-")
esmtp-value     = 1*(%d33-60 / %d62-127)
                     ; any CHAR excluding "=", SP, and control
					 ; characters
Keyword  = Ldh-str
Argument = Atom
Domain = (sub-domain 1*("." sub-domain)) / address-literal
sub-domain = Let-dig [Ldh-str]

address-literal = "[" IPv4-address-literal /
                         IPv6-address-literal /
                         General-address-literal "]"
                 ; See section 4.1.3

Mailbox = Local-part "@" Domain

Local-part = Dot-string / Quoted-string
              ; MAY be case-sensitive

Dot-string = Atom [ "." Atom ]

Atom = 1*atext

Quoted-string = DQUOTE *qcontent DQUOTE

String = Atom / Quoted-string


IPv4-address-literal = Snum 3("." Snum)
IPv6-address-literal = "IPv6:" IPv6-addr
General-address-literal = Standardized-tag ":" 1*dcontent
Standardized-tag = Ldh-str
                    ; MUST be specified in a standards-track RFC 
                    ; and registered with IANA

Snum = 1*3DIGIT  ; representing a decimal integer
                    ; value in the range 0 through 255
Let-dig = ALPHA / DIGIT
Ldh-str = *( ALPHA / DIGIT / "-" ) Let-dig

IPv6-addr = IPv6-full / IPv6-comp / IPv6v4-full / IPv6v4-comp
IPv6-hex  = 1*4HEXDIG
IPv6-full = IPv6-hex 7(":" IPv6-hex)
IPv6-comp = [IPv6-hex *5(":" IPv6-hex)] "::" [IPv6-hex *5(":"
              IPv6-hex)]
            ; The "::" represents at least 2 16-bit groups of zeros
            ; No more than 6 groups in addition to the "::" may be
IPv6v4-full = IPv6-hex 5(":" IPv6-hex) ":" IPv4-address-literal
IPv6v4-comp = [IPv6-hex *3(":" IPv6-hex)] "::"
                [IPv6-hex *3(":" IPv6-hex) ":"] IPv4-address-literal
            ; The "::" represents at least 2 16-bit groups of zeros
            ; No more than 4 groups in addition to the "::" and
            ; IPv4-address-literal may be present


Greeting = "220 " Domain [ SP text ] CRLF
Reply-line = Reply-code [ SP text ] CRLF

Return-path-line = "Return-Path:" FWS Reverse-path <CRLF>

Time-stamp-line = "Received:" FWS Stamp <CRLF>

Stamp = From-domain By-domain Opt-info ";"  FWS Daytime

From-domain = "FROM" FWS Extended-Domain CFWS

By-domain = "BY" FWS Extended-Domain CFWS

Extended-Domain = Domain /
              ( Domain FWS "(" TCP-info ")" ) /
              ( Address-literal FWS "(" TCP-info ")"
TCP-info = Address-literal / ( Domain FWS Address-literal )
                   ; Information derived by server from TCP connection
 				     not client EHLO.

Opt-info = [Via] [With] [ID] [For]

Via = "VIA" FWS Link CFWS

With = "WITH" FWS Protocol CFWS

ID = "ID" FWS String / msg-id CFWS

For = "FOR" FWS 1*( Path / Mailbox ) CFWS

Link = "TCP" / Addtl-Link
Addtl-Link = Atom       ; Additional standard names for links are
                             registered with the Internet Assigned
                             Numbers Authority (IANA).  "Via" is
							 primarily of value with non-Internet
							 transports.
                             SMTP servers SHOULD NOT use unregistered
                             names.
Protocol = "ESMTP" / "SMTP" / Attdl-Protocol
Attdl-Protocol = Atom    ; Additional standard names for protocols
                              are registered with the Internet Assigned
                              Numbers Authority (IANA).  SMTP servers
                              SHOULD NOT use unregistered names.

Daytime =  FWS [ day-of-week "," FWS ] Date FWS Time

Date = DD FWS Mon FWS YYYY
          ; Note that the earlier form, which permits two-digit years,
		  ; has been deprecated.  SMTP systems MUST use four-digit
		  ; years.

Time = HH ":" MM ":" SS  FWS  Zone

DD = 1*2Digit           ; the one or two digit integer day of the
                             month in the range 1 to 31.

Mon = "JAN" | "FEB" | "MAR" | "APR" | "MAY" | "JUN" |
                "JUL" | "AUG" | "SEP" | "OCT" | "NOV" | "DEC"

YYYY = 4*4Digit         ; the four decimal integer year in the range
                             0000 to 9999.

HH = 2*2Digit           ; the two decimal digit hour of the day in
                             the range 00 to 24.

MM = 2*2Digit            ; the two decimal digit integer minute of
                              the hour in the range 00 to 59.

SS = 2*2Digit [ "." 1*Digit ]
                            ; the two decimal digit integer second of
                              the minute in the range 00 to 60 (to allow
							  for leap seconds), with optional
							  fractional seconds.

Zone = ( "+" / "-" ) 4*4Digit [ <SP> "(" String ")" ]
                            ; A four digit, signed time zone offset,
                              such as -0500 for US Eastern Standard
                              Time.  This may be supplemented by a time
                              zone name in parentheses, e.g., "-0800
                              (PDT)". Note that there is no default;
                              time zone information is required and
                              MUST be supplied.

