DIGIT         = <DIGIT, defined in [Part1], Section 2.2>







comment       = <comment, defined in [Part1], Section 2.2>
quoted-string = <quoted-string, defined in [Part1], Section 2.2>
token         = <token, defined in [Part1], Section 2.2>

absoluteURI   = <absoluteURI, defined in [Part1], Section 3.2.1>
fragment      = <fragment, defined in [Part1], Section 3.2.1>
Host          = <Host, defined in [Part1], Section 8.4>
HTTP-date     = <HTTP-date, defined in [Part1], Section 3.3.1>
product       = <product, defined in [Part1], Section 3.5>
relativeURI   = <relativeURI, defined in [Part1], Section 3.2.1>
TE            = <TE, defined in [Part1], Section 8.8>


Accept        = <Accept, defined in [Part3], Section 6.1>
Accept-Charset =
                <Accept-Charset, defined in [Part3], Section 6.2>
Accept-Encoding =
                <Accept-Encoding, defined in [Part3], Section 6.3>
Accept-Language =
                <Accept-Language, defined in [Part3], Section 6.4>


ETag          = <ETag, defined in [Part4], Section 7.1>
If-Match      = <If-Match, defined in [Part4], Section 7.2>
If-Modified-Since =
                <If-Modified-Since, defined in [Part4], Section 7.3>
If-None-Match = <If-None-Match, defined in [Part4], Section 7.4>
If-Unmodified-Since =
                <If-Unmodified-Since, defined in [Part4], Section 7.5>


Accept-Ranges = <Accept-Ranges, defined in [Part5], Section 6.1>
If-Range      = <If-Range, defined in [Part5], Section 6.3>
Range         = <Range, defined in [Part5], Section 6.4>


Age           = <Age, defined in [Part6], Section 16.1>
Vary          = <Vary, defined in [Part6], Section 16.5>


Authorization = <Authorization, defined in [Part7], Section 4.1>
Proxy-Authenticate =
                <Proxy-Authenticate, defined in [Part7], Section 4.2>
Proxy-Authorization =
                <Proxy-Authorization, defined in [Part7], Section 4.3>
WWW-Authenticate =





                <WWW-Authenticate, defined in [Part7], Section 4.4>


Method         = %x4F.50.54.49.4F.4E.53   ; "OPTIONS", Section 8.2
                    | %x47.45.54               ; "GET", Section 8.3
                    | %x48.45.41.44            ; "HEAD", Section 8.4
                    | %x50.4F.53.54            ; "POST", Section 8.5
                    | %x50.55.54               ; "PUT", Section 8.6
                    | %x44.45.4C.45.54.45      ; "DELETE", Section 8.7
                    | %x54.52.41.43.45         ; "TRACE", Section 8.8
                    | %x43.4F.4E.4E.45.43.54   ; "CONNECT", Section 8.9
                    | extension-method
extension-method = token

request-header = Accept                   ; [Part3], Section 6.1
                    | Accept-Charset           ; [Part3], Section 6.2
                    | Accept-Encoding          ; [Part3], Section 6.3
                    | Accept-Language          ; [Part3], Section 6.4
                    | Authorization            ; [Part7], Section 4.1
                    | Expect                   ; Section 10.2
                    | From                     ; Section 10.3
                    | Host                     ; [Part1], Section 8.4
                    | If-Match                 ; [Part4], Section 7.2
                    | If-Modified-Since        ; [Part4], Section 7.3
                    | If-None-Match            ; [Part4], Section 7.4
                    | If-Range                 ; [Part5], Section 6.3
                    | If-Unmodified-Since      ; [Part4], Section 7.5
                    | Max-Forwards             ; Section 10.5
                    | Proxy-Authorization      ; [Part7], Section 4.3
                    | Range                    ; [Part5], Section 6.4
                    | Referer                  ; Section 10.6
                    | TE                       ; [Part1], Section 8.8
                    | User-Agent               ; Section 10.9

Status-Code    =
            "100"  ; Section 9.1.1: Continue
          | "101"  ; Section 9.1.2: Switching Protocols
          | "200"  ; Section 9.2.1: OK
          | "201"  ; Section 9.2.2: Created
          | "202"  ; Section 9.2.3: Accepted
          | "203"  ; Section 9.2.4: Non-Authoritative Information
          | "204"  ; Section 9.2.5: No Content
          | "205"  ; Section 9.2.6: Reset Content
          | "206"  ; Section 9.2.7: Partial Content
          | "300"  ; Section 9.3.1: Multiple Choices
          | "301"  ; Section 9.3.2: Moved Permanently
          | "302"  ; Section 9.3.3: Found
          | "303"  ; Section 9.3.4: See Other
          | "304"  ; Section 9.3.5: Not Modified
          | "305"  ; Section 9.3.6: Use Proxy
          | "307"  ; Section 9.3.8: Temporary Redirect
          | "400"  ; Section 9.4.1: Bad Request
          | "401"  ; Section 9.4.2: Unauthorized
          | "402"  ; Section 9.4.3: Payment Required
          | "403"  ; Section 9.4.4: Forbidden
          | "404"  ; Section 9.4.5: Not Found
          | "405"  ; Section 9.4.6: Method Not Allowed
          | "406"  ; Section 9.4.7: Not Acceptable
          | "407"  ; Section 9.4.8: Proxy Authentication Required
          | "408"  ; Section 9.4.9: Request Time-out
          | "409"  ; Section 9.4.10: Conflict
          | "410"  ; Section 9.4.11: Gone
          | "411"  ; Section 9.4.12: Length Required
          | "412"  ; Section 9.4.13: Precondition Failed
          | "413"  ; Section 9.4.14: Request Entity Too Large
          | "414"  ; Section 9.4.15: Request-URI Too Large
          | "415"  ; Section 9.4.16: Unsupported Media Type
          | "416"  ; Section 9.4.17: Requested range not satisfiable
          | "417"  ; Section 9.4.18: Expectation Failed
          | "500"  ; Section 9.5.1: Internal Server Error
          | "501"  ; Section 9.5.2: Not Implemented
          | "502"  ; Section 9.5.3: Bad Gateway
          | "503"  ; Section 9.5.4: Service Unavailable
          | "504"  ; Section 9.5.5: Gateway Time-out
          | "505"  ; Section 9.5.6: HTTP Version not supported
          | extension-code

extension-code = 3DIGIT
Reason-Phrase  = *<TEXT, excluding CR, LF>

response-header = Accept-Ranges           ; [Part5], Section 6.1
                     | Age                     ; [Part6], Section 16.1
                     | Allow                   ; Section 10.1
                     | ETag                    ; [Part4], Section 7.1
                     | Location                ; Section 10.4
                     | Proxy-Authenticate      ; [Part7], Section 4.2
                     | Retry-After             ; Section 10.7
                     | Server                  ; Section 10.8
                     | Vary                    ; [Part6], Section 16.5
                     | WWW-Authenticate        ; [Part7], Section 4.4

Allow   = "Allow" ":" #Method

Expect       =  "Expect" ":" 1#expectation

expectation  =  "100-continue" | expectation-extension
expectation-extension =  token [ "=" ( token | quoted-string )
                              *expect-params ]
expect-params =  ";" token [ "=" ( token | quoted-string ) ]

From    = "From" ":" mailbox

mailbox = <mailbox, defined in [RFC2822], Section 3.4>

Location       = "Location" ":" absoluteURI [ "#" fragment ]

Max-Forwards   = "Max-Forwards" ":" 1*DIGIT

Referer        = "Referer" ":" ( absoluteURI | relativeURI )

Retry-After   = "Retry-After" ":" ( HTTP-date | delta-seconds )

delta-seconds  = 1*DIGIT

Server         = "Server" ":" 1*( product | comment )

User-Agent     = "User-Agent" ":" 1*( product | comment )

