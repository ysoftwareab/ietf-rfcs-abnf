codecs=a.bb.ccc.d
codecs="a.bb.ccc.d, e.fff"
codecs      = cod-simple / cod-fancy

cod-simple  = "codecs" "=" unencodedv

unencodedv  = id-simple / simp-list

simp-list   = DQUOTE id-simple *( "," id-simple ) DQUOTE

id-simple   = element *( "." element )

element     = 1*octet-sim

octet-sim   = <any TOKEN character except ".">
                ; token defined in [MIME-Format]

cod-fancy   = "codecs*" "=" encodedv

encodedv    = fancy-sing / fancy-list

fancy-sing  = "''" id-encoded

fancy-list  = DQUOTE "''" id-list DQUOTE

id-list     = id-encoded *( "," id-encoded )

id-encoded  = encoded-elm *( "." encoded-elm )

encoded-elm = 1*octet-fancy
    
octet-fancy = ext-octet / attribute-char
                ; ext-octet and attribute-char defined in [MIME-coding]

DQUOTE      = %x22 ; " (double quote)
    
id-simple   =/ id-iso

id-encoded  =/ id-iso

id-iso      = iso-gen / iso-mpega / iso-mpegv
    
iso-gen     = cpid *( "." 1(element / encoded-elm ) )
                ; <element> used with <codecs-simple>
                ; <encoded> used with <codecs-fancy>
    
iso-mpega   = mp4a "." oti [ "." aud-oti ]
    


iso-mpegv   = mp4v "." oti [ "." vid-pli ]
     
cpid        = 4(octet-simple / octet-fancy)
                ; <octet-simple> used with <codecs-simple>
                ; <octet-fancy> used with <codecs-fancy>
    
mp4a        = %x6d.70.34.61 ; 'mp4a'

oti         = 2(DIGIT / "A" / "B" / "C" / "D" / "E" / "F")
                ; leading "0x" omitted
    
aud-oti     = 1*DIGIT
    
mp4v        = %x6d.70.34.76 ; 'mp4v'
    
vid-pli     = 1*DIGIT
    
    
