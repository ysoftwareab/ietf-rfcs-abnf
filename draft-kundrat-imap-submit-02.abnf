capability          =/ "SUBMIT" / "SUBMIT=DSN"
                         ;; This extension also reserves all further
                         ;; capabilities starting with the "SUBMIT="
                         ;; prefix for future extensions related to the
                         ;; message submission over IMAP
   
uid                 =/ "UID" SP sendmail
   
sendmail            = "SUBMIT" SP uniqueid [SP delivery-options]
   
delivery-options    = "(" delivery-option *( SP delivery-option ) ")"
   
delivery-option     = submission-from / submission-recipient
   
submission-from     = "FROM" SP one-email-addr [SP mail-from-params]
                         ;; MUST NOT be present more than once
                         ;; in the delivery-options block
   
submission-recipient= "RECIPIENT" SP one-email-addr [SP mail-rcpt-params]
                         ;; MAY be present more than once
   
mail-from-params    = "(" mail-from-param *( SP mail-from-param ) ")"
   
mail-from-param     = sub-option-dsn-envid / sub-generic-option
   
mail-rcpt-params    = "(" mail-rcpt-param *( SP mail-rcpt-param ) ")"
   
mail-rcpt-param     = sub-option-rcpt-dsn / sub-option-dsn-ret
                         / sub-generic-option
   
sub-generic-option  = string SP string
                         ;; FIXME: do we want to retain this in without any semantics?
   
sub-option-rcpt-dsn = "DSN" SP ( "NONE" / dsn-spec )
                         ;; MUST NOT be present more than once for each recipient
   
dsn-spec            = "(" dsn-spec-item *( SP dsn-spec-item ) ")"
                         ;; an individual dsn-spec-item MUST NOT
                         ;; be present more than once in this list
   
dsn-spec-item       = "DELAY" / "FAILURE" / "SUCCESS"
   
sub-option-dsn-ret  = "DSN-RET" SP ( "FULL" / "HDRS" )
                         ;; MUST NOT be present more than once for each recipient
   
sub-option-dsn-envid= "DSN-ENVID" SP xtext
                         ;; MUST NOT be present more than once
                         ;; <xtext> is defined in [RFC3461], section 4.
                         ;; The allowed syntax is further limited by
                         ;; its section 4.4.
   
one-email-addr      = string
                         ;; valid e-mail address as per [RFC5321]



