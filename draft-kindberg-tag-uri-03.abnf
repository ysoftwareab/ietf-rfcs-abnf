tagURI        = "tag:" tagAuthority ":" [specific]
tagAuthority  = authorityName "," date
authorityName = DNSname / emailAddress
date          = 4*dig ["-" 2*dig ["-" 2*dig ]] ; [RFC3339]
DNSname       = DNScomp / DNSname "." DNScomp  ; [RFC1035]
DNScomp       = lowAlphaNum [*(lowAlphaNum /"-") lowAlphaNum]
emailAddress  = 1*(lowAlphaNum /"-"/"."/"_") "@" DNSname
lowAlphaNum   = dig / lowAlpha
specific      = 1*(URIchars)  ; URIchars defined in [RFC2396]
lowAlpha      = %x61-7A ; any char in the range "a" through "z"
dig           = %x30-39 ; any char in the range "0" through "9"


