capability         =/ "CONDSTORE" / "PERFLAGMODTIME"

resp_text_code     =/ "HIGHESTMODTIME" SP modtime-value

mailbox-data       =/ "STATUS" SP mailbox SP "("
                         [status-att SP status-value *(SP status-att SP status-value)] ")"

status-value       = number / modtime-value

store              = "STORE" SP set store-modifiers SP store-att-flags
   
store-modifiers    = [ SP "(" 1*store-modifier ")" ]
    
store-modifier     = "UNCHANGEDSINCE" SP modtime-value

fetch-att          =/ fetch-modtime
                        ;; modifies original IMAP4 fetch-att

fetch-modtime      = "MODTIME" SP entry-names

fetch-mod-resp     = "MODTIME" SP "(" permessage-modtime *(entry-name SP modtime-value) ")"

search-key         =/ search-modtime
                        ;; modifies original IMAP4 search-key

search-modtime     = "MODTIME" SP entry-name SP modtime-value

resp-text-code     =/ "MODIFIED" SP set
                        ;; set of message numbers for STORE or
                        ;; set of UIDs for UID STORE

entry-names        = "(" *entry-name ")"
                        ;; empty list means that only per-message MODTIME should be returned 

entry-name         = '"' "/message/flags/system/" attr-flag '"'
                        ;; each system or user defined flag <flag> is mapped to
                        ;; "/message/flags/system/<flag>".
                        ;; system IMAP flags must have two leading "\",
                        ;; because "\" is an escape character.

permessage-modtime = modtime-value
                        ;; per message modtime, if server supports per flag modtimes,
                        ;; this is the highest modtime between all metadata items

modtime-value      = time

mailbox-data       =/ "MODTIME" SP set SP modtime-value


attr-flag          = "\\Answered" / "\\Flagged" / "\\Deleted" /
                        "\\Seen" / "\\Draft" / attr-flag-keyword / attr-flag-extension
                        ;; Does not include "\Recent"

attr-flag-extension = "\\" atom
                        ;; Future expansion.  Client implementations
                        ;; MUST accept flag-extension flags.  Server
                        ;; implementations MUST NOT generate
                        ;; flag-extension flags except as defined by
                        ;; future standard or standards-track
                        ;; revisions of this specification.

attr-flag-keyword   = atom


time               = <"> time-year time-month time-day time-hour
                        time-minute time-second time-subsecond <">
                        ;; Timestamp in UTC

time-day           = 2DIGIT ;; 01-31

time-hour          = 2DIGIT ;; 00-23

time-minute        = 2DIGIT ;; 00-59

time-month         = 2DIGIT ;; 01-12

time-second        = 2DIGIT ;; 00-60

time-subsecond     = *DIGIT

time-year          = 4DIGIT


