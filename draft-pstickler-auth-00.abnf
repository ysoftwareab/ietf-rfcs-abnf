voc-URI        = "auth://" root-authority [ "/" sub-auth-path ] [ dates ]

root-authority = ( [ user "@" ] host ) | UUID
sub-auth-path  = sub-authority *( "/" sub-authority )
sub-authority  = 1*uchar
dates          = ";" start-date [ ":" end-date ]
start-date     = date
end-date       = date
date           = year [ "-" month [ "-" day ] ]
year           = 4digit
month          = 2digit
day            = 2digit

UUID           = 8hex "-" 4hex "-" 4hex "-" 4hex "-" 12hex

host           = hostname | hostnumber
hostname       = *[ domainlabel "." ] toplabel
domainlabel    = alphadigit | alphadigit *[ alphadigit | "-" ] alphadigit
toplabel       = alpha | alpha *[ alphadigit | "-" ] alphadigit
hostnumber     = digits "." digits "." digits "." digits
user           = *[ uchar | ";" | "?" | "&" | "=" ]

lowalpha       = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" |
                    "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" |
                    "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" |
                    "y" | "z"
hialpha        = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" |
                    "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" |
                    "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
digit          = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" |
                    "8" | "9"
alpha          = lowalpha | hialpha
digits         = 1*digit
alphadigit     = alpha | digit

safe           = "$" | "-" | "_" | "." | "+"
extra          = "!" | "*" | "'" | "(" | ")" | ","
hex            = digit | "A" | "B" | "C" | "D" | "E" | "F" |
                    "a" | "b" | "c" | "d" | "e" | "f"
escape         = "%" hex hex
unreserved     = alpha | digit | safe | extra
uchar          = unreserved | escape



