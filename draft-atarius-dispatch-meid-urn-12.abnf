pp2-urn              = "urn:" pp2-NID ":" pp2-NSS
pp2-NID              = "3gpp2"
pp2-NSS              = meid-specifier / future-pp2-specifier
meid-specifier       = "meid:" meidval
future-pp2-specifier = future-specifier *( ";" future-param )
future-specifier     = pp2-defined-nonempty   ;3GPP2 defined
future-param         = par-name [ EQUAL par-value ]
par-name             = pp2-defined-nonempty
par-value            = pp2-defined-nonempty
EQUAL                = "="
pp2-defined-nonempty = 1*pp2-urn-char
pp2-urn-char         = ALPHA / DIGIT / "-" / "." / "_" / "%" / ":"
ALPHA                = %x41-5A / %x61-7A; A-Z / a-z
DIGIT                = %x30-39; 0-9

meidval           = Manufacturer-Code "-" Serial-Number
Manufacturer-Code = 8HEX
Serial-Number     = 6HEX
HEX               = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

