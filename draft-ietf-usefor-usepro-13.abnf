CRLF                = <see [RFC5234] appendix B.1>
DIGIT               = <see [RFC5234] appendix B.1>
HTAB                = <see [RFC5234] appendix B.1>
SP                  = <see [RFC5234] appendix B.1>
WSP                 = <see [RFC5234] appendix B.1>

argument            = <see [USEFOR] section 3.2.3>
article-locator     = <see [USEFOR] section 3.2.14>
component           = <see [USEFOR] section 3.1.4>
control-command     = <see [USEFOR] section 3.2.3>
diag-keyword        = <see [USEFOR] section 3.1.5>
diag-match          = <see [USEFOR] section 3.1.5>
diag-other          = <see [USEFOR] section 3.1.5>
dist-name           = <see [USEFOR] section 3.2.4>
msg-id              = <see [USEFOR] section 3.1.3>
newsgroup-name      = <see [USEFOR] section 3.1.4>
path-diagnostic     = <see [USEFOR] section 3.1.5>
path-identity       = <see [USEFOR] section 3.1.5>
path-nodot          = <see [USEFOR] section 3.1.5>
tail-entry          = <see [USEFOR] section 3.1.5>
verb                = <see [USEFOR] section 3.2.3>

display-name        = <see [RFC2822] section 3.4>
local-part          = <see [RFC2822] section 3.4.1>
mailbox             = <see [RFC2822] section 3.4>
utext               = <see [RFC2822] section 3.2.6>

header              =/ Original-Sender-header
usage=moderate indicates the article is intended for a moderator,
usage=inject for an injecting agent, and usage=relay for a relaying
groupinfo-body      = [ newsgroups-tag CRLF ]
                                  newsgroups-line CRLF
newsgroups-tag      = %x46.6F.72 SP %x79.6F.75.72 SP
                                  %x6E.65.77.73.67.72.6F.75.70.73 SP
                                  %x66.69.6C.65.3A
                                  ; case sensitive
                                  ; "For your newsgroups file:"
newsgroups-line     = newsgroup-name
                                  [ 1*HTAB newsgroup-description ]
                                  [ *WSP moderation-flag ]
moderation-flag     = SP "(" %x4D.6F.64.65.72.61.74.65.64 ")"
                                  ; SPACE + case sensitive "(Moderated)"
eightbit-utext      = utext / %d127-255

checkgroups-body    = *( valid-group CRLF )
valid-group         = newsgroups-line ; see 4.2

control-command     =/ Newgroup-command
Newgroup-command    = "newgroup" Newgroup-arguments
Newgroup-arguments  = 1*WSP newsgroup-name
                                  [ 1*WSP newgroup-flag ]
newgroup-flag       = "moderated"

control-command     =/ Rmgroup-command
Rmgroup-command     = "rmgroup" Rmgroup-arguments
Rmgroup-arguments   = 1*WSP newsgroup-name

control-command     =/ Checkgroup-command
Checkgroup-command  = "checkgroups" Checkgroup-arguments
Checkgroup-arguments= [ chkscope ] [ chksernr ]
chkscope            = 1*( 1*WSP ["!"] newsgroup-name )
chksernr            = 1*WSP "#" 1*DIGIT

control-command     =/ Cancel-command
Cancel-command      = "cancel" Cancel-arguments
Cancel-arguments    = 1*WSP msg-id

control-command     =/ Ihave-command
Ihave-command       = "ihave" Ihave-arguments
Ihave-arguments     = 1*WSP *( msg-id 1*WSP ) relayer-name
control-command     =/ Sendme-command
Sendme-command      = "sendme" Sendme-arguments
Sendme-arguments    = Ihave-arguments
relayer-name        = path-identity  ; see 3.1.5 of [USEFOR]
ihave-body          = *( msg-id CRLF )
sendme-body         = ihave-body

