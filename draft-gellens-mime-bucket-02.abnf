codecs      = "Codecs" *SP "=" *SP value

value       = id / DQUOTE *SP id *(*SP "," SP id) *SP DQUOTE

id          = id-gen

id-gen      = element *( "." element )

element     = 1*(idchar / idchar-spec)

idchar      = "!" / "#" / "$" / "&" / 
                  "'" / "*" / "+" / "-" / 
                  "^" / "_" / "`" / "/" /
                  "(" / ")" / "<" / ">" / 
                  "@" / ";" / ":" / "\" /
                  "{" / "}" / "~" / 
                  ALPHA / DIGIT


idchar-spec = "%" 2HEXDIG                  ; one character in hex

    
id          =/ id-iso

id-iso      = iso-gen / iso-mpega / iso-mpegv
    
iso-gen     = cpid *( "." element )
    
iso-mpega   = mp4a "." oti [ "." aud-oti ]
    
iso-mpegv   = mp4v "." oti [ "." vid-pli ]
     
cpid        = 4(idchar / idchar-spec)
    
mp4a        = %x6d.70.34.61 ;; 'mp4a'

oti         = "0x" 2HEXDIG
    
aud-oti     = 1*DIGIT
    
mp4v        = %x6d.70.34.76 ;; 'mp4v'
    
vid-pli     = 1*DIGIT
    
    
