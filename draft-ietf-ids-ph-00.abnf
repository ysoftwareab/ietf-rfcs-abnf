command     =   ph-command CRLF

ph-command  =   "status"
                       / a-command
                       / av-command
                       / query-command
                       / delete-command
                       / change-command
                       / "help"
                       / quit-command

a-command       =   ( siteinfo/types/fields/id/login/help ) [attribute]

av-command      =   ( set / add / make ) 1*attribute-value

query-command   =   ( query / ph ) 1*selection ["return" 1*attribute]

quit-command    =   "quit" / "exit" / "stop"

change-command  =   change 1*selection make 1*attribute-value 

delete-command  =   "delete" selection

selection       =  ( value / attribute-value ) 
                      *( <SPACE> ( value / attribute-value ) )

attribute-value =   attribute "=" value 

value           =   atom / quoted-string

atom            =   1*<any CHAR except specials, SPACE and CTLs> 

CTL             =   <any ASCII control character and DEL> ; ( 0-31 , 127 )
   
CHAR            =   < any ISO-8859-1 character >          ; ( 0-255 )

SPACE           =   < ASCII SP >                          ; ( 32 )

attribute       =   1*( ALPHA / DIGIT / "_" / "-" ) 

ALPHA           =  <any ASCII alphabetic character> ; (65-90,97-122 )

DIGIT           =  <any ASCII decimal digit)        ; (48-57 )

quoted-string   =  <"> *(qtext/quoted-pair) <">

quoted-pair     =  "\" CHAR

qtext           =  <any CHAR excepting <">,"\" & CR, and including
                       linear-white-space>

linear-white-space = 1*([CRLF] LWSP-char) 

LWSP-char       = SPACE / HTAB

CRLF            = CR LF
  
HTAB            = <ASCII HT, horizontal tab>       ; ( 9 )

CR              = <ASCII CR, carriage-return>      ; ( 13 )

LF              = <ASCII LF, linefeed>             ; ( 10 )

specials        =  "=" / "*" / "?" / "\" / <">
