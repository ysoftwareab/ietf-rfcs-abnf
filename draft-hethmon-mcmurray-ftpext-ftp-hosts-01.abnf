TCHAR = VCHAR / SP / HTAB    ; visible plus white space

error-response = error-code SP *TCHAR CRLF
error-code     = ("4" / "5") 2DIGIT

host-command  = "HOST" SP hostname CRLF
hostname      = domain / IP-literal

domain        = sub-domain *("." sub-domain)
sub-domain    = let-dig [ldh-str]
let-dig       = ALPHA / DIGIT
ldh-str       = *( ALPHA / DIGIT / "-" ) let-dig

IP-literal    = ( "[" IPv6address "]" ) / IPv4address

IPv6address   =                              6( h16 ":" ) ls32
                     /                       "::" 5( h16 ":" ) ls32
                     / [               h16 ] "::" 4( h16 ":" ) ls32
                     / [ *1( h16 ":" ) h16 ] "::" 3( h16 ":" ) ls32
                     / [ *2( h16 ":" ) h16 ] "::" 2( h16 ":" ) ls32
                     / [ *3( h16 ":" ) h16 ] "::"    h16 ":"   ls32
                     / [ *4( h16 ":" ) h16 ] "::"              ls32
                     / [ *5( h16 ":" ) h16 ] "::"              h16
                     / [ *6( h16 ":" ) h16 ] "::"

ls32          = ( h16 ":" h16 ) / IPv4address
                     ; least-significant 32 bits of address

h16           = 1*4HEXDIG
                     ; 16 bits of address represented in hexadecimal

IPv4address   = dec-octet "." dec-octet "." dec-octet "." dec-octet

dec-octet     = DIGIT                 ; 0-9
                     / %x31-39 DIGIT       ; 10-99
                     / "1" 2DIGIT          ; 100-199
                     / "2" %x30-34 DIGIT   ; 200-249
                     / "25" %x30-35        ; 250-255

host-response = host-ok / error-response
host-ok       = "220" [ SP *TCHAR ] CRLF

