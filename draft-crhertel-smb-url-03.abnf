smb_URL       = ( SMB_absURL | SMB_relURL ) [ "#" fragment ]
smb_absURL    = scheme "://"
                      [ smb_net_path ] [ "?" [ nbt_context ] ]
smb_relURL    = abs_path | rel_path
scheme        = "smb" | "cifs"

smb_net_path  = smb_server [ abs_path ]

smb_server    = [ smb_userinfo "@" ] smb_srv_name [ ":" port ]

smb_srv_name  = nbt_name | host
nbt_name      = netbiosname [ "." scope_id ]
netbiosname   = 1*( netbiosnamec ) *( netbiosnamec | "*" )
netbiosnamec  = ( alphanum | escaped  | ":" | "=" | "+" | "$" |
                        "," | "-" | "_" | "!" | "~" | "'" | "(" | ")" )
scope_id      = domainlabel *( "." domainlabel )

smb_userinfo  = [ ntdomain ";" ] username [ ":" password ]
ntdomain      = *( unreserved | escaped |
                         "&" | "=" | "+" | "$" | "," )
username      = *( unreserved | escaped |
                         "&" | "=" | "+" | "$" | "," )
password      = *( unreserved | escaped |
                         "&" | "=" | "+" | "$" | "," )

nbt_context   = nbt_param *(";" nbt_param )

nbt_param     = ( ( "NBNS=" | "WINS=" ) host )
                      | ( "CALLED=" netbiosname )
                      | ( "CALLING=" netbiosname )
                      | ( ( "WORKGROUP=" | "NTDOMAIN=" ) nbt_name )


