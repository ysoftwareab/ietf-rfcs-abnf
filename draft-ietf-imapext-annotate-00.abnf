append           = "APPEND" SP mailbox [SP flag-list] [SP date-time]
                      [SP "ANNOTATION" SP att-annotate]
                      SP literal
                       ; modifies original IMAP4 APPEND command

att-annotate     = "(" entry-att *(SP entry-att) ")"

fetch-att        =/ fetch-annotate
                       ; modifies original IMAP4 fetch-att
fetch-annotate   = "ANNOTATION" SP "(" entries SP attribs ")"
fetch-ann-resp   = "ANNOTATION" SP "(" entry-att *(SP entry-att) ")"

store-att-flags   =/ att-annotate
                       ; modifies original IMAP4 STORE command

search-key        =/ search-annotate
                       ; modifies original IMAP4 search-key

search-annotate   = "ANNOTATION" SP entry-match SP attrib-match
                       SP value

sort-key          =/ sort-annotate
                       ; modifies original
                       ; draft-crispin-imapext-sort-xx.txt sort-key

sort_annotate     = "ANNOTATION" SP entry SP attrib

status            =/ "ANNOTATION-MODTIME"
                       ; modifies original IMAP4 STATUS command

entries           = entry-match /
                       "(" entry-match *(SP entry-match) ")"
attribs           = attrib-match /
                       "(" attrib-match *(SP attrib-match) ")"
entry-att         = entry SP "(" att-value *(SP att-value) ")
att-value         = attrib SP value

utf8-char         =  %x01-FF
                       ; any character, excluding NUL
atom-slash        = any utf8-char except "/"
atom-dot          = any utf8-char except "."

entry             = DQUOTE 1*atom-slash *("/" 1*atom-slash) DQUOTE
entry-match       = DQUOTE 1*entry-match-atom
                       *("/" 1*entry-match-atom) DQUOTE
entry-match-atom  = 1*(list-wildcards / atom-slash)
                       *(list-wildcards / atom-slash)

attrib            = DQUOTE 1*atom-dot *("/" 1*atom-dot) DQUOTE
attrib-match      = DQUOTE 1*attrib-match-atom
                       *("/" 1*attrib-match-atom) DQUOTE
attrib-match-atom = 1*(list-wildcards / atom-dot)
                       *(list-wildcards / atom-dot)

value             = nstring
    
    
