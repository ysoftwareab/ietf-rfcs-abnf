IAC          = %d255    ; standard Telnet symbols
DO           = %d253
WILL         = %d251
SB           = %d250
SE           = %d240
SASL         = %dXXX    ; Telnet SASL option
LIST         = %d0      ; Telnet SASL sub-options
START        = %d1
STEP         = %d2
CANCEL       = %d3
DONE         = %d4

DIGIT        = %d30-39  ; US-ASCII digit character
UPALPHA      = %d65-90  ; Uppercase alphabetic characters
MECH-CHAR    = %d65-90 / DIGIT / "-" / "_"
SAFE-DATA    = %d0-254  ; octets which don't need quoting
TEXT         = %d1-254  ; human readable text
SP           = %d32     ; US-ASCII space
NUL          = %d0      ; US-ASCII NUL

quoted-255   = %d255 %d255
sasl-mech    = 1*20mech-char
subopt-data  = SAFE-DATA / quoted-255
text         = *subopt-data  ; human readable text
sasl-data    = *subopt-data
success      = %d0 sasl-data
error        = %d1-254 text

sasl-do      = IAC DO SASL
sasl-will    = IAC WILL SASL
sasl-list    = IAC SB SASL LIST *(sasl-mech SP) sasl-mech IAC SE
sasl-start   = IAC SB SASL START sasl-mech [NUL sasl-data] IAC SE
sasl-step    = IAC SB SASL STEP sasl-data IAC SE
sasl-cancel  = IAC SB SASL CANCEL IAC SE
sasl-done    = IAC SB SASL DONE (success / error) IAC SE

