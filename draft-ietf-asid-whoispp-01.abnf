SEARCH=   exact / lstring                         LOCAL/GLOBAL

FORMAT=   full / abridged / handle / summary      GLOBAL

MAXHITS=  1-<max-allowed>                         GLOBAL

SEARCH=       regex / fuzzy / substring                    LOCAL/GLOBAL

CASE=         ignore | consider                            LOCAL/GLOBAL

FORMAT=       server-to-ask                                GLOBAL

MAXFULL=      1-<max-allowed>                              GLOBAL

AUTHENTICATE= password                                     GLOBAL

NAME=         <string>                                     GLOBAL

PASSWORD=     <string>                                     GLOBAL

INCHARSET=    us-ascii / iso-8859-* /
                  UNICODE-1-1-UTF-8 / UNICODE-2-0-UTF-8      GLOBAL

OUTCHARSET=   us-ascii / iso-8859-* /
                  UNICODE-1-1-UTF-8 / UNICODE-2-0-UTF-8      GLOBAL

LANGUAGE=     <As defined in ISO 639:1988>                 GLOBAL

IGNORE=       <attributelist>                              GLOBAL

INCLUDE=      <attributelist>                              GLOBAL

                Table IV - Optional Whois++ constraints.

author=leslie and template=user

author=leslie and template=user:language=fr

value=phone;search=substring

whois-command   =   ( system-command [":" "hold"]
                       / terms [":" globalcnstrnts] ) NL

system-command  =   "constraints"
                       / "describe"
                       / "commands"
                       / "polled-by"
                       / "polled-for"
                       / "version"
                       / "list"
                       / "show" [1*SP string]
                       / "help" [1*SP string]
                       / "?" [string]

terms           =   and-expr *("or" and-expr)

and-expr        =   not-expr *("and" not-expr)

not-expr        =   ["not"] (term / ( "(" terms ")" ))

term            =   generalterm / specificterm
                       / shorthandle / combinedterm

generalterm     =   string *(";" localcnstrnt)

specificterm    =   specificname "=" string
                       *(";" localcnstrnt)

specificname    =   "handle" / "value"

shorthandle     =   "!" string *(";" localcnstrnt)

combinedterm    =   attributename "=" string *(";" localcnstrnt)

globalcnstrnts  =   globalcnstrnt *(";" globalcnstrnt)

globalcnstrnt   =   localcnstrnt
                       / "format" "=" format
                       / "maxfull" "=" 1*digit
                       / "maxhits" "=" 1*digit
                       / opt-globalcnst

opt-globalcnst  =   "hold"
                       / "authenticate" "=" auth-method
                       / "name" "=" string
                       / "password" "=" string
                       / "language" "=" language
                       / "incharset" "=" characterset
                       / "ignore" "=" string
                       / "include" "=" string

format          =   "full" / "abridged" / "handle" / "summary"
                       / "server-to-ask"

language        = <The language code defined in RFC1766 [ALVE95]>

characterset    =   "us-ascii" / "iso-8859-1" / "iso-8859-2" /
                       "iso-8859-3" / "iso-8859-4" / "iso-8859-5" /
                       "iso-8859-6" / "iso-8859-7" / "iso-8859-8" /
                       "iso-8859-9" / "iso-8859-10" / "UNICODE-1-1-UTF-8" /
                       "UNICODE-2-0-UTF-8" / charset-value

charset-value   =   1*char

localcnstrnt    =   "search" "=" searchvalue /
                       "case" "=" casevalue

searchvalue     =   "exact" / "substring" / "regex" / "fuzzy"
                       / "lstring"

casevalue       =   "ignore" / "consider"

auth-method     =   "password"

string          =   0*char

attributename   =   1*normalchar

char            =   "\" specialchar / normalchar

normalchar      =   <Characters 32 to 254(decimal) except specialchar>

specialchar     =   " " / <tab> / "=" / "," / ":" / ";" / "\" /
                       "*" / "." / "(" / ")" / "[" / "]" / "^" /
                       "$" / "!" / "?"
whitespace      =   1*(" " / <tab> / <CR> / <LF> / "@")

digit           =   "0" / "1" / "2" / "3" / "4" /
                       "5" / "6" / "7" / "8" / "9"

NL              =   <CR LF (decimal 13 10)>


server          =   goodmessage mnl output mnl endmessage onlynl
                     / badmessage onlynl endmessage onlynl

output          =   full / abridged / summary / handle

full            =   0*(full-record / server-to-ask)

abridged        =   0*(abridged-record / server-to-ask)

summary         =   summary-record 

handle          =   0*(handle-record / server-to-ask)
   
full-record     =   "# FULL " template serverhandle localhandle nl
                       1*(fulldata nl)
                       "# END" nl

abridged-record =   "# ABRIDGED " template serverhandle localhandle nl
                       abridgeddata nl
                       "# END" nl

summary-record   =  "# SUMMARY " serverhandle nl
                       summarydata nl
                       "# END" nl

handle-record    =  "# HANDLE " template serverhandle localhandle nl
   
server-to-ask   =   "# SERVER-TO-ASK " serverhandle nl
                       server-to-askdata nl
                       "# END" nl
   
fulldata        =   " " attributename ": " attributevalue
   
abridgeddata    =   " " 0*( attributevalue / tab )
   
summarydata     =   " Matches: " number nl 
                       [" Referrals: " number nl]
                       " Templates: " template 0*( nl "-" template)

server-to-ask-data = " Server-Handle:" <serverhandle> <nl>
                        " Host-Name: " hostname nl
                        " Host-Port: " number nl
                        [" Protocol: " prot nl]
                        0*(" " sstring ": " sstring nl)

   
attributename   =   sstring
   
attributevalue  =   longstring
   
template        =   sstring
   
serverhandle    =   sstring
   
localhandle     =   sstring
   
hostname        =   sstring

prot            =   sstring

longstring      =   string 0*( nl ( "+" / "-" ) string )
   
string          =   0*char
   
sstring         =   0*schar
   
schar           =   <Characters 32-254 (decimal) except special-char>
   
char            =   <Characters 32-254 (decimal) except nl>
   
special-char    =   ":" / " " / tab / nl
   
tab             =   <TAB (decimal 9)>
   
mnl             =   1*nl
   
nl              =   onlynl [ 1*(message onlynl) ]
   
onlynl          =   <CR LF (decimal 13 10)>
   
message         =   [1*( messagestart "-" string onlynl)]
                       messagestart " " string onlynl
   
messagestart    =   "% " digit digit digit
   
goodmessage     =   [1*( goodmessagestart "-" string onlynl)]
                       goodmessagestart " " string onlynl
   
goodmessagestart=   "% 200"
   
messagestart    =   "% " digit digit digit
   
badmessage      =   [1*( badmessagestart "-" string onlynl)]
                       badmessagestart " " string onlynl
   
badmessagestart =   "% 5" digit digit

endmessage      =   endmessageclose / endmessagecont
   
endmessageclose =   [endmessagestart " " string onlynl]
                       byemessage

endmessagecont  =   endmessagestart " " string onlynl

endmessagestart =   "% 226"

byemessage      =   byemessagestart " " string onlynl

endmessagestart =   "% 203"
  
number          =   1*( digit )
   
digit           =   "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"
   
   
   



