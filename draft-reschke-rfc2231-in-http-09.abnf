parameter     = attribute LWSP "=" LWSP value

attribute     = token
value         = token / quoted-string

quoted-string = <quoted-string, defined in [RFC2616], Section 2.2>
token         = <token, defined in [RFC2616], Section 2.2>

parameter     = reg-parameter / ext-parameter

reg-parameter = attribute LWSP "=" LWSP value

ext-parameter = attribute "*" LWSP "=" LWSP ext-value

ext-value     = charset  "'" [ language ] "'" value-chars
                   ; extended-initial-value,
                   ; defined in [RFC2231], Section 7

charset       = "UTF-8" / "ISO-8859-1" / mime-charset

mime-charset  = 1*mime-charsetc
mime-charsetc = ALPHA / DIGIT
                   / "!" / "#" / "$" / "%" / "&"
                   / "+" / "-" / "^" / "_" / "`"
                   / "{" / "}" / "~"
                   ; as <mime-charset> in Section 2.3 of [RFC2978]
                   ; except that the single quote is not included

language      = <Language-Tag, defined in [RFC5646], Section 2.1>

value-chars   = *( pct-encoded / attr-char )

pct-encoded   = "%" HEXDIG HEXDIG
                   ; see [RFC3986], Section 2.1

attr-char     = ALPHA / DIGIT
                   / "!" / "#" / "$" / "&" / "+" / "-" / "."
                   / "^" / "_" / "`" / "|" / "~"
                   ; token except ( "*" / "'" / "%" )


foo-header  = "foo" LWSP ":" LWSP token ";" LWSP title-param
title-param = "title" LWSP "=" LWSP value
                 / "title*" LWSP "=" LWSP ext-value
ext-value   = <see RFCxxxx, Section 3.2>





