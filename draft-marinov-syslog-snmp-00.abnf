STRUCTURED-DATA = NILVALUE / 1*SD-ELEMENT
SD-ELEMENT      = "[" SD-ID *(SP SD-PARAM) "]"
SD-PARAM        = PARAM-NAME "=" %d34 PARAM-VALUE %d34
SD-ID           = SD-NAME
PARAM-NAME      = SD-NAME
PARAM-VALUE     = UTF-8-STRING ; characters '"', '\' and
                                        ; ']' MUST be escaped.
SD-NAME         = 1*32PRINTUSASCII
                           ; except '=', SP, ']', %d34 (")

UTF-8-STRING    = *OCTET ; Any VALID UTF-8 String
                           ; "shortest form" MUST be used

OCTET           = %d00-255
SP              = %d32
PRINTUSASCII    = %d33-126
NILVALUE        = "-"


SNMP-SD-ELEMENT = "[" SNMP-SD-ID [CTX] REQID SYSUPTIME TRAP *VARBIND "]"
SNMP-SD-ID      = %x73.6E.6D.70         ; snmp
CTX             = CTXENGINE CTXNAME
CTXENGINE       = SP "ctxEngine=" %d34 HEXSTRING %d34
CTXNAME         = SP "ctxName=" %d34 HEXSTRING %d34
REQID           = SP "reqid=" %d34 INTEGER32 %d34
SYSUPTIME       = SP "sysUpTime=" %d34 UNSIGNED32 %d34
TRAP            = SP "snmpTrapOID=" %d34 OID %d34
VARBIND         = SP NAME SP VALUE
NAME            = VALOID
VALUE           = VALOID / VALSTRING / VALCOUNTER32 / VALCOUNTER64
                    / VALUNSIGNED32 / VALINTEGER32 / VALIP / VALNULL
                    / VALOPAQUE / VALTIMETICKS

VALOID          = "o=" %d34 OID %d34
VALSTRING       = "s=" %d34 HEXSTRING %d34
VALCOUNTER32    = "c=" %d34 UNSIGNED32 %d34
VALCOUNTER64    = "C=" %d34 UNSIGNED64 %d34
VALUNSIGNED32   = "u=" %d34 UNSIGNED32 %d34
VALINTEGER32    = "d=" %d34 INTEGER32 %d34
VALIP           = "i=" %d34 IPV4ADDRESS %d34
VALNULL         = "n=" %d34 NULL %d34
VALOPAQUE       = "p=" %d34 HEXSTRING %d34
VALTIMETICKS    = "t=" %d34 UNSIGNED32 %d34

OID             = 1*DIGIT "." 1*DIGIT *("." 1*DIGIT)
HEXSTRING       = *HEX
INTEGER32       = ["-"] NONZERODIGIT 0*DIGIT
UNSIGNED32      = NONZERODIGIT 0*DIGIT
UNSIGNED64      = NONZERODIGIT 0*DIGIT
NULL            = ""
IPV4ADDRESS     = d8 "." d8 "." d8 "." d8

d8              = DIGIT               ; 0-9
                    / %d49-57 DIGIT       ; 10-99
                    / "1" 2DIGIT          ; 100-199
                    / "2" %d48-52 DIGIT   ; 200-249
                    / "25" %d48-53        ; 250-255

HEX             = DIGIT / %x41-46 / %x61-66       ; 0-9 / A-F / a-f
NONZERODIGIT    = %d49-57
DIGIT           = %d48 / NONZERODIGIT
SP              = %d32

   Each SNMP-SD-ELEMENT starts with a SD-ID="snmp".  The first three
   PARAM-NAME elements are "ctxEngine", "ctxName" and "reqid".  They
   must be present in an SNMPv3 notification and therefore they must be
   present in a SYSLOG message generated by an snmp-to-syslog
   translator.  The contexdEngineID is encoded as as hexadecimal string,
   the contextName is encoded as a hexadecimal string, and the requestID
   is encoded as a signed decimal number.  Two special varbind elements
   are encoded next as an unsigned 32-bit number (sysUpTime) and an
   OBJECT IDENTIFIER value in dotted notation (snmpTrapOID).

   [DISCUSS: Why do we keep the requestid?  We do not keep the PDU type
   (inform / trap) - so why is the requestid relevant?]

   [DISCUSS: Does it really make sense to special case the first two
   varbinds sysUpTime and snmpTrapOID?  The value of the later one is
   hardly readable for humans anyways.]

   The remaining parameters correspond to the remaining varbind list
   elements.  The name of a varbind is encoded as an OID in dotted
   notation and the values are encoded according to the following rules:

   o  OBJECT IDENTIFIER - denoted by a PARAM-NAME = "o" and encoded in a
      dotted-decimal notation
   o  OCTET STRING - denoted by PARAM-NAME = "s" and encoded as a
      hexadecimal string
   o  Counter32 - denoted by PARAM-NAME ="c" and encoded as an unsigned
      decimal number
   o  Counter64 - denoted by PARAM-NAME ="C" and encoded as an unsigned
      decimal number
   o  Unsigned32 - denoted by PARAM-NAME ="u" and encoded as a decimal
      number
   o  INTEGER, Integer32 - denoted by PARAM-NAME = "d" and encoded as a
      signed decimal number
   o  IpAddress - denoted by PARAM-NAME = "i" and encoded in a quad-
      decimal notation
   o  OPAQUE - denoted by PARAM-NAME = "p" and encoded as a hexadecimal
      string
   o  Timeticks - denoted by PARAM-NAME = "t" and encoded as a decimal
      number
   o  NULL - denoted by PARAM-NAME = "n" and encoded as an empty string

   The syslog message generated by the snmp-to-syslog translator may
   include other structured data elements in its structured part in
   addition to the SNMP-SD-ELEMENT.  These structured data elements are
   included in the syslog message by the syslog sender at the snmp-to-
   syslog translator and must be compliant to the specification in
   [I-D.ietf-syslog-protocol] .

ctxName="ctx1"
reqid="7145575"
sysUpTime="94860"
snmpTrapOID="1.3.6.1.6.3.1.1.5.4"
o="1.3.6.1.2.1.2.2.1.1.3" d="3"
o="1.3.6.1.2.1.2.2.1.7.3" d="1"
o="1.3.6.1.2.1.2.2.1.8.3" d="1"]

o="1.3.6.1.2.1.2.2.1.7.3" d="1" mean that the SNMP notification
