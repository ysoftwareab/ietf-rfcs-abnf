ldapurl     = scheme COLON SLASH SLASH [hostport] [SLASH dn
                     [QUESTION [attributes] [QUESTION [scope]
                     [QUESTION [filter] [QUESTION extensions]]]]]
scheme      = "ldap"
hostport    = <hostport from Section 3.2.2 of [RFC2396]>
                       ; As updated by [RFC2732] to allow
                       ; IPv6 literal addresses
dn          = <distinguishedName from Section 3 of [LDAPDN]>
                       ; See the "Escaping Using the % Method"
                       ; section below.
attributes  = attrdesc *(COMMA attrdesc)
attrdesc    = <AttributeDescription
                                       from Section 4.1.4 of [Protocol]>
                     / ASTERISK
                       ; See the "Escaping Using the % Method"
                       ; section below.
scope       = "base" / "one" / "sub"
filter      = <filter from Section 4 of [Filters]>
                       ; See the "Escaping Using the % Method"
                       ; section below.
extensions  = extension *(COMMA extension)
extension   = [EXCLAMATION] extype [EQUALS exvalue]
extype      = oid / oiddescr
exvalue     = <LDAPString from section 4.1.2 of [Protocol]>
                       ; See the "Escaping Using the % Method"
                       ; section below.
oid         = <LDAPOID from section 4.1.2 of [Protocol]>
oiddescr    = <name from section 3.3 of [LDAPIANA]>


EXCLAMATION = %x21 ; exclamation mark ("!")
ASTERISK    = %x2A ; asterisk ("*")
SLASH       = %x2F ; forward slash ("/")
COLON       = %x3A ; colon (":")
QUESTION    = %x3F ; question mark ("?")
c=US?postalAddress


c=US??sub?(cn=Babs%20Jensen)


o=An Example\2c Inc.,c=US


