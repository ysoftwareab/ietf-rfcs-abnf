uMailbox = uLocal-part "@" ( uDomain / address-literal )
address-literal = <Defined in Section 4.1.2 of RFC 5321>

uLocal-part = uDot-string / uQuoted-string
uDot-string = uAtom *("." uAtom)
uAtom = 1*ucharacter
ucharacter = atext / UTF8-non-ascii

atext = <Defined in Section 3.2.3 of RFC 5322>
uQuoted-string = DQUOTE *uQcontentSMTP DQUOTE
DQUOTE = <Defined in appendix B.1 of RFC 5234>

uQcontentSMTP = qtextSMTP / quoted-pairSMTP / UTF8-non-ascii

qtextSMTP = <Defined in Section 4.1.2 of RFC 5321>

quoted-pairSMTP = <Defined in Section 4.1.2 of RFC 5321>

uDomain = sub-udomain *("." sub-udomain)
sub-udomain = uLet-dig [uLdh-str]
uLet-dig = Let-dig / UTF8-non-ascii

Let-dig = <Defined in Section 4.1.2 of RFC 5321>

uLdh-str = *( ALPHA / DIGIT / "-" / UTF8-non-ascii) uLet-dig
UTF8-non-ascii = <Defined in Section 4.1 of RFC5335bis>








uReturn-path-line = "Return-Path:" FWS uReverse-path <CRLF>
     ; Replaces Return-path-line in Section 4.4 of RFC 5321

uReverse-path = uPath / "<>"
     ; Replace Reverse-path in RFC 5321, section 4.1.2

uPath = "<" [ A-d-l ":" ] uMailbox ">"
     ; Replace Path in RFC 5321, section 4.1.2
     ; uMailbox is defined in section 3.3 of this document

A-d-l = <Defined in section 4.1.2 of RFC 5321>

uTime-stamp-line = "Received:" FWS uStamp <CRLF>
     ; Replaces Time-stamp-line in Section 4.4 of RFC 5321

uStamp = From-domain By-domain uOpt-info [CFWS] ";" FWS date-time
     ; Replaces Stamp in Section 4.4 of RFC 5321

From-domain = <Defined in section 4.4 of RFC 5321>

By-domain = <Defined in section 4.4 of RFC 5321>

date-time = <Defined in section 3.3 of RFC 5322>
       ; Same definition with date-time in Section 4.4 of RFC 5321

uOpt-info = [Via] [With] [ID] [uFor]
         [Additional-Registered-Clauses]
     ; Replaces Opt-info in Section 4.4 of RFC 5321
     ; The protocol value for With will allow a UTF8SMTPbis value

Via = <Defined in section 4.4 of RFC 5321>

With = <Defined in section 4.4 of RFC 5321>

ID = <Defined in section 4.4 of RFC 5321>

Additional-Registered-Clauses = <Defined in section 4.4 of RFC 5321>

uFor = CFWS "FOR" FWS ( uPath / uMailbox)
     ; Replaces For in Section 4.4 of RFC 5321
     ; uMailbox is defined in section 3.3 of this document

vrfy = "VRFY" SP uString
     [ SP "UTF8SMTPbis" ] CRLF

expn = "EXPN" SP uString
     [ SP "UTF8SMTPbis" ] CRLF

uString = uAtom / uQuoted-string
