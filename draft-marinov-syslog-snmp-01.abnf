STRUCTURED-DATA = NILVALUE / 1*SD-ELEMENT
SD-ELEMENT      = "[" SD-ID *(SP SD-PARAM) "]"
SD-PARAM        = PARAM-NAME "=" %d34 PARAM-VALUE %d34
SD-ID           = SD-NAME
PARAM-NAME      = SD-NAME
PARAM-VALUE     = UTF-8-STRING ; characters '"', '\' and
                                        ; ']' MUST be escaped.
SD-NAME         = 1*32PRINTUSASCII
                           ; except '=', SP, ']', %d34 (")

UTF-8-STRING    = *OCTET ; Any VALID UTF-8 String
                           ; "shortest form" MUST be used

OCTET           = %d00-255
SP              = %d32
PRINTUSASCII    = %d33-126
NILVALUE        = "-"


SNMP-SD-ELEMENT = "[" SNMP-SD-ID [CTX] SYSUPTIME TRAP *VARBIND "]"
SNMP-SD-ID      = %x73.6E.6D.70         ; snmp
CTX             = CTXENGINE CTXNAME
CTXENGINE       = SP "ctxEngine=" %d34 HEXSTRING %d34
CTXNAME         = SP "ctxName=" %d34 HEXSTRING %d34
SYSUPTIME       = SP "sysUpTime=" %d34 UNSIGNED32 %d34
TRAP            = SP "snmpTrapOID=" %d34 OID %d34
VARBIND         = SP NAME SP VALUE
NAME            = VALOID
VALUE           = VALOID / VALSTRING / VALCOUNTER32 / VALCOUNTER64
                       / VALUNSIGNED32 / VALINTEGER32 / VALIP / VALNULL
                       / VALOPAQUE / VALTIMETICKS

VALOID          = "o=" %d34 OID %d34
VALSTRING       = "s=" %d34 HEXSTRING %d34
VALCOUNTER32    = "c=" %d34 UNSIGNED32 %d34
VALCOUNTER64    = "C=" %d34 UNSIGNED64 %d34
VALUNSIGNED32   = "u=" %d34 UNSIGNED32 %d34
VALINTEGER32    = "d=" %d34 INTEGER32 %d34
VALIP           = "i=" %d34 IPV4ADDRESS %d34
VALNULL         = "n=" %d34 NULL %d34
VALOPAQUE       = "p=" %d34 HEXSTRING %d34
VALTIMETICKS    = "t=" %d34 UNSIGNED32 %d34

OID             = 1*DIGIT "." 1*DIGIT *("." 1*DIGIT)
HEXSTRING       = *HEX
INTEGER32       = ["-"] NONZERODIGIT 0*DIGIT
UNSIGNED32      = NONZERODIGIT 0*DIGIT
UNSIGNED64      = NONZERODIGIT 0*DIGIT
NULL            = ""
IPV4ADDRESS     = d8 "." d8 "." d8 "." d8

d8              = DIGIT               ; 0-9
                       / %d49-57 DIGIT       ; 10-99
                       / "1" 2DIGIT          ; 100-199
                       / "2" %d48-52 DIGIT   ; 200-249
                       / "25" %d48-53        ; 250-255

HEX             = DIGIT / %x41-46 / %x61-66       ; 0-9 / A-F / a-f
NONZERODIGIT    = %d49-57
DIGIT           = %d48 / NONZERODIGIT
SP              = %d32

ctxName="ctx1"
sysUpTime="94860"
snmpTrapOID="1.3.6.1.6.3.1.1.5.4"
o="1.3.6.1.2.1.2.2.1.1.3" d="3"
o="1.3.6.1.2.1.2.2.1.7.3" d="1"
o="1.3.6.1.2.1.2.2.1.8.3" d="1"]

o="1.3.6.1.2.1.2.2.1.7.3" d="1" mean that the SNMP notification
