ACHAR       = ALPHA                     ; Alphanumeric character
                   / DIGIT
SCHAR       = ACHAR                     ; Alphanumeric character
                   / "-"                       ;   or hyphen

ASTRING     = 1*ACHAR                   ; Alphanumeric string

NZDIGIT     = %x31-39                   ; Non-zero digit

UINT        = NZDIGIT *DIGIT            ; Unsigned integer
                   / "0"

ipv4-addr   = UINT-8 3("." UINT-8)      ; IPv4 address
ipv4-prfx   = ipv4-addr "/" UINT-5      ; IPv4 prefix

ip-addr     = ipv4-addr                 ; IP address
                   / ipv6-addr
ip-prfx     = ipv4-prfx                 ; IP prefix
                   / ipv6-prfx

asn16       = "AS" UINT-16              ; 16-bit ASN
asn32       = "AS" UINT-16 "." UINT-16  ; 32-bit ASN

asn         = asn16                     ; 16-bit or 32-bit ASN
                   / asn32

netloc-id   = ip-addr                   ; Network Location
                   / ip-prfx                   ;   Identifier
                   / asn

hostname    = ACHAR *(*SCHAR ACHAR)     ; Hostname
tld         = 1*ACHAR                   ; Top-level Domain
domainname  = 1*(hostname ".") tld      ; Domain name

isp-id      = domainname                ; ISP identifier

pid-ind-int = "i"                       ; Internal PID indicator
pid-ind-ext = "e"                       ; External PID indicator

pid-ind     = pid-ind-int               ; PID indicator
                   / pid-ind-ext

pid         = UINT-16 "." pid-ind "." isp-id
                                               ; PID

pdist       = UINT-16                   ; pDistance value
pdist-endp  = pid                       ; pDistance Endpoint
                   / netloc-id

getpid-data     = *(netloc-id CRLF)

pid-data        = 1*(netloc-id WSP pid CRLF)

getpidmap-data    = *(pid CRLF)

pidmap-line     = pid WSP UINT-32 1*(WSP netloc-id)

pidmap-data     = *(pidmap-line CRLF)

getpdist-line   = pdist-endp
                           WSP ("inc-reverse" / "no-reverse")
                           WSP UINT-32
                           1*(WSP pdist-endp)

getpdist-data   = *(getpdist-line CRLF)

dst-pdist       = pdist-endp 1*2(WSP pdist)

pdist-line      = pdist-endp
                           WSP ("inc-reverse" / "no-reverse")
                           WSP UINT-32
                           1*(WSP dst-pdist)

pdist-data      = *(pdist-line CRLF)

