resetkey        = "RESETKEY" [SP mailbox *(SP mechanism)]

capability      =/ "URLAUTH"

command-auth    =/ resetkey / genurlauth / urlfetch

resp-text-code  =/ "URLMECH" SP "INTERNAL" *(SP mechanism ["=" base64])

genurlauth      = "GENURLAUTH" 1*(SP url SP mechanism)

genurlauth-data = "*" SP "GENURLAUTH" 1*(SP url)

url             = astring
                    ; contains imapurl as defined below

urlfetch        = "URLFETCH" 1*(SP url)

urlfetch-data   = "*" SP "URLFETCH" 1*(SP url SP nstring)

    The following modifications are made to the Formal Syntax in
    [IMAPURL]:

enc-urlauth     = 32*HEXDIG

imessagepart    = enc-mailbox [uidvalidity] iuid [isection] [iurlauth]
                     ; enc-mailbox is called "enc_mailbox" in [IMAPURL]

iserver         = iuserauth "@" hostport
                     ; replaces iserver rule in [IMAPURL], as iuserauth
                     ; is mandatory with URLAUTH authorized URLs

iurlauth        = [expire] ";URLAUTH=" access ":" mechanism ":"
                   enc-urlauth

access          = ("submit+" iuserauth) / ("user+" iuserauth) /
                   "authuser" / "anonymous"

expire          = ";EXPIRE=" date-time
                     ; date-time defined in [DATETIME]

mechanism       = "INTERNAL" / 1*(alpha / digit / "-" / ".")
                    ; case-insensitive
                    ; new mechanisms MUST be registered with IANA


