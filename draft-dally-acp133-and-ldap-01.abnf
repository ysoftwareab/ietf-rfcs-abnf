aCPTelephoneFaxNumber = netid ", " telephonenum [ ", " 
                                   securedevid ]

nocommap = a /d /  """ / "(" / ")" / "+" / "-" / "." / "/" / 
                    ":" / "?" / " "

netid = 1*6nocommap
telephonenum = 1*32nocommap

securdevid = 1*8p

addresscapabilities = [ "description=" generalstring ] 
                    "address=" oraddress 
                    "capabilities=" [ capability *( "$"capability ) ]

generalstring =  ; The encoding of a value in this element 
                           ; of the syntax is the string value itself.

oraddress =  ; MHS OR Address syntax 
                       ; 1.3.6.1.4.1.1466.115.121.1.33

capability =  ; capability syntax 2.16.840.1.101.2.2.2.13 

addressees = [ 1*55p *( "$" 1*55p ) ]

addressListType = [ "-" ] numericstring  ; an INTEGER, where:
                                              ; 0 means AIG, 
                                              ; 1 means TYPE, 
                                              ; 2 means CAD, and
                                              ; 3 means TASKFORCE

capability = [ "content-types=" 
                              [numericoid *( ", " numericoid) ] ]
                       [ "maximum-content-length=" numericstring ]  
                              ; an INTEGER in the range 0 - 2147483647
                       [ "encoded-information-types-constraints=" 
                              [ "unaccept eits" unacceptable-eits ] 
                              [ "accept eits" acceptable-eits ] 
                              [ "only eits" 
                                   exclusively-acceptable-eits ] ] 
                       [ "security-labels=" securitycontext ]

unacceptable-eits = extendedencodedeits

acceptable-eits = extendedencodedeits

exclusively-acceptable-eits = extendedencodedeits

extendedencodedeits = numericoid *1023( ", "numericoid )

securitycontext = securitylabel *255securitylabel

securitylabel = [ "security-policy-id=" numericoid ]
                         [ "security-classification=" numericstring ]  
                              ; an INTEGER in the range 0 - 256, where 
                              ; 0 means Unmarked, 
                              ; 1 means Unclassified, 
                              ; 2 means Restricted, 
                              ; 3 means Confidential, 
                              ; 4 means Secret, and 
                              ; 5 means Top Secret
                         [ "privacy-mark=" 1*128p ]
                         [ "security-categories=" securitycategories ]

securitycategories =  ; the BER encoding of the set of type 
                           ; and value pairs for the instances of any 
                           ; data types that are specified to be 
                           ; SECURITY-CATEGORY types

dlpolicy = [ "report-propagation=" [ "-" ] numericstring ]  
                            ; where 0 means previous-dl-or-originator,
                            ; 1 means dl-owner, and
                            ; 2 means both
                    [ "report-from-dl=" [ "-" ] numericstring ]  
                              ; where 0 means whenever-requested and 
                              ; 1 means when-no-propagation 
                    [ "originating-MTA-report=" [ "-" ] numericstring ]
                              ; where 0 means unchanged, 
                              ; 2 means report, 
                              ; 3 means non-delivery-report, and
                              ; 4 means audited-report
                    [ "originator-report=" [ "-" ] numericstring ]  
                              ; where 0 means unchanged, 
                              ; 1 means no-report, 
                              ; 2 means report, and 
                              ; 3 means non-delivery-report
                    [ "return-of-content=" numericstring ]  
                              ; an INTEGER in the range 0 - 2, where 
                              ; 0 means unchanged, 
                              ; 1 means content-return-not-requested, 
                              ; and 2 means content-return-requested 
                    [ "priority=" [ "-" ] numericstring ]  ; where 
                              ; 0 means unchanged, 
                              ; 1 means normal, 
                              ; 2 means non-urgent, and 
                              ; 3 means urgent
                    [ "disclosure-of-other-recipients=" numericstring ]
                    ; an INTEGER in the range 0 - 2, where
                    ; 0 means unchanged, 
                    ; 1 means disclosure-of-other-recipients-prohibited,
                    ; and 2 means disclosure-of-other-recipients-allowed
                    [ "implicit-conversion-prohibited=" numericstring ]
                              ; an INTEGER in the range 0 - 2, where
                              ; 0 means unchanged, 
                              ; 1 means implicit-conversion-allowed, and
                              ; 2 means implicit-conversion-prohibited
                    [ "conversion-with-loss-prohibited=" numericstring ]
                         ; an INTEGER in the range 0 - 2, where
                         ; 0 means unchanged, 
                         ; 1 means conversion-with-loss-allowed, and 
                         ; 2 means conversion-with-loss-prohibited
                    [ "further-dl-expansion-allowed=" ( "TRUE" / 
                                                       "FALSE") ] 
                   [ "originator-requested-alternate-recipient-removed="
                         ( "TRUE" / "FALSE" ) ] 
                    [ "proof-of-delivery=" [ "-" ] numericstring ]  
                              ; where 0 means dl-expansion-point, 
                              ; 1 means dl-members, 
                              ; 2 means both, and 
                              ; 3 means neither
                    [ "requested-delivery-method=" ( "unchanged" / 
                         "removed" / 
                         ( "replaced:  " requested-delivery-method ) ) ]

requested-delivery-method = [ delivery-methods 
                                           *( ", " delivery-methods ) ]

delivery-methods = numericstring  ; an INTEGER in the range 
                              ; 0 - 256, where
                              ; 0 means any-delivery-method, 1 means 
                              ; mhs-delivery, 2 means physical-delivery,
                              ; 3 means telex-delivery, 
                              ; 4 means teletex-delivery, 
                              ; 5 means g3-facsimile-delivery, 
                              ; 6 means g4-facsimile-delivery, 
                              ; 7 means ia5-terminal-delivery, 
                              ; 8 means videotex-delivery, and 
                              ; 9 means telephone-delivery
dlsubmitpermission = ( "individual=" orname ) / 
                              ("member-of-dl=" orname ) / 
                              ("pattern-match=" orname ) / 
                              ( "member-of-group=" name )

orname =  ; O/R Name syntax 2.16.840.1.101.2.2.2.10

name =  ; DN syntax 1.3.6.1.4.1.1466.115.121.1.12

mLReceiptPolicy = none / insteadof / inadditionto

none = "none"

insteadof = "instead of" generalnames *15( "$" generalnames )

inadditionto = "in addition to" generalnames 
                         *15( "$" generalnames )

generalnames = generalname *( "%" generalname )

generalname = ( "otherName = " othername ) / 
                        ( "rfc822Name = " ia5string ) / 
                        ( "dNSName = " ia5string ) / 
                        ( "x400Address = " oraddress ) / 
                        ( "directoryName = " name ) / 
                        ( "ediPartyName = " 
                              [ "nameAssigner:" directorystring ] 
                              "partyName:" directorystring ) / 
                        ( "uniformResourceIdentifier = " ia5string ) / 
                        ( "iPAddress = " octetstring ) / 
                        ( "registeredID = " numericoid )

othername =  ; the BER encoding of the type and value pair 
                       ; for an instance of any data type that is 
                       ; specified to be an OTHER-NAME type.

ia5string =  ; IA5 String syntax 1.3.6.1.4.1.1466.115.121.1.26

oraddress =  ; MHS OR Address syntax 
                       ; 1.3.6.1.4.1.1466.115.121.1.33
name =  ; DN syntax 1.3.6.1.4.1.1466.115.121.1.12


directorystring =  ; Directory String syntax 
                             ; 1.3.6.1.4.1.1466.115.121.1.15

octetstring =  ; Octet String syntax  
                         ; 1.3.6.1.4.1.1466.115.121.1.40

oRName = oraddress [ "|" name ]

oraddress =  ; MHS OR Address syntax  
                       ; 1.3.6.1.4.1.1466.115.121.1.33

name =  ; DN syntax 1.3.6.1.4.1.1466.115.121.1.12

otherNotificationsSupported = namedbits / bitstring

namedbits = "{" [ namedbit *( "," namedbit ) ] "}"

namedbit = "acp127-nn" / "acp127-pn" / "acp127-tn"

bitstring = "'" *binary-digit "'B"

binary-digit = "0" / "1"

remarks = [ *p *( "$" *p ) ]
rIParameters = "rI=" *p whsp 
                         "rIType=" numericstring whsp  ; an INTEGER in 
                                             ; the range 0 - 2, where 
                                             ; 0 means normal,
                                             ; 1 means off-line, and
                                             ; 2 means partTimeTerminal
                         "minimize=FALSE" whsp  ; not used anymore
                         "sHD=" *p whsp 
                         "classification=" numericstring  ; an INTEGER 
                                             ; in the range 0 - 5, where
                                             ; 0 means unmarked, 
                                             ; 1 means unclassified, 
                                             ; 2 means restricted, 
                                             ; 3 means confidential, 
                                             ; 4 means secret, and 
                                             ; 5 means top secret
