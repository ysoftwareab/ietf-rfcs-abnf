Language-Tag  = langtag
                 / privateuse             ; private use tag
                 / irregular              ; tags grandfathered by rule

langtag       = (language
                    ["-" script]
                    ["-" region]
                    *("-" variant)
                    *("-" extension)
                    ["-" privateuse])

language      = (2*3ALPHA [ extlang ]) ; shortest ISO 639 code
                 / 4ALPHA                 ; reserved for future use
                 / 5*8ALPHA               ; registered language subtag

extlang       = *3("-" 3ALPHA)         ; specific ISO 639-3 codes

script        = 4ALPHA                 ; ISO 15924 code

region        = 2ALPHA                 ; ISO 3166 code
                 / 3DIGIT                 ; UN M.49 code

variant       = 5*8alphanum            ; registered variants
                 / (DIGIT 3alphanum)

extension     = singleton 1*("-" (2*8alphanum))

singleton     = %x41-57 / %x59-5A / %x61-77 / %x79-7A / DIGIT
                 ; "a"-"w" / "y"-"z" / "A"-"W" / "Y"-"Z" / "0"-"9"
                 ; Single alphanumerics
                 ; "x" is reserved for private use

privateuse    = "x" 1*("-" (1*8alphanum))

irregular     = "en-GB-oed" / "i-ami" / "i-bnn" / "i-default"
                 / "i-enochian" / "i-hak" / "i-klingon" / "i-lux"
                 / "i-mingo" / "i-navajo" / "i-pwn" / "i-tao"
                 / "i-tay" / "i-tsu" / "sgn-BE-fr" / "sgn-BE-nl"
                 / "sgn-CH-de"

alphanum      = (ALPHA / DIGIT)       ; letters and numbers

                        Figure 1: Language Tag ABNF

registry   = record *("%%" CRLF record)
record     = 1*( field-name *SP ":" *SP field-body CRLF )
field-name = (ALPHA / DIGIT) [*(ALPHA / DIGIT / "-") (ALPHA / DIGIT)]
field-body = *(([*WSP CRLF] 1*WSP) 1*ASCCHAR)
ASCCHAR    = %x21-25 / %x27-7E / UNICHAR ; Note: AMPERSAND is %x26
UNICHAR    = "&#x" 2*6HEXDIG ";"

                      Figure 2: Registry Format ABNF

language      =  3 (ISO 639-2; ISO 639-1 requires 2)
extlang1      =  4 (each subsequent subtag includes '-')
extlang2      =  4 (unlikely: needs prefix="language-extlang1")
extlang3      =  4 (extremely unlikely)
script        =  5 (if not suppressed: see Section 4.1)
region        =  4 (UN M.49; ISO 3166 requires 3)
variant1      =  9 (needs 'language' as a prefix)
variant2      =  9 (needs 'language-variant1' as a prefix)

total         = 42 characters

              Figure 7: Derivation of the Limit on Tag Length

