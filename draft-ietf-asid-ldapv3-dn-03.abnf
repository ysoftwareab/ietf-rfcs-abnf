distinguishedName = [name]                    ; may be empty string
 
name       = name-component *("," name-component)
 
name-component = attributeTypeAndValue *("+" attributeTypeAndValue)
 
attributeTypeAndValue = attributeType "=" attributeValue
 
attributeType = (ALPHA 1*keychar) / oid
keychar    = ALPHA / DIGIT / "-"
  
oid        = 1*DIGIT *("." 1*DIGIT)
 
attributeValue = string
 
string     = *( stringchar / pair )
                / "#" hexstring
                / QUOTATION *( quotechar / pair ) QUOTATION ; only from v2

quotechar     = <any character except "\" or QUOTATION >
 
special    = "," / "=" / "+" / "<" /  ">" / "#" / ";"
 
pair       = "\" ( special / "\" / QUOTATION / hexpair )
stringchar = <any character except one of special, "\" or QUOTATION >
 
hexstring  = 1*hexpair
hexpair    = hexchar hexchar

hexchar    = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
                / "a" / "b" / "c" / "d" / "e" / "f"
 
ALPHA      =  <any ASCII alphabetic character>
                                            ; (decimal 65-90 and 97-122)
DIGIT      =  <any ASCII decimal digit>  ; (decimal 48-57)
QUOTATION  =  <the ASCII double quotation mark character '"' decimal 34>
   
CN=Steve Kille,O=Isode Limited,C=GB
   
OU=Sales+CN=J. Smith,O=Widget Inc.,C=US

CN=L. Eagle,O=Sue\, Grabbit and Runn,C=GB

CN=Before\0DAfter,O=Test,C=GB

SN=Lu\C4\8Di\C4\87


