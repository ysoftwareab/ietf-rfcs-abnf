certstring = (certspec / multispec) [ "|" pkcsattrs ]

                         Figure 1: certstring ABNF

certspec         = certspec-hash / certspec-content / certspec-el /
                      certspec-path

certspec-hash    = "SHA-1"   ":" 40HEXDIG /
                      "SHA-256" ":" 64HEXDIG /
                      "SHA-384" ":" 96HEXDIG /
                      "SHA-512" ":" 128HEXDIG

certspec-content = ("HEX" / "BASE16") ":" 1*(2HEXDIG) /
                      "BASE64"  ":" base64string

base64char       = ALPHA / DIGIT / "+" / "/"
base64string     = 1*(4base64char)
                      [ 3base64char "=" / 2base64char "==" ]

distinguishedName = [ relativeDistinguishedName
             *( "," relativeDistinguishedName ) ]
relativeDistinguishedName = attributeTypeAndValue
             *( "+" attributeTypeAndValue )
attributeTypeAndValue = attributeType "=" attributeValue
attributeType = descr / numericoid

num = "0" / %x31-39 *DIGIT
descr = ALPHA *(ALPHA / DIGIT / "-")
numericoid = ("0" / "1" / "2") 1*("." num)

attributeValue = string / hexstring
string      = [ ( leadchar / pair ) [ *( stringchar / pair )
                 ( trailchar / pair ) ] ]
leadchar    = %x01-1F / %x21 / %x24-2A / %x2D-3A /
                 %x3D / %x3F-5B / %x5D-7F / BEYONDASCII
trailchar   = %x01-1F / %x21 / %x23-2A / %x2D-3A /
                 %x3D / %x3F-5B / %x5D-7F / BEYONDASCII
stringchar  = %x01-21 / %x23-2A / %x2D-3A /
                 %x3D / %x3F-5B / %x5D-7F / BEYONDASCII

pair        = "\" (" " / %x22-23 / %x2B-2C / %x3B-3E / "\" / 2HEXDIG)
hexstring   = "#" 2HEXDIG

certspec-el   = "ISSUERSN" ":" distinguishedName ";" serialNumber /
                   "SKI"      ":" 1*(2HEXDIG)

serialNumber  = 1*(2HEXDIG)

certspec-path     = certspec-uri / certspec-file / certspec-reg

URI-Template      = URI-Template@[RFC6570]

certspec-uri      = "URI:" URI-Template

certspec-file     = ("/" / "\" / [A-Z] ":" /
                       ("." / "..") ("/" / "\") / "~" / "%" / "$")
                       *filepathchar

filepathchar      = %x01-29 / %x2B-3B / "=" / %x40-5B /
                       %x5D-7B / %x7D-7F / quoted-fpc / BEYONDASCII

quoted-fpc        = "\" ("*" / "<" / ">" / "?" / "\" / "|")

certspec-reg      = reg-hive 1*("\" reg-key)
                       ["\\" reg-value-name]

reg-hive          = reg-local-hive / reg-remote-hive
reg-local-hive    = "HKEY_LOCAL_MACHINE" / "HKEY_CURRENT_USER" /
                       "HKEY_CLASSES_ROOT" / "HKEY_USERS" /
                       "HKEY_CURRENT_CONFIG" /
                       "HKLM:" / "HKCU:" / "HKCR:" /
                       "HKU:" / "HKCC:"
reg-remote-hive   = "\\" reg-name@[RFC3986] "\" ("HKLM" / "HKU") ":"

reg-key           = 1*(%x20-3B / %x3D / %x3F-5B %x5D-7B / %x7D.7E /
                          "\<" / "\>" / "\|" / BEYONDASCII)
reg-value-name    = *(%x20-3B / %x3D / %x3F-7B / %x7D.7E /
                          "\<" / "\>" / "\|" / BEYONDASCII)

                          Figure 2: certspec ABNF

multispec = 1*("<" certspec ">")

                         Figure 3: multispec ABNF

pkcsattrs = pkcsattr ["," pkcsattrs]
pkcsattr  = pkcsattrType ["=" pkcsattrValues]
pkcsattrType = descr / numericoid
pkcsattrValues = pkcsattrValue ["+" pkcsAttrValues]
pkcsattrValue = hexstring / string /
                   basic-xer-string / asn1-value-string

basic-xer-string = xer-element

xer-Name = ALPHA *(ALPHA / DIGIT / "_" / "-" / ".")

xW = *(HT / LF / CR / SP)

xer-element = xer-EmptyElemTag / xer-STag xer-content xer-ETag

xer-EmptyElemTag = "<" xer-Name xW "/>"

xer-STag = "<" xer-Name xW ">"

xer-content = *xer-CharData *((xer-element / xer-Reference)
                 *xer-CharData)

xer-ETag = "</" xer-Name xW ">"

xer-CharData = HT / LF / CR / %x20-25 / %x27-3B / "=" / %x3F-D7FF /
                  %xE000-%xFFFD / %x10000-10FFFF

xer-Reference = xer-EntityRef / xer-CharRef

xer-EntityRef = "&" (%s"amp" / %s"lt" / %s"gt") ";"

xer-CharRef = "&#" (1*DIGIT / %s"x" 1*HEXDIG) ";"

asn1-value-string = SP aValue aW

aid = %x61-7A *(["-"] (ALPHA / DIGIT))
aNL = %d10-13
aS = %d9-13 / SP / NBSP / acomment
aW = *aS

acomment = "--" *(["-"] ( UWSP / %x21-2C / %x2E-7E /
                             UVCHARBEYONDASCII / PUACHAR ) )
                   (aNL / "-" (aNL / "-"))

acstring = %x22 *(UWSP / aNL / %x21 / %x22.x22 / %x23-7E /
              UVCHARBEYONDASCII) %x22

aValue = %s"TRUE" / %s"FALSE" / %s"NULL" / %s"PLUS-INFINITY" /
        %s"MINUS-INFINITY" / %s"NOT-A-NUMBER" /
        "'" *("0" / "1" / aW) "'B" / "'" *(HEXDIG / aW) "'H" /
        %s"CONTAINING" 1*aS aValue /
        aid [aW ":" aW aValue] /
        aNumericRealValue / acstring / "{" aW alist "}"

aObjIdComponents = (num / aid) [aW "(" aW (num/aid) aW ")"]

aNumericRealValue = ["-" aW] 1*DIGIT ["."] *DIGIT ["e" ["-"] num]

alist = aValue aW          *("," aW aValue aW)          /
           aid 1*aS aValue aW *("," aW aid 1*aS aValue aW) /
localKeyId=#0402534C,friendlyName=Chubby\F0\9F\90\B0
