capability         =/ "CONDSTORE" / "PERFLAGMODSEQ"

mailbox-data       =/ "STATUS" SP mailbox SP "("
                         [status-att SP status-value *(SP status-att SP status-value)] ")"

status-value       = number / mod-sequence-value

store              = "STORE" SP set store-modifiers SP store-att-flags
   
store-modifiers    = [ SP "(" 1*store-modifier ")" ]
    
store-modifier     = "UNCHANGEDSINCE" SP mod-sequence-value

fetch-att          =/ fetch-mod-sequence
                        ;; modifies original IMAP4 fetch-att

fetch-mod-sequence = "MODSEQ" SP entry-names

fetch-mod-resp     = "MODSEQ" SP "(" permsg-modsequence
                        *(entry-name SP mod-sequence-value) ")"

search-key         =/ search-modsequence
                        ;; modifies original IMAP4 search-key

search-modsequence = "MODSEQ" SP entry-name SP mod-sequence-value

resp-text-code     =/ "HIGHESTMODSEQ" SP mod-sequence-value /
                         "MODIFIED" SP set /
                         "MODSEQ" SP set SP mod-sequence-value
                        ;; set of message numbers for STORE/FETCH or
                        ;; set of UIDs for UID STORE/UID FECTH

entry-names        = "(" *entry-name ")"
                        ;; empty list means that only per-message MODSEQ should be returned 

entry-name         = '"' "/message/flags/system/" attr-flag '"'
                        ;; each system or user defined flag <flag> is mapped to
                        ;; "/message/flags/system/<flag>".
                        ;; system IMAP flags must have two leading "\",
                        ;; because "\" is an escape character.

permsg-modsequence = mod-sequence-value
                        ;; per message mod-sequence, if server supports per flag mod-sequences,
                        ;; this is the highest mod-sequence between all metadata items

mod-sequence-value = 1*DIGIT
                        ;; Unsigned 64-bit integer (mod-sequence)
                        ;; (0 <= n < 18,446,744,073,709,551,615)

attr-flag          = "\\Answered" / "\\Flagged" / "\\Deleted" /
                        "\\Seen" / "\\Draft" / attr-flag-keyword / attr-flag-extension
                        ;; Does not include "\Recent"

attr-flag-extension = "\\" atom
                        ;; Future expansion.  Client implementations
                        ;; MUST accept flag-extension flags.  Server
                        ;; implementations MUST NOT generate
                        ;; flag-extension flags except as defined by
                        ;; future standard or standards-track
                        ;; revisions of this specification.

attr-flag-keyword   = atom


