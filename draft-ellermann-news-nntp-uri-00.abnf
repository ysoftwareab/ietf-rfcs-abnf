newsURL        = "news:" ( article / group )
article        = [ news-server "/" ] message-id
group          = [ news-server "/" ] ( newsgroup-name / "*" )

news-server    = "//" authority                 ; see RFC 3986
message-id     = unique "@" host                ; see RFC 1036
unique         = 1*( %d33-61 / %d63-126 )       ; excludes ">"
newsgroup-name = 1*( ALPHA / DIGIT / "-" / "+" / "_" / "." )

nntpURL        = "nntp:" news-server "/" newsgroup-name "/" range
range          = article-number ["-" [article-number]]
article-number = 1*DIGIT

