jid             = [ localpart "@" ] domain [ "/" resource ]
localpart       = 1*(nodepoint)
                        ; a "nodepoint" is a UTF-8 encoded Unicode code
                        ; point that satisfies the Nodeprep profile of
                        ; stringprep
domain          = fqdn / address-literal
fqdn            = *(ldhlabel ".") toplabel
ldhlabel        = letdig [*61(ldh) letdig]
toplabel        = ALPHA   *61(ldh) letdig
letdig          = ALPHA / DIGIT
ldh             = ALPHA / DIGIT / "-"
address-literal = IPv4address / IPv6address
                        ; the "IPv4address" and "IPv6address" rules are
                        ; defined in RFC 3986
resource        = 1*(resourcepoint)
                        ; a "resourcepoint" is a UTF-8 encoded Unicode
                        ; code point that satisfies the Resourceprep
                        ; profile of stringprep

