capability         =/ "X-DRAFT-I05-FILTERS"
                         ;; [[Note to RFC Editor: change the capability
                         ;; name before publication]]

search-criteria       =  search-key *(SP search-key)

search-key            =/  "FILTER" SP filter-name
                         ;; New SEARCH criterion for referencing filters

filter-name           =  1*<any ATOM-CHAR except "/">
                         ;; Note that filter-name disallows UTF-8 or
                         ;; the following characters: "(", ")", "{",
                         ;; " ", "%", "*", "]".  See definition of
                         ;; ATOM-CHAR [RFC3501].

resp-text-code        =/  "UNDEFINED-FILTER" SP filter-name

response-payload      =/  filter-resp

filter-resp           =  filter-type SP filter-name SP
                         filter-value SP filter-desc

filter-type           =  "PUBLIC" / "PRIVATE"

filter-value          =  astring
                         ;;<search-criteria> represented as <astring>

filter-desc           =  astring
                         ;;Human readable description of the filter.
                         ;;Empty string, if no description is defined.

command-auth          =/  listfilters / setfilter / deletefilter

listfilters           =  "LISTFILTERS" [SP filter-name]

setfilter             =  "SETFILTER" SP filter-name SP filter-type
                         filter-value [SP filter-desc]

deletefilter          =  "DELETEFILTER" SP filter-name SP filter-type
