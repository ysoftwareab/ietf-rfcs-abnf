TCHAR = VCHAR / SP / HTAB    ; visible plus white space


error-response = error-code SP *TCHAR CRLF
error-code     = ("4" / "5") 2DIGIT


host-command  = "HOST" SP hostname CRLF
hostname      = domain / IP-literal

domain        = sub-domain *("." sub-domain)
sub-domain    = let-dig [ldh-str]
let-dig       = ALPHA / DIGIT
ldh-str       = *( ALPHA / DIGIT / "-" ) let-dig

IP-literal    = ( "[" IPv6address "]" ) / IPv4address

IPv6address   = <see [RFC3986] Section 3.2.2>
IPv4address   = <see [RFC3986] Section 3.2.2>

host-response = host-ok / error-response
host-ok       = "220" [ SP *TCHAR ] CRLF


