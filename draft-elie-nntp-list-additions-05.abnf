list-arguments =/ "COUNTS" [WS wildmat]

list-arguments =/ "DISTRIBUTIONS" / "MODERATORS" / "MOTD"

list-arguments =/ "SUBSCRIPTIONS" [WS wildmat]

newsgroup-status =/ newsgroup-alias /
           %x78 / %x6a  ; case-sensitive "x" and "j"
newsgroup-alias =  "=" newsgroup-name

list-content =/ list-counts-content
list-counts-content =
           *(newsgroup-name 3(SPA article-number)
           SPA newsgroup-status CRLF)

list-content =/ list-distributions-content
list-distributions-content =
           *(distribution WS distribution-description CRLF)
distribution-description = S-TEXT

list-content =/ list-moderators-content
list-moderators-content =
           *(wildmat ":" moderators-address CRLF)
moderators-address = S-TEXT

list-content =/ list-motd-content
list-motd-content = *(*U-CHAR CRLF)

list-content =/ list-subscriptions-content
list-subscriptions-content = *(newsgroup-name CRLF)

