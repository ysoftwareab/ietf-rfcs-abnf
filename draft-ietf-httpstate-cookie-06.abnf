set-cookie-header = "Set-Cookie:" OWS set-cookie-string OWS
set-cookie-string = cookie-pair *( ";" cookie-av )
cookie-pair       = cookie-name "=" cookie-value
cookie-name       = token
cookie-value      = token
token             = <token, as defined in RFC 2616>

cookie-av         = expires-av / max-age-av / domain-av /
                       path-av / secure-av / httponly-av
expires-av        = "Expires" "=" sane-cookie-date
sane-cookie-date  = <rfc1123-date, as defined in RFC 2616>
max-age-av        = "Max-Age" "=" 1*DIGIT
domain-av         = "Domain" "=" domain-value
domain-value      = token
path-av           = "Path" "=" path-value
path-value        = <abs_path, as defined in RFC 2616>
secure-av         = "Secure"
httponly-av       = "HttpOnly"


cookie-header = "Cookie:" OWS cookie-string OWS
cookie-string = cookie-pair *( ";" cookie-pair )


cookie-date     = *delimiter date-token-list *delimiter
date-token-list = date-token *( 1*delimiter date-token )
delimiter       = %x09 / %x20 / %x21 / %x22 / %x23 / %x24 /
                     %x25 / %x26 / %x27 / %x28 / %x29 / %x2A /
                     %x2B / %x2C / %x2D / %x2E / %x2F / %x3B /
                     %x3C / %x3D / %x3E / %x3F / %x40 / %x5B /
                     %x5C / %x5D / %x5E / %x5F / %x60 / %x7B /
                     %x7C / %x7D / %x7E
date-token      = day-of-month / month / year / time / mystery
day-of-month    = 2DIGIT / DIGIT
month           = "jan" [ mystery ] / "feb" [ mystery ] /
                     "mar" [ mystery ] / "apr" [ mystery ] /
                     "may" [ mystery ] / "jun" [ mystery ] /
                     "jul" [ mystery ] / "aug" [ mystery ] /
                     "sep" [ mystery ] / "oct" [ mystery ] /
                     "nov" [ mystery ] / "dec" [ mystery ]
year            = 5DIGIT / 4DIGIT / 3DIGIT / 2DIGIT / DIGIT
time            = time-field ":" time-field ":" time-field
time-field      = 2DIGIT / DIGIT
mystery         = <anything except a delimiter>
