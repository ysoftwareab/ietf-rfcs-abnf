annotate-data     = "ANNOTATION" SP mailbox SP entry-list
                        ; empty string for mailbox implies
                        ; server annotation.
att-value         = attrib SP value

attrib            = string
                        ; dot-separated attribute name
                        ; MUST NOT contain "*" or "%"
attrib-match      = string
                        ; dot-separated attribute name
                        ; MAY contain "*" or "%" for use as wildcards 

attribs           = attrib-match / "(" attrib-match *(SP attrib-match) ")"
                        ; attribute specifiers that can include wildcards

entries           = entry-match / "(" entry-match *(SP entry-match) ")"
                        ; entry specifiers that can include wildcards

entry             = string
                        ; slash-separated path to entry
                        ; MUST NOT contain "*" or "%"

entry-att         = entry SP "(" att-value *(SP att-value) ")"

entry-list        = entry-att *(SP entry-att) /
                        "(" entry *(SP entry) ")"
                        ; entry attribute-value pairs list for 
                        ; GETANNOTATION response, or
                        ; parenthesised entry list for unsolicited
                        ; notification of annotation changes

entry-match       = string
                        ; slash-separated path to entry
                        ; MAY contain "*" or "%" for use as wildcards 

getannotation     = "GETANNOTATION" SP list-mailbox SP entries SP attribs
                        ; empty string for list-mailbox implies
                        ; server annotation.

resp-text-code    =/ "ANNOTATEMORE" SP "TOOBIG" /
                         "ANNOTATEMORE" SP "TOOMANY"
                        ; new response codes for SETANNOTATION failures

setannotation     = "SETANNOTATION" SP list-mailbox SP setentryatt
                        ; empty string for list-mailbox implies
                        ; server annotation.

setentryatt       = entry-att / "(" entry-att *(SP entry-att) ")"
value             = nstring
    
    
