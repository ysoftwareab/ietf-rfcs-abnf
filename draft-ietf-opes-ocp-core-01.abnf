message = name [anonym-parameters]
             [named-parameters]
             [payload]
             ";" CRLF

anonym-parameters = 1*(SP   anonym-parameter) ; spaced parameters
named-parameters =  1*(CRLF named-parameter)  ; CRLF-separated params
payload = CRLF data

anonym-parameter = value
named-parameter = name ":" SP value

value = atom / structure / list
atom = bare-value / quoted-value
structure = "{" [ value *(SP  value) ] "}"   ; spaced values
list =      "(" [ value *("," value) ] ")"   ; comma-separated values

name = ALPHA *safe-OCTET
bare-value = 1*safe-OCTET
quoted-value = DQUOTE data DQUOTE
data = size ":" <n>OCTET                 ; <n> == size

safe-OCTET = ALPHA / DIGIT / "-" / "_"
size = dec-number                        ; 0-2147483647
dec-number = 1*DIGIT                     ; no leading zeros or signs
                                Figure 3

