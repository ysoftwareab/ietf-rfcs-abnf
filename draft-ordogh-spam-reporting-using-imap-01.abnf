srep-command        = "SREP" SP directive *1(SP abuse-type) SP \
                         reference *1(SP part-id-list) \
                         *1(SP request-action)
directive           = "SET" / "CLEAR" / directive-ext
directive-ext       = atom
                         ; It is not required that new directives
                         ; begin with "X-", see [XDASH]
abuse-type          = "AT" SP abuse-type-id
abuse-type-id       = 1*DIGIT
                         ; no leading zeroes or signs
                         ; New abuse types MUST be registered with
                         ; IANA as standard or standards-track
reference           = reference-type SP reference-value
reference-type      = "UID" / "SEQ" / "URLAUTH" / reference-type-ext
reference-type-ext  = atom
                         ; It is not required that new reference types
                         ; begin with "X-", see [XDASH]
reference-value     = uniqueid /           ; see [IMAP]
                         sequence-set /       ; see [IMAP]
                         authorized-url /     ; see [URLAUTH]
                         reference-value-ext
authorized-url      = authimapurlfull /    ; see [URLAUTH]
                         authimapurlrump      ; see [URLAUTH]
reference-value-ext = atom
                         ; New reference values MUST correspond to
                         ; reference-type-ext
part-id-list        = "(" part-id *(SP part-id) ")"
part-id             = header-id / body-id
header-id           = "header." header-fld-name
                         ; see header-fld-name in [IMAP]
body-id             = "body" *("." 1*DIGIT)
                         ; no leading zeroes or signs in numberic part
request-action      = "DO" SP req-action *1(SP destination-box)
req-action          = "KEYWORD" /
                         "RELOCATE" /
                         "DELETE"
destination-box     = mailbox / nil
resp-text-code      = resp-spam-actions    ; responses specific to
                                              ; this command, extending
                                              ; existing resp-text-code
                                              ; defined in [IMAP]
resp-spam-actions   = "KEYWORD" flag-list / ; see flag-list in [IMAP]
                         "RELOCATE" flag-list /; see flag-list in [IMAP]
                         "RELOCATED" /
                         "DELETE" flag-list /  ; see flag-list in [IMAP]
                         "DELETED"







