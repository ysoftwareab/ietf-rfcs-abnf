channel             =  "CHANNEL" SP channel-uri-list
                          SP channel-set

channelschemes      =  "CHANNELSCHEMES"

channelschemes-data =  astring / "(" astring 1*(SP astring) ")"
                          ; <astring> encodes a <scheme>

channel-data        =  nz-number SP "CHANNEL" SP section-spec SP
                          astring
                          ; <astring> encodes an <absoluteURI>

channel-set         =  1*("(" nz-number SP section-spec ")")
                          ; <nz-number> refers to the message sequence
                          ; number or unique identifier.

channel-uri-list    =  "(" channel-uri
                          *(SP channel-uri) ")"

channel-uri         =  astring
                          ; <absoluteURI> / <scheme>
                          ; represented as an IMAP <astring>

command-auth        =/ channelschemes

command-select      =/ channel

response-data       =  "*" SP (resp-cond-state / resp-cond-bye /
                          mailbox-data / message-data /
                          capability-data / channel-data) CRLF
                          ; adds <channel-data> to <response-data>

uid                 =/ channel



