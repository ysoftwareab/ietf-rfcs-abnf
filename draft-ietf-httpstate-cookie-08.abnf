OWS            = *( [ obs-fold ] WSP )
                    ; "optional" whitespace
obs-fold       = CRLF


set-cookie-header = "Set-Cookie:" SP set-cookie-string
set-cookie-string = cookie-pair *( ";" SP cookie-av )
cookie-pair       = cookie-name "=" cookie-value
cookie-name       = token
cookie-value      = token
token             = <token, as defined in RFC 2616>

cookie-av         = expires-av / max-age-av / domain-av /
                       path-av / secure-av / httponly-av /
                       extension-av
expires-av        = "Expires=" sane-cookie-date
sane-cookie-date  = <rfc1123-date, as defined in RFC 2616>
max-age-av        = "Max-Age=" 1*DIGIT
domain-av         = "Domain=" domain-value
domain-value      = <subdomain, as defined in RFC 1034>
path-av           = "Path=" path-value
path-value        = <abs_path, except those containing ";">
secure-av         = "Secure"
httponly-av       = "HttpOnly"
extension-av      = <any CHAR except CTLs or ";">


cookie-header = "Cookie:" OWS cookie-string OWS
cookie-string = cookie-pair *( ";" SP cookie-pair )


cookie-date     = *delimiter date-token-list *delimiter
date-token-list = date-token *( 1*delimiter date-token )
delimiter       = %x09 / %x20 / %x21 / %x22 / %x23 / %x24 /
                     %x25 / %x26 / %x27 / %x28 / %x29 / %x2A /
                     %x2B / %x2C / %x2D / %x2E / %x2F / %x3B /
                     %x3C / %x3D / %x3E / %x3F / %x40 / %x5B /
                     %x5C / %x5D / %x5E / %x5F / %x60 / %x7B /
                     %x7C / %x7D / %x7E
date-token      = day-of-month / month / year / time / mystery
day-of-month    = 2DIGIT / DIGIT
month           = "jan" [ mystery ] / "feb" [ mystery ] /
                     "mar" [ mystery ] / "apr" [ mystery ] /
                     "may" [ mystery ] / "jun" [ mystery ] /
                     "jul" [ mystery ] / "aug" [ mystery ] /
                     "sep" [ mystery ] / "oct" [ mystery ] /
                     "nov" [ mystery ] / "dec" [ mystery ]
year            = 4DIGIT / 3DIGIT / 2DIGIT / DIGIT
time            = time-field ":" time-field ":" time-field
time-field      = 2DIGIT / DIGIT
CTLwoHTAB       = %x00-08 / %x0A-1F / %x7F
                     ; CTL except HTAB
mystery         = CTLwoHTAB / ":" / ALPHA / DIGIT / %x80-FF
                     ; any OCTET except a delimiter
