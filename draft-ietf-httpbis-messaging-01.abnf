BWS           = <BWS, see [Semantics], Section 4.3>
OWS           = <OWS, see [Semantics], Section 4.3>
RWS           = <RWS, see [Semantics], Section 4.3>
absolute-URI  = <absolute-URI, see [RFC3986], Section 4.3>
absolute-path = <absolute-path, see [Semantics], Section 2.4>
authority     = <authority, see [RFC3986], Section 3.2>
comment       = <comment, see [Semantics], Section 4.2.3>
field-name    = <field-name, see [Semantics], Section 4.2>
field-value   = <field-value, see [Semantics], Section 4.2>
obs-text      = <obs-text, see [Semantics], Section 4.2.3>
port          = <port, see [RFC3986], Section 3.2.3>
query         = <query, see [RFC3986], Section 3.4>
quoted-string = <quoted-string, see [Semantics], Section 4.2.3>
token         = <token, see [Semantics], Section 4.2.3>
uri-host      = <host, see [RFC3986], Section 3.2.2>
HTTP-message   = start-line
                      *( header-field CRLF )
                      CRLF
                      [ message-body ]

start-line     = request-line / status-line

HTTP-version  = HTTP-name "/" DIGIT "." DIGIT
HTTP-name     = %x48.54.54.50 ; "HTTP", case-sensitive

request-line   = method SP request-target SP HTTP-version CRLF

method         = token

request-target = origin-form
                    / absolute-form
                    / authority-form
                    / asterisk-form

origin-form    = absolute-path [ "?" query ]

absolute-form  = absolute-URI

authority-form = authority

asterisk-form  = "*"

status-line = HTTP-version SP status-code SP reason-phrase CRLF

status-code    = 3DIGIT

reason-phrase  = *( HTAB / SP / VCHAR / obs-text )

header-field   = field-name ":" OWS field-value OWS

obs-fold     = CRLF 1*( SP / HTAB )
                 ; obsolete line folding

message-body = *OCTET

Transfer-Encoding = 1#transfer-coding

transfer-coding    = "chunked" ; Section 7.1
                        / "compress" ; [Semantics], Section 6.1.2.1
                        / "deflate" ; [Semantics], Section 6.1.2.2
                        / "gzip" ; [Semantics], Section 6.1.2.3
                        / transfer-extension
transfer-extension = token *( OWS ";" OWS transfer-parameter )

transfer-parameter = token BWS "=" BWS ( token / quoted-string )

chunked-body   = *chunk
                      last-chunk
                      trailer-part
                      CRLF

chunk          = chunk-size [ chunk-ext ] CRLF
                      chunk-data CRLF
chunk-size     = 1*HEXDIG
last-chunk     = 1*("0") [ chunk-ext ] CRLF

chunk-data     = 1*OCTET ; a sequence of chunk-size octets

chunk-ext      = *( ";" chunk-ext-name [ "=" chunk-ext-val ] )

chunk-ext-name = token
chunk-ext-val  = token / quoted-string

trailer-part   = *( header-field CRLF )

TE        = #t-codings
t-codings = "trailers" / ( transfer-coding [ t-ranking ] )
t-ranking = OWS ";" OWS "q=" rank
rank      = ( "0" [ "." 0*3DIGIT ] )
                / ( "1" [ "." 0*3("0") ] )

Connection        = 1#connection-option
connection-option = token

Upgrade          = 1#protocol

protocol         = protocol-name ["/" protocol-version]
protocol-name    = token
protocol-version = token

BWS = <BWS, see [Semantics], Section 4.3>

Connection = *( "," OWS ) connection-option *( OWS "," [ OWS
    connection-option ] )

HTTP-message = start-line *( header-field CRLF ) CRLF [ message-body
    ]
HTTP-name = %x48.54.54.50 ; HTTP
HTTP-version = HTTP-name "/" DIGIT "." DIGIT

OWS = <OWS, see [Semantics], Section 4.3>

RWS = <RWS, see [Semantics], Section 4.3>

TE = [ ( "," / t-codings ) *( OWS "," [ OWS t-codings ] ) ]
Transfer-Encoding = *( "," OWS ) transfer-coding *( OWS "," [ OWS
    transfer-coding ] )

Upgrade = *( "," OWS ) protocol *( OWS "," [ OWS protocol ] )

absolute-URI = <absolute-URI, see [RFC3986], Section 4.3>
absolute-form = absolute-URI
absolute-path = <absolute-path, see [Semantics], Section 2.4>
asterisk-form = "*"
authority = <authority, see [RFC3986], Section 3.2>
authority-form = authority

chunk = chunk-size [ chunk-ext ] CRLF chunk-data CRLF
chunk-data = 1*OCTET
chunk-ext = *( ";" chunk-ext-name [ "=" chunk-ext-val ] )
chunk-ext-name = token
chunk-ext-val = token / quoted-string
chunk-size = 1*HEXDIG
chunked-body = *chunk last-chunk trailer-part CRLF
comment = <comment, see [Semantics], Section 4.2.3>
connection-option = token

field-name = <field-name, see [Semantics], Section 4.2>
field-value = <field-value, see [Semantics], Section 4.2>

header-field = field-name ":" OWS field-value OWS

last-chunk = 1*"0" [ chunk-ext ] CRLF
message-body = *OCTET
method = token

obs-fold = CRLF 1*( SP / HTAB )
obs-text = <obs-text, see [Semantics], Section 4.2.3>
origin-form = absolute-path [ "?" query ]

port = <port, see [RFC3986], Section 3.2.3>
protocol = protocol-name [ "/" protocol-version ]
protocol-name = token
protocol-version = token

query = <query, see [RFC3986], Section 3.4>
quoted-string = <quoted-string, see [Semantics], Section 4.2.3>

rank = ( "0" [ "." *3DIGIT ] ) / ( "1" [ "." *3"0" ] )
reason-phrase = *( HTAB / SP / VCHAR / obs-text )
request-line = method SP request-target SP HTTP-version CRLF
request-target = origin-form / absolute-form / authority-form /
    asterisk-form

start-line = request-line / status-line
status-code = 3DIGIT
status-line = HTTP-version SP status-code SP reason-phrase CRLF

t-codings = "trailers" / ( transfer-coding [ t-ranking ] )
t-ranking = OWS ";" OWS "q=" rank
token = <token, see [Semantics], Section 4.2.3>
trailer-part = *( header-field CRLF )
transfer-coding = "chunked" / "compress" / "deflate" / "gzip" /
    transfer-extension
transfer-extension = token *( OWS ";" OWS transfer-parameter )
transfer-parameter = token BWS "=" BWS ( token / quoted-string )

uri-host = <host, see [RFC3986], Section 3.2.2>

