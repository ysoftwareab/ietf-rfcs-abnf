getannotation   = "GETANNOTATION" SP entries SP attribs
					  ; new command

setannotation   = "SETANNOTATION" SP setentries
					  ; new command
annotate-data   = "ANNOTATION" SP entry-list
					  ; new response

entries         = entry-match / "(" entry-match *(SP entry-match) ")"
					  ; entry specifiers that can include wildcards

attribs         = attrib-match / "(" attrib-match *(SP attrib-match) ")"
				  	  ; attribute specifiers that can include wildcards

entry-list      = entry-att / "(" entry-att *(SP entry-att) ")"
					  ; entry, attribute-value pairs list

setentries      = entry-att *(SP entry-att)
					  ; multiple entry, attribute-value pairs list

entry-att       = entry SP "(" att-value *(SP att-value) ")"
att-value       = attrib SP value

atom-slash       = any ATOM-CHAR except "/"
atom-dot         = any ATOM-CHAR except "."

entry            = DQUOTE 1*atom-slash *("/" 1*atom-slash) DQUOTE
entry-match      = DQUOTE 1*entry-match-atom
							   *("/" 1*entry-match-atom) DQUOTE
entry-match-atom = 1*(list-wildcards / atom-slash)

attrib            = DQUOTE 1*atom-dot *("." 1*atom-dot) DQUOTE
attrib-match      = DQUOTE 1*attrib-match-atom
								*("." 1*attrib-match-atom) DQUOTE
attrib-match-atom = 1*(list-wildcards / atom-dot)

value             = nstring
    
    
