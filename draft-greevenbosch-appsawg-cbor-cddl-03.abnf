file             = 1*( structure / map / field)

field            = name  ":"  type ";" newline

name             = valid-name
type             = fixed-array / indefinite-array / valid-type

fixed-array      = valid-type "[" 1*DIGIT "]"
indefinite-array = valid-type "*"

structure        = (simple-structure / array-structure) newline
structure-body   = S "{" S 1*(field / comment) S "}"
simple-structure = name structure-body
array-structure  = "*" name structure-body

map              = name ":" map-header map-body
map-header       = map "(" (valid-type / ".") ","
                      (valid-type / ".") ")"
map-body         = S "{" newline 1*map-entry "}"

map-entry        = map-optional-entry / map-mandatory-entry / comment
map-mandatory-entry = cbor-data ":" type ";"
map-optional-entry = "?" map-mandatory-entry

cbor-data        = cbor-string / cbor-number / cbor-extension
cbor-string      = DQUOTE text DQUOTE
cbor-number      = cbor-leading-numerical / cbor-leading-dot /
                          cbor-hex-number / cbor-binary-number
cbor-leading-dot = '.' 1*DIGIT ['e' 1*DIGIT]
cbor-leading-numerical = 1*DIGIT ['.' 1*DIGIT] ['e' 1*DIGIT]
cbor-hex-number  = hex-prefix 1*HEXDIG
cbor-bin-number  = bin-prefix 1*BIT

hex-prefix       = %d48.120 ; 0x
bin-prefix       = %d48.98 ; 0b
cbor-extension   = *text-char

valid-char       = DIGIT / ALPHA / "_"
valid-name       = 1*valid-char
valid-type       = primitive-datatype / 1*valid-char

primitive-datatype = bool / bstr / float16





                      / float32 / float64 / int
                      / nint / simple / tstr / uint

S                = *(WS)
newline          = [CR] LF
text-char        = %20-7e
comment-char     = UTF8-char
comment          = "#" *(comment-char) newline
WS               =  SP / HTAB / newline / comment ; white space

bool             = %d98.111.111.108
bstr             = %d98.115.116.114
float16          = %d102.108.111.97.116.40.49.54.41 ; float(16)
float32          = %d102.108.111.97.116.40.51.50.41 ; float(32)
float64          = %d102.108.111.97.116.40.54.52.41 ; float(64)
int              = %d105.110.116
map              = %d109.97.112
nint             = %d110.105.110.116
simple           = %d115.105.109.112.108.101
tstr             = %d116.115.116.114
uint             = %d117.105.110.116































