OWS            = *( [ obs-fold ] WSP )
                    ; "optional" white space
RWS            = 1*( [ obs-fold ] WSP )
                    ; "required" white space
BWS            = OWS
                    ; "bad" white space
obs-fold       = CRLF

TEXT           = %x20-7E / %x80-FF / OWS
                    ; any OCTET except CTLs, but including OWS

tchar          = "!" / "#" / "$" / "%" / "&" / "'" / "*"
                    / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~"
                    / DIGIT / ALPHA

token          = 1*tchar

comment        = "(" *( ctext / quoted-pair / comment ) ")"
ctext          = <any TEXT excluding "(" and ")">

quoted-string  = DQUOTE *(qdtext / quoted-pair ) DQUOTE
qdtext         = <any TEXT excluding DQUOTE and "\">

quoted-text    = %x01-09 /
                      %x0B-0C /
                      %x0E-FF ; Characters excluding NUL, CR and LF
quoted-pair    = "\" quoted-text

request-header  = <request-header, defined in [Part2], Section 4>
response-header = <response-header, defined in [Part2], Section 6>


accept-params   = <accept-params, defined in [Part3], Section 6.1>
entity-body     = <entity-body, defined in [Part3], Section 4.2>
entity-header   = <entity-header, defined in [Part3], Section 4.1>


Cache-Control   = <Cache-Control, defined in [Part6], Section 16.4>
Pragma          = <Pragma, defined in [Part6], Section 16.4>
Warning         = <Warning, defined in [Part6], Section 16.6>





HTTP-Version   = HTTP-Prot-Name "/" 1*DIGIT "." 1*DIGIT
HTTP-Prot-Name = %x48.54.54.50 ; "HTTP", case-sensitive

absolute-URI   = <absolute-URI, defined in [RFC3986], Section 4.3>
authority     = <authority, defined in [RFC3986], Section 3.2>
fragment      = <fragment, defined in [RFC3986], Section 3.5>
path-abempty  = <path-abempty, defined in [RFC3986], Section 3.3>
path-absolute = <path-absolute, defined in [RFC3986], Section 3.3>
port          = <port, defined in [RFC3986], Section 3.2.3>
query         = <query, defined in [RFC3986], Section 3.4>
uri-host      = <host, defined in [RFC3986], Section 3.2.2>

relative-part = <relative-part, defined in [RFC3986], Section 4.2>
relativeURI   = relative-part [ "?" query ]

http-URI = "http:" "//" authority path-abempty [ "?" query ]

HTTP-date    = rfc1123-date / obsolete-date
obsolete-date = rfc850-date / asctime-date
rfc1123-date = wkday "," SP date1 SP time SP GMT
rfc850-date  = weekday "," SP date2 SP time SP GMT
asctime-date = wkday SP date3 SP time SP 4DIGIT
date1        = 2DIGIT SP month SP 4DIGIT
                    ; day month year (e.g., 02 Jun 1982)
date2        = 2DIGIT "-" month "-" 2DIGIT
                    ; day-month-year (e.g., 02-Jun-82)
date3        = month SP ( 2DIGIT / ( SP 1DIGIT ))
                    ; month day (e.g., Jun  2)
time         = 2DIGIT ":" 2DIGIT ":" 2DIGIT





                    ; 00:00:00 - 23:59:59
wkday        = s-Mon / s-Tue / s-Wed
                  / s-Thu / s-Fri / s-Sat / s-Sun
weekday      = l-Mon / l-Tue / l-Wed
                  / l-Thu / l-Fri / l-Sat / l-Sun
month        = s-Jan / s-Feb / s-Mar / s-Apr
                  / s-May / s-Jun / s-Jul / s-Aug
                  / s-Sep / s-Oct / s-Nov / s-Dec

GMT   = %x47.4D.54 ; "GMT", case-sensitive

s-Mon = %x4D.6F.6E ; "Mon", case-sensitive
s-Tue = %x54.75.65 ; "Tue", case-sensitive
s-Wed = %x57.65.64 ; "Wed", case-sensitive
s-Thu = %x54.68.75 ; "Thu", case-sensitive
s-Fri = %x46.72.69 ; "Fri", case-sensitive
s-Sat = %x53.61.74 ; "Sat", case-sensitive
s-Sun = %x53.75.6E ; "Sun", case-sensitive

l-Mon = %x4D.6F.6E.64.61.79          ; "Monday", case-sensitive
l-Tue = %x54.75.65.73.64.61.79       ; "Tuesday", case-sensitive
l-Wed = %x57.65.64.6E.65.73.64.61.79 ; "Wednesday", case-sensitive
l-Thu = %x54.68.75.72.73.64.61.79    ; "Thursday", case-sensitive
l-Fri = %x46.72.69.64.61.79          ; "Friday", case-sensitive
l-Sat = %x53.61.74.75.72.64.61.79    ; "Saturday", case-sensitive
l-Sun = %x53.75.6E.64.61.79          ; "Sunday", case-sensitive

s-Jan = %x4A.61.6E ; "Jan", case-sensitive
s-Feb = %x46.65.62 ; "Feb", case-sensitive
s-Mar = %x4D.61.72 ; "Mar", case-sensitive
s-Apr = %x41.70.72 ; "Apr", case-sensitive
s-May = %x4D.61.79 ; "May", case-sensitive
s-Jun = %x4A.75.6E ; "Jun", case-sensitive
s-Jul = %x4A.75.6C ; "Jul", case-sensitive
s-Aug = %x41.75.67 ; "Aug", case-sensitive
s-Sep = %x53.65.70 ; "Sep", case-sensitive
s-Oct = %x4F.63.74 ; "Oct", case-sensitive
s-Nov = %x4E.6F.76 ; "Nov", case-sensitive
s-Dec = %x44.65.63 ; "Dec", case-sensitive

transfer-coding         = "chunked" / transfer-extension
transfer-extension      = token *( OWS ";" OWS parameter )

parameter               = attribute BWS "=" BWS value
attribute               = token
value                   = token / quoted-string

Chunked-Body   = *chunk
                      last-chunk
                      trailer-part
                      CRLF

chunk          = chunk-size *WSP [ chunk-ext ] CRLF
                      chunk-data CRLF
chunk-size     = 1*HEXDIG
last-chunk     = 1*("0") *WSP [ chunk-ext ] CRLF

chunk-ext      = *( ";" *WSP chunk-ext-name
                         [ "=" chunk-ext-val ] *WSP )
chunk-ext-name = token
chunk-ext-val  = token / quoted-string
chunk-data     = 1*OCTET ; a sequence of chunk-size octets
trailer-part   = *(entity-header CRLF)

product         = token ["/" product-version]
product-version = token





HTTP-message   = Request / Response     ; HTTP/1.1 messages

generic-message = start-line
                       *(message-header CRLF)
                       CRLF
                       [ message-body ]
start-line      = Request-Line / Status-Line

message-header = field-name ":" [ field-value ]
field-name     = token
field-value    = *( field-content / OWS )
field-content  = <field content>

message-body = entity-body
                  / <entity-body encoded as per Transfer-Encoding>

general-header = Cache-Control            ; [Part6], Section 16.2
                    / Connection               ; Section 8.1
                    / Date                     ; Section 8.3
                    / Pragma                   ; [Part6], Section 16.4
                    / Trailer                  ; Section 8.6
                    / Transfer-Encoding        ; Section 8.7
                    / Upgrade                  ; Section 8.8
                    / Via                      ; Section 8.9
                    / Warning                  ; [Part6], Section 16.6

Request       = Request-Line              ; Section 5.1
                     *(( general-header        ; Section 4.5
                      / request-header         ; [Part2], Section 4
                      / entity-header ) CRLF)  ; [Part3], Section 4.1
                     CRLF
                     [ message-body ]          ; Section 4.3

Request-Line   = Method SP Request-URI SP HTTP-Version CRLF

Method         = token

Request-URI    = "*"
                    / absolute-URI
                    / ( path-absolute [ "?" query ] )
                    / authority

Response      = Status-Line               ; Section 6.1
                     *(( general-header        ; Section 4.5
                      / response-header        ; [Part2], Section 6
                      / entity-header ) CRLF)  ; [Part3], Section 4.1
                     CRLF
                     [ message-body ]          ; Section 4.3






Status-Line = HTTP-Version SP Status-Code SP Reason-Phrase CRLF

Status-Code    = 3DIGIT
Reason-Phrase  = *<TEXT, excluding CR, LF>


Connection       = "Connection" ":" OWS Connection-v
Connection-v     = 1#connection-token
connection-token = token

Content-Length   = "Content-Length" ":" OWS 1*Content-Length-v
Content-Length-v = 1*DIGIT

Date   = "Date" ":" OWS Date-v
Date-v = HTTP-date

Host   = "Host" ":" OWS Host-v
Host-v = uri-host [ ":" port ] ; Section 3.2.1

TE        = "TE" ":" OWS TE-v
TE-v      = #t-codings
t-codings = "trailers" / ( transfer-extension [ accept-params ] )

Trailer   = "Trailer" ":" OWS Trailer-v
Trailer-v = 1#field-name

Transfer-Encoding   = "Transfer-Encoding" ":" OWS
                           Transfer-Encoding-v
Transfer-Encoding-v = 1#transfer-coding

Upgrade   = "Upgrade" ":" OWS Upgrade-v
Upgrade-v = 1#product

Via               = "Via" ":" OWS Via-v
Via-v             = 1#( received-protocol RWS received-by
                             [ RWS comment ] )
received-protocol = [ protocol-name "/" ] protocol-version
protocol-name     = token
protocol-version  = token
received-by       = ( uri-host [ ":" port ] ) / pseudonym
pseudonym         = token

