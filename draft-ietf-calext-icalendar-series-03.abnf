SPLIT=YES is added to the series rule to indicate that this master is
reltypeparam = "RELTYPE" "="
                 ("PARENT"    ; Parent relationship - Default
                / "CHILD"     ; Child relationship
                / "SIBLING"   ; Sibling relationship
                / "DEPENDS-ON" ; refers to previous task
                / "REFID"     ; Relationship based on REFID
                / "STRUCTURED-CATEGORY"
                        ; Relationship based on STRUCTURED-CATEGORY
                / "FINISHTOSTART" ;  Temporal relationship
                / "FINISHTOFINISH" ;  Temporal relationship
                / "STARTTOFINISH" ;  Temporal relationship
                / "STARTTOSTART" ;  Temporal relationship
                / "SERIES-MASTER" ;  link to the master component
                / iana-token  ; Some other IANA-registered
                              ; iCalendar relationship type
                / x-name)     ; A non-standard, experimental
                              ; relationship type

                                  Figure 1

RELTYPE=PARENT  See [RFC5545].

RELTYPE=CHILD  See [RFC5545].

RELTYPE=SIBLING  See [RFC5545].

RELTYPE=DEPENDS-ON  Indicates that the current calendar component
      depends on the referenced calendar component in some manner.  For
      example a task may be blocked waiting on the other, referenced,
      task.

RELTYPE=REFID  Establishes a reference from the current component to
      components with a REFID property which matches the value given in
      the associated RELATED-TO property.

RELTYPE=SERIES-MASTER  Indicates that the current calendar component
      is based on the referenced calendar component.  The value is a
      UID.

RELTYPE=STRUCTURED-CATEGORY  Establishes a reference from the current
      component to components with a STRUCTURED-CATEGORY property which
      matches the value given in the associated RELATED- TO property.

RELTYPE=FINISHTOSTART

RELTYPE=FINISHTOFINISH

RELTYPE=STARTTOFINISH

RELTYPE=STARTTOSTART

splitparam     = "SPLIT" "="
                 ("YES"      ; The series is split
                 / "NO"      ; The series is not split (default)
                 / x-name        ; Experimental reference type
                 / iana-token)   ; Other IANA registered type
                                  Figure 6

lookahead-countparam      = "LOOKAHEAD-COUNT" "=" 1*DIGIT

                                  Figure 7

lookahead-periodparam      = "LOOKAHEAD-PERIOD" "="
                        DQUOTE dur-value DQUOTE

                                  Figure 8

seruid        = "SERIES-UID" seruidparam ":" text CRLF

seruidparam   = *(";" other-param)

                                  Figure 9

sxdate     = "SXDATE" sxdtparam ":" sxdtval *("," sxdtval) CRLF

sxdtparam  = *(
              ;
              ; The following are OPTIONAL,
              ; but MUST NOT occur more than once.
              ;
              (";" "VALUE" "=" ("DATE-TIME" / "DATE")) /
              ;
              (";" tzidparam) /
              ;
              ; The following is OPTIONAL,
              ; and MAY occur more than once.
              ;
              (";" other-param)
              ;
              )

sxdtval    = date-time / date
              ;Value MUST match value type

                                 Figure 11

sdate      = "SDATE" sdtparam ":" sdtval *("," sdtval) CRLF

sdtparam   = *(
                ;
                ; The following are OPTIONAL,
                ; but MUST NOT occur more than once.
                ;
                (";" "VALUE" "=" ("DATE-TIME" / "DATE" / "PERIOD")) /
                (";" tzidparam) /
                ;
                ; The following is OPTIONAL,
                ; and MAY occur more than once.
                ;
                (";" other-param)
                ;
                )

sdtval     = date-time / date
                ;Value MUST match value type

                                 Figure 13

serid    = "SERIES-ID" sidparam ":" sidval CRLF

sidparam   = *(
                  ;
                  ; The following are OPTIONAL,
                  ; but MUST NOT occur more than once.
                  ;
                  (";" "VALUE" "=" ("DATE-TIME" / "DATE")) /
                  (";" tzidparam) /
                  ;
                  ; The following is OPTIONAL,
                  ; and MAY occur more than once.
                  ;
                  (";" other-param)
                  ;
                  )

sidval     = date-time / date
                  ;Value MUST match value type

                                 Figure 19

last-series-i   = "LAST-SERIES-ID" lastseriesidparam  /
                    (
                      ";" "VALUE" "=" "TEXT"
                      ":" text
                    )
                    (
                      ";" "VALUE" "=" "REFERENCE"
                      ":" text
                    )
                    (
                      ";" "VALUE" "=" "URI"
                      ":" uri
                    )
                    CRLF


lastseriesidparam = *(

                  ; the following is MANDATORY
                  ; and MAY occur more than once

                  (";" relparam) /

                  ; the following are MANDATORY
                  ; but MUST NOT occur more than once

                  (";" fmttypeparam) /
                  (";" labelparam) /
                  ; labelparam is defined in ...

                  ; the following is OPTIONAL
                  ; and MAY occur more than once

                  (";" xparam)

                  )

                                 Figure 21

srule      = "SRULE" srulparam ":" recur CRLF

sruleparam = *(
                ; the following are OPTIONAL
                ; but MUST NOT occur more than once

                (";" lookahead-countparam) /
                (";" lookahead-periodparam) /

                ; the following is OPTIONAL
                ; and MAY occur more than once

                (";" xparam)

                )

                                 Figure 23

related    = "RELATED-TO" relparam ( ":" text ) /
                   (
                     ";" "VALUE" "=" "UID"
                     ":" uid
                   )
                   (
                     ";" "VALUE" "=" "URI"
                     ":" uri
                   )
                   CRLF

relparam   = *(
                  ;
                  ; The following are OPTIONAL,
                  ; but MUST NOT occur more than once.
                  ;
                  (";" reltypeparam) /
                  (";" gapparam) /
                  ;
                  ; The following is OPTIONAL,
                  ; and MAY occur more than once.
                  ;
                  (";" other-param)
                  ;
                  )

                                  Figure 24

