conf-capability-string   = namespace-uri [ parameter-list ]
parameter-list      = "?" parameter *( "&" parameter )
parameter           = package-parameter /
                              revision-parameter /
                              profile-parameter
package-parameter   = "package=" package-name
revision-parameter  = "revision=" revision-date
profile-parameter   = "profiles=" profile-list
profile-list        = profile-name *("," profile-name)

package-stmt     = optsep package-keyword sep identifier-arg-str
                          optsep
                          "{" stmtsep
                              [yangconf-version-stmt]
                              namespace-stmt
                              meta-stmts
                              revision-stmts
                              package-body-stmts
                          "}" optsep

yangconf-version-stmt   = yangconf-version-keyword sep
                                 yangconf-version-arg-str
                                 optsep stmtend

yangconf-version-arg-str = < a string that matches the rule
                                    yangconf-version-arg >

yangconf-version-arg    = "1"

namespace-stmt      = namespace-keyword sep uri-str
                             optsep stmtend

package-body-stmts  = 1*(profile-stmt stmtsep)

profile-stmt =    profile-keyword sep identifier-arg-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              [status-stmt stmtsep]
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                              *(include-profile-stmt stmtsep)
                              *(require-package-stmt stmtsep)
                              *(require-capability-stmt stmtsep)
                              *(require-module-stmt stmtsep)
                          "}")

include-profile-stmt = include-profile-keyword sep
                              identifier-arg-str stmtend

require-module-stmt = require-module-keyword sep
                         identifier-arg-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              revision-stmt stmtsep
                              [require-conformance-stmt stmtsep]
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                              *(require-feature-stmt stmtsep)
                              *(require-object-stmt stmtsep)
                          "}")

require-object-stmt = require-object-keyword sep
                         require-object-arg-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                          "}")

require-object-arg-str = < a string that matches the rule
                                 require-object-arg >

require-object-arg   = pkg-absolute-schema-nodeid

require-package-stmt = require-package-keyword sep
                         identifier-arg-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              revision-stmt stmtsep
                              [require-profile-stmt stmtsep]
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                          "}")

require-profile-stmt = require-profile-keyword sep
                         identifier-arg-str stmtend

require-capability-stmt = require-capability-keyword sep
                         uri-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                              *(require-parameter-stmt stmtsep)
                          "}")

require-parameter-stmt = require-parameter-keyword sep
                         identifier-arg-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                              *(require-value-stmt stmtsep)
                          "}")

require-value-stmt = require-value-keyword sep
                         value-arg-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                          "}")

value-arg-str = <string containing allowed value>

revision-stmt = revision-keyword sep date-arg-str stmtend

require-feature-stmt = require-feature-keyword sep
                         identifier-arg-str optsep
                         (";" /
                          "{" stmtsep
                              ;; these stmts can appear in any order
                              [description-stmt stmtsep]
                              [reference-stmt stmtsep]
                          "}")

require-conformance-stmt = require-conformance-keyword sep
                          require-conformance-arg-str stmtend

require-conformance-arg-str =
                            < a string that matches the rule
                             require-conformance-arg >

require-conformance-arg = full-keyword /
                                 augment-keyword /
                                 import-keyword /
                                 ad-hoc-keyword

pkg-schema-nodeid   = pkg-absolute-schema-nodeid /
                             pkg-descendant-schema-nodeid

pkg-absolute-schema-nodeid = 1*("/" pkg-node-identifier)
pkg-descendant-schema-nodeid =
                             pkg-node-identifier
                             pkg-absolute-schema-nodeid

pkg-node-identifier = identifier

ad-hoc-keyword              = 'ad-hoc'
augment-keyword             = 'augment'
conformance-keyword         = 'conformance'
full-keyword                = 'full'
include-profile-keyword     = 'include-profile'
package-keyword             = 'package'
profile-keyword             = 'profile'
require-capability-keyword  = 'require-capability'
require-module-keyword      = 'require-module'
require-feature-keyword     = 'require-feature'
require-package-keyword     = 'require-package'
require-parameter-keyword   = 'require-parameter'
require-profile-keyword     = 'require-profile'
require-value-keyword       = 'require-value'
revision-keyword            = 'revision'
yangconf-version-keyword    = 'yangconf-version'

