tagURI        = "tag:" taggingEntity ":" specific

taggingEntity = authorityName "," date
authorityName = DNSname / emailAddress
date          = year ["-" month ["-" day ]] ; see ISO8601 [3] &
year          = 4DIGIT
month         = 2DIGIT
day           = 2DIGIT
DNSname       = DNScomp *( "." DNScomp )  ; see RFC1035 [4]
DNScomp       = dnsChar [*(dnsChar /"-") dnsChar]
dnsChar       = alphaNum / pct-encoded ; pct-encoded from RFCXXXX
emailAddress  = 1*(alphaNum /"-"/"."/"_"/pct-encoded) "@" DNSname
alphaNum      = DIGIT / ALPHA
specific      = *( pchar / "/" / "?" ) ; pchar from RFCXXXX [1]

