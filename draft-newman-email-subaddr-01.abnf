local-part     = local-quoted / local-unquoted

local-quoted   = DQUOTE user-part-q ["+" detail-q] DQUOTE

local-unquoted = [user-part-c] ["+" [detail-c]]

user-part      = *(SAFEQ-CHAR / QUOTE-CHAR)
                    ;; This is the syntax for an unquoted user-part
                    ;; address. If it fits canonical form (user-part-c)
                    ;; it is used directly in a local-part.  Otherwise
                    ;; QUOTE-CHARs are escaped to fit quoted form.

user-part-c    = userc-word *("." userc-word)

user-part-q    = *(SAFEQ-CHAR / quoted)

userc-word     = 1*SAFE-CHAR

quoted         = "\" QUOTE-CHAR

detail-part    = *(SAFEQ-CHAR / QUOTE-CHAR / "+")
                    ;; This is the syntax for an unquoted subaddress.
                    ;; If it fits canonical form (subaddress-c) it is
                    ;; used directly in a local-part.  Otherwise
                    ;; QUOTE-CHARs are escaped to fit quoted form.

detail-c       = detailc-word *("." detailc-word)

detail-q       = *(SAFEQ-CHAR / "+" / quoted)

detailc-word   = 1*(SAFE-CHAR / "+")

QUOTE-CHAR     = DQUOTE / "\"

SAFE-CHAR      = %x21 / %x23-27 / %x2A / %x2D / %x2F-39 / %x3D /
                    %x3F / %x41-5A / %x5E-7E
                    ;; All printable US-ASCII characters except
                    ;; space, plus "+" and <specials> as defined
                    ;; in [IMAIL]

SAFEQ-CHAR     = %x20-21 / %x23-2A / %x2C-5B / %x5D-7E
                    ;; All printable US-ASCII characters except
                    ;; plus, quote and backslash.
