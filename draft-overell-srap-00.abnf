response =     *(3DIGIT "-" *UTF-8-char CRLF)
                     3DIGIT [SP *UTF-8-char] CRLF

UTF-8-char =   <defined by [UTF-8]>
greeting =     "220" "-" *UTF-8-char CRLF
                     *("220" "-" capability CRLF)
                     "220" SP capability CRLF

capability =   "AUTH" "=" mechanism
                     / "LANG" "=" Language-Tag

mechanism =    <registered SASL mechanism>

Language-Tag = <defined in [LANG]>

ALERT-command ="ALERT" CRLF
                     *(*CHAR CRLF)
                     "." CRLF

AUTH-command = "AUTH" SP mechanism
                     [SP encoded-challenge] CRLF
AUTH-response =*("334" "-" encoded-response CRLF)
                     "334" [SP encoded-response] CRLF

AUTH-challenge =([encoded-challenge] / "*") CRLF

encoded-challenge = base64

encoded-response = base64

base64 =       *(4base64_char) [base64_terminal]

base64_char =  %30-39 / "+" / "/" / %x41-5A / %x61-7A

base64_terminal = (2base64_char "==") / (3base64_char "=")

NOP-command =  "NOP" CRLF

QUIT-command = "QUIT" CRLF

