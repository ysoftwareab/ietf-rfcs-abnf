reginfo-bulk-body  = version HTAB doc-state CR *reginfo-bulk-entry
reginfo-bulk-entry = delim-char simple-regex delim-char contact
                        delim-char HTAB id HTAB event
                        [ HTAB state [ HTAB expires
                        [ HTAB [ qvalue ] [ HTAB [ retry-after ]
                        [ HTAB [ callid ] [ HTAB [ cseq ]
                        [ HTAB [ duration-reg]
                        [ HTAB [ unknown-params ]
                        [ HTAB display-name ] ] ] ] ] ] ] ] ] CR
version            = 1 * DIGIT
doc-state          = "full" / "partial"
delim-char         = "/" / "!" / ... <Any non-digit or non-flag
                        character other than backslash '\'. All
                        occurances of a delim_char in a
                        reginfo-bulk-entry must be the same
                        character.>
utf8-display-str   = * ( %x20-7F /
                           ( %xC2-DF %x80-BF ) /
                           ( %xE0-EF 2 ( %x80-BF ) ) /
                           ( %xF0-F4 3 ( %x80-BF ) ) )
simple-regex       = 1 * ( OCTET )
contact            = 1 * ( OCTET / backref )
backref            = "\" %x31-39
id                 = display-char
event              = "registered" / "created" / "refreshed" /
                        "shortened" / "expired" / "deactivated" /
                        "probation" / "unregistered" / "rejected"
state              = "init" / "active" / "terminated"
expires            = 1 * DIGIT
qvalue             = "1.0" / ( "0." 1*4 DIGIT)
retry-after        = 1 * DIGIT
callid             = 1 * VCHAR
cseq               = 1 * DIGIT
duration-reg       = 1 * DIGIT
unknown-params     = 1 * VCHAR
display-name       = utf8-display-str

reginfo-err-body = 1 * ( simple-regex CR )


