NSS           = std-nss

      All URNs conforming to this specification begin the NSS with the
      prefix "std:", to denote the restriction to documents developed by
      the ISO standards development procedures as defined in the ISO/IEC
      Directives, Part 1 [ISODIR-1] and the ISO Supplement [ISODIR-S].
      Prefixes that identify ISO objects of other kinds may be defined
      in future revisions of this specification.

std-nss       = "std:" docidentifier *supplements *docelement
                   [additions]

      The prefix "std:" distinguishes an "std-nss".  An std-nss
      identifies the ISO document that is designated by the
      "docidentifier", as extended or modified by any identified
      "supplements".  (An std-nss that identifies all parts of a
      multipart ISO document is a special case as described under the
      element "partnumber".)  If the std-nss contains an "additions"
      element, the NSS identifies a resource extracted from the ISO
      document or otherwise associated with it (see below).

docidentifier = originator [":" type] ":" docnumber [":" partnumber]
                   [[":" status] ":" edition]
                   [":" docversion] [":" language]

      "docidentifier" provides the complete identification of an ISO
      document.  Each of its component elements is described below.

originator    = "iso" / "iso-iec" / "iso-cie" / "iso-astm" /
                   "iso-ieee" / "iec"

      "originator" is the organization (usually an international body)
      from which a document emanates.

      Current values:

iso      = International Organization for Standardization

iec      = International Electrotechnical Commission (IEC), or
                 Commission internationale electrotechnique (CIE)

iso-iec  = jointly developed by ISO and IEC

iso-cie  = jointly developed by ISO and the Commission
                 internationale d'eclairage (CEI)

iso-astm = jointly developed by ISO and the American Society for
                 Testing and Materials International (ASTM)

iso-ieee = jointly developed by ISO and the Institute for
                 Electrical and Electronics Engineers (IEEE)

type          = "data" / "guide" / "isp" / "iwa" /
                   "pas" / "r" / "tr" / "ts" / "tta"

      "type" designates the ISO deliverable type.  If the "type" element
      is not present, the classification is "international standard".
      Other current values:

data  = Data (document type no longer published)
guide = Guide

isp   = International Standardized Profile

iwa   = International Workshop Agreement

pas   = Publicly Available Specification

r     = Recommendation (document type no longer published)

tr    = Technical Report

ts    = Technical Specification

tta   = Technology Trends Assessment

docnumber     = DIGITS

      "docnumber" is the reference number assigned to the document by
      ISO and/or IEC.  An ISO document may comprise a single document,
      or two or more separate parts each of which is identified by
      "partnumber".

partnumber    = "-" 1*( DIGIT / ALPHA / "-" )

      "partnumber" is the reference number that identifies a part of a
      multipart standard.

      Where it is required to refer to a multipart ISO document in its
      entirety, this can be designated by omitting the "partnumber"
      element.  However, this precludes the possibility of using any
      further elements except "additions".

      _NOTE_ The option to refer to a multipart ISO document by omitting
      the "partnumber" element has been included to align with the
      provision in the ISO/IEC Directives, Part 2, 2004 [ISODIR-2]
      subclause 6.2.2 of making an undated reference to all parts of an
      ISO document.  It is only permissible to use this option where the
      URN is referring to a multipart ISO document in its entirety.
      Since the use of this option precludes the designation of the
      elements "status" and "edition", it is implicit that the URN needs
      to remain valid irrespective of any future changes to the
      multipart document (see the rules for undated references given in
      the ISO/IEC Directives, Part 2, 2004 [ISODIR-2] subclause
      6.6.7.5.2).  This shall be taken into consideration in the use
      (and maintenance) of any URN specification employing this option.
      _NOTE_ In the case where the multipart document comprises
      different types of ISO deliverable, the "type" of the core part
      (usually part 1) applies.  See the example "Reference to a
      resource related to all parts of a multipart document".

      Except for the case where it is required to refer to a multipart
      document in its entirety, the element "partnumber" is required if
      the identified resource is a part of an ISO document.  Otherwise,
      this element is not used.

status        = ( "draft" / "cancelled" ) / stage

      "status" indicates the publication status of the document.  When
      the "status" element is not present, the NSS refers to a published
      document.  Other values:

draft     = document that has not yet been accepted for
                  publication by international ballot

cancelled = document that has been deleted or withdrawn

stage         = "stage-" stagecode ["." iteration]

      "stage" indicates the stage code and iteration of the document.

stagecode     = DIGIT DIGIT "."  DIGIT DIGIT

      "stagecode" is the harmonized stage code in accordance with ISO
      Guide 69:1999, "Harmonized Stage Code system (Edition 2) --
      Principles and guidelines for use" [ISOGUIDE69].

iteration     = "v" DIGITS

      "iteration" is a sequential number which refers to a specific
      iteration of the project's lifecycle through the designated stage

      If no "iteration" is specified the reference is to the highest
      iteration available for the specified stagecode.

      _NOTE_ In the ISO Central Secretariat project management database
      the "iteration" is referred to as the "project version".

edition       = "ed-" DIGITS

      "edition" designates a specific edition of the document.  (DIGITS
      is the (sequential) edition number.)  If no "edition" is
      specified, the NSS refers to the latest edition.
docversion    = "v" (simpleversion / isoversion)

simpleversion = DIGITS

      "docversion" designates the version number of a document's
      "edition".  It is altered by correction (corrected version;
      Technical Corrigendum) or amendment (Amendment; Addendum) and is
      distinct from a revision, which changes the edition number.

      In the "simpleversion", the first version published is "1", and
      each subsequent correction or amendment increases the version
      number by 1.

      If no "docversion" is specified, the reference is to the highest
      version number available for the denoted "edition".

      Current values of "simpleversion":

         1 - first version published

         2 - corrected version published

isoversion    = baseversion *includedsuppl

baseversion   = DIGITS

includedsuppl = "-" suppltype supplnumber [ "." supplversion ]

      An "isoversion" can be linked to a simpleversion by defining an
      existing simpleversion as baseversion and listing all the
      "supplements" (corrections and amendments) incorporated into that
      version.

      Examples for the "isoversion" (internal ISO version) scheme:

         1 = first version of standard

         1-amd1.v1 = first version of standard incorporating first
         version of Amendment 1

         1-amd1.v1-amd2.v1 = first version of standard incorporating
         first version of Amendment 1 and first version of Amendment 2

         1-amd1.v2-amd2.v1-amd3 = first version of standard
         incorporating corrected version of Amendment 1, first version
         of Amendment 2, and highest version of Amendment 3
         1-cor3 = first version of standard incorporating highest
         version of Technical Corrigendum 3

         1-amd1-cor3 = first version of standard incorporating highest
         version of Amendment 1 and highest version of Technical
         Corrigendum 3

language      = monolingual / bilingual / trilingual

monolingual   = "en" / "fr" / "ru" / "es" / "ar"

bilingual     = "en-fr" / "en-ru" / "fr-ru"

trilingual    = "en-fr-ru"

      "language" designates the official ISO language(s), or the
      language of an official translation, in which the document
      (object) is processed and published by ISO (excluding languages
      which constitute only specific elements of the content).  The
      value is one or more alpha-2 codes, each of which designates a
      language, as specified in ISO 639-1 [ISO639-1].  If no language
      element is specified, "en" is assumed.

supplements   = ":" suppltype ":" supplnumber
                   [":" supplversion ] [":" language ]

suppltype     = "amd" / "cor" / "add"

supplnumber   = DIGITS

supplversion  = "v" DIGITS

      "supplements" designate technical alterations of or additions to
      an ISO standard that do not result in a new "edition" or
      "version".  Supplements are of three types, designated by
      "suppltype":

amd = Amendment -- a document that alters and/or adds to
            previously agreed technical provisions in an existing ISO
            document; an amendment is subject to acceptance by ballot in
            accordance with the ISO/IEC Directives, Part 1, 2004
            [ISODIR-1] subclause 2.10.3

cor = Technical Corrigendum -- a document that corrects a
            technical error or ambiguity, or updates the ISO document in
            such a way that the modification has no effect on the
            technical normative elements; a Technical Corrigendum is not
            balloted -- see the ISO/IEC Directives, Part 1, 2004
            [ISODIR-1] subclause 2.10.2

add = Addendum -- (document type no longer published) Addenda were
            documents that changed (by correction, addition or deletion)
            the technical requirements of an ISO document; an addendum
            was subject to acceptance by ballot in accordance with the
            ISO/IEC Directives, Part 1.  (Addenda are included in this
            RFC because some of them are still valid.)

docelement    = clause / term

clause        = ":clause:" clauseno / clauserange
                   *( "," clauseno / clauserange )

clauseno      = ( ALPHA / DIGITS ) *( "."  DIGITS )

clauserange   = clauseno "-" clauseno

term          = ":term:" termno / termrange
                   *( "," termno / termrange)

termno        = ( ALPHA / DIGITS ) *( "."  DIGITS )

termrange     = termno "-" termno

      "docelement" identifies one or more numbered subdivisions of a
      document.  Types of numbered subdivision are specified in the ISO/
      IEC Directives, Part 2 [ISODIR-2].  This RFC currently specifies
      forms for reference to terms and clauses only.  Revisions of this
      specification may define additional values.

      "clause" represents the selection of one or more clauses or
      subclauses of the specification.  The form "clauseno HYPHEN
      clauseno" designates a range of (sub)clauses and the form
      "clauseno COMMA clauseno" a list.  A list can contain ranges.

      "clauseno" designates one clause or subclause in a specification.
      When the first character of a "clauseno" is a digit, the reference
      is to the clause designated by that digit string, and to the
      subclause designated by any additional digit strings separated by
      periods.  When the first character of a "clauseno" is
      alphabetical, the reference is to the corresponding Annex, and to
      the (sub)clauses designated by additional digit strings.

      "term" represents the selection of one or more consecutive terms
      of the specification.  The form "termno HYPHEN termno" designates
      a range of terms and the form "termno COMMA termno" a list.  A
      list can contain ranges.

      "termno" designates one term in a specification.  When the first
      character of a "termno" is a digit, the reference is to the term
      designated by that digit string and by any additional digit
      strings separated by periods.  When the first character of a
      "termno" is alphabetical, the reference is to the corresponding
      Annex, and to the terms designated by additional digit strings.

additions     = techdefined / isodefined

techdefined   = ":tech" *techelement

techelement   = ; unspecified

isodefined    = ; unspecified

      "additions" are additional elements of the NSS intended to
      identify a representation of an ISO document, an extract from an
      ISO document, or some related information set, as a resource in
      its own right.

      "techdefined" represents an associated or embedded resource
      defined by the committee that develops or maintains the identified
      document.  All such "additions" begin with the keyword "tech", but
      the further syntax is defined by the committee.

      "isodefined" represents associated or embedded resources defined
      by the ISO Central Secretariat.  The definition of "additions"
      beginning with any symbol other than "tech" is reserved to the ISO
      Central Secretariat.

      The syntax of these additions is not specified in this RFC.
      Specific syntax for these additions will be specified as needed by
      the ISO Central Secretariat, or by the individual Committee that
      has the responsibility for developing or maintaining the
      identified document.  (See further discussion under Process for
      Identifier Resolution below.)

DIGITS        = DIGIT *DIGIT

DIGIT         = "0" / "1" / "2" / "3" / "4" /
                   "5" / "6" / "7" / "8" / "9"

ALPHA         = "A" / "B" / "C" / "D" / "E" / "F" / "G" /
                   "H" / "I" / "J" / "K" / "L" / "M" / "N" /
                   "O" / "P" / "Q" / "R" / "S" / "T" / "U" /
                   "V" / "W" / "X" / "Y" / "Z"

