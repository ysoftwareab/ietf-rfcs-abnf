ircURL   = type "://" location "/" [ channel ] [ options ]

type     = "irc" / "ircs"

location = [ authinfo "@" ] hostport
		     ; See Section 3.2.2 of [RFC2396] for the
		     ; definition of 'hostport'.

authinfo = [ nickname *2( "," nickname ) ] [ ":" password ]

nickname = *( escaped / unreserved )
		     ; Further restrictions may apply upon connection,
		     ; depending on the server.	 Some common nickname
		     ; characters must be encoded as per
		     ; recommendations in Section 2.4.3 of [RFC2396].

password = *( escaped / unreserved )

channel  = [ "#" / "&" ] optparam [ "," optparam ]
		     ; The definition of 'channel' is the same as
		     ; 'optvalue' for the 'channel' option (see Section
		     ; 2.6.1), except that the two most common channel
		     ; prefixes, "#" and "&", may be used
		     ; without escapes.

channame = optparam

chankey  = optparam

options  = "?" option *( "&" option )

option   = optname [ "=" optvalue ]

optname  = *( ALPHA )
		     ; Option names are case-insensitive.

optvalue = optparam *( "," optparam )

optparam = *( escaped / unreserved )

optvalue = name [ "," key ]

