Link            = link-value-list
link-value-list = [ link-value *[ "," link-value ]]
link-value     = "<" URI-Reference ">" *( ";" link-param )
link-param     = ( ( "rel" "=" relation-types )
                 / ( "anchor" "=" <"> URI-Reference <"> )
                 / ( "rev" "=" relation-types )
                 / ( "hreflang" "=" Language-Tag )
                 / ( "media" "=" ( MediaDesc / ( <"> MediaDesc <"> ) ) )
                 / ( "title" "=" quoted-string )
                 / ( "title*" "=" ext-value )
                 / ( "type" "=" ( media-type / quoted-mt ) )
                 / ( "rt" "=" relation-types )
                 / ( "if" "=" relation-types )
                 / ( "sz" "=" cardinal )
                 / ( link-extension ) )
link-extension = ( parmname [ "=" ( ptoken / quoted-string ) ] )
                 / ( ext-name-star "=" ext-value )
ext-name-star  = parmname "*" ; reserved for RFC2231-profiled
                                ; extensions.  Whitespace NOT
                                ; allowed in between.
ptoken         = 1*ptokenchar
ptokenchar     = "!" / "#" / "$" / "%" / "&" / "'" / "("
                 / ")" / "*" / "+" / "-" / "." / "/" / DIGIT
                 / ":" / "<" / "=" / ">" / "?" / "@" / ALPHA
                 / "[" / "]" / "^" / "_" / "`" / "{" / "|"
                 / "}" / "~"
media-type     = type-name "/" subtype-name
quoted-mt      = <"> media-type <">
relation-types = relation-type
                 / <"> relation-type *( 1*SP relation-type ) <">
relation-type  = reg-rel-type / ext-rel-type
reg-rel-type   = LOALPHA *( LOALPHA / DIGIT / "." / "-" )
ext-rel-type   = URI
cardinal       = "0" / ( %x31-39 *DIGIT )
LOALPHA        = <defined in RFC2616>
quoted-string  = <defined in RFC2616>
URI            = <defined in RFC3986>
URI-Reference  = <defined in RFC3986>
type-name      = <defined in RFC4288>
subtype-name   = <defined in RFC4288>
MediaDesc      = <defined in W3C.REC-html401-19991224>
Language-Tag   = <defined in RFC5646>
ext-value      = <defined in RFC5987>
parmname       = <defined in RFC5987>
filter-query   = resource-param "=" query-pattern
resource-param = "href" / parmname
query-pattern  = search-token [ "*" ]
search-token   = *search-char
search-char    = unreserved / pct-encoded
                      / ":" / "@"   ; from pchar
                      / "/" / "?"   ; from query
                      / "!" / "$" / "'" / "(" / ")"
                      / "+" / "," / ";" / "="  ; from sub-delims
parmname       = <defined in RFC5987>
pct-encoding   = <defined in RFC3986>
unreserved     = <defined in RFC3986>


rel=) (#199)

