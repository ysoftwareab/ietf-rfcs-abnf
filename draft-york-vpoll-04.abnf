statvalue-poll = "IN-PROCESS"
                            / "COMPLETED"  ; Poll has closed,
                                           ; nothing has been chosen yet
                            / "CONFIRMED"  ; Poll has closed and
                                           ; winning items confirmed
                            / "SUBMITTED"  ; The winning item has been
                                           ; submitted
                            / "CANCELLED"

requirededparam = "REQUIRED"  "=" ("TRUE" / "FALSE")
                                              ; Default is FALSE

stayinformedparam = "STAY-INFORMED"  "=" ("TRUE" / "FALSE")
                       ; Default is FALSE

acceptresponse = "ACCEPT-RESPONSE" acceptresponseparams ":"
                          iana-token ("," iana-token) CRLF

acceptresponseparams = *(";" other-param)

POLL-COMPLETION=SERVER-SUBMIT allows the client to set the poll-
poll-completion = "POLL-COMPLETION" pcparam ":" pcvalue CRLF

pcparam = *(";" other-param)

pcvalue = "SERVER"  ; The server is responsible for both choosing and
                       ; submitting the winner(s)
            / "SERVER-SUBMIT" ; The server is responsible for
                       ; submitting the winner(s). The client chooses.
            / "SERVER-CHOICE"  ; The server is responsible for
                       ; choosing the winner(s). The client will submit.
            / "CLIENT" ; The client is responsible for both choosing and
                       ; submitting the winner(s)
            / iana-token
            / x-name
            ;Default is CLIENT

pollitemid = "POLL-ITEM-ID" pollitemdparams ":"
                        integer CRLF

pollitemidparams = *(
                         (";" other-param)
                  )


POLL=ITEM-ID.

pollmode = "POLL-MODE" pollmodeparams ":"
                 ("BASIC" / iana-token / other-token) CRLF

pollmodeparams = *(";" other-param)


pollproperties = "POLL-PROPERTIES" pollpropparams ":"
                 text *("," text) CRLF

pollpropparams = *(";" other-param)
pollwinner = "POLL-WINNER" pollwinnerparams ":"
                       integer CRLF

pollwinnerparams = *(";" other-param)

             ; Used with a STATUS:CONFIRMED VPOLL to indicate which
             ; components have been confirmed


reply-url = "REPLY-URL" reply-urlparams ":" uri CRLF

reply-urlparams = *(
                        (";" requiredparam) /
                        (";" other-param)
                        )

response = "RESPONSE" response-params ":" integer CRLF
                       ; integer value 0..100

responseparams = *(";" other-param)

voter = "VOTER" voterparams ":" cal-address CRLF

voterparam   = *(
                       ;
                       ; The following are OPTIONAL,
                       ; but MUST NOT occur more than once.
                       ;
                       (";" cutypeparam) / (";" memberparam) /
                       (";" roleparam) /
                       (";" rsvpparam) / (";" deltoparam) /
                       (";" delfromparam) / (";" sentbyparam) /
                       (";" cnparam) / (";" dirparam) /
                       (";" languageparam) /
                       (";" stayinformedparam) /

                       ;
                       ; The following are OPTIONAL, but MUST NOT occur
                       ; more than once. They are defined in RFC6638
                       ;
                       (";" scheduleagentparam) /
                       (";" scheduleforcesendparam) /
                       (";" schedulestatusparam) /

                       ;
                       ; The following is OPTIONAL,
                       ; and MAY occur more than once.
                       ;
                       (";" other-param)
                       ;
                 )


pollc    = "BEGIN" ":" "VPOLL" CRLF
                  pollprop
                  *voterc *eventc *todoc *journalc *freebusyc
                  *availabilityc *alarmc *iana-comp *x-comp
                  "END" ":" "VPOLL" CRLF

pollprop = *(
                ;
                ; The following are REQUIRED,
                ; but MUST NOT occur more than once.
                ;
                dtstamp / uid / organizer /
                ;
                ; The following are OPTIONAL,
                ; but MUST NOT occur more than once.
                ;
                acceptresponse / class / created / completed /
                description / dtstart / last-mod / pollmode /
                pollproperties / priority / seq / status /
                summary / url /
                ;
                ; Either 'dtend' or 'duration' MAY appear in
                ; a 'pollprop', but 'dtend' and 'duration'
                ; MUST NOT occur in the same 'pollprop'.
                ; 'duration' MUST only occur when 'dtstart'
                ; is present
                ;
                dtend / duration /
                ;
                ; The following are OPTIONAL,
                ; and MAY occur more than once.
                ;
                attach / categories / comment /
                contact / rstatus / related /
                resources / x-prop / iana-prop
                ;
                ; The following is OPTIONAL, it SHOULD appear
                ; once for the confirmation of a BASIC mode
                ; VPOLL. Other modes may define differing
                ; requirements.
                ;
                pollwinner /
                ;
                )
voterc   = "BEGIN" ":" "VVOTER" CRLF
                 voterprop
                 *votec *iana-comp *x-comp
                 "END" ":" "VVOTER" CRLF

voterprop = *(
                 ;
                 ; The following are REQUIRED,
                 ; but MUST NOT occur more than once.
                 ;
                 dtstamp  / voter /
                 ;
                 ; The following are OPTIONAL,
                 ; but MUST NOT occur more than once.
                 ;
                 created / description / last-mod / seq /
                 status / summary / url /
                 ;
                 ; The following are OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 attach / categories / comment /
                 contact / rstatus / related /
                 resources / x-prop / iana-prop
                 ;
                 )

votec     = "BEGIN" ":" "VOTE" CRLF
                  voteprop
                  *eventc *todoc *journalc *freebusyc
                  *availabilityc *alarmc *iana-comp *x-comp
                  "END" ":" "VOTE" CRLF

voteprop = *(
                 ;
                 ; The following are REQUIRED,
                 ; but MUST NOT occur more than once.
                 ;
                 pollitemid / response /
                 ;
                 ; The following are OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 comment / x-prop / iana-prop
                 ;
                 )

end="20121205T000000Z"/>
