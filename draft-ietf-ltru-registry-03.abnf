Language-Tag = (lang
                   *3("-" extlang)
                   ["-" script]
                   ["-" region]
                   *("-" variant)
                   *("-" extension)
                   ["-" privateuse])
                   / privateuse         ; private-use tag
                   / grandfathered      ; grandfathered registrations

lang            = 2*4ALPHA           ; shortest ISO 639 code
                   / registered-lang
extlang         = 3ALPHA             ; reserved for future use
script          = 4ALPHA             ; ISO 15924 code
region          = 2ALPHA             ; ISO 3166 code
                   / 3DIGIT             ; UN country number
variant         =  5*8alphanum       ; registered variants
                   / ( DIGIT 3alphanum )
extension       = singleton 1*("-" (2*8alphanum))
privateuse      = ("x"/"X") 1*("-" (1*8alphanum))
singleton       = %x41-57 / %x59-5A / %x61-77 / %x79-7A / DIGIT
                   ; "a"-"w" / "y"-"z" / "A"-"W" / "Y"-"Z" / "0"-"9"
                   ; Single letters: x/X is reserved for private use
registered-lang = 4*8ALPHA          ; registered language subtag
grandfathered   = 1*3ALPHA 1*2("-" (2*8alphanum))
                                       ; grandfathered registration
                                       ; Note: i is the only singleton
                                       ; that starts a grandfathered tag
alphanum        = (ALPHA / DIGIT)   ; letters and numbers

                        Figure 1: Language Tag ABNF

language      = 3
extlang1      = 4
extlang2      = 4 (unlikely: needs prefix="language-extlang1")
extlang3      = 4 (extremely unlikely)
script        = 5
region        = 4 (UN M.49)
variant1      = 9
variant2      = 9 (unlikely: needs prefix="language-variant1")
total         = 62 characters

                  Figure 2: Derviation of the Longest Tag


registry   = record *("%%" CRLF record)
record     = 1*( field-name *SP ":" *SP field-body CRLF )
field-name = *(ALPHA / DIGIT / "-")
field-body = *(ASCCHAR/LWSP)
ASCCHAR    = %x21-25 / %x27-7E / UNICHAR ; Note: AMPERSAND is %x26
UNICHAR    = "&#x" 2*6HEXDIG ";"

