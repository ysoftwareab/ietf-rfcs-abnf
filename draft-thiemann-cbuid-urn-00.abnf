cbuid-nss   = type-spec ":" hash *1(":" type-specific-extension)
type-spec   = "*" / media-type *parameter
parameter   = ";" "mode" "=" 1*DIGIT / ";" token "=" token
token       = 1*(ALPHA / DIGIT)
hash        = hash-scheme ":" hash-value *("/" hash-value)
hash-scheme = "md5" / "sha1" / "hash127" / hash-token
hash-token  = token
hash-value  = "*" / 1*HEXDIG

         The following are comments and restrictions not captured by the
         above grammar.

         A <media-type> is any MIME media type [RFC2046] which is
         registered in the appropriate IANA registry [IANA-MT].

         A <parameter> is a pair of a parameter name and a parameter
         value.  If the parameter name is "mode" then the parameter
         value is a string of digits.

         A <hash-scheme> is an alphanumeric token.  A <hash-value> may
         be "*" (unspecific) or it is a non-empty sequence of hexdigits
         whose value as a sequence of bits must be a valid hash for the
         specified hash-scheme.  If hash-scheme is "md5" or "hash127",
         then each specified hash-value consists of 32 HEXDIG.  If hash-
         scheme is "sha1", then each specified hash-value consists of 40
         HEXDIG.

         The "mode" parameter MUST specify the number of additional
         <hash-value>s.  Its value defaults to 0 if the parameter is
         omitted.  That is, mode=0 (or mode parameter omitted) means
         that there is one <hash-value>, for mode=1 there are two <hash-
         value>s, and so on.

         If the type specification is "*" (unspecific), then the hash
         part MUST specify exactly one <hash-value>, the "mode"
         parameter MUST be 0, and there MUST NOT be a <type-specific-
         extension>.  If a type is specified, then more than one <hash-
         value> MAY BE given, the "mode" parameter MUST specify the
         number of extra <hash-value>s, and a <type-specific-extension>
         MAY BE allowed.

         If an identifier contains only one <hash-value>, then this
         <hash-value> MUST NOT be "*" (unspecific).

         Examples:

           urn:cbuid:*:md5:5307d294b6ccd9854f2deed8c1628b72

           urn:cbuid:*:sha1:7660c8efbe7f656ce7612636c83a138c085bad3f

           urn:cbuid:message/rfc822:md5:5307d294b6ccd9854f2deed8c1628b72

           urn:cbuid:message/rfc822;mode=1:md5:*
                                      /d97a43ed7125019c363b00bd27411fa7

           urn:cbuid:message/rfc822;mode=1:md5
                                      :b260fb53d7ec3b530e5a6332763a2bfb
                                      /d97a43ed7125019c363b00bd27411fa7


         In the last two examples, the linebreak and whitespace before
         the ":" and the "/" are inserted for editorial reasons, they
         are not part of the URN.

