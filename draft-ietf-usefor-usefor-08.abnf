token         = <see RFC2045 Section 5.1>
dot-atom-text = <see RFC2045 Section 5.1>

parameter     = <see RFC2231 Section 7>
attribute     = <see RFC2231 Section 7>

FWS           = <see RFC2822 Section 3.2.3>
CFWS          = <see RFC2822 Section 3.2.3>
atext         = <see RFC2822 Section 3.2.4>
dot-atom-text = <see RFC2822 Section 3.2.4>
phrase        = <see RFC2822 Section 3.2.6>
utext         = <see RFC2822 Section 3.2.6>
date-time     = <see RFC2822 Section 3.3>
mailbox       = <see RFC2822 Section 3.4>
mailbox-list  = <see RFC2822 Section 3.4>
address-list  = <see RFC2822 Section 3.4>
fields        = <see RFC2822 Section 3.6>

IPv6address   = <see RFC3986 Section 3.2.2>
IPv4address   = <see RFC3986 Section 3.2.2>

ALPHA         = <see RFC4234 Appendix B.1>
CRLF          = <see RFC4234 Appendix B.1>
DIGIT         = <see RFC4234 Appendix B.1>
DQUOTE        = <see RFC4234 Appendix B.1>
SP            = <see RFC4234 Appendix B.1>

unstructured    =  *WSP utext *( [FWS] utext ) *WSP

         NOTE: The [RFC2822] specification sometimes uses [FWS] at the
         beginning or end of ABNF describing header field content.  This
         specification uses *WSP in such cases, also in cases where this
         specification redefines constructs from [RFC2822].  This is
         done for consistency with the restriction described here, but
         the restriction applies to all header fields, not just those
         where ABNF is defined in this document.

fields          =/ *( newsgroups /
                         path /
                         injection-date /
                         followup-to /
                         expires /
                         control /
                         supersedes /
                         distribution /
                         summary /
                         approved /
                         organization /
                         xref /
                         archive /
                         user-agent /
                         injection-info /
                         lines )

from            =  "From:" SP mailbox-list CRLF

orig-date       =  "Date:" SP date-time CRLF

      NOTE: This specification does not change [RFC2822], which says
      that agents MUST NOT generate <date-time> constructs which include
      any zone names defined by <obs-zone>.

message-id      =  "Message-ID:" SP *WSP msg-id *WSP CRLF

msg-id          =  "<" id-left "@" id-right ">"
                      ; maximum length is 250 octets

id-left         =  dot-atom-text / no-fold-quote

id-right        =  dot-atom-text / no-fold-literal

no-fold-quote   =  DQUOTE
                         ( "." *mqtext /
                           *mqtext "." /
                           *mqtext mqspecial *mqtext )
                         DQUOTE

mqtext          =  atext / "." / mqspecial

mqspecial       =  "(" / ")" /      ; same as specials except
                      "<" /            ; "\" and DQUOTE quoted
                      "[" / "]" /      ; "." doubled and ">" omitted
                      ":" / ";" /
                      "@" / "," /
                      ".." / "\\" / "\" DQUOTE

no-fold-literal =  "[" *( mdtext / "\[" / "\]" / "\\" ) "]"

mdtext          =  %d33-61 /        ; The rest of the US-ASCII
                      %d63-90 /        ; characters not including
                      %d94-126         ; ">", "[", "]", or "\"

subject         =  "Subject:" SP unstructured CRLF

newsgroups      =  "Newsgroups:" SP newsgroup-list CRLF

newsgroup-list  =  *WSP newsgroup-name
                      *( [FWS] "," [FWS] newsgroup-name ) *WSP

newsgroup-name  =  component *( "." component )

component       =  1*component-char

component-char  =  ALPHA / DIGIT / "+" / "-" / "_"

path            =  "Path:" SP *WSP path-list tail-entry *WSP CRLF

path-list       =  *( path-identity [FWS] [path-diagnostic] "!" )

path-diagnostic =  diag-match / diag-other / diag-deprecated

diag-match      =  "!"                          ; another "!"

diag-other      =  "!." diag-keyword [ "." diag-identity ] [FWS]

diag-deprecated =  "!" IPv4address [FWS]

diag-keyword    =  1*ALPHA                      ; see USEPRO

diag-identity   =  path-identity / IPv4address / IPv6address

tail-entry      =  path-nodot
                      ; may be the string "not-for-mail"

path-identity   =  ( 1*( label "." ) toplabel ) / path-nodot

path-nodot      =  1*( alphanum / "-" / "_" )   ; legacy names

label           =  alphanum [ *( alphanum / "-" ) alphanum ]

toplabel        =  ( [ label *( "-" ) ] ALPHA *( "-" ) label ) /
                      ( label *( "-" ) ALPHA [ *( "-" ) label ] ) /
                      ( label 1*( "-" ) label )

alphanum        =  ALPHA / DIGIT                ; compare RFC3696

sender          =  "Sender:" SP mailbox CRLF

reply-to        =  "Reply-To:" SP address-list CRLF

comments        =  "Comments:" SP unstructured CRLF

keywords        =  "Keywords:" SP phrase *("," phrase) CRLF

injection-date  =  "Injection-Date:" SP date-time CRLF


references      =  "References:" SP [CFWS] msg-id *(CFWS msg-id)
                      [CFWS] CRLF

followup-to     =  "Followup-To:" SP ( newsgroup-list / poster-text )
                      CRLF

poster-text     =  *WSP %d112.111.115.116.101.114 *WSP
                      ; "poster" in lower-case

expires         =  "Expires:" SP date-time CRLF

control         =  "Control:" SP *WSP control-command *WSP CRLF

control-command =  verb *( 1*WSP argument )

verb            =  token

argument        =  1*( %x21-7E )

supersedes      =  "Supersedes:" SP *WSP msg-id *WSP CRLF

      NOTE: There is no "c" in Supersedes.

      NOTE: The Supersedes header field defined here has no connection
      with the Supersedes header field that sometimes appears in Email
      messages converted from X.400 according to [RFC2156]; in
      particular, the syntax here permits only one <msg-id> in contrast
      to the multiple <msg-id>s in that Email version.

distribution    =  "Distribution:" SP dist-list CRLF

dist-list       =  *WSP dist-name
                      *( [FWS] "," [FWS] dist-name ) *WSP

dist-name       =  ALPHA / DIGIT
                      *( ALPHA / DIGIT / "+" / "-" / "_" )

summary         =  "Summary:" SP unstructured CRLF

approved        =  "Approved:" SP mailbox-list CRLF

organization    =  "Organization:" SP unstructured CRLF

xref            =  "Xref:" SP *WSP server-name
                      1*( FWS location ) *WSP CRLF

server-name     =  path-identity

location        =  newsgroup-name ":" article-locator

article-locator =  1*( %x21-27 / %x29-3A / %x3C-7E )
                      ; US-ASCII printable characters
                      ; except '(' and ';'

archive         =  "Archive:" SP [CFWS] ("no" / "yes")
                      *( [CFWS] ";" [CFWS] archive-param ) [CFWS] CRLF

archive-param   =  parameter

user-agent      =  "User-Agent:" SP 1*product [CFWS] CRLF

product         =  [CFWS] token [ [CFWS] "/" product-version ]

product-version =  [CFWS] token

injection-info  =  "Injection-Info:" SP [CFWS] path-identity
                      [CFWS] *( ";" [CFWS] parameter ) [CFWS] CRLF

      NOTE: The syntax of <parameter> ([RFC2045] Section 5.1 as amended
      by [RFC2231]), taken in conjunction with the folding rules of
      [RFC0822], effectively allows [CFWS] to occur on either side of
      the "=" inside a <parameter>.

host-value      =  dot-atom-text / IPv4address / IPv6address /
                      (dot-atom-text ":" ( IPv4address / IPv6address ))

      NOTE: Since any such <host-value> or <address-list> also has to be
      a syntactically correct <value>, it will usually be necessary to
      encapsulate it as a <quoted-string>, for example:

posting-host = "posting@example.com:192.168.0.1"

lines           =  "Lines:" SP *WSP 1*DIGIT *WSP CRLF

