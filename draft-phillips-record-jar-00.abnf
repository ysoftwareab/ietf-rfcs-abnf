record-jar   = [encodingSig] [separator] *record
record       = 1*field separator
field        = ( field-name field-sep field-body CRLF )
field-name   = (ALPHA / DIGIT)
                  [*(ALPHA / DIGIT / "-") (ALPHA / DIGIT)]
field-sep    = *SP ":" *SP
field-body   = *(character / continuation)
continuation = ["\"] LWSP
separator    = [blank-line] *("%%" [comment] CRLF)
comment      = SP *69(character)
character    = SP / ASCCHAR / UNICHAR / ESCAPE
encodingSig  = "%%encoding" field-sep
                    *(ALPHA / DIGIT / "-" / "_") CRLF
blank-line   = WSP CRLF

ASCCHAR      = %x21-25 / %x27-5B / %x5D-7E
UNICHAR      = %x80-%x10FFFF                      ; Unicode chars
ESCAPE       = "\" ("\" / "&" / "r" / "n" / "t" )
                / "&#x" 2*6HEXDIG ";"

