append         =/  "APPEND" SP mailbox [SP flag_list]
                        [SP date_time] SP literal8

fetch_att      =/  "BINARY" [".PEEK"] section_number
                        ["<" number "." nz_number ">"] /
                        "BINARY.SIZE" section_number

literal8       =   "~{" number "}" CRLF *OCTET
                        ; <number> represents the number of OCTETs
                        ; in the response string.

msg_att        =   "(" 1#("ENVELOPE" SP envelope /
                        "FLAGS" SP "(" #(flag / "\Recent") ")" /
                        "INTERNALDATE" SP date_time /
                        "RFC822" [".HEADER" / ".TEXT"] SP nstring /
                        "RFC822.SIZE" SP number /
                        "BODY" ["STRUCTURE"] SP body /
                        "BODY" section ["<" number ">"] SP nstring /
                        "UID" SP uniqueid /
                        "BINARY" section_number ["<" number ">"] SP
                                 (nstring / literal8) /
                        "BINARY.SIZE" section_number SP number) ")"
                        ;; Append BINARY and BINARY.SIZE to <msg_att>.
                        ;; This is not meant to be an absolute list;
                        ;; it was done this way due to ABNF syntax
                        ;; restrictions.

resp_code_text =/  "UNKNOWN-CTE"

section_number =   (nz_number *["." nz_number])

