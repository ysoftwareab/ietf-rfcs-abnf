authres = 1*( "AUTHRES" "=" methodspec ":" propspec )
                 ; relays a single unit of authentication results
                 ; information


methodspec = method "=" result
                 ; indicates which authentication method was evaluated


propspec = ptype "." property "=" value
               ; an indication of which property of the message
               ; was evaluated by the authentication scheme being
               ; applied to yield the reported result


method = token [ "/" version ]
             ; a method indicates which method's result is
             ; is represented by "value", and is one of the methods
             ; explicitly defined as valid in this document
             ; or is an extension method as defined below


version = 1*( ALPHA / DIGIT ) 0*( "." 1*( ALPHA / DIGIT ) )
              ; indicates which version of the method was applied


result = "pass" / "hardfail" / "softfail" / "neutral" /
                "temperror" / "permerror"
             ; an indication of the results of the attempt to
             ; authenticate the message


ptype = "smtp" / "header" / "body" / "policy"
            ; indicates whether the property being evaluated was
            ; a parameter to an [SMTP] command, or was a value taken
            ; from a message header field, or was some property of
            ; the message body, or some other property evaluated by
            ; the receiving MTA
property = token
              ; if "ptype" is "smtp", this indicates which [SMTP]
              ; command provided the value which was evaluated by the
              ; authentication scheme being applied; if "ptype" is
              ; "header", this indicates from which header field the
              ; value being evaluated was extracted; if "ptype" is
              ; "body", this indicates the offset into the body at which
              ; content of interest was detected; if "ptype" is "policy"
              ; then this indicates the name of the policy which caused
              ; this header field to be added (see below)


value = token / mailbox
            ; the value extracted from the message property defined
            ; by the "ptype.property" construction; if the value
            ; identifies a mailbox, then it is a "mailbox"
            ; as defined in section 3.4 of [MAIL];
            ; "mailbox" allows CFWS so something better should
            ; either be defined or referenced

