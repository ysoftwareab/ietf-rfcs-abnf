command-any     =/ language-cmd
        ; LANGUAGE command is valid in all states

language-cmd    = "LANGUAGE" *(SP lang-range-quoted)
response-payload  =/ language-data / comparator-data

language-data     = "LANGUAGE" SP "(" lang-tag-quoted *(SP lang-tag-
                      quoted) ")"

namespace-trans   = SP DQUOTE "TRANSLATION" DQUOTE SP "(" string ")"
        ; the string is encoded in Modified UTF-7.
        ; this is a subset of the syntax permitted by
        ; the Namespace_Response_Extension rule in [RFC2342]

lang-range-quoted = astring
        ; Once any literal wrapper or quoting is removed, this
        ; follows the language-range rule in [RFC4647]

lang-tag-quoted = astring
        ; Once any literal wrapper or quoting is removed, this follows
        ; the Language-Tag rule in [RFC4646]

resp-text       = ["[" resp-text-code "]" SP ] UTF8-TEXT-CHAR
                      *(UTF8-TEXT-CHAR / "[")
        ; After the server is changed to a language other than
        ; i-default, this resp-text rule replaces the resp-text
        ; rule from [RFC3501].

UTF8-TEXT-CHAR  = %x20-5A / %x5C-7E / UTF8-2 / UTF8-3 / UTF8-4
        ; UTF-8 excluding 7-bit control characters and "["


command-auth      =/ comparator-cmd

resp-text-code    =/ "BADCOMPARATOR" / "BADMATCH"

comparator-cmd    = "COMPARATOR" *(SP comp-order-quoted)

comparator-data   = "COMPARATOR" SP comp-sel-quoted [SP "("
                        comp-name-quoted *(SP comp-name-quoted) ")"]

comp-name-quoted  = astring
            ; Once any literal wrapper or quoting is removed, this
            ; follows the collation-name rule from [RFC4790]

comp-order-quoted = astring
            ; Once any literal wrapper or quoting is removed, this
            ; follows the collation-order rule from [RFC4790]

comp-sel-quoted   = astring
            ; Once any literal wrapper or quoting is removed, this
            ; follows the collation-sel rule from [RFC4790]
