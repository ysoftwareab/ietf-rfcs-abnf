Foo-Example = sh-integer

sh-list       = list-member *( *SP "," *SP list-member )
list-member   = sh-item / inner-list

inner-list    = "(" *SP [ sh-item *( 1*SP sh-item ) *SP ] ")"
                   parameters
parameters    = *( ";" *SP parameter )
parameter     = param-name [ "=" param-value ]
param-name    = key
key           = ( lcalpha / "*" )
                   *( lcalpha / DIGIT / "_" / "-" / "." / "*" )
lcalpha       = %x61-7A ; a-z
param-value   = bare-item

sh-dictionary  = dict-member *( *SP "," *SP dict-member )
dict-member    = member-name [ "=" member-value ]
member-name    = key
member-value   = sh-item / inner-list

sh-item   = bare-item parameters
bare-item = sh-integer / sh-decimal / sh-string / sh-token
               / sh-binary / sh-boolean

sh-integer = ["-"] 1*15DIGIT

sh-decimal  = ["-"] 1*12DIGIT "." 1*3DIGIT

sh-string = DQUOTE *(chr) DQUOTE
chr       = unescaped / escaped
unescaped = %x20-21 / %x23-5B / %x5D-7E
escaped   = "\" ( DQUOTE / "\" )

sh-token = ( ALPHA / "*" ) *( tchar / ":" / "/" )

sh-binary = ":" *(base64) ":"
base64    = ALPHA / DIGIT / "+" / "/" / "="

sh-boolean = "?" boolean
boolean    = "0" / "1"

