ldapurl     = scheme COLON SLASH SLASH [hostport] [SLASH dn
                     [QUESTION [attributes] [QUESTION [scope]
                     [QUESTION [filter] [QUESTION extensions]]]]]
                                       ; <hostport> is from Section
                                       ;   3.2.2 of [RFC2396] as updated
                                       ;   by [RFC2732] to allow IPv6
                                       ;   literal addresses.
                                       ; <filter> is from Section 3 of
                                       ;   [Filters], subject to the
                                       ;   provisions of the "Escaping
                                       ;   Using the % Method" section
                                       ;   below.

scheme      = "ldap"

dn          = distinguishedName ; From Section 3 of [LDAPDN].
                                       ; See the "Escaping Using the
                                       ; % Method" section below.

attributes  = attrdesc *(COMMA attrdesc)
attrdesc    = selector *(COMMA selector)
selector    = attributeSelector ; From Section 4.5.1 of
                                       ; [Protocol].  See the "Escaping
                                       ; Using the % Method" section
                                       ; below.

scope       = "base" / "one" / "sub"
extensions  = extension *(COMMA extension)
extension   = [EXCLAMATION] extype [EQUALS exvalue]
extype      = oid               ; From section 1.4 of [Models].
exvalue     = LDAPString        ; From section 4.1.2 of
                                       ; [Protocol].  See the "Escaping
                                       ; Using the % Method" section
                                       ; below.

EXCLAMATION = %x21              ; exclamation mark ("!")
SLASH       = %x2F              ; forward slash ("/")
COLON       = %x3A              ; colon (":")
QUESTION    = %x3F              ; question mark ("?")


c=US?postalAddress

c=US??sub?(cn=Babs%20Jensen)

o=An Example\2C Inc.,c=US

