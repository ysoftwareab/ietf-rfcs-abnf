language-range = language-tag / "*"
language-tag   = 1*8[alphanum] *["-" 1*8alphanum]
alphanum       = ALPHA / DIGIT

extended-language-range  = range ; a range
                 / privateuse              ; private-use tag
                 / grandfathered           ; grandfathered registrations

range         = (language
                    ["-" script]
                    ["-" region]
                    *("-" variant)
                    *("-" extension)
                    ["-" privateuse])

language      = (2*3ALPHA [ extlang ]) ; shortest ISO 639 code
                 / 4ALPHA                 ; reserved for future use
                 / 5*8ALPHA               ; registered language subtag
                 / "*"                    ; ... or wildcard

extlang       = *2("-" 3ALPHA) ("-" ( 3ALPHA / "*"))
                                          ; reserved for future use
                                          ; wildcard can only appear
                                          ;   at the end

script        = 4ALPHA                 ; ISO 15924 code
                 / "*"                    ; or wildcard

region        = 2ALPHA                 ; ISO 3166 code
                 / 3DIGIT                 ; UN M.49 code
                 / "*"                    ; ... or wildcard

variant       = 5*8alphanum            ; registered variants
                 / (DIGIT 3alphanum)      ;
                 / "*"                    ; ... or wildcard

extension     = singleton *("-" (2*8alphanum)) [ "-*" ]
                                          ; extension subtags
                                          ; wildcard can only appear
                                          ;   at the end

singleton     = "a"-"w" / "y"-"z" / "A"-"W" / "Y"-"Z" / "0"-"9"
                 ; Single letters: x/X is reserved for private use

privateuse    = ("x"/"X") 1*("-" (1*8alphanum))

grandfathered = 1*3ALPHA 1*2("-" (2*8alphanum))
                   ; grandfathered registration
                   ; Note: I is the only singleton
                   ; that starts a grandfathered tag

alphanum      = (ALPHA / DIGIT)       ; letters and numbers
