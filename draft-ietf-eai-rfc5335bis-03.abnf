UTF8-xtra-char= UTF8-2 / UTF8-3 / UTF8-4

UTF8-2        = %xC2-DF UTF8-tail

UTF8-3        = %xE0 %xA0-BF UTF8-tail / %xE1-EC 2(UTF8-tail) /
                  %xED %x80-9F UTF8-tail / %xEE-EF 2(UTF8-tail)

UTF8-4        = %xF0 %x90-BF 2( UTF8-tail ) / %xF1-F3 3( UTF8-tail ) /
                  %xF4 %x80-8F 2( UTF8-tail )

UTF8-tail     = %x80-BF

   These are normatively defined in [RFC3629], but kept in this document
   for reasons of convenience.

   See [RFC5198] for a discussion of normalization; the use of
   normalization form NFC is RECOMMENDED.  Actually, if one is going to
   do internationalization properly, one of the most often-cited goals
   is to permit people to spell their names correctly.  Since many
   mailbox local parts reflect personal names, that principle applies as
   well.  And NFKC is not recommended because it may lose information
   that is needed to correctly spell some names except in unusual
   circumstances.





FWS     =  <see [RFC5322] Section 3.2.2, folding white space>

CFWS    =  <see [RFC5322] Section 3.2.2>

ctext   =/  UTF8-xtra-char

utext   =/  UTF8-xtra-char

comment =   "(" *([FWS] uCcontent) [FWS] ")"

word    =   uAtom / uQuoted-String

uText          = %d1-9 /    ; all UTF-8 characters except
                    %d11-12 /  ; US-ASCII NUL, CR, and LF
                    %d14-127 /
                    UTF8-xtra-char

uQuoted-Pair   = ("\" uText) / obs-qp

uQcontent      =  uQtext / uQuoted-Pair

uQuoted-String = [CFWS] DQUOTE *([FWS] uQcontent) [FWS] DQUOTE [CFWS]

DQUOTE         = <See appendix B.1 of RFC 5234>

uCcontent      = ctext / uQuoted-Pair / comment

uQtext         = qtext / UTF8-xtra-char

uAtext         = ALPHA / DIGIT /
                    "!" / "#" /  ; Any character except
                    "$" / "%" /  ; controls, SP, and specials.
                    "&" / "'" /  ; Used for atoms.
                    "*" / "+" /
                    "-" / "/" /
                    "=" / "?" /
                    "^" / "_" /
                    "`" / "{" /
                    "|" / "}" /
                    "~" /
                    UTF8-xtra-char

uAtom          = FWS] 1*uAtext [CFWS]

uDot-Atom      = CFWS] uDot-Atom-text [CFWS]

uDot-Atom-text = 1*uAtext *("." 1*uAtext)

qcontent       =  uQcontent


description   = "Content-Description:" unstructured CRLF

mailbox      =  name-addr / addr-spec / uAddr-Spec
                   ; Replace mailbox in Section 3.4.1 of RFC 5322
angle-addr   =/ [CFWS] "<" uAddr-Spec">" [CFWS] / obs-angle-addr
                   ; Replace angle-addr in Section 3.4 of RFC 5322

uAddr-Spec   =  uLocal-Part "@" uDomain

uLocal-Part  =  uDot-String / uQuoted-String
                   ; Replace Local-Part in Section 3.4.1 of RFC 5322

uDot-string  =  uAtom *("." uAtom)

uDomain      =  (sub-udomain 1*("." sub-uDomain)) /
                   dot-atom / domain-literal / obs-domain
                   ; Replace Domain in Section 4.1.2 of  RFC 5321

sub-udomain  =  uLet-dig [uLdh-str]
                   ; Replace sub-domain in Section 4.1.2 of RFC 5321

uLet-dig     =  Let-dig / UTF8-xtra-char
                   ; Replace uLet-dig in Section 4.1.2 of RFC 5321

Let-dig      =  <See Section 4.1.3 of RFC 5321>

uLdh-str     =  *( ALPHA / DIGIT / "-" / UTF8-xtra-char) uLet-dig
                   ; Replace Ldh-str Section 4.1.2 of RFC 5321




item-value      =/      uAddr-Spec

