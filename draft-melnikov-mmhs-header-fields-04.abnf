NZ-DIGIT       =  %x31-39
                      ; "1".."9"

nonneg-integer = "0" / (NZ-DIGIT *DIGIT)

integer = ["-" / "+"] nonneg-integer
              ; 32bit integer

military-string = 1*69( ps-char )

quoted-military-string = DQUOTE military-string DQUOTE

military-string-sequence = military-string
                        [ [FWS] ";" [FWS] military-string-sequence ]

Exempted-Address = "MMHS-Exempted-Address:"
                       [FWS] address-list [FWS] CRLF

Extended-Authorisation-Info = "MMHS-Extended-Authorisation-Info:"
                                  [FWS] date-time CRLF

Subject-Indicator-Codes = "MMHS-Subject-Indicator-Codes:"
                              [FWS] [sic-sequence] [FWS] CRLF

sic-sequence = sic *( [FWS] ";" [FWS] sic )
                   ; STANAG 4406 specifies that the maximum number of
                   ; SICs is 8. Use of more than 8 SIC codes is
                   ; permitted, but additional SIC codes might not
                   ; be transferred to STANAG 4406 system.

sic = 3*8( ps-char )

Handling-Instructions = "MMHS-Handling-Instructions:"
                            [FWS] military-string-sequence [FWS] CRLF

Message-Instructions = "MMHS-Message-Instructions:"
                           [FWS] military-string-sequence [FWS] CRLF

Codress-Message-Indicator = "MMHS-Codress-Message-Indicator:"
                                [FWS] integer [FWS] CRLF

Originator-Reference = "MMHS-Originator-Reference:"
                           [FWS] military-string [FWS] CRLF

PrimaryPrecedence = "MMHS-Primary-Precedence:" [FWS] precedence CRLF

CopyPrecedence = "MMHS-Copy-Precedence:" [FWS] precedence CRLF

precedence = (nonneg-integer / std-precedence) [CFWS]

std-precedence = "deferred" / "routine" / "priority" /
                     "immediate" / "flash" / "override"
                     ; deferred == 0
                     ; routine == 1
                     ; priority == 2
                     ; immediate == 3
                     ; flash == 4
                     ; override == 5

MessageType = "MMHS-Message-Type:" [FWS] message-type [CFWS]
                  [";" [FWS] MessageTypeParam [FWS] ] CRLF

message-type = nonneg-integer / std-message-type

std-message-type = "exercise" / "operation" / "project" /  "drill"
                       ; exercise  == 0
                       ; operation == 1
                       ; project == 2
                       ; drill == 3

MessageTypeParam = "identifier" [FWS] "=" [FWS]
                       quoted-military-string

DesignatorType = "primary" / "copy"

Designator = military-string
OtherRecipIndicatorPrimary = "MMHS-Other-Recipients-Indicator-To:"
                         [FWS] Designator *([FWS] ";" [FWS] Designator)
                         [FWS] CRLF

OtherRecipIndicatorCopy = "MMHS-Other-Recipients-Indicator-Cc:"
                         [FWS] Designator *([FWS] ";" [FWS] Designator)
                         [FWS] CRLF

Acp127MessageIdentifier = "MMHS-Acp127-Message-Identifier:"
                              [FWS] military-string [FWS] CRLF

OriginatorPLAD = "MMHS-Originator-PLAD:" [FWS] military-string [FWS]
                     CRLF

address-list = <Defined in RFC 5322>

