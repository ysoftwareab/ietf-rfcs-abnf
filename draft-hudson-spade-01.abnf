element         = byte / integer / symbol / list / struct
                          / union-val
byte            = OCTET
unsigned        = ("0" / PDIGIT *DIGIT) ":"
integer         = unsigned / ("-" PDIGIT *DIGIT ":")
symbol          = ALPHA *(ALPHA / DIGIT / "-") ":"
list            = unsigned *element
                          ; unsigned determines the number of elements
                          ; all elements must be of same type
struct          = 1*element
                          ; number, type, and order depend on type
union-val       = symbol unsigned [element]
                          ; unsigned is the length of the element
                          ; unsigned is 0 if element is omitted
PDIGIT          = %x31-39
                          ; 1-9

element         =/ unsigned

element         =/ fixed-list
fixed-list      =  *element
                           ; number of elements depend on type

element         =/ maybe
maybe           =  unsigned [element]
                           ; unsigned is 1 if element is present, 0 if
                           ; not

