SYSLOG-MSG      = HEADER SP STRUCTURED-DATA SP MSG

HEADER          = VERSION SP FACILITY SP SEVERITY SP
                        TIMESTAMP SP HOSTNAME SP SENDER-NAME SP
                        SENDER-INST SP MSGID
VERSION         = NONZERO-DIGIT 0*2DIGIT
FACILITY        = "0" / (NONZERO-DIGIT 0*9DIGIT)
                        ; range 0..2147483647
SEVERITY        = "0" / "1" / "2" / "3" / "4" / "5" /
                        "6" / "7"
HOSTNAME        = 1*255PRINTUSASCII  ; a FQDN

SENDER-NAME     = 1*48PRINTUSASCII
SENDER-INST     = "-" / 1*16PRINTUSASCII
MSGID           = "-" / 1*32PRINTUSASCII

TIMESTAMP       = FULL-DATE "T" FULL-TIME
FULL-DATE       = DATE-FULLYEAR "-" DATE-MONTH "-" DATE-MDAY
DATE-FULLYEAR   = 4DIGIT
DATE-MONTH      = 2DIGIT  ; 01-12
DATE-MDAY       = 2DIGIT  ; 01-28, 01-29, 01-30, 01-31 based on
                                ; month/year
FULL-TIME       = PARTIAL-TIME TIME-OFFSET
PARTIAL-TIME    = TIME-HOUR ":" TIME-MINUTE ":" TIME-SECOND
                        [TIME-SECFRAC]
TIME-HOUR       = 2DIGIT  ; 00-23
TIME-MINUTE     = 2DIGIT  ; 00-59
TIME-SECOND     = 2DIGIT  ; 00-58, 00-59, 00-60 based on leap
                                ; second rules
TIME-SECFRAC    = "." 1*6DIGIT
TIME-OFFSET     = "Z" / TIME-NUMOFFSET
TIME-NUMOFFSET  = ("+" / "-") TIME-HOUR ":" TIME-MINUTE


STRUCTURED-DATA = *SD-ELEMENT
SD-ELEMENT      = "[" SD-ID 0*(1*SP SD-PARAM) "]"
SD-PARAM        = PARAM-NAME "=" %d34 PARAM-VALUE %d34
SD-ID           = SD-NAME
PARAM-NAME      = SD-NAME
PARAM-VALUE     = UTF-8-STRING ; characters '"', '\' and
                                     ; ']' MUST be escaped.
SD-NAME         = 1*32OCTET ; VALID UTF-8 String
                        ; except '=', SP, ']', %d34 (")
MSG             = UTF-8-STRING
UTF-8-STRING    = *OCTET ; Any VALID UTF-8 String

OCTET           = %d00..255
SP              = %d32
PRINTUSASCII    = %d33-126
NONZERO-DIGIT   = "1" / "2" / "3" / "4" / "5" /
                        "6" / "7" / "8" / "9"
DIGIT           = "0" / NONZERO-DIGIT


eventID="1011"]

eventID="1011"][x-examplePriority class="high"]

eventID="1011"] [x-examplePriority class="high"]

eventID="1011"][x-examplePriority class="high"]

eventID="1011"] An application event log entry...

eventID="1011"][x-examplePriority class="high"]

