capability          =/ "CHANNEL=" scheme *("," scheme)

channel             =  ["UID" SP] "CHANNEL" SP channel-uri-list
                            SP channel-set

channel-data        =  "CHANNEL" SP section-spec SP
                            (absoluteURI / nil)

channel-set         =  1*("(" nz-number SP section-spec ")")

channel-uri-list    =  "(" channel-uri-reference
                            1*(SP channel-uri-reference) ")"

channel-uri-reference = absoluteURI / scheme ":"

command-select      =/ channel

response-data       =  "*" SP (resp-cond-state / resp-cond-bye /
                            mailbox-data / message-data /
                            capability-data / channel-data) CRLF
                            ;  adds <channel-data> to <response-data>

