wire-message = *key-value
key-value = key ":" value CRLF
key = ALPHA *( ALPHA / "-" / DIGIT )
value = utf8-text
utf8-text = 1*( VCHAR / SP / UTF-2 / UTF8-3 / UTF8-4 )
hash-output = 1*( HEXDIG )
client-init-message = authcid [ authzid ] hashes client-nonce
       [ channel-bindings ] *( extension )
server-challenge-message = realm hash hash-cycles server-nonce
       [ channel-binding ] *( extension )
client-response-message = hash-exchange *( extension )
server-auth-message = server-auth *( extension )

authcid = "Authcid" ":" utf8text CRLF
authzid = "Authzid" ":" utf8text CRLF
hashes = "Hashes" ":" hash-name *( SP hash-name ) CRLF
client-nonce = "Client-Nonce" ":" utf8text CRLF
channel-bindings = "Channel-Bindings" ":" channel-binding-name
       *( SP channel-binding-name ) CRLF

realm = "Realm" ":" utf8text CRLF
hash = "Hash" ":" hash-name CRLF
hash-cycles = "Cycles" ":" 1*( DIGIT ) CRLF
server-nonce = "Server-Nonce" ":" utf8text CRLF
channel-binding = "Channel-Binding:" ":" channel-binding-name CRLF

hash-exchange = "Hash-Exchange" ":" hash-output CRLF

server-auth = "Server-Auth" ":" hash-output CRLF

channel-binding-name = ALPHA *( ALPHA / DIGIT / "-" )
hash-name = ALPHA *( ALPHA / DIGIT / "-" )

extension = key-value


