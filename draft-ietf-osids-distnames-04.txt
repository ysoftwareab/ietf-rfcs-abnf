






Network Working                                  S.E. Hardcastle-Kille
Group                                        University College London
INTERNET-DRAFT                                            October 1992
                                                  Expires:  April 1993


            A String Representation of Distinguished Names

                           (OSI-DS 23 (v4))




Status of this Memo
This document is an Internet Draft.  Internet Drafts are working
documents of the Internet Engineering Task Force (IETF), its Areas,
and its Working Groups.  Note that other groups may also distribute
working documents as Internet Drafts.

Internet Drafts are draft documents valid for a maximum of six months.
Internet Drafts may be updated, replaced, or obsoleted by other
documents at any time.  It is not appropriate to use Internet Drafts
as reference material or to cite them other than as a "working draft"
or "work in progress."
Please check the I-D abstract listing contained in each Internet Draft
directory to learn the current status of this or any other Internet
Draft.
Abstract

The OSI Directory uses distinguished names as the primary keys to
entries in the directory.  Distinguished Names are encoded in ASN.1.
When a distinguished name is communicated between to users not using a
directory protocol (e.g., in a mail message), there is a need to have
a user-oriented string representation of distinguished name.
This specification defines a string format for representing names,
which is designed to give a clean representation of commonly used
names, whilst being able to represent any distinguished name.
This draft document will be submitted to the RFC editor as a protocol
standard.  Distribution of this memo is unlimited.  Please send
comments to the author or to the discussion group
<osi-ds@CS.UCL.AC.UK>.




INTERNET--DRAFT             DN Representation             October 1992


Contents

1   Why a notation is needed                                         2

2   A notation for Distinguished Name                                2

    2.1    Goals     .   .   .   .   .   .   .   .   .   .   .       2
    2.2    Informal definition   .   .   .   .   .   .   .   .       2

    2.3    Formal definition     .   .   .   .   .   .   .   .       3
        2.3.1    An Alternative Approach     .   .   .   .   .       6

3   Examples                                                         6


4   Security Considerations                                          7

5   Author's Address                                                 7



























Hardcastle-Kille                         Expires:  April 1993   Page 1




INTERNET--DRAFT             DN Representation             October 1992


1  Why a notation is needed

Many OSI Applications make use of Distinguished Names (DN) as defined
in the OSI Directory, commonly known as X.500 [CCI88].  This
specification assumes familiarity with X.500, and the concept of
Distinguished Name.  It is important to have a common format to be
able to unambiguously represent a distinguished name.  This might be
done to represent a directory name on a business card or in an email
message.  There is a need for a format to support human to human
communication, which must be string based (not ASN.1) and user
oriented.
This notation is targeted towards a general user oriented system, and
in particular to represent the names of humans.  Other syntaxes may be
more appropriate for other uses of the directory.  For example, the
OSF Syntax may be more appropriate for some system oriented uses.
(The OSF Syntax uses ``/'' as a separator, and forms names in a manner
intended to resemble UNIX filenames).


2  A notation for Distinguished Name


2.1  Goals

The following goals are laid out:


 o  To provide an unambiguous representation of a distinguished name

 o  To be an intuitive format for the majority of names

 o  To be fully general, and able to represent any distinguished name

 o  To be amenable to a number of different layouts to achieve an
    attractive representation.

 o  To give a clear representation of the contents of the
    distinguished name

2.2  Informal definition


This notation is designed to be convenient for common forms of name.
Some examples are given.  The author's directory distinguished name

Hardcastle-Kille                         Expires:  April 1993   Page 2




INTERNET--DRAFT             DN Representation             October 1992


would be written:

CN=Steve Hardcastle-Kille, OU=Computer Science,
O=University College London, C=GB


This may be folded, perhaps to display in multi-column format.  For
example:

CN=Steve Hardcastle-Kille,
OU=Computer Science,
O=University College London,
C=GB

Another name might be:


CN=Christian Huitema, O=INRIA, C=FR

Semicolon (``;'') may be used as an alternate separator.


CN=Christian Huitema; O=INRIA; C=FR

In running text, this would be written as <CN=Christian Huitema;
O=INRIA; C=FR>.  Another example, shows how different attribute types
are handled:


CN=James Hacker,
L=Basingstoke,
O=Widget Inc,
CN=GB

The final example shows quoting of a comma in an Organisation name:


CN=L. Eagle, O="Sue, Grabbit and Runn", C=GB

2.3  Formal definition


A formal definition can now be given.  The structure is specified in a
BNF grammar in Figure 1.  This BNF uses the grammar defined in

Hardcastle-Kille                         Expires:  April 1993   Page 3




INTERNET--DRAFT             DN Representation             October 1992


RFC 822, with the terminals enclosed in <> [Cro82].  This definition
is in an abstract character set, and so may be written in any
character set supporting the explicitly defined special characters.
The quoting mechanism is used for the following cases:


 o  Strings containing ``,'', ``+'', ``=''or ``"'' , <CR>, ``<'',
    ``>'', ``#'', or ``;''.

 o  Strings with leading or trailing spaces

 o  Strings containing consecutive spaces

There is an escape mechanism from the normal user oriented form, so
that this syntax may be used to print any valid distinguished name.
This is ugly.  It is expected to be used only in pathological cases.
There are two parts to this mechanism:


1.  Attributes types are represented in a (big-endian) dotted
    notation.  (e.g., OID.2.6.53).

2.  Attribute values are represented in hexadecimal (e.g.  #0A56CF).

The keyword specification is optional in the BNF, but mandatory for
this specification.  This is so that the same BNF may be used for the
related specification on User Friendly Naming [HK92].  When this
specification is followed, the attribute type keywords must always be
present.
A list of valid keywords for well known attribute types used in naming
is given in Table 1.  This is a list of keywords which must be
supported.  These are chosen because they appear in common forms of
name, and can do so in a place which does not correspond to the
default schema used.  If other attributes are used for naming, this
can always be extended locally.

Only string type attributes are considered, but other attribute
syntaxes could be supported locally.  It is assumed that the interface
will translate from the supplied string into PrintableString or T.61.
The "+" notation is used to specify multi-component RDNs.  In this
case, the types for attributes in the RDN must be explicit.

The name is presented/input in a little-endian order (most significant
component last).

Hardcastle-Kille                         Expires:  April 1993   Page 4




INTERNET--DRAFT             DN Representation             October 1992



<name> ::= <name-component> ( <spaced-separator> )
       | <name-component> <spaced-separator> <name>

<spaced-separator> ::= <optional-space>
                <separator>
                <optional-space>

<separator> ::=  "," | ";"

<optional-space> ::= ( <CR> ) *( " " )

<name-component> ::= <attribute>
        | <attribute> <optional-space> "+"
          <optional-space> <name-component>

<attribute> ::= <string>
        | <key> <optional-space> "=" <optional-space> <string>

<key> ::= 1*( <keychar> ) | "OID." <oid>
<keychar> ::= letters, numbers, and space

<oid> ::= <digitstring> | <digitstring> "." <oid>
<digitstring> ::= 1*<digit>
<digit> ::= digits 0-9

<string> ::= *( <stringchar> | <pair> )
         | '"' *( <stringchar> | <special> | <pair> ) '"'
 | "#" <hex>


<special> ::= "," | "=" | '"' | <CR> | "+" | "<" |  ">"
         | "#" | ";"

<pair> ::= "\" ( <special> | "\" )
<stringchar> ::= any char except <special> or "\"

<hex> ::= 2*<hexchar>
<hexchar> ::= 0-9, a-f, A-F



            Figure 1:  BNF Grammar for Distinguished Name


Hardcastle-Kille                         Expires:  April 1993   Page 5




INTERNET--DRAFT             DN Representation             October 1992



                    _Key__Attribute_(X.520_keys)_
                     CN   CommonName
                     L    LocalityName
                     ST   StateOrProvinceName
                     O    OrganizationName
                     OU   OrganizationalUnitName
                     C    CountryName


                   Table 1:  Standardised Keywords

When an address is written in a context where there is a need to
delimit the entire address (e.g., in free text), it is recommended
that the delimiters <> are used.  The terminator > is a special in the
notation to facilitate this delimitation.


2.3.1  An Alternative Approach

An alternative approach is described in this section.  It is NOT
recommended by these guidelines.  Whilst this approach has merits, it
is diametrically opposite to the recommended approach.  A mixture of
both approaches is not beneficial.

This alternative is to use short names, which will be easily memorable
by users (e.g., UCL for University College London, and CS for Computer
Science).  In general, the style of name component should be similar
to that chosen for names in the domain system.
The benefit of this approach is that it will lead to mnemonic names,
which are easy to remember and to type.  The directory will then be
able to operate mainly on the read operation, with much less use of
the more expensive search operation.
It is argued that name conflicts may be dealt with by introducing more
levels into the DIT.


3  Examples

This section gives a few examples of distinguished names written using
this notation:




Hardcastle-Kille                          Expires: April 1993   Page 6




INTERNET--DRAFT             DN Representation             October 1992


CN=Marshall T. Rose, O=Dover Beach Consulting, L=Santa Clara,
ST=California, C=US

CN=FTAM Service, CN=Bells, OU=Computer Science,
O=University College London, C=GB


CN=Steve Hardcastle-Kille,
OU=Computer Science,
O=University College London,
C=GB

CN=Steve Hardcastle-Kille, OU=Computer Science,
O=University College London, C=GB


References

[CCI88] The Directory --- overview of concepts, models and services,
        December 1988. CCITT X.500 Series Recommendations.

[Cro82] D.H. Crocker. Standard of the format of ARPA internet text
        messages. Request for Comments 822, University of Delaware,
        August 1982.

[HK92]  S.E. Hardcastle-Kille. Using the OSI directory to achieve
        user friendly naming. Request for Comments in preparation,
        Department of Computer Science, University College London,
        January 1992.


4  Security Considerations

Security considerations are not discussed in this INTERNET--DRAFT .


5  Author's Address


    Steve Hardcastle-Kille
    Department of Computer Science
    University College London
    Gower Street
    WC1E 6BT

Hardcastle-Kille                         Expires:  April 1993   Page 7




INTERNET--DRAFT             DN Representation             October 1992


    England

    Phone:  +44-71-380-7294


    EMail:  S.Kille@CS.UCL.AC.UK

    DN: CN=Steve Hardcastle-Kille, OU=Computer Science,
    O=University College London, C=GB


    UFN: S. Hardcastle-Kille, Computer Science,
    University College London, GB
































Hardcastle-Kille                         Expires:  April 1993   Page 8
