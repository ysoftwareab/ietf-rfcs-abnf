IRI-reference  = [ absoluteIRI | relativeIRI ] [ "#" ifragment ]
absoluteIRI    = scheme ":" ( ihier_part | iopaque_part )
relativeIRI    = ( inet_path | iabs_path | irel_path )
                        [ "?" iquery ]
ihier_part     = ( inet_path | iabs_path ) [ "?" iquery ]
iopaque_part   = iric_no_slash *iric
iric_no_slash  = iunreserved | escaped | ";" | "?" | ":" | "@" |
                       "&" | "=" | "+" | "$" | ","
inet_path      = "//" iauthority [ iabs_path ]
iabs_path      = "/"  ipath_segments
irel_path      = irel_segment [ iabs_path ]
irel_segment   = 1*( iunreserved | escaped |
                           ";" | "@" | "&" | "=" | "+" | "$" | "," )
iauthority     = server | ireg_name
ireg_name      = 1*( iunreserved | escaped | "$" | "," |
                           ";" | ":" | "@" | "&" | "=" | "+" )
ipath_segments = isegment *( "/" isegment )
isegment       = *ipchar *( ";" iparam )
iparam         = *ipchar
ipchar         = iunreserved | escaped |
                         ":" | "@" | "&" | "=" | "+" | "$" | ","
iquery         = *iric
ifragment      = *iric
iric           = reserved | iunreserved | escaped
iunreserved    = ichar | unreserved
ichar          = << any character of the UCS [ISO10646] of U+00A0
                           and beyond, subject to limitations in Section
                            3.1. >> | space | delims | unwise

reserved    = ";" | "/" | "?" | ":" | "@" | "&" | "=" | "+" |
                     "$" | "," | "[" | "]"
unreserved    = alphanum | mark
mark          = "-" | "_" | "." | "!" | "~" | "*" | "'" |
                       "(" | ")"
escaped       = "%" HEXDIG HEXDIG
server        = [ [ userinfo "@" ] hostport ]
userinfo      = *( unreserved | escaped |
                          ";" | ":" | "&" | "=" | "+" | "$" | "," )
hostport      = host [ ":" port ]
host          = hostname | IPv4address | IPv6reference
ipv6reference = "[" IPv6address "]"
hostname      = *( domainlabel "." ) toplabel [ "." ]
domainlabel   = alphanum | alphanum *( alphanum | "-" ) alphanum
toplabel      = alpha | alpha *( alphanum | "-" ) alphanum
IPv6address   = hexpart [ ":" IPv4address ]
IPv4address   = 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6prefix    = hexpart "/" 1*2DIGIT
hexpart       = hexseq | hexseq "::" [ hexseq ] | "::"
                       [ hexseq ]
hexseq        = hex4 *( ":" hex4)
hex4          = 1*4HEXDIG
port          = *DIGIT

scheme        = alpha *( alpha | digit | "+" | "-" | "." )
alphanum      = alpha | digit
alpha         = lowalpha | upalpha
lowalpha = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" |
                  "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" |
                  "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
upalpha  = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" |
                  "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" |
                  "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
space    = <US-ASCII coded character 20 hexadecimal>
delims   = "<" | ">" | "#" | "%" | <">
unwise   = "{" | "}" | "|" | "\" | "^" | "[" | "]" | "`"


