number = 1*DIGIT

short-float = 1*3DIGIT [ "." 0*3DIGIT ]

variant-description =
                  "{" <"> URI <"> source-quality *variant-attribute"}"

source-quality = qvalue

variant-attribute = "{" "type" media-type "}"
                         | "{" "charset" charset "}"
                         | "{" "language"  1#language-tag "}"
                         | "{" "length" 1*DIGIT "}"
                         | "{" "features" feature-list "}"
                         | "{" "description" quoted-string "}"
                         | extension-attribute

extension-attribute = "{" extension-name extension-value "}"
extension-name      = token
extension-value     = *( token | quoted-string | LWS
                              | extension-specials )

extension-specials  =
                          <any element of tspecials except <"> and "}">

ftag = 1*<any CHAR except CTLs or tspecials or "!">

tspecials      = "(" | ")" | "<" | ">" | "@"
                      | "," | ";" | ":" | "\" | <">
                      | "/" | "[" | "]" | "?" | "="
                      | "{" | "}" | SP | HT

        (tspecials definition reproduced from [1])

Accept-Features = "Accept-Features" ":"
                   #( feature-expr *( ";" feature-extension ) )

feature-expr = [ "!" ] ftag
                    | ftag [ "!" ] "=" tag-value
                    | ftag "=" "{" tag-value "}"
                    | ftag "<=" number
                    | ftag "=" "<" numeric-range ">"
                    | "*"

tag-value  = token | quoted-string

numeric-range = [ number ] "-" [ number ]

feature-extension = token [ "=" ( token | quoted-string ) ]

UA-media={stationary}, paper = a4, paper!="a0",
x_version=<100-205>, *

ftag=V     ftag is present with the value V (it may also be
                 present with other values)

ftag={V}   ftag is present with the value V, and not with any
                 other values

ftag=<N-M> ftag is present with the numeric values from N up to
                 and including M, and not with any other values.  If N
                 is missing, the lower bound is 0.  If M is missing,
                 the upper bound is infinity.

fpred = [ "!" ] ftag
            | ftag [ "!" ] "=" tag-value
            | ftag "=" "<" numeric-range ">"

dpi=<300-599>, colordepth=<24->

ftag=V     true if the feature is known to be present with
                 the value V,

                 false if the feature is known not to be present with
                 the value V

ftag=<N-M> true if the feature is known to be present with some
                 numeric values, while the highest value with which it
                 is present is known and in the range N-M,

                 false if the feature is known to be absent, or if it
                 is known to be present with some numeric values,
                 while the highest value with which it is present is
                 known and not in the range N-M.

                 If N is missing, the lower bound is 0.  If M is
                 missing, the upper bound is infinity.

UA-media={stationary}, paper = a4, paper!="a0",
x_version=<100-205>, *

UA-media=stationary, !UA-media=screen, paper=a4, paper =!a0,
colordepth=< 4 - 6 >, x_version="101"

blex=wox, blex!=wox,  paper=a5,
UA-media=screen, paper=a0


feature-list = 1%feature-list-element

feature-list-element = ( fpred | fpred-bag )
                              [ ":" true-improvement ]
                              [ "/" false-degradation ]

fpred-bag = "[" 1%fpred "]"

true-improvement   =  short-float
false-degradation  =  short-float

rvsa-version = major "." minor

major = 1*4DIGIT
minor = 1*4DIGIT

Alternates = "Alternates" ":" variant-list

variant-list = 1#( variant-description
                        | fallback-variant
                        | list-directive )

fallback-variant = "{" <"> URI <"> "}"

list-directive = ( "proxy-rvsa" "=" <"> 0#rvsa-version <"> )
                        | extension-list-directive

extension-list-directive = token [ "=" ( token | quoted-string ) ]

proxy-rvsa="1.0, 2.5"

Content-Features = "Content-Features" ":" feature-list

Negotiate = "Negotiate" ":" 1#negotiate-directive

negotiate-directive = "trans" | rvsa-version | "*"
                          | negotiate-extension

negotiate-extension = token [ "=" token ]

TCN = "TCN" ":" #( response-type | tcn-extension )

response-type = "list" | "choice" | "adhoc"

tcn-extension = token [ "=" ( token | quoted-string ) ]

Variant-Vary  = "Variant-Vary" ":" ( "*" | 1#field-name )

variant-list-validator = <quoted-string not containing any ";">

