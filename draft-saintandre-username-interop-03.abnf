localpart = 1*1023(localpoint)
                ;
                ; a "localpoint" is a UTF-8 encoded Unicode code point
                ; that conforms to the "LocalpartIdentifierClass"
                ; profile of the PRECIS IdentifierClass

dot-atom-text =  1*atext *("." 1*atext)
atext         =  ALPHA / DIGIT /    ; Any character except
                       "!" / "#" / "$" /  ; controls, SP, and
                       "%" / "&" / "'" /  ; specials. Used for
                       "*" / "+" / "-" /  ; atoms.
                       "/" / "=" / "?" /
                       "^" / "_" / "`" /
                       "{" / "|" / "}" /
                       "~"

username    =  dot-string
dot-string  =  string
dot-string  =/ dot-string "." string
string      =  char
string      =/ string char
char        =  c
char        =/ "\" x
c           =  %x21    ; '!'              allowed
                             ; '"'              not allowed
c           =/ %x23    ; '#'              allowed
c           =/ %x24    ; '$'              allowed
c           =/ %x25    ; '%'              allowed
c           =/ %x26    ; '&'              allowed
c           =/ %x27    ; '''              allowed
                             ; '(', ')'         not allowed
c           =/ %x2A    ; '*'              allowed
c           =/ %x2B    ; '+'              allowed
                             ; ','              not allowed
c           =/ %x2D    ; '-'              allowed
                             ; '.'              not allowed
c           =/ %x2F    ; '/'              allowed
c           =/ %x30-39 ; '0'-'9'          allowed
                             ; ';', ':', '<'    not allowed
c           =/ %x3D    ; '='              allowed
                             ; '>'              not allowed
c           =/ %x3F    ; '?'              allowed
                             ; '@'              not allowed
c           =/ %x41-5a ; 'A'-'Z'          allowed
                             ; '[', '\', ']'    not allowed
c           =/ %x5E    ; '^'              allowed
c           =/ %x5F    ; '_'              allowed
c           =/ %x60    ; '`'              allowed
c           =/ %x61-7A ; 'a'-'z'          allowed
c           =/ %x7B    ; '{'              allowed
c           =/ %x7C    ; '|'              allowed
c           =/ %x7D    ; '}'              allowed
c           =/ %x7E    ; '~'              allowed
                             ; DEL              not allowed
c           =/ %x80-FF ; UTF-8-Octet      allowed
x           =  %x00-FF ; all 128 ASCII characters

user             =  1*( unreserved / escaped / user-unreserved )
user-unreserved  =  "&" / "=" / "+" / "$" / "," / ";" / "?" / "/"
unreserved       =  alphanum / mark
mark             =  "-" / "_" / "." / "!" / "~" / "*" / "'"
                          / "(" / ")"

acctURI      =  "acct" ":" userpart "@" host
userpart     =  unreserved / sub-delims
                      0*( unreserved / pct-encoded / sub-delims )

