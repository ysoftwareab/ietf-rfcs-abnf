rtsp_URL = ( "rtsp:" | "rtspu:" | "rtsps:" )
                "//" host [ ":" port ] [abs_path]
host     = <A legal Internet host domain name of IP address
             (in dotted decimal form), as defined by Section 2.1
             of RFC 1123>
port     = *DIGIT

   abs_path is defined in [H3.2.1].

     Note that fragment and query identifiers do not have a well-defined
     meaning at this time, with the interpretation left to the RTSP
     server.

   The scheme rtsp requires that commands are issued via a reliable
   protocol (within the Internet, TCP), while the scheme rtspu identifies
   an unreliable protocol (within the Internet, UDP). The scheme rtsps
   indicates that a TCP connection secured by TLS [7] must be used.

   If the port is empty or not given, port 554 is assumed. The semantics
   are that the identified resource can be controlled be RTSP at the
   server listening for TCP (scheme ``rtsp'') connections or UDP (scheme
   ``rtspu'') packets on that port of host, and the Request-URI for the
   resource is rtsp_URL.

   The use of IP addresses in URLs SHOULD be avoided whenever possible
   (see RFC 1924 [15]).





conference-id = 1*OCTET  ; LWS must be URL-escaped

     Conference identifiers are used to allow to allow RTSP sessions to
     obtain parameters from multimedia conferences the media server is
     participating in. These conferences are created by protocols
     outside the scope of this specification, e.g., H.323 [17] or SIP
     [12]. Instead of the RTSP client explicitly providing transport
     information, for example, it asks the media server to use the
     values in the conference description instead. If the conference
     participant inviting the media server would only supply a
     conference identifier which is unique for that inviting party, the
     media server could add an internal identifier for that party, e.g.,
     its Internet address. However, this would prevent that the
     conference participant and the initiator of the RTSP commands are
     two different entities.

session-id = 1*OCTET      ; LWS must be URL-escaped

smpte-range = "smpte" "=" smpte-time "-" [ smpte-time ]
smpte-time = 2DIGIT ":" 2DIGIT ":" 2DIGIT [ ":" 2DIGIT ] [ "." 2DIGIT]

   Examples:
smpte=10:12:33:20-
smpte=10:07:33-
smpte=10:07:00-10:07:33:05.01





npt-range = "npt" "=" npt-time "-" [ npt-time ]
npt-time  = 1*DIGIT [ ":" *DIGIT ]

   Examples:
npt=123:45-125

utc-range = "clock" "=" utc-time "-" [ utc-time ]
utc-time = utc-date "T" utc-time "Z"
utc-date = 8DIGIT                  ; < YYYYMMDD >
utc-time = 6DIGIT [ "." fraction ] ; < HHMMSS.fraction >

   Example for November 8, 1996 at 14h37 and 20 and a quarter seconds
   UTC:

general-header     =     Cache-Control     ; Section 12.10
                         |     Connection        ; Section 12.12
                         |     Date              ; Section 12.17
                         |     Via               ; Section 12.39

Request      =       Request-Line          ; Section 6.1
                    *(      general-header        ; Section 5
                    |       request-header        ; Section 6.2
                    |       entity-header )       ; Section 8.1
                            CRLF
                            [ message-body ]      ; Section 4.3

Request-Line = Method SP Request-URI SP RTSP-Version SP seq-no CRLF

Method         =         "DESCRIBE"              ; Section 10.2
                  |         "ANNOUNCE"              ; Section 10.3
                  |         "GET_PARAMETER"         ; Section 10.8
                  |         "OPTIONS"               ; Section 10.1
                  |         "PAUSE"                 ; Section 10.6
                  |         "PLAY"                  ; Section 10.5
                  |         "RECORD"                ; Section 10.11
                  |         "REDIRECT"              ; Section 10.10
                  |         "SETUP"                 ; Section 10.4
                  |         "SET_PARAMETER"         ; Section 10.9
                  |         "TEARDOWN"              ; Section 10.7
                  |         extension-method

extension-method = token

Request-URI = "*" | absolute_URI

RTSP-Version = "RTSP" "/" 1*DIGIT "." 1*DIGIT

seq-no = 1*DIGIT

request-header  =          Accept                   ; Section 12.1
                  |          Accept-Encoding          ; Section 12.2
                  |          Accept-Language          ; Section 12.3
                  |          Authorization            ; Section 12.5
                  |          From                     ; Section 12.19
                  |          If-Modified-Since        ; Section 12.21
                  |          Range                    ; Section 12.28
                  |          Referer                  ; Section 12.29
                  |          User-Agent               ; Section 12.37






Response     =      Status-Line          ; Section 7.1
                    *(     general-header       ; Section 5
                    |      response-header      ; Section 7.1.2
                    |      entity-header )      ; Section 8.1
                           CRLF
                           [ message-body ]     ; Section 4.3

Status-Line = RTSP-Version SP Status-Code SP seq-no SP Reason-Phrase CRLF

Status-Code    = "100"   ; Continue
                  | "200"   ; OK
                  | "201"   ; Created
                  | "300"   ; Multiple Choices
                  | "301"   ; Moved Permanently
                  | "302"   ; Moved Temporarily
                  | "303"   ; See Other
                  | "304"   ; Not Modified
                  | "305"   ; Use Proxy
                  | "400"   ; Bad Request
                  | "401"   ; Unauthorized
                  | "402"   ; Payment Required
                  | "403"   ; Forbidden
                  | "404"   ; Not Found
                  | "405"   ; Method Not Allowed
                  | "406"   ; Not Acceptable
                  | "407"   ; Proxy Authentication Required
                  | "408"   ; Request Time-out
                  | "409"   ; Conflict
                  | "410"   ; Gone
                  | "411"   ; Length Required
                  | "412"   ; Precondition Failed
                  | "413"   ; Request Entity Too Large
                  | "414"   ; Request-URI Too Large
                  | "415"   ; Unsupported Media Type
                  | "451"   ; Parameter Not Understood
                  | "452"   ; Conference Not Found
                  | "453"   ; Not Enough Bandwidth
                  | "45x"   ; Session Not Found
                  | "45x"   ; Method Not Valid in This State
                  | "45x"   ; Header Field Not Valid for Resource
                  | "45x"   ; Invalid Range
                  | "45x"   ; Parameter Is Read-Only
                  | "45x"   ; Aggregate operation not allowed
                  | "45x"   ; Only aggregate operation allowed
                  | "500"   ; Internal Server Error
                  | "501"   ; Not Implemented
                  | "502"   ; Bad Gateway
                  | "503"   ; Service Unavailable
                  | "504"   ; Gateway Time-out
                  | "505"   ; RTSP Version not supported
                  | extension-code







extension-code = 3DIGIT

Reason-Phrase  = *<TEXT, excluding CR, LF>

response-header   =     Location            ; Section 12.23
                     |     Proxy-Authenticate  ; Section 12.26
                     |     Public              ; Section 12.27
                     |     Retry-After         ; Section 12.30
                     |     Server              ; Section 12.33
                     |     Vary                ; Section 12.38
                     |     WWW-Authenticate    ; Section 12.40

entity-header       =    Allow               ; Section 12.4
                         |    Content-Encoding    ; Section 12.13
                         |    Content-Language    ; Section 12.14
                         |    Content-Length      ; Section 12.15
                         |    Content-Type        ; Section 12.16
                         |    Expires             ; Section 12.18
                         |    Last-Modified       ; Section 12.22
                         |    extension-header
extension-header    =    message-header

v=0
o=mhandley 2890844526 2890842807 IN IP4 126.16.64.4
s=SDP Seminar
i=A Seminar on the session description protocol
u=http://www.cs.ucl.ac.uk/staff/M.Handley/sdp.03.ps
e=mjh@isi.edu (Mark Handley)
c=IN IP4 224.2.17.12/127
t=2873397496 2873404696
a=recvonly
m=audio 3456 RTP/AVP 0
m=video 2232 RTP/AVP 31
m=whiteboard 32416 UDP WB
a=orient:portrait

v=0
o=mhandley 2890844526 2890845468 IN IP4 126.16.64.4
s=SDP Seminar
i=A Seminar on the session description protocol
u=http://www.cs.ucl.ac.uk/staff/M.Handley/sdp.03.ps
e=mjh@isi.edu (Mark Handley)
c=IN IP4 224.2.17.12/127
t=2873397496 2873404696
a=recvonly
m=audio 3456 RTP/AVP 0
m=video 2232 RTP/AVP 31

Bandwidth  = "Bandwidth" ":" 1*DIGIT

   Example:
Cache-Control   = "Cache-Control" ":" 1#cache-directive

cache-directive = cache-request-directive
                  | cache-response-directive

cache-request-directive =
        "no-cache"
      | "max-stale"
      | "min-fresh"
      | "only-if-cached"
      | cache-extension

cache-response-directive =
        "public"
      | "private"
      | "no-cache"
      | "no-transform"
      | "must-revalidate"
      | "proxy-revalidate"
      | "max-age" "=" delta-seconds
      | cache-extension

cache-extension = token [ "=" ( token | quoted-string ) ]

Conference = "Conference" ":" conference-id

   Example:
Expires = "Expires" ":" HTTP-date

   An example of its use is

If-Modified-Since = "If-Modified-Since" ":" HTTP-date

   An example of the field is:

Range = "Range" ":" 1#ranges-specifier [ ";" "time" "=" utc-time ]

ranges-specifier = npt-range | utc-range | smpte-range

   Example:
Scale = "Scale" ":" [ "-" ] 1*DIGIT [ "." *DIGIT ]

   Example of playing in reverse at 3.5 times normal rate:

Speed = "Speed" ":" 1*DIGIT [ "." *DIGIT ]

   Example:
Session  = "Session" ":" session-id

   Note that a session identifier identifies a RTSP session across
   transport sessions or connections. Control messages for more than one
   RTSP URL may be sent within a single RTSP session. Hence, it is
   possible that clients use the same session for controlling many
   streams comprising a presentation, as long as all the streams come
   from the same server. (See example in Section 14). However, multiple
   ``user'' sessions for the same URL from the same client MUST use
   different session identifiers.

     The session identifier is needed to distinguish several delivery
     requests for the same URL coming from the same client.





Transport = "Transport" ":"
              1#transport-protocol/profile[/lower-transport] *parameter
transport-protocol  = "RTP"
profile     = "AVP"
lower-transport = "TCP" | "UDP"
parameter   = ";" "destination" [ "=" address ]
              | ";" "compressed"
              | ";" "channel" "=" channel
              | ";" "append"
              | ";" "ttl" "=" ttl
              | ";" "port" "=" port
              | ";" "cport" "=" port
              | ";" "ssrc" "=" ssrc
              | ";" "mode" = <"> 1#mode <">
ttl         = 1*3(DIGIT)
port        = 1*5(DIGIT)
ssrc        = 8*8(HEX)
channel     = 1*3(DIGIT)
address     = host
mode        = "PLAY" | "RECORD" *parameter

   Example:
mode="PLAY,RECORD;append"

     The Transport header is restricted to describing a single RTP
     stream. (RTSP can also control multiple streams as a single
     entity.) Making it part of RTSP rather than relying on a multitude
     of session description formats greatly simplifies designs of
     firewalls.

Transport-Info = "Transport-Info" ":"
              1#transport-protocol/profile[/lower-transport] ";"
              streamid
              *parameter
transport-protocol  = "RTP"
profile     = "AVP"
lower-transport = "TCP" | "UDP"
stream-id = "streamid" "=" streamid
parameter   = ";" "seq" "=" sequence number
sequence-number = 1*16(DIGIT)

   Example:
v=0
o=- 2890844526 2890842807 IN IP4 192.16.24.202
s=RTSP Session
m=audio 0 RTP/AVP 0
a=murl:rtsp://audio.example.com/twister/audio.en
m=video 0 RTP/AVP 31
a=murl:rtsp://audio.example.com/twister/video

s= sample rtsp presentation
r = rtsp://foo/twister   /* aggregate URL*/
m= audio 0 RTP/AVP 0
r = rtsp://foo/twister/audio
m=video 0 RTP/AVP 26
r = rtsp://foo/twister/video

v=0
o=- 2890844526 2890842807 IN IP4 192.16.24.202
s=RTSP Session
m=audio 3456 RTP/AVP 0
c=IN IP4 224.2.0.1/16

v=0
o=- 2890844526 2890842807 IN IP4 192.16.24.202
s=RTSP Session
m=audio 0 RTP/AVP 0

v=0
s=Mbone Audio
i=Discussion of Mbone Engineering Issues







OCTET     = <any 8-bit sequence of data>
CHAR      = <any US-ASCII character (octets 0 - 127)>
UPALPHA   = <any US-ASCII uppercase letter "A".."Z">
LOALPHA   = <any US-ASCII lowercase letter "a".."z">
ALPHA     = UPALPHA | LOALPHA
DIGIT     = <any US-ASCII digit "0".."9">
CTL       = <any US-ASCII control character
             (octets 0 - 31) and DEL (127)>
CR        = <US-ASCII CR, carriage return (13)>
LF        = <US-ASCII LF, linefeed (10)>
SP        = <US-ASCII SP, space (32)>
HT        = <US-ASCII HT, horizontal-tab (9)>
CRLF      = CR LF
LWS       = [CRLF] 1*( SP | HT )
TEXT      = <any OCTET except CTLs>
tspecials = "(" | ")" | "<" | ">" | "@"
          | "," | ";" | ":" | "\" | <">
          | "/" | "[" | "]" | "?" | "="
          | "{" | "}" | SP | HT
token = 1*<any CHAR except CTLs or tspecials>
quoted-string = ( <"> *(qdtext) <"> )
qdtext = <any TEXT except <">>
quoted-pair = "\" CHAR





message-header = field-name ":" [ field-value ] CRLF
field-name = token
field-value = *( field-content | LWS )
field-content = <the OCTETs making up the field-value and consisting
                 of either *TEXT or combinations of token, tspecials,
                 and quoted-string>

