addto-position      = number
       ;; Number can be 0 for SEARCH result additions.
       ;; number from RFC3501

modifier-context    = "CONTEXT"

modifier-partial    = "PARTIAL" SP seq-range
       ;; seq-range from RFC3501

modifier-notify     = "NOTIFY"

search-return-opt   =/ modifier-context / modifier-partial /
                          modifier-notify
       ;; All conform to search-return-opt, from RFC4466

ret-data-addto      = "ADDTO"
                          SP "(" addto-position SP sequence-set ")"
       ;; sequence-set from RFC3501

ret-data-partial    = "PARTIAL"
                         SP "(" seq-range SP sequence-set ")"
       ;; sequence-set from RFC3501

ret-data-removefrom = "REMOVEFROM" SP sequence-set
       ;; sequence-set from RFC3501

search-return-data  =/ ret-data-partial / ret-data-addto /
                          ret-data-removefrom
       ;; All conform to search-return-data, from RFC4466.


