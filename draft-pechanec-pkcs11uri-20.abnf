pk11-URI             = "pkcs11:" pk11-path [ "?" pk11-query ]
pk11-path            = [ pk11-pattr *(";" pk11-pattr) ]
pk11-pattr           = pk11-token / pk11-manuf / pk11-serial /
                         pk11-model / pk11-lib-manuf /
                         pk11-lib-ver / pk11-lib-desc /
                         pk11-object / pk11-type / pk11-id /
                         pk11-slot-desc / pk11-slot-manuf /
                         pk11-slot-id / pk11-v-pattr
pk11-qattr           = pk11-pin-source / pk11-pin-value /
                         pk11-module-name / pk11-module-path /
                         pk11-v-qattr
pk11-query           = [ pk11-qattr *("&" pk11-qattr) ]
pk11-res-avail       = ":" / "[" / "]" / "@" / "!" / "$" /
                         "'" / "(" / ")" / "*" / "+" / "," / "="
pk11-path-res-avail  = pk11-res-avail / "&"
pk11-query-res-avail = pk11-res-avail / "/" / "?" / "|"
pk11-pchar           = unreserved / pk11-path-res-avail / pct-encoded
pk11-qchar           = unreserved / pk11-query-res-avail / pct-encoded
pk11-token           = "token" "=" *pk11-pchar
pk11-manuf           = "manufacturer" "=" *pk11-pchar
pk11-serial          = "serial" "=" *pk11-pchar
pk11-model           = "model" "=" *pk11-pchar
pk11-lib-manuf       = "library-manufacturer" "=" *pk11-pchar
pk11-lib-desc        = "library-description" "=" *pk11-pchar
pk11-lib-ver         = "library-version" "=" 1*DIGIT [ "." 1*DIGIT ]
pk11-object          = "object" "=" *pk11-pchar
pk11-type            = "type" "=" ( "public" / "private" / "cert" /
                         "secret-key" / "data" )
pk11-id              = "id" "=" *pk11-pchar
pk11-slot-manuf      = "slot-manufacturer" "=" *pk11-pchar
pk11-slot-desc       = "slot-description" "=" *pk11-pchar
pk11-slot-id         = "slot-id" "=" 1*DIGIT
pk11-pin-source      = "pin-source" "=" *pk11-qchar
pk11-pin-value       = "pin-value" "=" *pk11-qchar
pk11-module-name     = "module-name" "=" *pk11-qchar
pk11-module-path     = "module-path" "=" *pk11-qchar
pk11-v-attr-nm-char  = ALPHA / DIGIT / "-" / "_"
pk11-v-pattr         = 1*pk11-v-attr-nm-char "=" *pk11-pchar
pk11-v-qattr         = 1*pk11-v-attr-nm-char "=" *pk11-qchar
   The URI path component contains attributes that identify a resource
   in a one level hierarchy provided by Cryptoki producers.  The query
   component can contain a few attributes that may be needed to retrieve
   the resource identified by the URI path component.  Attributes in the
   path component are delimited by ';' character, attributes in the
   query component use '&' as a delimiter.

   Both path and query components MAY contain vendor specific
   attributes.  Such attribute names MUST NOT clash with existing
   attribute names.  Note that in accordance with [BCP178], previously
   used convention of starting vendor attributes with an "x-" prefix is
   now deprecated.

   The general '/' delimiter MUST be percent-encoded in the path
   component so that generic URI parsers never split the path component
   into multiple segments.  It MAY be unencoded in the query component.
   Delimiter '?'  MUST be percent-encoded in the path component since
   the PKCS#11 URI uses a query component.  Delimiter '#' MUST be always
   percent-encoded so that generic URI parsers do not treat a hash as a
   beginning of a fragment identifier component.  All other generic
   delimiters MAY be used unencoded (':', '[', ']', and '@') in the
   PKCS#11 URI.

   The following table presents mapping between the PKCS#11 URI path
   component attributes and the PKCS#11 API structure members and object
   attributes.  Given that PKCS#11 URI users may be quite ignorant about
   the PKCS#11 specification the mapping is a product of a necessary
   compromise between how precisely are the URI attribute names mapped
   to the names in the specification and the ease of use and
   understanding of the URI scheme.

   +----------------------+---------------------+----------------------+
   | URI component path   | Attribute           | Attribute            |
   | attribute name       | represents          | corresponds in the   |
   |                      |                     | PKCS#11              |
   |                      |                     | specification to     |
   +----------------------+---------------------+----------------------+
   |                      |                     |                      |
   +----------------------+---------------------+----------------------+
   | id                   | key identifier for  | "CKA_ID" object      |
   |                      | object              | attribute            |
   +----------------------+---------------------+----------------------+
   | library-description  | character-string    | "libraryDescription" |
   |                      | description of the  | member of CK_INFO    |
   |                      | library             | structure.  It is a  |
   |                      |                     | UTF-8 string.        |
   +----------------------+---------------------+----------------------+
   | library-manufacturer | ID of the Cryptoki  | "manufacturerID"     |
   |                      | library             | member of the        |
   |                      | manufacturer        | CK_INFO structure.   |
   |                      |                     | It is a UTF-8        |
   |                      |                     | string.              |
   +----------------------+---------------------+----------------------+
   | library-version      | Cryptoki library    | "libraryVersion"     |
   |                      | version number      | member of CK_INFO    |
   |                      |                     | structure            |
   +----------------------+---------------------+----------------------+
   | manufacturer         | ID of the token     | "manufacturerID"     |
   |                      | manufacturer        | member of            |
   |                      |                     | CK_TOKEN_INFO        |
   |                      |                     | structure.  It is a  |
   |                      |                     | UTF-8 string.        |
   +----------------------+---------------------+----------------------+
   | model                | token model         | "model" member of    |
   |                      |                     | CK_TOKEN_INFO        |
   |                      |                     | structure.  It is a  |
   |                      |                     | UTF-8 string.        |
   +----------------------+---------------------+----------------------+
   | object               | description (name)  | "CKA_LABEL" object   |
   |                      | of the object       | attribute.  It is a  |
   |                      |                     | UTF-8 string.        |
   +----------------------+---------------------+----------------------+
   | serial               | character-string    | "serialNumber"       |
   |                      | serial number of    | member of            |
   |                      | the token           | CK_TOKEN_INFO        |
   |                      |                     | structure            |
   +----------------------+---------------------+----------------------+
   | slot-description     | slot description    | "slotDescription"    |
   |                      |                     | member of            |
   |                      |                     | CK_SLOT_INFO         |
   |                      |                     | structure.  It is a  |
   |                      |                     | UTF-8 string.        |
   +----------------------+---------------------+----------------------+
   | slot-id              | Cryptoki-assigned   | decimal number of    |
   |                      | value that          | "CK_SLOT_ID" type    |
   |                      | identifies a slot   |                      |
   +----------------------+---------------------+----------------------+
   | slot-manufacturer    | ID of the slot      | "manufacturerID"     |
   |                      | manufacturer        | member of            |
   |                      |                     | CK_SLOT_INFO         |
   |                      |                     | structure.  It is a  |
   |                      |                     | UTF-8 string.        |
   +----------------------+---------------------+----------------------+
   | token                | application-defined | "label" member of    |
   |                      | label, assigned     | the CK_TOKEN_INFO    |
   |                      | during token        | structure.  It is a  |
   |                      | initialization      | UTF-8 string.        |
   +----------------------+---------------------+----------------------+
   | type                 | object class (type) | "CKA_CLASS" object   |
   |                      |                     | attribute            |
   +----------------------+---------------------+----------------------+

    Table 1: Mapping between URI path component attributes and PKCS#11
                            specification names

   The following table presents mapping between the "type" attribute
   values and corresponding PKCS#11 object classes.

                +-----------------+----------------------+
                | Attribute value | PKCS#11 object class |
                +-----------------+----------------------+
                | public          | CKO_PUBLIC_KEY       |
                | private         | CKO_PRIVATE_KEY      |
                | cert            | CKO_CERTIFICATE      |
                | secret-key      | CKO_SECRET_KEY       |
                | data            | CKO_DATA             |
                +-----------------+----------------------+

     Table 2: Mapping between the "type" attribute and PKCS#11 object
                                  classes

   The query component attribute "pin-source" specifies where the
   application or library should find the normal user's token PIN, the
   "pin-value" attribute provides the normal user's PIN value directly,
   if needed, and the "module-name" and "module-path" attributes modify
   default settings for accessing PKCS#11 providers.  For the definition
   of a "normal user", see [PKCS11].

   The ABNF rules above is a best effort definition and this paragraph
   specifies additional constraints.  The PKCS#11 URI MUST NOT contain
   duplicate attributes of the same name in the URI path component.  It
   means that each attribute may be present at most once in the PKCS#11
   URI path component.  Aside from the query attributes defined in this
   document, duplicate (vendor) attributes MAY be present in the URI
   query component and it is up to the URI consumer to decide on how to
   deal with such duplicates.

   The whole value of the "id" attribute SHOULD be percent-encoded since
   the corresponding PKCS#11 "CKA_ID" object attribute can contain
   arbitrary binary data.

   The "library-version" attribute represents the major and minor
   version number of the library and its format is "M.N".  Both numbers
   are one byte in size, see the "libraryVersion" member of the CK_INFO
   structure in [PKCS11] for more information.  Value "M" for the
   attribute MUST be interpreted as "M" for the major and "0" for the
   minor version of the library.  If the attribute is present the major
   version number is REQUIRED.  Both "M" and "N" MUST be decimal
   numbers.

   Slot ID is a Cryptoki-assigned number that is not guaranteed stable
   across PKCS#11 module initializations.  However, there are certain
   libraries and modules which provide stable slot identifiers.  For
   these cases, when the slot description and manufacturer ID is not
   sufficient to uniquely identify a specific reader, the slot ID MAY be
   used to increase the precision of the token identification.  In other
   scenarios, using the slot IDs is likely to cause usability issues.

   An empty PKCS#11 URI path component attribute that does allow for an
   empty value matches a corresponding structure member or an object
   attribute with an empty value.  Note that according to the PKCS#11
   specification [PKCS11], empty character values in a PKCS#11 API
   producer must be padded with spaces and should not be NULL
   terminated.

manufacturer=Snake%20Oil,%20Inc.;
model=1.0;
object=my-certificate;
type=cert;
id=%69%95%3E%5C%F4%BD%EC%91;
serial=
type=private
type=private
manufacturer=Snake%20Oil,%20Inc.
library-description=Soft%20Token%20Library;
library-version=1.23
library-version=3;
id=%01%02%03%Ba%dd%Ca%fe%04%05%06

object=my-certificate;
type=cert

object=my-certificate;
type=cert

object=my-certificate;
type=cert;
vendor-aaa=value-a
