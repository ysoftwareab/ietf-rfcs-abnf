H323-URL		=	"h323:" address [ url-parameters ]
address		=	user / "@" hostport / user "@" hostport
user			=	%x21-x24 / %x26-x3F / %x41-7F / escaped
					; The symbols "%", "@", and symbols with a
					; character value below 0x21 may be represented
					; as escaped sequences.
hostport		=	host [ ":" port]
host			=	hostname / IPv4address / IPv6reference
hostname		=	*( domainlabel "." ) toplabel [ "." ]
domainlabel		=	alphanum / alphanum *( alphanum / "-" ) alphanum
toplabel		=	ALPHA / ALPHA *( alphanum / "-" ) alphanum
IPv4address		=	1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6reference		=	"[" IPv6address "]"
IPV6address		=	hexpart [ ":" IPv4address ]
hexpart			=	hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
hexseq			=	hex4 *( ":" hex4 )
hex4			=	1*4HEXDIG
port			=	1*DIGIT
url-parameters	=	*( ";" url-parameter )
url-parameter	=	%x21-x24 / %x26-x3A / %x3C-x7F / escaped
					; Specific parameter definitions are for further
					; study. The symbols "%", ";", and symbols with
					; a character value below 0x21 may be
					; represented as escaped sequences.
unreserved		=	alphanum / mark
alphanum		=	ALPHA / DIGIT
mark			=	"-" / "_" / "." / "!" / "~" / "*" / "'"
				/	"(" / ")" / "&" / "=" / "+" / "$" / ","
escaped		=	"%" HEXDIG HEXDIG

