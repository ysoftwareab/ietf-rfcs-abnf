SLURMFile = head body

head = firstLine *(commentLine / headLine)

body = *(commentLine / bodyLine)

firstLine = %x53.4c.55.52.4d SP "1.0" EOL ; "SLURM 1.0"

commentLine = *WSP [comment] EOL

headLine = *WSP headCommand [ 1*WSP [comment] ] EOL

bodyLine = *WSP bodyCommand [ 1*WSP [comment] ] EOL

comment = "#" *(VCHAR / WSP)

EOL = CRLF / LF

headCommand = target

target =
         %x74.61.72.67.65.74 1*WSP ; "target"
         1*VCHAR

bodyCommand = add / del

add =
         %x61.64.64 1*WSP ; "add"
         addItem

del =
         %x64.65.6c 1*WSP ; "del"
         delItem

addItem = addItemPrefixAS / addItemASKey

addItemPrefixAS =
         %x6f.72.69.67.69.6e.61.74.69.6f.6e 1*WSP ; "origination"
         IPprefixMaxLen 1*WSP
         ASnum

addItemASKey =
         %x62.67.70.73.65.63 1*WSP ; "bgpsec"
         ASnum 1*WSP
         RouterSKI 1*WSP
         RouterPubKey

delItem = delItemPrefix / delItemAS

delItemPrefix =
         %x6f.72.69.67.69.6e.61.74.69.6f.6e 1*WSP ; "origination"
         IPprefix

delItemAS =
         %x62.67.70.73.65.63 1*WSP ; "bgpsec"
         ASnum

IPprefix = IPv4prefix / IPv6prefix

IPprefixMaxLen = IPv4prefixMaxLen / IPv6prefixMaxLen

IPv4prefix = IPv4address "/" 1*2DIGIT
IPv6prefix = IPv6address "/" 1*3DIGIT

IPv4prefixMaxLen = IPv4prefix ["-" 1*2DIGIT]
IPv6prefixMaxLen = IPv6prefix ["-" 1*3DIGIT]

ASnum = 1*DIGIT

RouterSKI = Base64

RouterPubKey = Base64

Base64 = 1*(ALPHA / DIGIT / "+" / "/") 0*2"="

