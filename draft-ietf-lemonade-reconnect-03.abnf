login            = "LOGIN" SP userid SP password [login-params]

authenticate     = "AUTHENTICATE" SP auth-type
                      [SP (base64 / "=")] [login-params]
                      *(CRLF base64)

login-params    =/ *(SP "(" login-param-data ")"
                      ;; modifies the original IMAP4 LOGIN
                      ;; command to accept optional parameters

login-param-data = login-param *(SP login-param)

login-param      = astring /
                      "(" astring SP astring *(SP astring) ")"
                      ;; parameters to LOGIN may contain one or
                      ;; more atoms or strings - multiple items
                      ;; are always parenthesized

login-sid        = "SID" SP session-id [SP uidvalidity SP mod-
                      sequence-value [SP known-uids]]
                      ;; conforms to login-param-data syntax

uidvalidity      = nz-number

known-uids       = sequence-set
                      ;; sequence of UIDs, "*" is not allowed

command-any     =/ logout

logout           = "LOGOUT" [logout-params]

logout-params   =/ *(SP "(" logout-param-data ")"
                      ;; modifies the original IMAP4 LOGOUT
                      ;; command to accept optional parameters
logout-param-data= logout-param *(SP logout-param)

logout-param     = astring /
                      "(" astring SP astring *(SP astring) ")"
                      ;; parameters to LOGOUT may contain one or
                      ;; more atoms or strings - multiple items
                      ;; are always parenthesized

logout-preserve  = "PRESERVE"
                      ;; conforms to logout-param-data syntax

login-newsid     = "NEWSID"
                      ;; conforms to login-param-data syntax

mailbox-data    =/ folder-resp / newsid-resp / nosid-resp
                      ;; [[Is it Ok to use mailbox-data?]]

folder-resp      = "FOLDER" SP (folder | no_folder)

no_folder        = "()"

newsid-resp      = "NEWSID" SP session-id

session-id       = string ;; a unique session identifier,
                      ;; implementation specific.  If starts from "P"
                      ;; it must be followed by an international
                      ;; phone number.

nosid-resp       = "NOSID"
