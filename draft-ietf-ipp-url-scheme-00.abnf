ippURI        = "ipp://" hostport [ path ]
   
hostport      = host [ ":" port ]
host          = hostname | IPv4address | IPv6reference
hostname      = *( domainlabel "." ) toplabel [ "." ]
domainlabel   = alphanum | alphanum *( alphanum | "-" ) alphanum
toplabel      = alpha | alpha *( alphanum | "-" ) alphanum
port          = *digit
   
                 ; note - IPv6 support is added here
                 ; see RFC 2732 and RFC 2373 which update RFC 2396
IPv6reference = "[" IPv6address "]"
IPv6address   = hexpart [ ":" IPv4address ]
IPv4address   = 1*3digit "." 1*3digit "." 1*3digit "." 1*3digit
   
hexpart       = hexseq | hexseq "::" [ hexseq ] | "::" [ hexseq ]
hexseq        = hex4 *( ":" hex4)
hex4          = 1*4hex
   
path          = "/" segments
segments      = segment *( "/" segment )
   
                 ; note - parameters are NOT allowed on IPP URLs
                 ; see RFC 2396 which does allow parameters
segment       = *segchar
segchar       = unreserved | escaped |
                   ":" | "@" | "&" | "=" | "+" | "$" | ","
   
                 ; note - "[" and "]" are ADDED to 'reserved' for IPv6
                 ; see RFC 2732 and RFC 2373 which update RFC 2396
reserved      = ";" | "/" | "?" | ":" | "@" | "&" | "=" | "+" |
                   "$" | "," | "[" | "]"
   
unreserved    = alphanum | mark
mark          = "-" | "_" | "." | "!" | "~" | "*" | "'" |
                   "(" | ")"
   
escaped       = "%" hex hex
hex           = digit | "A" | "B" | "C" | "D" | "E" | "F" |
                           "a" | "b" | "c" | "d" | "e" | "f"
   
alphanum      = alpha | digit
alpha         = lowalpha | upalpha
   
lowalpha      = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" |
                   "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" |
                   "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
upalpha       = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" |
                   "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" |
                   "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
digit         = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" |
                   "8" | "9"
   
   
   
