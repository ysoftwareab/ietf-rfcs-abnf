parameter               =/ rn / cic / npdi
rn                      = ";rn=" (global-rn / local-rn)
npdi                    = ";npdi"
cic                     = ";cic=" (global-cic / local-cic)
global-rn               = global-hex-digits
local-rn                = 1*hex-phonedigit rn-context
rn-context              = ";rn-context=" rn-descriptor
rn-descriptor           = domainname / global-hex-digits
global-hex-digits       = "+" 1*3(DIGIT) *hex-phonedigit
hex-phonedigit          = HEXDIG / visual-separator
visual-separator        = "-" / "." / "(" / ")"
domainname              = *( domainlabel "." ) toplabel ["."]
domainlabel             = alphanum
                             / alphanum *( alphanum / "-" ) alphanum
toplabel                = ALPHA / ALPHA *( alphanum / "-" ) alphanum
alphanum                = ALPHA / DIGIT
global-cic              = global-hex-digits
local-cic               = 1*hex-phonedigit cic-context
cic-context             = ";cic-context=" rn-descriptor

