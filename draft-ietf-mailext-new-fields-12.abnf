identifier      =       "<" id-left-side "@" id-right-side ">"

id-left-side    =       dot-atom-text / no-fold-quote

id-right-side   =       dot-atom-text / no-fold-literal

no-fold-quote   =       DQUOTE *(qtext / quoted-pair) DQUOTE

dot-atom-text   =       1*atext *("." 1*atext)

atext           =       ALPHA / DIGIT / ; Any character except controls,
                        "!" / "#" /     ;  SP, and specials.
                        "$" / "%" /     ;  Used for atoms
                        "&" / "'" /
                        "*" / "+" /
                        "-" / "/" /
                        "=" / "?" /
                        "^" / "_" /
                        "`" / "{" /
                        "|" / "}" /
                        "~"

qtext           =       NO-WS-CTL /     ; Non-white-space controls
                        %d33 /          ; The rest of the US-ASCII
                        %d35-91 /       ;  characters not including "\"
                        %d93-127        ;  or the quote character

quoted-pair     =       ("\" text)

text            =       %d1-9 /         ; Characters excluding CR and LF
                        %d11-12 /
                        %d14-127

WSP             =       SP / HTAB               ; Whitespace characters

FWS             =       ([*WSP CRLF] 1*WSP)     ; Folding white-space

ctext           =       NO-WS-CTL /     ; Non-white-space controls
                        %d33-39 /       ; The rest of the US-ASCII
                        %d42-91 /       ;  characters not including "(",
                        %d93-127        ;  ")", or "\"

comment         =       "(" *([FWS] (ctext / quoted-pair / comment))
                        [FWS] ")"

CFWS            =       *([FWS] comment) (([FWS] comment) / FWS)


date-time       =       [ day-of-week "," ] date CFWS time [CFWS]

day-of-week     =       CFWS day-name CFWS

day-name        =       "Mon" / "Tue" / "Wed" / "Thu" /
                        "Fri" / "Sat" / "Sun"

date            =       day month year

year            =       ([CFWS] 4*DIGIT [CFWS]) / obs-year

month           =       CFWS month-name CFWS

month-name      =       "Jan" / "Feb" / "Mar" / "Apr" /
                        "May" / "Jun" / "Jul" / "Aug" /
                        "Sep" / "Oct" / "Nov" / "Dec"

day             =       [CFWS] 1*2DIGIT [CFWS]

time            =       time-of-day CFWS zone

time-of-day     =       hour ":" minute [ ":" second ]

hour            =       [CFWS] 2DIGIT [CFWS]

minute          =       [CFWS] 2DIGIT [CFWS]

second          =       [CFWS] 2DIGIT [CFWS]

zone            =       (( "+" / "-" ) 4DIGIT) / obs-zone


auto-submitted-field     = "Auto-Submitted:" [CFWS]
                                auto-submitted [CFWS] CRLF

auto-submitted           = ( "no" / "auto-generated" /
                                "auto-replied" /
                                private-extension /
                                future-extension )
                                optional-parameter-list

optional-parameter-list   = *( ";" [CFWS] parameter )

private-extension         = x-token

Future-extension          = token

Supersedes-field=       "Supersedes:" [CFWS] identifier *([CFWS]
                        identifier) [CFWS] CRLF

