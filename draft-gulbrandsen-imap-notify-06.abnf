capability      =/ "X-DRAFT-I06-NOTIFY"
                        ;; [[Note to RFC Editor: change the capability
                        ;; name before publication]]

command-auth    =/ notify
notify          = "NOTIFY" SP event-groups

event-groups    = event-group *(SP event-group)

event-group     = "(" filter-mailboxes SP events ")"

filter-mailboxes = "selected" / "inboxes" / "personal" /
                        "subscribed" / ( "pattern" SP mbox-or-pat )

events          = ( "(" event *(SP event) ")" ) / "NONE"
                        ;; As in draft-ietf-lemonade-msgevent-XX.txt
                        ;; "NONE" means that the client does not wish
                        ;; to receive any events for the specified
                        ;; mailboxes

event           = message-event
                        / mailbox-event / user-event / event-ext

message-match-criteria = "(" search-key ")"

message-event   = ( "NewMessage" SP
                            "(" fetch-att *(SP fetch-att) ")"
                          SP message-match-criteria )
                        / "ExpungeMessage"
                        / "FlagChange" SP message-match-criteria
                        / "AnnotationChange" SP message-match-criteria
                        ;; "NewMessage" includes "AppendMessage" from
                        [MSGEVENT].
                        ;; "FlagChange" is any of "ReadMessages",
                        ;; "TrashMessages", "SetFlags", "ClearFlags"
                        [MSGEVENT].  ;; "ExpungeMessage" includes
                        "ExpireMessage" [MSGEVENT].

mailbox-event   = "CreateMailbox" / "DeleteMailbox" /
                        "RenameMailbox" /
                        "SubscriptionChange" / "MailboxMetadataChange"
                        / "QuotaChange" / "AdminMailbox"
                        ;; [[Alexey: Should
                        "{Create/Delete/Rename}Mailbox" be
                        ;; replaced with a single event?]]
                        ;; [[Alexey: Add "LocationChange" - e.g. mailbox
                        ;; migration between servers?]]

user-event      = "OverQuota" / "UnderQuota"

event-ext       = atom
                        ;; For future extensions
oldname-extended-item =  "OLDNAME" SP "(" mailbox ")"
                        ;; Extended data item (mbox-list-extended-item)
                        ;; returned in a LIST response when a mailbox is
                        ;; renamed.
                        ;; Note 1: the OLDNAME tag can be returned
                        ;; with and without surrounding quotes, as per
                        ;; mbox-list-extended-item-tag production.

resp-text-code  =/ "NOTIFICATIONOVERFLOW"


