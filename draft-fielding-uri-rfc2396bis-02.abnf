uric        = reserved / unreserved / escaped

reserved    = "/" / "?" / "#" / "[" / "]" / ";" /
                    ":" / "@" / "&" / "=" / "+" / "$" / ","

unreserved  = ALPHA / DIGIT / mark

mark        = "-" / "_" / "." / "!" / "~" / "*" / "'" / "(" / ")"

escaped     = "%" HEXDIG HEXDIG

excluded    = invisible / delims / unwise

invisible   = CTL / SP / %x80-FF

delims      = "<" / ">" / "%" / DQUOTE

unwise      = "{" / "}" / "|" / "\" / "^" / "`"
URI         = scheme ":" hier-part [ "?" query ] [ "#" fragment ]

hier-part   = net-path / abs-path / rel-path

net-path    = "//" authority [ abs-path ]
abs-path    = "/"  path-segments
rel-path    = path-segments

scheme      = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )

authority   = [ userinfo "@" ] host [ ":" port ]

userinfo    = *( unreserved / escaped / ";" /
                       ":" / "&" / "=" / "+" / "$" / "," )

host        = [ IPv6reference / IPv4address / hostname ]

hostname    = domainlabel qualified
qualified   = *( "." domainlabel ) [ "." ]
domainlabel = alphanum [ 0*61( alphanum / "-" ) alphanum ]
alphanum    = ALPHA / DIGIT

IPv4address = dec-octet "." dec-octet "." dec-octet "." dec-octet
dec-octet   = DIGIT                 ; 0-9
                  / %x31-39 DIGIT         ; 10-99
                  / "1" 2DIGIT            ; 100-199
                  / "2" %x30-34 DIGIT     ; 200-249
                  / "25" %x30-35          ; 250-255

IPv6reference = "[" IPv6address "]"

IPv6address =                          6( h4 ":" ) ls32
                  /                     "::" 5( h4 ":" ) ls32
                  / [              h4 ] "::" 4( h4 ":" ) ls32
                  / [ *1( h4 ":" ) h4 ] "::" 3( h4 ":" ) ls32
                  / [ *2( h4 ":" ) h4 ] "::" 2( h4 ":" ) ls32
                  / [ *3( h4 ":" ) h4 ] "::"    h4 ":"   ls32
                  / [ *4( h4 ":" ) h4 ] "::"             ls32
                  / [ *5( h4 ":" ) h4 ] "::"             h4
                  / [ *6( h4 ":" ) h4 ] "::"

ls32        = ( h4 ":" h4 ) / IPv4address
                  ; least-significant 32 bits of address

h4          = 1*4HEXDIG

port        = *DIGIT

path-segments = segment *( "/" segment )
segment       = *pchar

pchar         = unreserved / escaped / ";" /
                      ":" / "@" / "&" / "=" / "+" / "$" / ","

query       = *( pchar / "/" / "?" )

fragment    = *( pchar / "/" / "?" )

URI-reference = URI / relative-URI

relative-URI  = hier-part [ "?" query ] [ "#" fragment ]

absolute-URI  = scheme ":" hier-part [ "?" query ]


result = ""

scheme    = $2
authority = $4
path      = $5
query     = $7
fragment  = $9

