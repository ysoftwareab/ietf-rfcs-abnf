acr-uri         = "acr:" anonymous-subscriber-identifier
anonymous-subscriber-identifier = 1*alphanum *par
par             = parameter / network-code / domainname
network-code    = ";ncc=" 1*uric
domainname      = ";domain=" *( domainlabel "." ) toplabel [ "." ]
domainlabel     = alphanum
                           / alphanum *( alphanum / "-" ) alphanum
toplabel        = ALPHA / ALPHA *( alphanum / "-" ) alphanum
parameter       = ";" pname [ "=" pvalue ]
pname           = 1*( alphanum / "-" )
pvalue          = 1*paramchar
paramchar       = param-unreserved / unreserved / pct-encoded
unreserved      = alphanum / mark
mark            = "-" / "_" / "." / "!" / "~" / "*" / "'" / "(" / ")"
pct-encoded     = "%" HEXDIG HEXDIG
param-unreserved= "[" / "]" / "/" / ":" / "&" / "+" / "$"
alphanum        = ALPHA / DIGIT
reserved        = ";" / "/" / "?" / ":" / "@" / "&" / "=" / "+" / "$"
                   / ","
uric            = reserved / unreserved / pct-encoded
DIGIT           = "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8"
                   / "9"
HEXDIG          = DIGIT / "A" / "B" / "C" / "D" / "E" / "F" / "a"
                   / "b" / "c" / "d" / "e" / "f"
ALPHA           = lowalpha / upalpha
lowalpha        = "a" / "b" / "c" / "d" / "e" / "f" / "g" / "h" / "i"
                   / "j" / "k" / "l" / "m" / "n" / "o" / "p" / "q" / "r"
                   / "s" / "t" / "u" / "v" / "w" / "x" / "y" / "z"
upalpha         = "A" / "B" / "C" / "D" / "E" / "F" / "G" / "H" / "I"
                   / "J" / "K" / "L" / "M" / "N" / "O" / "P" / "Q" / "R"
                   / "S" / "T" / "U" / "V" / "W" / "X" / "Y" / "Z"


                                 Figure 1

