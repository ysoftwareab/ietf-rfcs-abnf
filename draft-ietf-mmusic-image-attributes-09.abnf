sar=1.0 (1:1) etc. then the receiver can select the option that
image-attr = "imageattr:" PT 1*2( 1*WSP ( "send" / "recv" )
                                         1*WSP attr-list )

PT = 1*DIGIT / "*"
attr-list = ( set *(1*WSP set) ) / "*"
         ;  WSP and DIGIT defined in [RFC5234]
set= "[" "x=" xyrange "," "y=" xyrange *( "," key-value ) "]"
                  ; x is the horizontal image size range (pixel count)
                  ; y is the vertical image size range (pixel count)
key-value = ( "sar=" srange )
                 / ( "par=" prange )
                 / ( "q=" qvalue )
                  ; Key-value MAY be extended with other keyword
                  ;  parameters.
                  ; At most one instance each of sar, par, or q
                  ;  allowed in a set.
                  ;
                  ; sar (sample aspect ratio) is the sample aspect ratio
                  ;  associated with the set (optional,MAY be ignored)
                  ; par (picture aspect ratio) is the allowed
                  ;  ratio between the display's x and y physical
                  ;  size (optional)
                  ; q (optional, range [0.0..1.0], default value 0.5)
                  ;  is the preference for the given set,
                  ;  a higher value means a higher preference
onetonine = "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"
                  ; Digit between 1 and 9
xyvalue = onetonine *5DIGIT
                  ; Digit between 1 and 9 which is
                  ; followed by 0 to 5 other digits
step = xyvalue
xyrange = ( "[" xyvalue ":" [ step ":" ] xyvalue "]" )
                  ; Range between a lower and an upper value
                  ; with an optional step, default step = 1
                  ; The rightmost occurence of xyvalue MUST have a
                  ; higher value than the leftmost occurence.
               / ( "[" xyvalue 1*( "," xyvalue ) "]" )
                  ; Discrete values separated by ','
               / ( xyvalue )
                  ; A single value
spvalue = ( "0" "." onetonine *3DIGIT )
                  ; Values between 0.1000 and 0.9999
               / ( onetonine "." 1*4DIGIT )
                  ; Values between 1.0000 and 9.9999
srange =  ( "[" spvalue 1*( "," spvalue ) "]" )
                  ; Discrete values separated by ','.
                  ; Each occurrence of spvalue MUST be
                  ; greater than the previous occurrence.
               / ( "[" spvalue "-" spvalue "]" )
                  ; Range between a lower and an upper level (inclusive)
                  ; The second occurence of spvalue MUST  have a higher
                  ; value than the first
               / ( spvalue )
                  ; A single value
prange =  ( "[" spvalue "-" spvalue "]" )
                  ; Range between a lower and an upper level (inclusive)
                  ; The second occurence of spvalue MUST  have a higher
                  ; value than the first

qvalue  = ( "0" "." 1*2DIGIT )
               / ( "1" "." 1*2("0") )
                  ; Values between 0.00 and 1.00
par=[ratio_min-ratio_max]
a=imageattr:97 send * recv *
a=imageattr:97 send [x=720,y=576,sar=1.1]
a=imageattr:97 send [x=720,y=576,sar=[0.91,1.0,1.09,1.45]]
a=acap:1 imageattr:97 send [x=720,y=576,sar=[0.91,1.0,1.09,1.45]]
a=mcap:1 video H264/90000
a=acap:1 imageattr:%1% send [x=720,y=576,sar=[0.91,1.0,1.09,1.45]]
a=mcap:1 video H264/90000
a=mscap:1 imageattr send [x=720,y=576,sar=[0.91,1.0,1.09,1.45]]
a=imageattr:PT send attr-list recv attr-list
a=imageattr:PT send attr-list recv *
a=imageattr:PT send attr-list recv attr-list
a=imageattr:PT recv attr-list
sar=1.1 having the highest preference
a=imageattr:97 send [x=800,y=640,sar=1.1,q=0.6] [x=480,y=320] \
                   recv [x=330,y=250]
a=imageattr:97 recv [x=800,y=640,sar=1.1] \
                   send [x=330,y=250]
a=imageattr:97 recv [x=800,y=640,sar=1.1] \
                   send [x=[320:16:640],y=[240:16:480],par=[1.2-1.3]]
a=imageattr:97 send [x=800,y=640,sar=1.1] \
                   recv [x=336,y=256]
a=imageattr:97 recv [x=800,y=640,sar=1.1] \
                   send [x=336,y=256]
a=imageattr:97 \
      send [x=[480:16:800],y=[320:16:640],par=[1.2-1.3],q=0.6] \
           [x=[176:8:208],y=[144:8:176],par=[1.2-1.3]] \
      recv *
m=video 49154 RTP/AVP 99
a=rtpmap:99 H264/90000
a=fmtp:99 packetization-mode=0;profile-level-id=42e011; \
sprop-parameter-sets=Z0LgC5ZUCg/I,aM4BrFSAa
a=imageattr:99 \
      send [x=176,y=144] [x=224,y=176] [x=272,y=224] [x=320,y=240] \
      recv [x=176,y=144] [x=224,y=176] [x=272,y=224,q=0.6] [x=320,y=240]
m=video 49154 RTP/AVP 99
a=rtpmap:99 H264/90000
a=fmtp:99 packetization-mode=0;profile-level-id=42e011; \
sprop-parameter-sets=Z0LgC5ZUCg/I,aM4BrFSAa
a=imageattr:99 send [x=320,y=240] recv [x=320,y=240]
a=imageattr:97 \
       send [x=400:16:800],y=[320:16:640],sar=[1.0-1.3],par=[1.2-1.3]] \
       recv [x=800,y=600,sar=1.1]
a=imageattr:97 \
      recv [x=464,y=384,sar=1.15] \
      send [x=800,y=600,sar=1.1]
