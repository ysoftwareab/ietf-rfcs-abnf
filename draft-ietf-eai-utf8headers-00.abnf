UTF8-xtra-char  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   %xC2-DF UTF8-tail

UTF8-3          =   %xE0 %xA0-BF UTF8-tail /
                       %xE1-EC 2(UTF8-tail) /
                       %xED %x80-9F UTF8-tail /
                       %xEE-EF 2(UTF8-tail)

UTF8-4          =   %xF0 %x90-BF 2(UTF8-tail) /
                       %xF1-F7 3(UTF8-tail)

UTF8-tail       =   %x80-BF

ctext        =  NO-WS-CTL /     ; all of <text> except
                   %d33-39 /       ; SP, HTAB, "(", ")"
                   %d42-91 /       ; and "\"
                   %d93-126 /
                   UTF8-xtra-char

qtext        =  NO-WS-CTL /     ; all of <text> except
                   %d33 /          ; The rest of the US-ASCII
                   %d35-91 /           ; characters not including "\"
                   %d93-126 /          ; or the quote character
                   UTF8-xtra-char

text         =  %d1-9 /         ; all UTF-8 characters except
                   %d11-12 /       ; US-ASCII NUL, CR and LF
                   %d14-127 /
                   UTF8-xtra-char

utext        =  NO-WS-CTL /     ; Non white space controls
                   %d33-126 /      ; The rest of US-ASCII
                   UTF8-xtra-char

unstructured =  1*( [FWS] utext ) [FWS]

atext        =  ALPHA / DIGIT /
                   "!" / "#" /     ; Any character except
                   "$" / "%" /     ; controls, SP, and specials.
                   "&" / "'" /     ; Used for atoms
                   "*" / "+" /
                   "-" / "/" /
                   "=" / "?" /
                   "^" / "_" /
                   "`" / "{" /
                   "|" / "}" /
                   "~" /
                   UTF8-xtra-char

angle-addr     =  [CFWS] "<" addr-spec [ alt-address ] ">" [CFWS]

alt-address    =  alt-separator stct-addr /
                     alt-separator "atomic"

alt-separator  =  [FWS] "," [FWS]

stct-addr           =   stct-local-part "@" stct-domain
stct-local-part     =   stct-dot-atom / stct-quoted-string
stct-dot-atom       =   [CFWS] stct-dot-atom-text [CFWS]
stct-dot-atom-text  =   1*stct-atext *( "." 1*stct-atext 0
stct-atext          =   ALPHA / DIGIT /
                           "!" / "#" /      ; Any character except
                           "$" / "%" /      ; controls, SP, and
                           "&" / "'" /      ; specials. Used for
                           "*" / "+" /      ; atoms
                           "-" / "/" /
                           "=" / "?" /
                           "^" / "_" /
                           "`" / "{" /
                           "|" / "}" /
                           "~"

stct-quoted-string  =   [CFWS] DQUOTE
                           *( [FWS] strict-qcontent ) [FWS]
                           DQUOTE [CFWS]

stct-qcontent       =   stct-qtext / stct-quoted-pair

stct-qtext          =   NO-WS-CTL /      ; qtext restricted to
                           %d33 /           ; US-ASCII
                           %d35-91 /
                           %d93-126
stct-quoted-pair    =   "\" stct-text

stct-text           =   %d1-9 /          ; text restricted to
                           %d11-12 /        ; US-ASCII
                           %d14-127
stct-domain         =   stct-dot-atom / domain-literal

