rule-collection =  RULE-SET / 1*(CONDITIONAL-SET)
conditional-set =  CONDITION RULE-SET
condition       =  "<" NUMLIST ">"

rule-set        =  *(RULE ';')
rule            =  [ AF ] FLOW EXTRA ACTION [ WHERE ]

af              = "ip4" | "ip6"
flow            =  ("tcp" / "udp") (PORT-ADDR-PAIR / ANY-PORT)
flow            =/ ("ipsec" / "ah" / "esp") SPI-ADDR-PAIR
flow            =/ "icmp" [ ICMP-SPEC ] ADDR-PAIR
flow            =/ "proto" NEG-RANGE ADDR-PAIR
flow            =/ "any" LABEL-ADDR-PAIR
extra           =  [ "hoplimit" NEG-RANGE ] [ "tclass" NEG-RANGE ]
                      [ "ip6h" NEG-RANGE ]
extra           =/ [ "tos" NEG-RANGE ] [ "ttl" TTL ]
action          =  "on" (RR-LIST / CAST-LIST) / "drop"
where           = "at" ("local" / PREFIX)

port-addr-pair  =  [ "local" PORT-ADDR ] [ "peer" PORT-ADDR ]
port-addr       =  [ PREFIX ] PORT-SPEC / PREFIX
any-port        =  PORT-SPEC ADDR-PAIR
spi-addr-pair   =  [ "local" SPI-ADDR ] [ "peer" SPI-ADDR ]
spi-addr        =  [ PREFIX ] "spi" NEG-RANGE / PREFIX
label-addr-pair =  [ "local" LABEL-ADDR ] [ "peer" LABEL-ADDR ]
label-addr      =  [ PREFIX ] "label" NEG-RANGE / PREFIX
addr-pair       =  [ "local" PREFIX ] [ "peer" PREFIX ]
icmp-spec       =  "type" NEG-RANGE [ "code" NEG-RANGE ]
rr-list         =  NUMLIST
cast-list       =  NUMBER 1*("&" NUMBER)

prefix          =  ADDR [ "/" NUMBER ]
addr            =  HEXSEQ [ "::" [ HEXSEQ ] ] / "::" [ HEXSEQ ]
addr            =/ 1*3DIGIT 3("." 1*3DIGIT)
hexseq          =  HEX4 *(":" HEX4)
hex4            =  1*4HEXDIGIT
port-spec       =  "port" NEG-RANGE
neg-range       =  [ NOT ] RANGE
not             =  "!" / "not" / "no"
range           =  NUMBER [ "-" NUMBER ]
numlist         =  NUMBER *("," NUMBER)
number          =  DEC-NUMBER / HEX-NUMBER
dec-number      =  1*DIGIT
hex-number      =  "0x" 1*HEXDIGIT
hexdigit        =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
digit           =  %x30-39

