fields          =/ *( newsgroups /
                         path /
                         injection-date /
                         followup-to /
                         expires /
                         control /
                         supersedes /
                         distribution /
                         summary /
                         approved /
                         organization /
                         xref /
                         archive /
                         user-agent /
                         injection-info )

message-id      =  "Message-ID:" SP msg-id CRLF

msg-id          =  [FWS] msg-id-core [FWS]

msg-id-core     =  "<" id-left "@" id-right ">"
                      ; maximum length is 250 octets

id-left         =  dot-atom-text / no-fold-quote / obs-id-left

id-right        =  dot-atom-text / no-fold-literal / obs-id-right

no-fold-quote   =  DQUOTE *( qtext / no-space-qp ) DQUOTE

no-fold-literal =  "[" *( htext / no-space-qp ) "]"

no-space-qp     =  ( "\" ptext ) / obs-qp

ptext           =  %d33-61 /  ; Printable characters excluding ">"
                      %d63-126 /
                      obs-text

htext           =  HEXDIG /  ; hexadecimal digits, case-insensitive
                      "." /     ; IPv4 separator
                      ":"       ; IPv6 separator

newsgroups      =  "Newsgroups:" SP newsgroup-list CRLF

newsgroup-list  =  [FWS] newsgroup-name
                      *( "," [FWS] newsgroup-name ) [FWS]

newsgroup-name  =  component *( "." component ) ; 66 character max

component       =  plain-component

plain-component =  component-start *29component-rest

component-start =  ALPHA / DIGIT

component-rest  =  ALPHA / DIGIT / "+" / "-" / "_"

path            =  "Path:" SP path-list CRLF

path-list       =  [FWS] path-identity
                      *( path-delimiter [FWS] path-identity ) [FWS]

path-identity   =  ( ALPHA / DIGIT )
                      *( ALPHA / DIGIT / "-" / "." / ":" / "_" )

path-delimiter  =  "!"
injection-date  =  "Injection-Date:" SP date-time CRLF


followup-to     =  "Followup-To:" SP ( newsgroup-list / poster-text )
                      CRLF

poster-text     =  [FWS] %d112.111.115.116.101.114 [FWS]
                      ; "poster" in lower-case

expires         =  "Expires:" SP date-time CRLF

control         =  "Control:" SP [CFWS] control-message [CFWS] CRLF

control-message = <empty>

supersedes      =  "Supersedes:" SP [CFWS] msg-id-core [CFWS] CRLF

distribution    =  "Distribution:" SP dist-list CRLF

list-list       =  [FWS] dist-name *( "," [FWS] dist-name ) [FWS]

dist-name       =  ALPHA / DIGIT
                      *( ALPHA / DIGIT / "+" / "-" / "_" )

summary         =  "Summary:" SP unstructured CRLF
approved        =  "Approved:" SP mailbox-list CRLF


organization    =  "Organization:" SP unstructured CRLF

xref            =  "Xref:" SP [CFWS] server-name
                      1*( CFWS location ) [CFWS] CRLF

server-name     =  path-identity

location        =  newsgroup-name ":" article-locator

article-locator =  1*( %x21-27 / %x29-3A / %x3C-7E )
                      ; US-ASCII printable characters
                      ; except '(' and ';'

archive         =  "Archive:" SP [CFWS] ("no" / "yes")
                      *( [CFWS] ";" archive-param ) CRLF

archive-param   =  <a parameter with attribute "filename"
                       and any value>


user-agent      =  "User-Agent:" SP 1*product CRLF

product         =  [CFWS] token [CFWS] [ "/" product-version ]

product-version =  [CFWS] token [CFWS]


injection-info  =  "Injection-Info:" SP [CFWS] path-identity [CFWS]
                      *( ";" injection-info-parameter )

host-value      =  dot-atom / [ dot-atom ":" ]
                      ( IPv4address / IPv6address ) ;  see [RFC 2373]

sender-value    =  mailbox / "verified"

