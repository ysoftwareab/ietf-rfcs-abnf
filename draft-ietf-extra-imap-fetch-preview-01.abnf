capability        =/ "PREVIEW=FUZZY"

fetch-att         =/ "PREVIEW" [SP "(" preview-alg-fetch *(SP
                          preview-alg-fetch) ")"]

msg-att-dynamic   =/ "PREVIEW" SP "(" preview-alg SP nstring ")"

preview-alg       =  "FUZZY" / preview-alg-ext

preview-alg-ext   =  preview-atom  ; New algorithm names MUST
                                        ; conform with the
                                        ; recommendations described in
                                        ; RFC 6648, Section 3

preview-alg-fetch =  preview-alg / preview-mod "=" preview-alg

preview-atom      =  1*<any ATOM-CHAR except "=">

preview-mod       =  "LAZY" / preview-mod-ext

preview-mod-ext   =  preview-atom  ; New priority modifier names
                                        ; MUST conform with the
                                        ; recommendations described in
                                        ; RFC 6648, Section 3

