rule           =  (flush-rule / permit-all-rule  
                           / l2-filter-rule / l2-tunnel-rule 
 
 
                           / ip-filter-rule / ip-tunnel-rule  
                           / http-filter-rule / http-redir-rule)  
                           rule-delim 
    
flush-rule     =  "flush" 
    
permit-all-rule = "permit inout any from any to any" 
    
l2-filter-rule =  ("permit" / "deny") " "  
                           ("in" / "out" / "inout") " "  
                           l2-filter-body [" " log-rule] 
l2-filter-body =  (l2-proto " from " l2-address " to "  
                           l2-address) / l2-rmon-str 
l2-proto       =  "l2:ether2" [":0x" 1*4HEXDIG]    
l2-rmon-str    =  "l2:" 1*DIGIT *("." 1*DIGIT) 
l2-address     =  ["!"] (macaddr / (macaddr "/" macmask)  
                           / "any") 
macaddr        =  2HEXDIG 5("-" 2HEXDIG) 
macmask        =  DIGIT                ; 0-9                    
                           / %x31-33 DIGIT      ; 10-39 
                           / "4" %x30-38        ; 40-48 
    
l2-tunnel-rule =  "tunnel " tunnel-id " "  
                           ("in" / "out" / "inout") " " 
                           l2-filter-body [" " log-rule] 
    
ip-filter-rule =  ("permit" / "deny") " "  
                           ("in" / "out" / "inout") " " 
                           ("ip" / ip-proto) filter-body  
                           [" " ip-option] [" " log-rule] 
ip-proto       =  d8 
ip-address     =  ["!"] (ipv4-address ["/" ipv4mask] /  
                                  ipv6-address ["/" ipv6mask] / 
                                  "any" /  
                                  "assigned")  
ipv4-address   =  d8 "." d8 "." d8 "." d8    
ipv4mask       =  DIGIT                   ; 0-9 
                           / %x31-32 DIGIT         ; 10-29 
                           / "3" %x30-32           ; 30-32 
ipv6-address   =  1*4HEXDIG 7(":" 1*4HEXDIG) 
ipv6mask       =  DIGIT                   ; 0-9 
                           / %x31-39 DIGIT         ; 10-99 
                           / "1" %x30-31 DIGIT     ; 100-119 
                           / "1" %x32 %x30-38      ; 120-128 
tcp-ports      =  tcp-port *("," tcp-port)  
 
 
tcp-port       =  d16 / d16 "-" d16 
ip-option      =  "frag" / 
                           ["ipoptions " ["!"] ipopt *("," ["!"] ipopt)] 
                           ["tcpoptions " ["!"] tcpopt  
                            *("," ["!"] tcpopt)] 
                           ["established"] 
                           ["setup"] 
                           ["tcpflags " ["!"] tcpflag  
                            *("," ["!"] tcpflag)] 
                           ["icmptypes " icmptype *("," icmptype)] 
ipopt          =  "ssrr" / "lsrr" / "rr" / "ts" 
tcpopt         =  "mss" / "window" / "sack" / "ts" / "cc" 
tcpflag        =  "fin" / "syn" / "rst" / "psh" / "ack" / "urg" 
       
icmptype       =  d8 / d8 "-" d8  
                           / "echo reply" / "destination unreachable"  
                           / "source quench" / "redirect"  
                           / "echo request" / "router advertisement"  
                           / "router solicit" / "time-to-live exceeded"   
                           / "IP header bad" / "timestamp request"  
                           / "timestamp reply" / "information request"  
                           / "information reply"  
                           / "address mask request"  
                           / "address mask reply" 
       
ip-tunnel-rule =  "tunnel " tunnel-id " "  
                           ("in" / "out" / "inout") " " 
                           ("ip" / ip-proto) filter-body  
                           [" " ip-option] [" " log-rule] 
       
http-filter-rule= ("permit" / "deny") org-url " "  
                           ("in" / "out" / "inout") filter-body  
                           [" " log-rule] 
       
          
http-redir-rule=  "redirect " [redir-cnt " "] redir-url  
                           filter-body [" " org-url] 
                           [" " log-rule] 
redir-cnt      =  1*DIGIT 
org-url        =  http_URL  
                           ;Note: Syntax for http_URL defined in  
                           ;[RFC2616], section 3.2.2 
redir-url      =  http_URL 
       
filter-body    =  " from " ip-address [" " tcp-ports] 
                           " to " ip-address [" " tcp-ports] 
 
 
tunnel-id      =  DQUOTE          
                           1*(TEXTDATA / ("%" 2HEXDIG)) 
                           DQUOTE 
log-rule       =  "cnt" 
       
LF             =  %x0A                  ; linefeed 
DIGIT          =  %x30-39               ; 0-9 
DQUOTE         =  %x22                  ; " (Double Quote) 
HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"   
rule-delim     =  LF 
d8             =  DIGIT                 ; 0-9 
                           / %x31-39 DIGIT       ; 10-99 
                           / "1" 2DIGIT          ; 100-199 
                           / "2" %x30-34 DIGIT   ; 200-249 
                           / "25" %x30-35        ; 250-255 
d16            =  DIGIT                 ; 0-9 
                           / %x31-35 1*4DIGIT    ; 10-59999 
                           / "6" "4" 3DIGIT      ; 60000-64999 
                           / "6" "5" %x30-34 2DIGIT     ; 65000-65499 
                           / "6" "5" "5" %x30-32 1DIGIT  ; 65500-65529 
                           / "6" "5" "5" "3" %x30-36     ; 65530-65536 
TEXTDATA       =  %x20-21 / %x23-24 / %x26-7E 
       
