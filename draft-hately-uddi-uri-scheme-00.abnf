uddiScheme           =  %d117.100.100.105 ; "uddi" in lower case 
    
uddiKey              =  keyGeneratorKey / nonKeyGeneratorKey 
    
nonKeyGeneratorKey   =  uuidKey / domainKey / derivedKey 
    
uuidKey              =  uddiScheme ":" uuid_part 
    
 
 
uuid_part            =  8HEXDIG "-" 
                           4HEXDIG "-" 
                           4HEXDIG "-" 
                           4HEXDIG "-" 
                           12HEXDIG 
    
domainKey            =  uddiScheme ":" hostname 
    
hostname             =  *(domainlabel ".") toplabel ["."] 
    
domainlabel          =  alphanum /  
                           alphanum *(alphanum / "-") alphanum 
    
toplabel             =  ALPHA / ALPHA *(alphanum / "-") alphanum 
    
alphanum             =  ALPHA / DIGIT 
    
derivedKey           =  nonKeyGeneratorKey ":" KSS 
    
keyGeneratorKey      =  nonKeyGeneratorKey ":" "keygenerator" 
    
KSS                  =  1*uric ; KSS MUST NOT BE "keygenerator" 
    
Uric                 =  reserved / unreserved / escaped 
    
reserved             =  ";" / "/" / "?" / ":" / "@" / "&" / 
                           "=" / "+" / "$" / "," 
    
unreserved           =  alphanum / mark 
    
mark                 =  "-" / "_" / "." / "!" / "~" / "*" / 
                           "'" / "(" / ")" 
    
escaped              =  "%"  HEXDIG HEXDIG 
    
