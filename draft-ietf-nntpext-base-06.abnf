article-command = "ARTICLE" [1*WSP (msg-id / article-number)]
               *WSP CRLF
article-number = 1*16DIGIT
augument = parameter ; excluding sequence ".."
body-command = "BODY" [1*WSP (msg-id / article-number)] *WSP
               CRLF
command = article-command /
               body-command /
               date-command /
               group-command /
               head-command /
               help-command /
               ihave-command /
               last-command /
               list-active-times-command /
               list-distrib-pats-command /
               list-distributions-command /
               list-extensions-command /
               list-newsgroups-command /
               list-overview-fmt-command /
               list-subscriptions-command /
               list-command /
               listgroup-command /
               mode-reader-command /
               newgroups-command /
               newnews-command /
               next-command /
               over-command /
               pat-command /
               post-command /
               quit-command /
               stat-command
CR = %x0D
CRLF = CR LF
date-command = "DATE" *WSP CRLF
date = 6*8DIGIT
DIGIT = %x30-39
distribution = parameter
group-command = "GROUP" 1*WSP newsgroup *WSP CRLF
head-command = "HEAD" [1*WSP (msg-id / article-number)] *WSP
               CRLF
header = parameter
help-command = "HELP" *WSP CRLF
HT = %x09
ihave-command = "IHAVE" 1*WSP msg-id *WSP CRLF
last-command = "LAST" *WSP CRLF
LF = %x0A
list-active-times-command = "LIST" 1*WSP "ACTIVE.TIMES"
               [1*WSP wildmat] *WSP CRLF
list-command = "LIST" [1*WSP "ACTIVE" [1*WSP wildmat]] *WSP
               CRLF
list-distrib-pats-command = "LIST" 1*WSP "DISTRIB.PATS" *WSP
               CRLF
list-distributions-command = "LIST" 1*WSP "DISTRIBUTIONS" *WSP
               CRLF
list-extensions-command = "LIST" 1*WSP "EXTENSIONS" *WSP CRLF
list-newsgroups-command = "LIST" 1*WSP "NEWSGROUPS" [1*WSP
               wildmat]
               *WSP CRLF
list-overview-fmt-command = "LIST" 1*WSP "OVERVIEW.FMT" *WSP
               CRLF
list-subscriptions-command = "LIST" 1*WSP "SUBSCRIPTIONS" *WSP
               CRLF
listgroup-command = "LISTGROUP" [1*WSP newsgroup] *WSP CRLF
mode-reader-command = "MODE" 1*WSP "READER" *WSP CRLF
msg-id = <defined in RFC822>
newgroups-command = "NEWGROUPS" 1*WSP date 1*WSP time [1*WSP
               "GMT"/"UTC"][1*WSP "<" distribution *("," distribution)
               ">"] *WSP CRLF
newnews-command = "NEWNEWS" 1*WSP newsgroup *("," newsgroup)
               1*WSP date 1*WSP time [1*WSP "GMT"/"UTC"]
               [1*WSP "<" distribution *("," distribution) ">"]
               *WSP CRLF
newsgroup = parameter
next-command = "NEXT" *WSP CRLF
over-command = "OVER" [1*WSP range] *WSP CRLF
parameter = 1*(%x21-FF) ; generic command parameter
pat-command = "PAT" 1*WSP header 1*WSP (range / msg-id)
               *(1*WSP wildmat) *WSP CRLF
post-command = "POST" *WSP CRLF
quit-command = "QUIT" *WSP CRLF
range = article-number ["-" [article-number]]
SP = %x20
stat-command = "STAT" [1*WSP (msg-id / article-number)] *WSP
               CRLF
time = 6DIGIT
UTF-8-non-ascii = %xC0-FF 1*(%x80-BF) ; UTF-8 encoding of non-
               ASCII character
wildmat = 1*("*" / "?" / wildmat-exact / wildmat-set /
wildmat-exact = %x21-29 / %x2B-3E / %x40-5A / %x5D-7F / UTF-8-
               non-ascii
wildmat-non-hyphen = %x21-2C / %x2E-7F / UTF-8-non-ascii ;
               exclude space -
wildmat-set = "[" ["^"] ["]" / "-"] *(wildmat-non-hyphen ["-"
WSP = SP / HT


