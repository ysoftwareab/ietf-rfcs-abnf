pkixmsg    = preeb
               *eolWSP
               base64text
               posteb

preeb      = "-----BEGIN " label "-----" eol

posteb     = "-----END " label "-----" eol

base64char = ALPHA / DIGIT / "+" / "/"

base64pad  = "="

base64line = 1*base64char eol

base64finl = *base64char (base64pad eol base64pad /
                 *2base64pad) eol
                 ; ...AB= <CRLF> = <CRLF> is not good, but is valid

base64text = *base64line base64finl
          ; we could also use <encbinbody> from RFC 1421, which requires
          ; 16 groups of 4 chars, which means exactly 64 chars per
          ; line, except the final line, but this is more accurate

labelchar  = %x21-2C / %x2E-%7E   ; any printable character,
                                      ; except hyphen

label      = labelchar *(labelchar / labelchar "-" / SP) labelchar

eol        = CRLF / CR / LF

eolWSP     = WSP / CR / LF    ; compare with LWSP

                              Figure 1: ABNF
pkixmsgstrict    = preeb
                      strictbase64text
                      posteb

strictbase64finl = *15(4base64char) (4base64char / 3base64char
                        base64pad / 2base64char 2base64pad) eol

base64fullline   = 64base64char eol

strictbase64text = *base64fullline strictbase64finl

                          Figure 2: ABNF (Strict)

OsYGYUFdAH0RNc1p4VbKEAQUM2Xo8PMHBoYdqEcsbTodlCFAZH4=
dvb05OXi5XLPLEtViMwvLVLwSE0sKlFIVHAqSk3MBkkBAJv0Fx0=
QC7k0NNzUHTV9yGDwfqMbw==
Smluak1aZIttePeTAHeJJs8izNJ5aR3Wcd3A5gLztQ==
OsYGYUFdAH0RNc1p4VbKEAQUM2Xo8PMHBoYdqEcsbTodlCFAZH4=
