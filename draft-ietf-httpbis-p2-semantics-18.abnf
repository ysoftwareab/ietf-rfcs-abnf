BWS           = <BWS, defined in [Part1], Section 1.2.2>
OWS           = <OWS, defined in [Part1], Section 1.2.2>
RWS           = <RWS, defined in [Part1], Section 1.2.2>
obs-text      = <obs-text, defined in [Part1], Section 1.2.2>
quoted-string = <quoted-string, defined in [Part1], Section 3.2.3>
token         = <token, defined in [Part1], Section 3.2.3>

absolute-URI  = <absolute-URI, defined in [Part1], Section 2.7>
comment       = <comment, defined in [Part1], Section 3.2>
partial-URI   = <partial-URI, defined in [Part1], Section 2.7>
product       = <product, defined in [Part1], Section 5.2>
URI-reference = <URI-reference, defined in [Part1], Section 2.7>

Method         = token

Status-Code    = 3DIGIT
Reason-Phrase  = *( HTAB / SP / VCHAR / obs-text )

HTTP-date    = rfc1123-date / obs-date

rfc1123-date = day-name "," SP date1 SP time-of-day SP GMT
day-name     = %x4D.6F.6E ; "Mon", case-sensitive
                  / %x54.75.65 ; "Tue", case-sensitive
                  / %x57.65.64 ; "Wed", case-sensitive
                  / %x54.68.75 ; "Thu", case-sensitive
                  / %x46.72.69 ; "Fri", case-sensitive
                  / %x53.61.74 ; "Sat", case-sensitive
                  / %x53.75.6E ; "Sun", case-sensitive

date1        = day SP month SP year
                  ; e.g., 02 Jun 1982

day          = 2DIGIT
month        = %x4A.61.6E ; "Jan", case-sensitive
                  / %x46.65.62 ; "Feb", case-sensitive
                  / %x4D.61.72 ; "Mar", case-sensitive
                  / %x41.70.72 ; "Apr", case-sensitive
                  / %x4D.61.79 ; "May", case-sensitive
                  / %x4A.75.6E ; "Jun", case-sensitive
                  / %x4A.75.6C ; "Jul", case-sensitive
                  / %x41.75.67 ; "Aug", case-sensitive
                  / %x53.65.70 ; "Sep", case-sensitive
                  / %x4F.63.74 ; "Oct", case-sensitive
                  / %x4E.6F.76 ; "Nov", case-sensitive
                  / %x44.65.63 ; "Dec", case-sensitive
year         = 4DIGIT

GMT   = %x47.4D.54 ; "GMT", case-sensitive

time-of-day  = hour ":" minute ":" second
                    ; 00:00:00 - 23:59:59

hour         = 2DIGIT
minute       = 2DIGIT
second       = 2DIGIT

obs-date     = rfc850-date / asctime-date
rfc850-date  = day-name-l "," SP date2 SP time-of-day SP GMT
date2        = day "-" month "-" 2DIGIT
                    ; day-month-year (e.g., 02-Jun-82)

day-name-l   = %x4D.6F.6E.64.61.79 ; "Monday", case-sensitive
            / %x54.75.65.73.64.61.79 ; "Tuesday", case-sensitive
            / %x57.65.64.6E.65.73.64.61.79 ; "Wednesday", case-sensitive
            / %x54.68.75.72.73.64.61.79 ; "Thursday", case-sensitive
            / %x46.72.69.64.61.79 ; "Friday", case-sensitive
            / %x53.61.74.75.72.64.61.79 ; "Saturday", case-sensitive
            / %x53.75.6E.64.61.79 ; "Sunday", case-sensitive


asctime-date = day-name SP date3 SP time-of-day SP year
date3        = month SP ( 2DIGIT / ( SP 1DIGIT ))
                    ; month day (e.g., Jun  2)

      Note: Recipients of date values are encouraged to be robust in
      accepting date values that might have been sent by non-HTTP
      applications, as is sometimes the case when retrieving or posting
      messages via proxies/gateways to SMTP or NNTP.

      Note: HTTP requirements for the date/time stamp format apply only
      to their usage within the protocol stream.  Clients and servers
      are not required to use these formats for user presentation,
      request logging, etc.

Allow = #Method

Date = HTTP-date

Expect       = 1#expectation

expectation  = expect-name [ BWS "=" BWS expect-value ]
                                *( OWS ";" [ OWS expect-param ] )
expect-param = expect-name [ BWS "=" BWS expect-value ]

expect-name  = token
expect-value = token / quoted-string

From    = mailbox

mailbox = <mailbox, defined in [RFC5322], Section 3.4>

Location = URI-reference
Max-Forwards = 1*DIGIT

Referer = absolute-URI / partial-URI

Retry-After = HTTP-date / delta-seconds

delta-seconds  = 1*DIGIT

Server = product *( RWS ( product / comment ) )

User-Agent = product *( RWS ( product / comment ) )

Allow = [ ( "," / Method ) *( OWS "," [ OWS Method ] ) ]

BWS = <BWS, defined in [Part1], Section 1.2.2>

Date = HTTP-date

Expect = *( "," OWS ) expectation *( OWS "," [ OWS expectation ] )

From = mailbox

GMT = %x47.4D.54 ; GMT

HTTP-date = rfc1123-date / obs-date

Location = URI-reference

Max-Forwards = 1*DIGIT
Method = token

OWS = <OWS, defined in [Part1], Section 1.2.2>

RWS = <RWS, defined in [Part1], Section 1.2.2>
Reason-Phrase = *( HTAB / SP / VCHAR / obs-text )
Referer = absolute-URI / partial-URI
Retry-After = HTTP-date / delta-seconds

Server = product *( RWS ( product / comment ) )
Status-Code = 3DIGIT

URI-reference = <URI-reference, defined in [Part1], Section 2.7>
User-Agent = product *( RWS ( product / comment ) )

absolute-URI = <absolute-URI, defined in [Part1], Section 2.7>
asctime-date = day-name SP date3 SP time-of-day SP year

comment = <comment, defined in [Part1], Section 3.2>

date1 = day SP month SP year
date2 = day "-" month "-" 2DIGIT
date3 = month SP ( 2DIGIT / ( SP DIGIT ) )
day = 2DIGIT
day-name = %x4D.6F.6E ; Mon
    / %x54.75.65 ; Tue
    / %x57.65.64 ; Wed
    / %x54.68.75 ; Thu
    / %x46.72.69 ; Fri
    / %x53.61.74 ; Sat
    / %x53.75.6E ; Sun
day-name-l = %x4D.6F.6E.64.61.79 ; Monday
    / %x54.75.65.73.64.61.79 ; Tuesday
    / %x57.65.64.6E.65.73.64.61.79 ; Wednesday
    / %x54.68.75.72.73.64.61.79 ; Thursday
    / %x46.72.69.64.61.79 ; Friday
    / %x53.61.74.75.72.64.61.79 ; Saturday
    / %x53.75.6E.64.61.79 ; Sunday
delta-seconds = 1*DIGIT

expect-name = token
expect-param = expect-name [ BWS "=" BWS expect-value ]
expect-value = token / quoted-string
expectation = expect-name [ BWS "=" BWS expect-value ] *( OWS ";" [
    OWS expect-param ] )

hour = 2DIGIT
mailbox = <mailbox, defined in [RFC5322], Section 3.4>
minute = 2DIGIT
month = %x4A.61.6E ; Jan
    / %x46.65.62 ; Feb
    / %x4D.61.72 ; Mar
    / %x41.70.72 ; Apr
    / %x4D.61.79 ; May
    / %x4A.75.6E ; Jun
    / %x4A.75.6C ; Jul
    / %x41.75.67 ; Aug
    / %x53.65.70 ; Sep
    / %x4F.63.74 ; Oct
    / %x4E.6F.76 ; Nov
    / %x44.65.63 ; Dec

obs-date = rfc850-date / asctime-date
obs-text = <obs-text, defined in [Part1], Section 1.2.2>

partial-URI = <partial-URI, defined in [Part1], Section 2.7>
product = <product, defined in [Part1], Section 5.2>

quoted-string = <quoted-string, defined in [Part1], Section 3.2.3>

rfc1123-date = day-name "," SP date1 SP time-of-day SP GMT
rfc850-date = day-name-l "," SP date2 SP time-of-day SP GMT

second = 2DIGIT

time-of-day = hour ":" minute ":" second
token = <token, defined in [Part1], Section 3.2.3>

year = 4DIGIT

