Internet Draft                                         N. Tsarmpopoulos
October 19, 1999                                            UMEM, UMIST
Expires: April 1999
<draft-tsarmpopoulos-sms-address-mail-01.txt>




                  SMS address format in Internet Mail



Status of this Memo

   This document is an Internet-Draft and is in full conformance with
   all provisions of Section 10 of RFC2026 [8].

   Internet-Drafts are working documents of the Internet Engineering
   Task Force (IETF), its areas, and its working groups. Note that
   other groups may also distribute working documents as Internet-
   Drafts. Internet-Drafts are draft documents valid for a maximum of
   six months and may be updated, replaced, or obsoleted by other
   documents at any time. It is inappropriate to use Internet-Drafts as
   reference material or to cite them other than as "work in progress."

   The list of current Internet-Drafts can be accessed at
   http://www.ietf.org/ietf/1id-abstracts.txt

   The list of Internet-Draft Shadow Directories can be accessed at
   http://www.ietf.org/shadow.html.


Copyright Notice

   Copyright (C) The Internet Society (1999).  All Rights Reserved.


Abstract

   The Short Messaging System (SMS) is widely used by GSM mobile phones
   for the transmission of text messages. This memo specifies the
   general syntax for email addresses of terminal equipment with SMS
   capabilities.

   The purpose of the memo is to facilitate the transmission of SMS
   messages using a familiar e-mail client application.
             Minimal SMS address format in Internet Mail      May 1999


1. Introduction

   Short Messages are two-way alphanumeric messages that can be sent
   and received by mobile phones or server equipment with Short Message
   Service (SMS) capabilities. The SMS service is provided by the
   Global System for Mobile Communications (GSM) [10], but other types
   of mobile or land-based networks also support similar services.

   Short Messages sent from mobile phones are called mobile originated,
   while messages received by mobile phones are called mobile
   terminated. Short messages are stored and forwarded by the Short
   Message Service Centres (SMSC). An SMSC is identified by a Global
   Switched Telephone Network (GSTN) number, which has to be known to
   the sender of a mobile originated message. The availability of the
   SMS service over different mobile networks depends on roaming
   agreements of the networks, as well as on a mechanism to deliver the
   messages. It is the network operator's responsibility to inform the
   user about the success or failure of the message delivery.

   This memo facilitates the integration of Internet Mail services with
   the Short Message Service, by defining the address element of an
   Internet Mail message sent to a terminal device or server equipment
   with SMS capabilities.

   This memo aims in achieving the following goals:

   - To provide an Internet Mail addressing format for GSTN-addressed
   terminals and server equipment with SMS capabilities.
   - To facilitate the development of gateways, which forward Internet
   Mail messages to the GSM network through the appropriate SMSC
   system.
   - To facilitate the use of Internet Mail as a transport mechanism
   for SMS messages.

   A general format for representing GSTN addresses with e-mail
   addresses is defined in [1] and [11]. This memo uses the extension
   mechanism introduced in [1] and the GSTN address element extensions
   of [2], to define a standard addressing format for terminals and
   server equipment with SMS capabilities.

   The format presented in this memo has been defined with respect to
   the requirements of the SMS service provided by GSM-based networks,
   but similar services can also be found on other types of mobile and
   land-based networks. Such services may use the format and syntax
   defined in this memo, by defining a new value of the "NET" service
   qualifier and the required network-specific service qualifiers.

   This memo covers only addressing issues. Message encoding and
   transport issues are not discussed. The addressing format presented
   here complies fully with the general syntax specified in [11], i.e.
   addresses defined with this memo SHOULD be parsed successfully by
   software implementations complying with [11].
             Minimal SMS address format in Internet Mail      May 1999


   All implementation supporting this SMS over e-mail address format
   MUST support as a minimum the specification described in this
   document.


2. Terminology and Conventions used in this document

   In this document, the formal definitions are given using the ABNF
   syntax [3]. Certain basic rules are in uppercase, such as SP, HTAB,
   CRLF, DIGIT, ALPHA, etc.

   The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT",
   "SHOULD", "SHOULD NOT", "RECOMMENDED",  "MAY", and "OPTIONAL" in
   this document are to be interpreted as described in RFC-2119 [4].

   The following terms are also defined:

     sms-email:
        The complete Internet e-mail address structure, which is used
        to transport an SMS address over the Internet e-mail service.

     mta-I-sms:
        The Internet domain name that identifies uniquely an I-sms
        device over the Internet

     I-sms device:
        A device, which has an Internet domain name and is able to send
        directly or indirectly SMS messages.

   The following abbreviations are frequently used:

     GSM  : Global System for Mobile communications
     SMS  : Short Message Service
     SMSC : Short Message Service Centre


3. The sms-email

   The complete structure of an SMS-to-Email address is defined by the
   <sms-email> element. This object is a valid Internet mail address,
   based on the rules defined in [6] and [7]. It's structure allows the
   SMS number to be identified.

        sms-email = ["/"] sms-address ["/"] "@" mta-I-sms

   The "/" characters may result from other mail transport service
   gateways, such as X.400.

   Implementers' Note:
   The "/" characters SHOULD be accepted but SHOULD NOT be generated.
   Gateways are allowed to strip them off when converting to Internet
   mail addressing.
             Minimal SMS address format in Internet Mail      May 1999


   It is essential to remind that <sms-address> element MUST strictly
   follow the quoting rules specified in the relevant standards [6],
   [7].


4. The e-mail address of the I-sms device: mta-I-sms

   An I-sms device has a name, which identifies it by the Internet Mail
   system. This is the right hand side (RHS) part of the address, i.e.
   the part on the right of the "@" sign. This part is defined by the
   <mta-I-sms> element as follows:

        mta-I-sms = domain

   For <domain> strings used in SMTP transmissions, the string MUST
   conform to the requirements of the specifications [5], [7]. For
   <domain> strings used in message content headers, the string MUST
   conform to the requirements of the standards [6], [7].


5. The <sms-address> element

   The <sms-address> element identifies the individual recipient (human
   or server) of the message, within the supported by the <mta-I-sms>
   domain. This is the left-hand side (LHS) part of the address, i.e.
   the part on the left of the "@" sign. When the message is produced
   by an X.400 gateway of other device, <sms-address> MAY have a prefix
   and/or a suffix with the "/" character.

        sms-address = sms-mbox [ via-qualifier ] [ net-qualifier ]
                        [1*( qualif-type1) ]

   The <via-qualifier> and the <net-qualifier> SHOULD NOT appear more
   than once in a single <sms-address>.

   The <qualif-type1> element is defined in [1], paragraph 2.

   The <sms-address> element definition allows new service qualifiers
   to be defined in the future, in order to support additional
   functionality provided by the Short Message Service. However, new
   <qualif-type1> elements MUST be defined by separate RFCs or
   submitted to the author of this memo for inclusion in next versions
   of the document, and MUST be registered with the appropriate IANA
   form template, which can be found in [11], appendix B.2.

   The definition for sms-mbox is:

        sms-mbox = "SMS=" gstn-phone

   The <sms-mbox> element definition complies with the <pstn-mbox>
   element, which is defined in [11].
             Minimal SMS address format in Internet Mail      May 1999

   The syntax for <gstn-phone> is given in [11] and supports the global
   phone scheme, as well as local phone schemes.

   If a local phone or a proprietary schema is used, it MUST NOT use
   the leading "+" between the "=" sign and the dialling string. The
   "+" sign is strictly reserved for the standard "global-phone"
   syntax.

   Implementation requirements:

        It is REQUIRED that the <global-phone> values are supported by
        software implementations, in order to comply with this
        specification. Global phone numbers MUST be properly parsed and
        converted to the local phone scheme when this is required, in
        order to send the message.

        It is REQUIRED that gateways and other software implementations
        are capable of processing any valid <sms-address> string. When
        an optional <qualif-type1> element is not supported by a
        software implementation or a <qualif-type1> element, which is
        defined for use with other <service-selector> values, is found:

        a) this element SHOULD be ignored,
        b) the process of parsing the <sms-address> SHOULD proceed as
           normal and,
        c) the user SHOULD be notified about the unsupported element by
           an e-mail message.


6. The "VIA" service qualifier

   Short Message Services usually rely on a middleman to deliver the
   messages. For GSM-based networks this is called the Short Message
   Service Centre and it is identified by a <gstn-phone> address. When
   specifying the phone number of this middleman is required, the <via-
   qualifier> element MUST be used.

        via-qualifier = "VIA=" message-centre-id

        message-centre-id = gstn-phone

   An alternative definition, based on the <qualif-type1> element
   definition [11], is the following:

   Qualif-type1 "keyword" name:

        VIA

   Qualif-type1 "value" ABNF definition:

        value             = message-centre-id
        message-centre-id = gstn-phone
             Minimal SMS address format in Internet Mail      May 1999

   If the "VIA" qualifier is present, the gateway MUST send the message
   via the specified SMSC or not send it at all.


7. The "NET" service qualifier

   This qualifier specifies the type of the network in use. "GSM" means
   a GSM-based network of any frequency band.

        net-qualifier = "NET=" network-type
        network-type = "GSM" / other-type
        other-type   = string

   An alternative definition, based on the <qualif-type1> element [11],
   is the following:

     qualif-type1 "keyword" name:

        NET

     qualif-type1 "value" ABNF definition:

        value        = network-type
        network-type = "GSM" / other-type
        other-type   = string

   The <network-type> element may be extended by registering the
   appropriate <other-type> value. Any <network-type> value MUST be
   specified by an RFC document and be registered using the appropriate
   IANA registration template.


8. Examples of SMS e-mail addresses

        SMS=+448957375644@smsgateway.org

        /SMS=+44342385874/@smsworld.org

        SMS=+44-5869-318945@smsservice.org

        SMS=07044573897@smsservice.org

        SMS=07044573897/VIA=+358-50-123-1234@smsservice.org

        SMS=+44-8934-349856/VIA=+358-50-123-1234/NET=GSM@smsgateway.org


9. Additional implementation considerations

   This memo provides a compact definition for the SMS address format
   in internet mail, which facilitates the development of email-to-sms
   gateways. The grammar specified in this memo defines email
             Minimal SMS address format in Internet Mail      May 1999

   addresses, which are valid <pstn-email> elements [11] and MUST be
   used ONLY for the "SMS" <service-selector> value.

   Gateways supporting multiple services, by identifying different
   "service-selector" values, MUST use the full specification defined
   in [11].


10. Conclusion

   By using the standard extension mechanisms of the GSTN address
   format in Internet Mail, this proposal defines new elements that
   facilitate the transmission of SMS messages through Internet Mail. A
   new service selector is defined, namely "SMS" and two service
   qualifiers, the "VIA" and the "NET", which provide the Short Message
   Service Centre address and the network type of the recipient's
   network. The "GSM" network type was defined, to provide support for
   GSM-based networks.

   The extension proposed in this memo does not affect or require any
   changes to the existing e-mail software.


11. Collected grammar

        sms-email = ["/"] sms-address ["/"] "@" mta-I-sms

        mta-I-sms = domain

        sms-address = sms-mbox [ via-qualifier ] [ net-qualifier ]
                        [1*( qualif-type1) ]

        sms-mbox = "SMS=" gstn-phone

        via-qualifier = "VIA=" message-centre-id

        net-qualifier = "NET=" network-type

        message-centre-id = gstn-phone

        network-type = "GSM" / other-type

        other-type   = string

        qualif-type1 = "/" keyword "=" string

        keyword = 1*( DIGIT / ALPHA / "-" )

        string = PCHAR

        gstn-phone = ( global-phone / local-phone )

        global-phone = "+" 1*( DIGIT / written-sep )
             Minimal SMS address format in Internet Mail      May 1999


        local-phone = [ exit-code ] [ dial-number ]

        exit-code = phone-string

        dial-number = phone-string

        phone-string = 1*( DTMF / pause / tonewait / written-sep )

        DTMF = ( DIGIT / "#" / "*" / "A" / "B" / "C" / "D" )

        written-sep = ( "-" / "." )

        pause = "p"

        tonewait = "w"



12. Security Considerations

   The extension proposed in this memo is based on the existing e-mail
   mechanisms; thus it is vulnerable to the same type of attacks found
   for the Internet Mail System. The routing of e-mail messages is
   determined by Domain Name System (DNS) information, therefore a
   successful attach on this service could force the mail path via some
   particular gateway or message transfer agent, where mail security
   can be affected by compromised software. DNS security mechanisms are
   defined in [9] and, as they become more widely supported by software
   implementations, clients MAY employ those mechanisms to verify the
   authenticity and integrity of mail routing records.

   Junk email is also considered to be an important threat to the
   system, because it may flood the mobile network with SMS messages.
   When SMTP servers process Internet mail messages with the addressing
   format presented in this memo, they SHOULD be configured to restrict
   the messages sent to or from a source, based on some heuristic.



13. IANA considerations

   New values for the <service-qualifier> element MUST be registered
   with IANA. To register a <service-qualifier> value, the form
   template given in B.1 [11] MUST be used.

   All IANA considerations mentioned in appendix B [11] apply to the
   <service-qualifier>.
             Minimal SMS address format in Internet Mail      May 1999

14. References

   [1] Allocchio, C., "Minimal PSTN address format in Internet Mail",
   RFC 2303, March 1998.

   [2] Allocchio, C., "Minimal FAX address format in Internet Mail",
   RFC 2304, March 1998.

   [3] Crocker, D. and P. Overell, "Augmented BNF for Syntax
   Specifications: ABNF", RFC 2234, Internet Mail Consortium and Demon
   Internet Ltd., November 1997.

   [4] Bradner, S., "Key words for use in RFCs to Indicate Requirement
   Levels", RFC 2119, March 1997.

   [5] Postel, J., "Simple Mail Transfer Protocol", STD 10, RFC 821,
   August 1982.

   [6] Crocker, D., "Standard for the format of ARPA Internet text
   messages", STD 11, RFC 822, August 1982.

   [7] Braden, R., "Requirements for Internet hosts - application and
   support", RFC 1123, October 1989.

   [9] Eastlake, D. and C. Kaufman, "Domain Name System Security
   Extensions", RFC 2065, January 1997.

   [10] TS 100 901 V6.1.0 (1998-07); GSM 03.40 version 6.1.0 Release
   1997; Technical realization of the Short Message Service (SMS);
   Point-to-Point, European Telecommunications Standards Institute
   (ETSI).

   [11] Allocchio, C., "GSTN address element extensions in e-mail
   services", Internet Draft (Work in progress), April 1999,
   <ftp://ftp.nordu.net/internet-drafts/draft-ietf-fax-fulladdr-06.txt>


15. Acknowledgements

   The following people have participated with their feedback in the
   realisation of this memo:

    Vaha-Sipila, A., NOKIA
    Allocchio, C., GARR-Italy
    Karakostas, V., UMIST
    Wing, D., CISCO

   The author would also like to thank the members of the IETF Internet
   Fax working group, for their contributions in extending the
   functionality and the scope of Internet Mail.
             Minimal SMS address format in Internet Mail      May 1999

16. Author's address

   Nikolaos Tsarmpopoulos
   Ulysses Maritime Electronic Market Ltd.
   10 Ely Place,
   London ECIN 6RY

   Internet mail: Tsarb@acm.org
                  Tsarb@excite.com


17. Full Copyright Statement

   "Copyright (C) The Internet Society (1999). All Rights Reserved.

   This document and translations of it may be copied and furnished to
   others, and derivative works that comment on or otherwise explain it
   or assist in its implementation may be prepared, copied, published
   and distributed, in whole or in part, without restriction of any
   kind, provided that the above copyright notice and this paragraph
   are included on all such copies and derivative works. However, this
   document itself may not be modified in any way, such as by removing
   the copyright notice or references to the Internet Society or other
   Internet organisations, except as needed for the purpose of
   developing Internet standards in which case the procedures for
   copyrights defined in the Internet Standards process must be
   followed, or as required to translate it into languages other than
   English.

   The limited permissions granted above are perpetual and will not be
   revoked by the Internet Society or its successors or assigns.

   The document and the information contained herein is provided on an
   "AS IS" basis and THE INTERNET SOCIETY AND THE INTERNET ENGINEERING
   TASK FORCE DISCLAIMS ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING
   BUT NOT LIMITED TO ANY WARRANTY THAT THE USE OF THE INFORMATION
   HEREIN WILL NOT INFRINGE ANY RIGHTS OR ANY IMPLIED WARRANTIES OF
   MECHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.


   Appendix A. IANA registration templates

   A.1 "SMS" Service Selector

   To: IANA@isi.edu
   Subject: Registration of new values for the GSTN address
            service-selector specifier "SMS"

        Service Selector Name:

             SMS

        Description of Use:
             Minimal SMS address format in Internet Mail      May 1999

             SMS - SMS, an acronym for Short Message Service, is a
                  (possibly bidirectional) alphanumeric paging message
                  found in several mobile phone systems. The recipient
                  of the Short Message is identified by a phone number.
                  The Service Selector and its Qualifiers are defined
                  in RFC[].

        Security Considerations:

            It should be noted that the user agent SHOULD NOT send out
            Short Messages without the knowledge of the user because of
            associated risks, which include sending masses of Short
            Messages to a subscriber without her consent and the costs
            involved in sending a Short Message. The user agent SHOULD
            provide a mechanism to restrict the messages sent to or
            from a source, based on some heuristic.

   Person & Email Address to Contact for Further Information:
   To be added to the final version.


   A.2 "VIA" Qualifier

   To: IANA@isi.edu
   Subject: Registration of new values for the GSTN address qualif-
   type1 element "VIA"


        qualif-type1 "keyword" name:

                VIA

        qualif-type1 "value" ABNF definition:

            value               = message-centre-id
             message-centre-id  = gstn-phone

        Description of Use:

                This Qualifier specifies the phone number of the Short
                Message Centre, which acts as an intermediary when
                delivering the Short Message.

        Use Restriction:
                This qualifier element is only meaningful for the "SMS"
                Service Selector.
                This qualifier element SHOULD NOT be used more than
                once in a single e-mail address.

        Security Considerations:

                The security considerations are the same as for the
                "SMS" Service Selector.
             Minimal SMS address format in Internet Mail      May 1999



   Person & email address to contact for further information:
   To be added to the final version.


   A.3 "NET" Qualifier

   To: IANA@isi.edu
   Subject: Registration of new values for the GSTN address qualif-
   type1 element "NET"

        qualif-type1 "keyword" name:

            NET

        qualif-type1 "value" ABNF definition:

                value        = network-type
                network-type = "GSM" / other-type
                other-type   = string

        Description of Use:

                This Qualifier specifies the recipient's network type.
                "GSM" means any GSM-based network (independent of the
                frequency band used). If there is a requirement for
                other network types, the names for those MUST be
                specified by another RFC and registered using another
                IANA registration template.

        Use Restriction:

                This qualifier element is only meaningful for the "SMS"
                Service Selector.
                This qualifier element SHOULD NOT be used more than
                once in a single e-mail address.

        Security Considerations:

                The securiry considerations are the same as for the
                "SMS" Service Selector.

   Person & email address to contact for further information:
   To be added in the final version.
