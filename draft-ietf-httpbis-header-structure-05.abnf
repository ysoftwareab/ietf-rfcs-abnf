barURLs = URI-reference *( 1*SP URI-reference )

dictionary  = dict-member *( OWS "," OWS dict-member )
dict-member = identifier "=" item

list = list-member *( OWS "," OWS list-member )
list-member = item

param-list = param-id *( OWS "," OWS param-id )
param-id   = identifier *( OWS ";" OWS identifier [ "=" item ] )

item = integer / float / string / binary

integer   = ["-"] 1*19DIGIT

float    = ["-"] (
                DIGIT "." 1*14DIGIT /
               2DIGIT "." 1*13DIGIT /
               3DIGIT "." 1*12DIGIT /
               4DIGIT "." 1*11DIGIT /
               5DIGIT "." 1*10DIGIT /
               6DIGIT "." 1*9DIGIT /
               7DIGIT "." 1*8DIGIT /
               8DIGIT "." 1*7DIGIT /
               9DIGIT "." 1*6DIGIT /
              10DIGIT "." 1*5DIGIT /
              11DIGIT "." 1*4DIGIT /
              12DIGIT "." 1*3DIGIT /
              13DIGIT "." 1*2DIGIT /
              14DIGIT "." 1DIGIT )

string    = DQUOTE *(chr) DQUOTE
chr       = unescaped / escaped
unescaped = %x20-21 / %x23-5B / %x5D-7E
escaped   = "\" ( DQUOTE / "\" )

identifier = lcalpha *( lcalpha / DIGIT / "_" / "-"/ "*" / "/" )
lcalpha    = %x61-7A ; a-z

binary = "*" *(base64) "*"
base64 = ALPHA / DIGIT / "+" / "/" / "="

