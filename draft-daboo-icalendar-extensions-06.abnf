name       = "NAME" nameparam ":" text CRLF

nameparam  = *(
                 ;
                 ; The following are OPTIONAL,
                 ; but MUST NOT occur more than once.
                 ;
                 (";" altrepparam) / (";" languageparam) /
                 ;
                 ; The following is OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
                 )

timezid      = "TIMEZONE-ID" timezidparam
                    ":" text CRLF
                    ;Same value syntax as "TZID" property.

timezidparam = *(";" other-param)

refresh      = "REFRESH-INTERVAL" refreshparam
                    ":" dur-value CRLF
                    ;consisting of a positive duration of time.

refreshparam = *(
                   ;
                   ; The following is REQUIRED,
                   ; but MUST NOT occur more than once.
                   ;
                   (";" "VALUE" "=" "DURATION") /
                   ;
                   ; The following is OPTIONAL,
                   ; and MAY occur more than once.
                   ;
                   (";" other-param)
                   ;
                   )

valid      = "VALID" validparam ":" validval CRLF

validparam   = *(
                   ;
                   ; The following is REQUIRED,
                   ; but MUST NOT occur more than once.
                   ;
                   (";" "VALUE" "=" ("DATE-TIME" / "PERIOD")) /
                   ;
                   ; The following is OPTIONAL,
                   ; and MAY occur more than once.
                   ;
                   (";" other-param)
                   ;
                   )

validval     = date-time / period
color          = "COLOR" colorparam ":" colorvalue CRLF

colorparam     = *(";" other-param)

colorvalue     = integer ":" integer ":" integer
                    ; Red, green, and blue values in the range
                    ; 0 - 255.

image      = "IMAGE" imageparam ( ":" uri ) /
                (
                  ";" "ENCODING" "=" "BASE64"
                  ";" "VALUE" "=" "BINARY"
                  ":" binary
                )
                CRLF

imageparam = *(
                 ;
                 ; The following is REQUIRED,
                 ; but MUST NOT occur more than once.
                 ;
                 (";" "VALUE" "=" "URI") /
                 ;
                 ; The following is OPTIONAL for a URI value,
                 ; RECOMMENDED for a BINARY value,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" fmttypeparam) /
                 ;
                 ; The following are OPTIONAL,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" alturiparam) / (";" displayparam) /
                 ;
                 ; The following is OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
                 )

alturiparam = "ALTURI" "=" DQUOTE uri DQUOTE

displayparam = "DISPLAY" "="
                  ("BADGE" /       ; A small "badge" image
                   "BACKGROUND" /  ; Use as a background image
                   "OVERLAY" /     ; Use as an overlay image
                   "BANNER" /      ; Use as a "banner" across the top
                   x-name /        ; Experimental type
                   iana-token)     ; Other IANA registered type
                                   ;
                                   ; Default is BADGE

