quoted-string  = <quoted-string, defined in [Part1], Section 1.2.2>
token          = <token, defined in [Part1], Section 1.2.2>
OWS            = <OWS, defined in [Part1], Section 1.2.2>

absolute-URI   = <absolute-URI, defined in [Part1], Section 2.1>
Content-Length = <Content-Length, defined in [Part1], Section 8.2>
message-header = <message-header, defined in [Part1], Section 4.2>
partial-URI    = <partial-URI, defined in [Part1], Section 2.1>
qvalue         = <qvalue, defined in [Part1], Section 3.5>


Last-Modified  = <Last-Modified, defined in [Part4], Section 6.6>


Content-Range  = <Content-Range, defined in [Part5], Section 5.2>


Expires        = <Expires, defined in [Part6], Section 3.3>


charset = token

content-coding   = token

media-type = type "/" subtype *( OWS ";" OWS parameter )
type       = token
subtype    = token

parameter      = attribute "=" value
attribute      = token
value          = token / quoted-string

language-tag  = primary-tag *( "-" subtag )
primary-tag   = 1*8ALPHA
subtag        = 1*8ALPHA

entity-header  = Content-Encoding         ; Section 5.5
                    / Content-Language         ; Section 5.6
                    / Content-Length           ; [Part1], Section 8.2
                    / Content-Location         ; Section 5.7
                    / Content-MD5              ; Section 5.8
                    / Content-Range            ; [Part5], Section 5.2
                    / Content-Type             ; Section 5.9
                    / Expires                  ; [Part6], Section 3.3
                    / Last-Modified            ; [Part4], Section 6.6
                    / extension-header

extension-header = message-header

entity-body    = *OCTET

Accept   = "Accept" ":" OWS Accept-v
Accept-v = #( media-range [ accept-params ] )

media-range    = ( "*/*"
                      / ( type "/" "*" )
                      / ( type "/" subtype )
                      ) *( OWS ";" OWS parameter )
accept-params  = OWS ";" OWS "q=" qvalue *( accept-ext )
accept-ext     = OWS ";" OWS token
                      [ "=" ( token / quoted-string ) ]

Accept-Charset   = "Accept-Charset" ":" OWS
             Accept-Charset-v
Accept-Charset-v = 1#( ( charset / "*" )
                            [ OWS ";" OWS "q=" qvalue ] )

Accept-Encoding    = "Accept-Encoding" ":" OWS
                        Accept-Encoding-v
Accept-Encoding-v  =
                        #( codings [ OWS ";" OWS "q=" qvalue ] )
codings            = ( content-coding / "*" )

q=1.

Accept-Language   = "Accept-Language" ":" OWS
                       Accept-Language-v
Accept-Language-v =
                       1#( language-range [ OWS ";" OWS "q=" qvalue ] )
language-range    =
               <language-range, defined in [RFC4647], Section 2.1>

Content-Encoding   = "Content-Encoding" ":" OWS Content-Encoding-v
Content-Encoding-v = 1#content-coding

Content-Language   = "Content-Language" ":" OWS Content-Language-v
Content-Language-v = 1#language-tag

Content-Location   = "Content-Location" ":" OWS
                       Content-Location-v
Content-Location-v =
                       absolute-URI / partial-URI

Content-MD5   = "Content-MD5" ":" OWS Content-MD5-v
Content-MD5-v = <base64 of 128 bit MD5 digest as per [RFC1864]>

Content-Type   = "Content-Type" ":" OWS Content-Type-v
Content-Type-v = media-type

MIME-Version   = "MIME-Version" ":" OWS MIME-Version-v
MIME-Version-v = 1*DIGIT "." 1*DIGIT

content-disposition = "Content-Disposition" ":" OWS
                           content-disposition-v
content-disposition-v = disposition-type
                             *( OWS ";" OWS disposition-parm )
disposition-type = "attachment" / disp-extension-token
disposition-parm = filename-parm / disp-extension-parm
filename-parm = "filename" "=" quoted-string
disp-extension-token = token
disp-extension-parm = token "=" ( token / quoted-string )

Accept = "Accept:" OWS Accept-v
Accept-Charset = "Accept-Charset:" OWS Accept-Charset-v
Accept-Charset-v = *( "," OWS ) ( charset / "*" ) [ OWS ";" OWS "q="
    qvalue ] *( OWS "," [ OWS ( charset / "*" ) [ OWS ";" OWS "q="
    qvalue ] ] )
Accept-Encoding = "Accept-Encoding:" OWS Accept-Encoding-v
Accept-Encoding-v = [ ( "," / ( codings [ OWS ";" OWS "q=" qvalue ] )
    ) *( OWS "," [ OWS codings [ OWS ";" OWS "q=" qvalue ] ] ) ]
Accept-Language = "Accept-Language:" OWS Accept-Language-v
Accept-Language-v = *( "," OWS ) language-range [ OWS ";" OWS "q="
    qvalue ] *( OWS "," [ OWS language-range [ OWS ";" OWS "q=" qvalue ]
    ] )
Accept-v = [ ( "," / ( media-range [ accept-params ] ) ) *( OWS "," [
    OWS media-range [ accept-params ] ] ) ]

Content-Encoding = "Content-Encoding:" OWS Content-Encoding-v
Content-Encoding-v = *( "," OWS ) content-coding *( OWS "," [ OWS
    content-coding ] )
Content-Language = "Content-Language:" OWS Content-Language-v
Content-Language-v = *( "," OWS ) language-tag *( OWS "," [ OWS
    language-tag ] )
Content-Length = <Content-Length, defined in [Part1], Section 8.2>
Content-Location = "Content-Location:" OWS Content-Location-v
Content-Location-v = absolute-URI / partial-URI
Content-MD5 = "Content-MD5:" OWS Content-MD5-v
Content-MD5-v = <base64 of 128 bit MD5 digest as per [RFC1864]>





Content-Range = <Content-Range, defined in [Part5], Section 5.2>
Content-Type = "Content-Type:" OWS Content-Type-v
Content-Type-v = media-type

Expires = <Expires, defined in [Part6], Section 3.3>

Last-Modified = <Last-Modified, defined in [Part4], Section 6.6>

MIME-Version = "MIME-Version:" OWS MIME-Version-v
MIME-Version-v = 1*DIGIT "." 1*DIGIT

OWS = <OWS, defined in [Part1], Section 1.2.2>

absolute-URI = <absolute-URI, defined in [Part1], Section 2.1>
accept-ext = OWS ";" OWS token [ "=" ( token / quoted-string ) ]
accept-params = OWS ";" OWS "q=" qvalue *accept-ext
attribute = token

charset = token
codings = ( content-coding / "*" )
content-coding = token
content-disposition = "Content-Disposition:" OWS
    content-disposition-v
content-disposition-v = disposition-type *( OWS ";" OWS
    disposition-parm )

disp-extension-parm = token "=" ( token / quoted-string )
disp-extension-token = token
disposition-parm = filename-parm / disp-extension-parm
disposition-type = "attachment" / disp-extension-token

entity-body = *OCTET
entity-header = Content-Encoding / Content-Language / Content-Length
    / Content-Location / Content-MD5 / Content-Range / Content-Type /
    Expires / Last-Modified / extension-header
extension-header = message-header

filename-parm = "filename=" quoted-string

language-range = <language-range, defined in [RFC4647], Section 2.1>
language-tag = primary-tag *( "-" subtag )

media-range = ( "*/*" / ( type "/*" ) / ( type "/" subtype ) ) *( OWS
    ";" OWS parameter )
media-type = type "/" subtype *( OWS ";" OWS parameter )
message-header = <message-header, defined in [Part1], Section 4.2>

parameter = attribute "=" value





partial-URI = <partial-URI, defined in [Part1], Section 2.1>
primary-tag = 1*8ALPHA

quoted-string = <quoted-string, defined in [Part1], Section 1.2.2>
qvalue = <qvalue, defined in [Part1], Section 3.5>

subtag = 1*8ALPHA
subtype = token

token = <token, defined in [Part1], Section 1.2.2>
type = token

value = token / quoted-string



