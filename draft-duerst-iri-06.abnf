IRI           = scheme ":" ["//" iauthority] ipath ["?" iquery]
                       ["#" ifragment]
IRI-reference = IRI / relative-IRI

relative-IRI  = ["//" iauthority] ipath ["?" iquery]
                       ["#" ifragment]

absolute-IRI  = scheme ":" ["//" iauthority] ipath ["?" iquery]

iauthority    = [ iuserinfo "@" ] ihost [ ":" port ]

iuserinfo     = *( iunreserved / pct-encoded / sub-delims
    	                   / ":" )

ihost         = IP-literal / IPv4address / ireg-name

ireg-name     = 0*255( iunreserved / pct-encoded / sub-delims )

ipath         = isegment *( "/" isegment )

isegment      = *ipchar

iquery        = *( ipchar / iprivate / "/" / "?" )

ifragment     = *( ipchar / "/" / "?" )

ipchar        = iunreserved / pct-encoded / sub-delims / ":"
    	              / "@"

iunreserved   = ALPHA / DIGIT / "-" / "." / "_" / "~" / ucschar

ucschar       = %xA0-D7FF / %xF900-FDCF / %xFDF0-FFEF /
                     / %x10000-1FFFD / %x20000-2FFFD / %x30000-3FFFD
                     / %x40000-4FFFD / %x50000-5FFFD / %x60000-6FFFD
                     / %x70000-7FFFD / %x80000-8FFFD / %x90000-9FFFD
                     / %xA0000-AFFFD / %xB0000-BFFFD / %xC0000-CFFFD
                     / %xD0000-DFFFD / %xE1000-EFFFD

iprivate      = %xE000-F8FF / %xF0000-FFFFD / %x100000-10FFFD

scheme        = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )

port          = *DIGIT

IP-literal    = "[" ( IPv6address | IPvFuture  ) "]"
IPvFuture     = "v" HEXDIG "." 1*( unreserved / sub-delims / ":" )

IPv6address   =                          6( h4 ":" ) ls32
                     /                     "::" 5( h4 ":" ) ls32
                     / [              h4 ] "::" 4( h4 ":" ) ls32
                     / [ *1( h4 ":" ) h4 ] "::" 3( h4 ":" ) ls32
                     / [ *2( h4 ":" ) h4 ] "::" 2( h4 ":" ) ls32
                     / [ *3( h4 ":" ) h4 ] "::"    h4 ":"   ls32
                     / [ *4( h4 ":" ) h4 ] "::"             ls32
                     / [ *5( h4 ":" ) h4 ] "::"             h4
                     / [ *6( h4 ":" ) h4 ] "::"

h4            = 1*4HEXDIG

ls32          = ( h4 ":" h4 ) / IPv4address

IPv4address   = dec-octet "." dec-octet "." dec-octet "." dec-octet

dec-octet     = DIGIT                 ; 0-9
                     / %x31-39 DIGIT         ; 10-99
                     / "1" 2DIGIT            ; 100-199
                     / "2" %x30-34 DIGIT     ; 200-249
                     / "25" %x30-35          ; 250-255

pct-encoded   = "%" HEXDIG HEXDIG

unreserved    = ALPHA / DIGIT / "-" / "." / "_" / "~"

reserved      = gen-delims / sub-delims

gen-delims    = ":" / "/" / "?" / "#" / "[" / "]" / "@"

sub-delims    = "!" / "$" / "&" / "'" / "(" / ")"
                     / "*" / "+" / "," / ";" / "="


