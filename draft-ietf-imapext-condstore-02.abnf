SORT=MODSEQ extension makes the following additions to the SORT extension:

        a) extends syntax of untagged SORT responses to include mod-sequence
           (see section 3.6)

        b) adds a new MODSEQ sort criterion (see sections 3.4 and 3.5)

SORT=MODSEQ extension that allows for sorting on per-message
capability          =/ "CONDSTORE" / "SORT=MODSEQ"

status              = "STATUS" SP mailbox SP 
                          "(" status-att-req *(SP status-att-req) ")"
                          ;; redefine STATUS command syntax defined in [IMAP4]

status-att-req      = status-att / "HIGHESTMODSEQ"

status-rsp-info     = status-att SP number /
                          "HIGHESTMODSEQ" SP mod-sequence-value

store               = "STORE" SP set store-modifiers SP store-att-flags

store-modifiers     = [ SP "(" store-modifier *(SP store-modifier) ")" ]

store-modifier      = "UNCHANGEDSINCE" SP mod-sequence-valzer
                          ;; Only single "UNCHANGEDSINCE" may be specified
                          ;; in a STORE operation

fetch-att           =/ fetch-mod-sequence
                           ;; modifies original IMAP4 fetch-att

fetch-mod-sequence  = "MODSEQ"

fetch-mod-resp      = "MODSEQ" SP "(" permsg-modsequence ")"

search-key          =/ search-modsequence
                           ;; modifies original IMAP4 search-key
                           ;;
                           ;; This change applies to all command referencing this
                           ;; non-terminal, in particular SEARCH and SORT.

search-modsequence  = "MODSEQ" [search-modseq-ext] SP mod-sequence-valzer

search-modseq-ext   = SP entry-name SP entry-type-req

resp-text-code      =/ "HIGHESTMODSEQ" SP mod-sequence-value /
                           "NOMODSEQ" /
                           "MODIFIED" SP set

entry-name          = entry-name-flag / entry-annotate-name
                          ;; The server MUST understand entry-name-flag.
                          ;; If the server also supports [ANNOTATE], it MUST
                          ;; also support entry-annotate-name. 

entry-flag-name     = '"' "/flags/" attr-flag '"'
                          ;; each system or user defined flag <flag>
                          ;; is mapped to "/flags/<flag>".
                          ;;
                          ;; <entry-flag-name> follows the escape rules used
                          ;; by "quoted" string as described in Section
                          ;; 4.3 of [IMAP4], e.g. for the flag \Seen
                          ;; the corresponding <entry-name> is
                          ;; "/flags/\\seen", and for the flag
                          ;; $MDNSent, the corresponding <entry-name>
                          ;; is "/flags/$mdnsent".

entry-annotate-name = entry
                          ;; <entry> is defined in [ANNOTATE]

entry-type-resp     = "priv" | "shared"
                          ;; metadata item type

entry-type-req      = entry-type-resp | "all"
                          ;; perform SEARCH operation on private
                          ;; metadata item, shared metadata item or both

permsg-modsequence  = mod-sequence-value
                          ;; per message mod-sequence

mod-sequence-value  = 1*DIGIT
                          ;; Positive unsigned 64-bit integer (mod-sequence)
                          ;; (1 <= n < 18,446,744,073,709,551,615)

mod-sequence-valzer = "0" | mod-sequence-value

search_sort_mod_seq = "(" "MODSEQ" SP mod-sequence-value ")"

sort-key            =/ "MODSEQ"

condstore-param     = "CONDSTORE"
                          ;; defines the select parameter used with
                          ;; CONDSTORE extension


mailbox-data        =/ "STATUS" SP mailbox SP "("
                           [status-rsp-info *(SP status-rsp-info)] ")" /
                           "SEARCH" [1*(SP nz-number) SP search_sort_mod_seq] /
                           "SORT" [1*(SP nz-number) SP search_sort_mod_seq]

attr-flag           = "\\Answered" / "\\Flagged" / "\\Deleted" /
                          "\\Seen" / "\\Draft" / attr-flag-keyword /
                          attr-flag-extension
                          ;; Does not include "\\Recent"

attr-flag-extension = "\\" atom
                          ;; Future expansion.  Client implementations
                          ;; MUST accept flag-extension flags.  Server
                          ;; implementations MUST NOT generate
                          ;; flag-extension flags except as defined by
                          ;; future standard or standards-track
                          ;; revisions of [IMAP4].

attr-flag-keyword   = atom


