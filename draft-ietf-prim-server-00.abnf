presence-id     = word-pres ":" local-part "@" domain
im-id           = word-im ":" local-part "@" domain
local-part      = 1*( unreserved / escaped )
unreserved      = ALPHA / DIGIT / "!" / "$" / "&" / "'" / "*"
                        / "." / "+" / "-" / "/" / "=" / "?" / "_" / "~"
escaped         = "%" hex-char hex-char
hex-char        = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
                        / "a" / "b" / "c" / "d" / "e" / "f"
domain          = 1*domain-label *("." 1*domain-label)
domain-label    = 1*( unreserved / escaped )
word-pres       = %x70.72.65.73     ; "pres"
word-im         = %x69.6D           ; "im"
decimal-byte    = 1*3DIGIT
ALPHA           = <defined by RFC 2234 -- 'A'-'Z' / 'a'-'z'>
DIGIT           = <defined by RFC 2234 -- '0'-'9'>

hex4            = 1*4hex-char
hexseq          = hex4 *(":" hex4)
ip6-address     = hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
ip4-address     = "::" 1*1decimal-byte 3*3("." 1*1decimal-byte)

PRIM-command = request / response

generic-command = start-line
                             *command-header
                             CRLF
                             [ command-body ]

start-line = request-line / response-line

command-header = (general-header     ;
                              / presence-header  ;
                              / im-header        ;
                              / entity-header    ;
                              )


request-line = method
                            SP version
                            SP request-identifier
                            SP content-length
                            CRLF

request = (presence-request / im-request)

presence-request = request-line
                            *((general-header
                             / presence-header
                             / entity-header) CRLF)
                             CRLF
                             [ command-body ]

im-request = request-line
                            *((general-header
                             / im-header
                             / entity-header) CRLF)
                             CRLF
                             [ command-body ]


method = general-method
                          / presence-method
                          / im-method

general-method = "LOGIN"               ; Section 7.3.1
                          / "STARTTLS"            ; Section 7.3.2
                          / "LOGOUT"              ; Section 7.3.3
                          / "PING"                ; Section 7.3.4
                          / "VERIFYSERVER"        ; Section 7.3.5

presence-method = "SUBSCRIBE"           ; Section 7.1.1
                          / "UNSUBSCRIBE"         ; Section 7.1.2
                          / "NOTIFY"              ; Section 7.1.3

im-method = "SEND"                ; Section 7.2.1

version = "PRIM" "/" 1*DIGIT "." 1*DIGIT

request-identifier = 1*[ALPHA / DIGIT] / "-"

content-length = 1*DIGIT


response-line = version
                            SP request-identifier
                            SP content-length
                            SP status-code
                            SP response-phrase
                            CRLF

general-header = (from-header
                              / to-header
                              / auth-state-header
                              / SASL-mechanism-header
                              / redirect-header
                              / server-address-header
                              / astrength-header
                              / date-header
                              )

presence-header =  duration-header

im-header = (message-id-header
                              / conversation-id-header
                              / reply-to-header
                              )

entity-header =  content-type-header


from-header = "From: " ( presence-id / im-id )

to-header = "To: " ( presence-id / im-id )

domain-header = "Domain: " domain

auth-state-header = "Auth-State: "
                               ( "init"
                               / "continue"
                               / "abort" )

SASL-mechanism-header = "SASL-Mech: " mechanisms
mechanisms = mechanism [ *(SP mechanism) ]
mechanism = 1*20(ALPHA / DIGIT / "-" / "_")
redirect-header = "Redirect: " address SP port
address = domain / ip4-address / ip6-address
port = 1*DIGIT

server-address-header = "Server-Address: "
                             ( ip4-address / ip6-address )

astrength-header = "AStrength: " astrength
astrength = "strong" / "medium" / "weak" / "none"

date-header = "Date: " date-time
                                     ; as defined in Section 15.5

duration-header = "Duration: " 1*DIGIT

subscription-id-header = "Subscription-ID: " 1*(unreserved / escaped)


message-id-header = "Message-ID" 1*(DIGIT / ALPHA) ": " im-id

conversation-id-header = "Conversation-ID: " 1*(unreserved / escaped)

reply-to-header = "Reply-To: " im-id


