warc-file    = 1*warc-record
warc-record  = header CRLF
                    block CRLF CRLF
header       = version warc-fields
version      = "WARC/0.16" CRLF
warc-fields  = *(named-field CRLF)
block        = *OCTET

named-field     = field-name ":" [ field-value ]
field-name      = token
field-value     = *( field-content | LWS )     ; further qualified
                                                  ; by field definitions
field-content   = <the OCTETs making up the field-value
                     and consisting of either *TEXT or combinations
                     of token, separators, and quoted-string>
token           = 1*<any US-ASCII character
                     except CTLs or separators>
separators      = "(" | ")" | "<" | ">" | "@"
                       | "," | ";" | ":" | "\" | <">
                       | "/" | "[" | "]" | "?" | "="
                       | "{" | "}" | SP | HT
TEXT            = <any OCTET except CTLs,
                     but including LWS>
CHAR            = <UTF-8 characters; RFC3629>  ; (0-191, 194-244)
DIGIT           = <any US-ASCII digit "0".."9">
CTL             = <any US-ASCII control character
                     (octets 0 - 31) and DEL (127)>
CR              = <ASCII CR, carriage return>  ; (13)
LF              = <ASCII LF, linefeed>         ; (10)
SP              = <ASCII SP, space>            ; (32)
HT              = <ASCII HT, horizontal-tab>   ; (9)
CRLF            = CR LF
LWS             = [CRLF] 1*( SP | HT )         ; semantics same as
                                                  ; single SP
quoted-string   = ( <"> *(qdtext | quoted-pair ) <"> )
qdtext          = <any TEXT except <">>
quoted-pair     = "\" CHAR                ; single-character quoting
uri             = "<" <'URI' per RFC3986> ">"


defined-field  = WARC-Type
                    | WARC-Record-ID
                    | WARC-Date
                    | Content-Length
                    | Content-Type
                    | WARC-Concurrent-To
                    | WARC-Block-Digest
                    | WARC-Payload-Digest
                    | WARC-IP-Address
                    | WARC-Refers-To
                    | WARC-Target-URI
                    | WARC-Truncated
                    | WARC-Warcinfo-ID
                    | WARC-Filename                ; warcinfo only
                    | WARC-Profile                 ; revisit only
                    | WARC-Identified-Payload-Type
                    | WARC-Segment-Origin-ID       ; continuation only
                    | WARC-Segment-Number
                    | WARC-Segment-Total-Length    ; continuation only

WARC-Record-ID   = "WARC-Record-ID" ":" uri

Content-Length   = "Content-Length" ":" 1*DIGIT

WARC-Date   = "WARC-Date" ":" w3c-iso8601
w3c-iso8601 = <YYYY-MM-DDThh:mm:ssZ>

WARC-Type   = "WARC-Type" ":" record-type
record-type = "warcinfo" | "response" | "resource"
                 | "request" | "metadata" | "revisit"
                 | "conversion" | "contination" |  future-type
future-type = token

msgtype=response' respectively).  In particular, the content-type is
Content-Type   = "Content-Type" ":" media-type
media-type     = type "/" subtype *( ";" parameter )
type           = token
subtype        = token
parameter      = attribute "=" value
attribute      = token
value          = token | quoted-string

WARC-Concurrent-To = "WARC-Concurrent-To" ":" 1*uri

WARC-Block-Digest = "WARC-Block-Digest" ":" labelled-digest
labelled-digest  = algorithm ":" digest-value
algorithm        = token
digest-value     = token

WARC-Payload-Digest = "WARC-Payload-Digest" ":" labelled-digest

WARC-IP-Address   = "WARC-IP-Address" ":" (ipv4 | ipv6)
ipv4              = <"dotted quad">
ipv6              = <per section 2.2 of RFC1884>

WARC-Refers-To     = "WARC-Refers-To" ":" uri

WARC-Target-URI    = "WARC-Target-URI" ":" uri

WARC-Truncated    = "WARC-Truncated" ":" reason-token
reason-token      = "length"         ; exceeds configured max length
                      | "time"           ; exceeds configured max time
                      | "disconnect"     ; network disconnect
                      | "unspecified"    ; other/unknown reason
                      | future-reason
future-reason     = token

WARC-Warcinfo-ID = "WARC-Warcinfo-ID" ":" uri

WARC-Filename = "WARC-Filename" ":" ( TEXT | quoted-string )

WARC-Profile = "WARC-Profile" ":" uri

WARC-Identified-Payload-Type = "WARC-Identified-Payload-Type" ":"
                                    media-type

WARC-Segment-Number = "WARC-Segment-Number" ":" 1*DIGIT

WARC-Segment-Origin-ID = "WARC-Segment-Origin-ID" ":" uri

WARC-Segment-Total-Length = "WARC-Segment-Total-Length" ":" 1*DIGIT

