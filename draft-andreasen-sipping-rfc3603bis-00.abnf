P-DCS-Trace-Party-ID = "P-DCS-Trace-Party-ID" HCOLON                       
                                         name-addr 

trace-param       = called-param  ; defined in Section 7.1.  
                             / remote-param ; defined in [TDIALOG] 
                             / local-param ; defined in [TDIALOG] 
                             / timestamp-param 
callid-param      = "call-id" EQUAL called ; called from [RFC3261] 

P-DCS-OSPS      = "P-DCS-OSPS" HCOLON OSPS-Tag     
OSPS-Tag        = "BLV" / "EI" / "RING" / token 

P-DCS-Billing-Info      = "P-DCS-Billing-Info" HCOLON                         
                                   Billing-Correlation-ID "/" FEID                    
                                   *(SEMI Billing-Info-param)  
Billing-Correlation-ID  = 1*48(HEXDIG)  
FEID                    = 1*16(HEXDIG) "@" host  
Billing-Info-param      = RKS-Group-ID-param / Charge-param /                 
                                  Calling-param / Called-param /                      
                                  Routing-param / Loc-Routing-param / 
                                  JIP-param / generic-param  
RKS-Group-ID-param      = "rksgroup" EQUAL RKS-Group-ID  
RKS-Group-ID            = token  
Charge-param            = "charge" EQUAL Acct-Charge-URI  
Acct-Charge-URI         = LDQUOT addr-spec RDQUOT  
Calling-param           = "calling" EQUAL Acct-Calling-URI  
Acct-Calling-URI        = LDQUOT addr-spec RDQUOT  
Called-param            = "called" EQUAL Acct-Called-URI  
Acct-Called-URI         = LDQUOT addr-spec RDQUOT  
Routing-param           = "routing" EQUAL Acct-Routing-URI  
Acct-Routing-URI        = LDQUOT addr-spec RDQUOT  
Loc-Routing-param       = "locroute" EQUAL Acct-Loc-Routing-URI  
Acct-Loc-Routing-URI    = LDQUOT addr-spec RDQUOT 

JIP-param               =  "jip" EQUAL jip 
jip                     = LDQUOT 1*phonedigit-hex jip-context RDQUOT 
jip-context             = ";jip-context=" jip-descriptor 
jip-descriptor          = global-hex-digits 
global-hex-digits       = "+" 1*3 (phonedigit) *phonedigit-hex 
phonedigit              = DIGIT / [ visual-separator ] 
phonedigit-hex          = HEXDIG / "*" / "#" / [ visual-separator ] 
P-DCS-LAES        = "P-DCS-LAES" HCOLON Laes-sig                         
                                *(SEMI Laes-param) 
Laes-sig          = hostport     
Laes-param        = Laes-content / Laes-bcid 
                               Laes-cccid / generic-param     
Laes-content      = "content" EQUAL hostport     
         
Laes-bcid         = "bcid" EQUAL 1*48 (HEXDIG) 
Laes-cccid        = "cccid" EQUAL 1*48 (HEXDIG) 

P-DCS-Redirect    = "P-DCS-Redirect" HCOLON Called-ID                      
                               *(SEMI redir-params)     
Called-ID         = LDQUOT addr-spec RDQUOT     
redir-params      = redir-uri-param / redir-count-param /                  
                               generic-param     
redir-uri-param   = "redirector-uri" EQUAL Redirector     
Redirector        = LDQUOT addr-spec RDQUOT     
redir-count-param = "count" EQUAL Redir-count     
Redir-count       = 1*DIGIT 

