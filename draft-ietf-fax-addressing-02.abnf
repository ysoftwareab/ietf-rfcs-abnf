pstn-address = pstn-mbox
                    [ pstn-recipient ]

pstn-mbox = service-selector "=" ( global-phone / local-phone )
                 [ sub-addr-spec ] [post-sep post-dial]

service-selector = 1*( DIGIT / ALPHA / "-" )
                         ; note that SP (space) is not allowed in 
                         ; service-selector.
                         ; service-selector MUST be handled as a case 
                         ; INSENSITIVE string by implementations.

fax-service-selector = "FAX"

fax-address = fax-mbox
                    [ pstn-recipient ]

fax-mbox = "FAX=" ( global-phone / local-phone )
                 [ sub-addr-spec ] [post-sep post-dial]


global-phone = "+" int-country-code
                     [ area-code ]
                     local-number
                     ; This is in agreement with ITU E.164 [12] specification.
                     ; area-code can be omitted only for countries
                     ; which do not use an area-code system. 
                     ; This is a globally unique identifier on the PSTN
                     ; and it must be reachable from anywhere.

local-phone = ( local-type1 / local-type2 / local-type3 )

local-type1 = [ exit-code ]
                    int-access-code
                    int-country-code
                    [ [ pause ] area-code ]
                    [ pause ] local-number
                    ; this is the usual number dialed on traditional
                    ; PSTN devices for an international call.
                    ; area-code can be omitted only for countries
                    ; which do not use an area-code system.

local-type2 = [ exit-code ]
                    long-distance-access area-code
                    [ pause ] local-number
                    ; this is the usual number dialed on traditional
                    ; PSTN devices for a national long distance call.

local-type3 = [ exit-code ]
                    [ local-number ]
                    ; this is the usual number dialed on traditional
                    ; PSTN devices for a local call or in countries
                    ; where the area-code is not in use.
                    ; Note that it could also be totally missing for
                    ; cases where the local dialling device rely on
                    ; local post-dial sequences only.

write-sep = ( "-" / "." )


int-country-code = 1*( DIGIT / write-sep )
                         ; without the local international access
                         ; prefix

int-access-code = 1*( DIGIT / write-sep )
                        ; examples: "00", "010", "19"

long-distance-access = 1*( DIGIT / write-sep )
                             ; in many countries this code is "0" or "1"

exit-code = phone-string
                  ; this include anything needed to enable dialling, like
                  ; the digit to access outside line, the long distance
                  ; carrier access code, the access password to the service
                  ; and also allows special DTMF "#", "*", "A", "B", "C"
                  ; and "D" codes

area-code = 1*( DIGIT / write-sep )
                  ; without the eventual leading long-distance-access code.
                  ; NOTE: some countries do not use an internal area-code
                  ; structure

local-number = 1*( DIGIT / pause / write-sep )
                     ; this is the usual number dialed on traditional
                     ; devices for a local call

phone-string = 1*( DIGIT / pause / tonewait / "#" / "*" /
                         "A" / "B" / "C" / "D" / write-sep )
                     ; this includes also special DTMF codes like "*", 
                     ; "#", "A", "B", "C", "D" defined in [14]
                     ; Important Note: this is NOT the alpha to digit
                     ; convention in use in some countries.


sub-addr-spec = [ isdn-sep sub-addr ] [ t33-sep sub-addr ]

sub-addr-spec = [ isdn-sep sub-addr ]

isdn-sep = "/ISUB="
                 ; note that "/ISUB=" is case INSENSITIVE

t33-sep =  "/TSUB="
                 ; note that "/TSUB=" is case INSENSITIVE and can be used
                 ; within fax service only.


sub-addr = 1*( DIGIT / write-sep )

pause = ( "p" / "P" )

tonewait = ( "w" / "W" )

post-sep =  "/POSTD="
                  ; note that "/POSTD=" is case INSENSITIVE


post-dial = phone-string
                  ; this include anything needed to access the final device
                  ; on the PSTN, like further dialing sequenes 
                  ; and also allows special DTMF "#", "*", "A", "B", "C"
                  ; and "D" codes

pstn-recipient = [ recipient-name ]
                      [ 1*( recipient-qualifier ) ]

recipient-name = ( personal-type1 / personal-type2 )

personal-type1 = "/PN="
                       [ givenname [ "." ] ]
                       [ initials [ "." ] ]
                       surname
                       ; this is compatible with ITU F.401 [10] [11]

personal-type2 = [ "/G=" givenname ]
                       [ "/I=" initials ]
                       "/S=" surname
                       ; this is compatible with ITU F.401 [10] [11]


surname = printablestring

givenname = 1*( DIGIT /  ALPHA / SP / "'" / "+" /
                    "," / "-" / "/" / ":" / "=" / "?" )

initials = 1*ALPHA


qualif-type1 = "/" keyword "=" string

keyword = 1*( DIGIT / ALPHA / "-" )
                ; note that SP (space) is not allowed in keyword

qualif-type2 = "/" x400-label "=" printablestring
      
x400-label = ( "GQ" / "O" / "OU1" / "OU2" / "OU3" / "OU4" /
                     "P" / "A" / "C" / "PD-PN" / "PD-EA" / "PD-ED" /
                     "PD-OFN" / "PD-OF" / "PD-O" / "PD-S" / "PD-A" /
                     "PD-U" / "PD-L" / "PD-R" / "PD-B" / "PD-PC" /
                     "PD-SN" / "PD-C" / "X121" / "E164" / "PSAP" /
                     "N-ID" / "T-ID" / "T-TY" / "DD." dd-key )
                    ; this is the full list of existing text labels
                    ; according to ITU F.401 [10] [11]. "PD-x" labels 
                    ; provide a set of predefined attributes for physical
                    ; delivery which can be also useful for message
                    ; recipient identification.

dd-key = 1*( DIGIT / ALPHA / "-" )
               ; note that SP (space) is not allowed in dd-key

string = PCHAR
               ; note that printable characters are %x20-7E

printablestring = 1*( DIGIT / ALPHA / SP / 
                            "'" / "(" / ")" / "+" / "," / "-" / 
                            "." / "/" / ":" / "=" / "?" )
                        ; this definition comes from ITU F.401 [10] and [5]

mta-I-pstn = domain

pstn-email =  ["/"] pstn-address ["/"] "@" mta-I-pstn

fax-email =  ["/"] fax-address ["/"] "@" mta-I-pstn

FAX=+3940226338

XYZ=+49.81.7856345/ISUB=1234

FAX=+1-202-455-7622/TSUB=8745/PostD=p1w7005393w373

FAX=003940226338/Isub=9823/Tsub=4312

FAX=9p040p22.63.38/tsub=4312

XYZ=+1.202.344-5723


FAX=0p0134782289/TSUB=3345

FAX=/postd=w6743w99p51


FAX=+12023445723

XYZ=+3940226338/PN=Mark.Collins

FAX=9p040p22.63.38/tsub=4312/office=T2-33A/dept=Q-C


FAX=+12023445723@faxgw

FAX=+39-40-226338/PN=Mark.Collins@faxgw

FAX=9p040p226338/TSUB=4312/office=T2-33A/dept=Q-C@faxgw

FAX=+3940226338/PN=Mark.Collins/@faxgw

S=Allocchio;G=Claudio;
