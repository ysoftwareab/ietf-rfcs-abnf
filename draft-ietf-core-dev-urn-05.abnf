devurn = "urn:dev:" body componentpart
body = macbody / owbody / orgbody / osbody / opsbody / otherbody
macbody = "mac:" hexstring
owbody = "ow:" hexstring
orgbody = "org:" number "-" identifier *( ":"  identifier )
osbody = "os:" number "-" serial *( ":"  identifier )
opsbody = "ops:" number "-" product "-" serial *( ":"  identifier )
otherbody = subtype ":" identifier *( ":"  identifier )
subtype = ALPHA *(DIGIT / ALPHA)
identifier = 1*unreserved
identifiernodash = 1*unreservednodash
product = identifiernodash
serial = identifier
componentpart = *( "_" identifier )
unreservednodash = ALPHA / DIGIT / "."
unreserved = unreservednodash / "-"
hexstring = 1*(hexdigit hexdigit)
hexdigit = DIGIT / "a" / "b" / "c" / "d" / "e" / "f"
number = 1*DIGIT
ALPHA =  %x41-5A / %x61-7A
DIGIT   =  %x30-39

