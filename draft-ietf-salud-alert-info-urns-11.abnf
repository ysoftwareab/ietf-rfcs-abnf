alert-URN         = "urn:alert:" alert-identifier
alert-identifier  = alert-category ":" alert-indication
alert-category    = alert-name
alert-indication  = alert-ind-part *(":" alert-ind-part)
alert-ind-part    = alert-name
alert-name        = alert-label / private-name
private-name      = alert-label "@" provider
provider          = provider-id ["(" date ")"]
provider-id       = 1*(domain-label ".") domain-label
alert-label       = let-dig [ *let-dig-hyp let-dig ]
domain-label      = let-dig [ *let-dig-hyp let-dig ]
let-dig-hyp       = let-dig / "-"
let-dig           = ALPHA / DIGIT
date              = [CC] YY [ "-" MM ["-" DD] ]
CC                = DIGIT DIGIT
YY                = DIGIT DIGIT
MM                = ( "0" %x31-39 ) / ( "1" %x30-32 )
DD                = ( "0" %x31-39 ) / ( %x31-32 DIGIT ) / "30"
                              / "31"
ALPHA             = %x41-5A / %x61-7A   ; A-Z / a-z
DIGIT             = %x30-39 ; 0-9


branch=z9hG4bK4b43c2ff8.1
