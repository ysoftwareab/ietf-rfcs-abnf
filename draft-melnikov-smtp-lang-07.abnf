lang-parameter = "LANG=" language-tag

per-recipient-fields =
          [ original-recipient-field CRLF ]
          final-recipient-field CRLF
          action-field CRLF
          status-field CRLF
          [ remote-mta-field CRLF ]
          [ *(localized-diagnostic-text-field CRLF)
            diagnostic-code-field CRLF ]
          [ last-attempt-date-field CRLF ]
          [ will-retry-until-field CRLF ]
          *( extension-field CRLF )

localized-diagnostic-text-field = "Localized-Diagnostic" ":"
                                      language ";" *utf8-text

LANG-Command = "LANG" 1*(SP language-tag) CRLF

LANGUAGE-List = "LANGUAGE" *(SP <language-tag>) CRLF
      ; Note 1: When "i-default" is used, all responses MUST be
      ; entirely in ASCII.
      ;
      ; Note 2: See [LANG-TAGS] for the list of allowed
      ; language tags.

language-tag =  <language-tag> as defined in [LANG-TAGS]

Lang-Reply-line = Reply-code [ SP ext-text ] CRLF
      ; Reply line for a LANG command

ext-text = [ext-data] utf8-text

utf8-text = 1*utf8-no-cr-lf

utf8-no-cr-lf = %d1-9 /      ; Any Unicode character except for NUL,
                   %d11 /       ; CR and LF
                   %d12 /
                   %d14-127 /
                   UTFMB

UTFMB   = UTF2 / UTF3 / UTF4

UTF0    = %x80-BF

UTF2    = %xC2-DF UTF0

UTF3    = %xE0 %xA0-BF UTF0 / %xE1-EC 2(UTF0) /
             %xED %x80-9F UTF0 / %xEE-EF 2(UTF0)

UTF4    = %xF0 %x90-BF 2(UTF0) / %xF1-F3 3(UTF0) /
             %xF4 %x80-8F 2(UTF0)

ext-data = "[" ext-name SP ext-value "]"
      ; Note 1: In the case of multiline response the same ext-data
      ; SHOULD appear on every line.
      ;
      ; Note 2: In case when server also supports "SMTP Service
      ; Extension for Returning Enhanced Error Codes" [RFC-2034],
      ; ext-data MUST follow any Enhanced Error Code.

ext-name = "LANG"

ext-value = Primary-tag
      ; Primary tag as defined by [LANG-TAGS]  


