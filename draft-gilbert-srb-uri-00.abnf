SRB-URI     =   "srb://" address [ abs_path ]
address     =   [ accountinfo "@" ] hostport
accountinfo =   userinfo [ ":" password ]
userinfo    =   username "." mdasdomain [ "." zone ]
username    =   1*(alphanum / "-" / "_" / escaped)
mdasdomain  =   1*(alphanum / "-" / "_" / escaped)
zone        =   1*(alphanum / "-" / "_" / escaped)
password    =   1*(unreserved / escaped)
hostport    =   host [ ":" port ]
host        =   hostname / IPv4address / IPv6reference
hostname    =   *( domainlabel "." ) toplabel [ "." ]
domainlabel =   alphanum / alphanum *( alphanum / "-" ) alphanum
toplabel    =   ALPHA / ALPHA *( alphanum / "-" ) alphanum
IPv4address =   1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6reference   =  ; as specified in RFC2732 [3]
port        =   1*DIGIT
abs_path    =   ; as specified in RFC2396 [4]
alphanum    =   ALPHA / DIGIT
escaped     =   "%" HEXDIG HEXDIG

