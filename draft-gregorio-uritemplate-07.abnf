name=value lists.  The expansions are based on the URI generic
ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z
DIGIT          =  %x30-39             ; 0-9
HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

pct-encoded    =  "%" HEXDIG HEXDIG
unreserved     =  ALPHA / DIGIT / "-" / "." / "_" / "~"
reserved       =  gen-delims / sub-delims
gen-delims     =  ":" / "/" / "?" / "#" / "[" / "]" / "@"
sub-delims     =  "!" / "$" / "&" / "'" / "(" / ")"
                    /  "*" / "+" / "," / ";" / "="

ucschar        =  %xA0-D7FF / %xF900-FDCF / %xFDF0-FFEF
                    /  %x10000-1FFFD / %x20000-2FFFD / %x30000-3FFFD
                    /  %x40000-4FFFD / %x50000-5FFFD / %x60000-6FFFD
                    /  %x70000-7FFFD / %x80000-8FFFD / %x90000-9FFFD
                    /  %xA0000-AFFFD / %xB0000-BFFFD / %xC0000-CFFFD
                    /  %xD0000-DFFFD / %xE1000-EFFFD

iprivate       =  %xE000-F8FF / %xF0000-FFFFD / %x100000-10FFFD
URI-Template  = *( literals / expression )

literals      =  %x21 / %x23-24 / %x26 / %x28-3B / %x3D / %x3F-5B
                   /  %x5D-5F / %x61-7A / %x7E / ucschar / iprivate
                   /  pct-encoded
                        ; any Unicode character except: CTL, SP,
                        ;  DQUOTE, "'", "%" (aside from pct-encoded),
                        ;  "<", ">", "\", "^", "`", "{", "|", "}"

expression    =  "{" [ operator ] variable-list "}"
operator      =  "+" / "#" / "." / "/" / ";" / "?" / "&"
                   /  op-reserve
op-reserve    =  "=" / "," / "!" / "@" / "|"
                          ; reserved for local use: "$" / "(" / ")"

name=value pairs separated by "&"; and,

variable-list =  varspec *( "," varspec )
varspec       =  varname [ modifier ]
varname       =  varchar *( varchar / "." )
varchar       =  ALPHA / DIGIT / "_" / pct-encoded

modifier      =  prefix / explode

prefix        =  ":" max-length
max-length    =  %x31-39 *DIGIT   ; positive integer

explode       =  "*"

