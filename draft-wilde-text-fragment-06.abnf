text-fragment =  text-scheme 0*( ";" text-scheme) 0*( ";" hash-scheme)
text-scheme   =  ( char-scheme / line-scheme / match-scheme )
hash-scheme   =  ( length-scheme / md5-scheme ) [ "," charenc ]
char-scheme   =  %x63.68.61.72 "=" ( position / range )  ; "char="
line-scheme   =  %x6C.69.6E.65 "=" ( position / range )  ; "line="
match-scheme  =  %x6D.61.74.63.68 "=" regex  ; "match="
position      =  number
range         =  (position "," [ position ]) / ("," position )
number        =  1*( DIGIT )
regex         =  StringWithEscapedSemicolon
length-scheme =  %x6C.65.6E.67.74.68 "=" number  ; "length="
md5-scheme    =  %x6D.64 "5=" md5-value  ; "md5="
md5-value     =  32HEXDIG
charenc       =  StringWithEscapedSemicolon

   The StringWithEscapedSemicolon is a string where all characters may
   appear literally (except the characters which are required by the URI
   syntax to be escaped), with the exception of a semicolon.  A
   semicolon that is part of the regular expression must be escaped with
   a leading backslash, and implementations MUST properly interpret
   regular expressions, dereferencing all escape mechanisms that apply,
   i.e. any escaping present due to the context of the URI, semicolon
   escaping, URI percent-encoding, and BRE escaping, in that order).

