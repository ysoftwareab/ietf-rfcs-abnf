ICAP_URI = Scheme ":" Net_Path [ "?" Query ]

Scheme = "icap"
   
Net_Path = "//" Authority [ Abs_Path ]

Authority = [ userinfo "@" ] host [ ":" port ]
      
reqhdr  = "req-hdr" "=" (decimal integer) 
reshdr  = "res-hdr" "=" (decimal integer) 
reqbody = { "req-body" | "null-body" } "=" (decimal integer)
resbody = { "res-body" | "null-body" } "=" (decimal integer)
optbody = { "opt-body" | "null-body" } "=" (decimal integer)

ISTag = "ISTag: " quoted-string

ICAP-Version = "ICAP/1.0"

ICAP-Message = Request | Response
Request      = Request-Line
               *(Request-Header CRLF)
               CRLF
               [ Request-Body ]

Request-Line = Method SP ICAP_URI SP ICAP-Version CRLF

Method       = "REQMOD"         ; Section 4.8
             | "RESPMOD"        ; Section 4.9
             | "OPTIONS"        ; Section 4.10
             | Extension-Method ; Section 4.3.2

Extension-Method = token

ICAP_URI = Scheme ":" Net_Path [ "?" Query ]  ; Section 4.2

Scheme      = "icap"

Net_Path    = "//" Authority [ Abs_Path ]

Authority   = [ userinfo "@" ] host [ ":" port ]


Request-Header     = Request-Fields ":" [ Generic-Field-Value ]

Request-Fields     = Request-Field-Name
                   | Common-Field-Name

Request-Field-Name = "Authorization"   ; Section 4.3.2
                   | "Allow"           ; Section 4.3.2
                   | "From"            ; Section 4.3.2
                   | "Host"            ; Section 4.3.2
                   | "Referer"         ; Section 4.3.2
                   | "User-Agent"      ; Section 4.3.2
                   | "Preview"         ; Section 4.5

Common-Field-Name  = "Cache-Control"   ; Section 4.3.1
                   | "Connection"      ; Section 4.3.1
                   | "Date"            ; Section 4.3.1
                   | "Expires"         ; Section 4.3.1
                   | "Pragma"          ; Section 4.3.1
                   | "Trailer"         ; Section 4.3.1
                   | "Upgrade"         ; Section 4.3.1
                   | "Encapsulated"    ; Section 4.4
                   | Extension-Field-Name   ; Section 4.3

Extension-Field-Name  = "X-" token

Generic-Field-Value   = *( Generic-Field-Content | LWS )
Generic-Field-Content = <the OCTETs making up the field-value
                         and consisting of either *TEXT or combinations
                         of token, separators, and quoted-string>
Request-Body = *OCTET   ; See Sections 4.4 and 4.5 for semantics

Response    = Status-Line
              *(Response-Header CRLF)
              CRLF
              [ Response-Body ]

Status-Line = ICAP-Version SP Status-Code SP Reason-Phrase CRLF

Status-Code = "100"  ; Section 4.5
            | "101"  ; Section 10.1.2 of [4]
            | "200"  ; Section 10.2.1 of [4]
            | "201"  ; Section 10.2.2 of [4]
            | "202"  ; Section 10.2.3 of [4]
            | "203"  ; Section 10.2.4 of [4]
            | "204"  ; Section 4.6
            | "205"  ; Section 10.2.6 of [4]
            | "206"  ; Section 10.2.7 of [4]
            | "300"  ; Section 10.3.1 of [4]
            | "301"  ; Section 10.3.2 of [4]
            | "302"  ; Section 10.3.3 of [4]
            | "303"  ; Section 10.3.4 of [4]
            | "304"  ; Section 10.3.5 of [4]
            | "305"  ; Section 10.3.6 of [4]
            | "306"  ; Section 10.3.7 of [4]
            | "307"  ; Section 10.3.8 of [4]
            | "400"  ; Section 4.3.3
            | "401"  ; Section 10.4.2 of [4]
            | "402"  ; Section 10.4.3 of [4]
            | "403"  ; Section 10.4.4 of [4]
            | "404"  ; Section 4.3.3
            | "405"  ; Section 4.3.3
            | "406"  ; Section 10.4.7 of [4]
            | "407"  ; Section 10.4.8 of [4]
            | "408"  ; Section 4.3.3
            | "409"  ; Section 10.4.10 of [4]
            | "410"  ; Section 10.4.11 of [4]
            | "411"  ; Section 10.4.12 of [4]
            | "412"  ; Section 10.4.13 of [4]
            | "413"  ; Section 10.4.14 of [4]
            | "414"  ; Section 10.4.15 of [4]
            | "415"  ; Section 10.4.16 of [4]
            | "416"  ; Section 10.4.17 of [4]
            | "417"  ; Section 10.4.18 of [4]
            | "500"  ; Section 4.3.3
            | "501"  ; Section 4.3.3
            | "502"  ; Section 4.3.3
            | "503"  ; Section 4.3.3
            | "504"  ; Section 10.5.5 of [4]
            | "505"  ; Section 4.3.3
            | Extension-Code

Extension-Code = 3DIGIT
Reason-Phrase = *<TEXT, excluding CR, LF>

Response-Header     = Response-Fields ":" [ Generic-Field-Value ]

Response-Fields     = Response-Field-Name
                    | Common-Field-Name

Response-Field-Name = "Server"         ; Section 4.3.3
                    | "ISTag"          ; Section 4.7

Response-Body = *OCTET  ; See Sections 4.4 and 4.5 for semantics
