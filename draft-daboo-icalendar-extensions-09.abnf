name       = "NAME" nameparam ":" text CRLF

nameparam  = *(
                 ;
                 ; The following are OPTIONAL,
                 ; but MUST NOT occur more than once.
                 ;
                 (";" altrepparam) / (";" languageparam) /
                 ;
                 ; The following is OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
                 )

refresh      = "REFRESH-INTERVAL" refreshparam
                    ":" dur-value CRLF
                    ;consisting of a positive duration of time.

refreshparam = *(
                   ;
                   ; The following is REQUIRED,
                   ; but MUST NOT occur more than once.
                   ;
                   (";" "VALUE" "=" "DURATION") /
                   ;
                   ; The following is OPTIONAL,
                   ; and MAY occur more than once.
                   ;
                   (";" other-param)
                   ;
                   )

source       = "SOURCE" sourceparam ":" uri CRLF

sourceparam = *(";" other-param)

color          = "COLOR" colorparam ":" text CRLF
                     ; Value is CSS3 color name

colorparam     = *(";" other-param)

image      = "IMAGE" imageparam ( ":" uri ) /
                (
                  ";" "ENCODING" "=" "BASE64"
                  ";" "VALUE" "=" "BINARY"
                  ":" binary
                ) /
                CRLF

imageparam = *(
                 ;
                 ; The following is REQUIRED,
                 ; but MUST NOT occur more than once.
                 ;
                 (";" "VALUE" "=" "URI") /
                 ;
                 ; The following is OPTIONAL for a URI value,
                 ; RECOMMENDED for a BINARY value,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" fmttypeparam) /
                 ;
                 ; The following are OPTIONAL,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" altrepparam) / (";" displayparam) /
                 ;
                 ; The following is OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
                 )

conference = "CONFERENCE" confparam  ":" uri CRLF

confparam  = *(
                 ;
                 ; The following is REQUIRED,
                 ; but MUST NOT occur more than once.
                 ;
                 (";" "VALUE" "=" "URI") /
                 ;
                 ; The following are OPTIONAL,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" featureparam) / (";" labelparam) /
                 ;
                 ; The following is OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
                 )

LABEL=Moderator dial-in:tel:+1-412-555-0123,,,654321
LABEL=Attendee dial-in:tel:+1-412-555-0123,,,555123
LABEL=Attendee dial-in:tel:+1-888-555-0456,,,555123
LABEL=Chat room:xmpp:chat-123@conference.example.com
LABEL=Attendee dial-in:http://chat.example.com/audio?id=123456

displayparam = "DISPLAY" "=" displayval *("," displayval)

displayval =   ("BADGE" /    ; image inline with the title of the event
                 "GRAPHIC" /  ; a full image replacement for the event
                              ; itself
                 "FULLSIZE /  ; an image that is used to enhance the
                              ; event
                 "THUMBNAIL / ; a smaller variant of "FULLSIZE" to be
                              ; used when space for the image is
                              ; constrained
                 x-name /     ; Experimental type
                 iana-token)  ; Other IANA registered type
                              ;
                              ; Default is BADGE

   Description:  This property parameter MAY be specified on "IMAGE"
      properties.  In the absence of this parameter, the value "BADGE"
      MUST be used for the default behavior.  The value determines how a
      client ought to present an image supplied in iCalendar data to the
      user.

      Values for this parameter are registered with IANA as per
      Section 8.3.  New values can be added to this registry following
      the procedure outlined in Section 8.2.1 of [RFC5545].

      Servers and clients MUST handle x-name and iana-token values they
      don't recognize by not displaying any image at all.

   Example:

   IMAGE;VALUE=URI;DISPLAY=BADGE,THUMBNAIL,;FMTTYPE=image/png:http://exa
    mple.com/images/weather-cloudy.png

emailparam = "EMAIL" "=" param-value

featureparam = "FEATURE" "=" featuretext *("," featuretext)
featuretext  =  ("AUDIO" /     ; Audio capability
                    "CHAT" /      ; Chat or instant messaging
                    "FEED" /      ; Blog or Atom feed
                    "MODERATOR" / ; Moderator dial-in code
                    "PHONE" /     ; Phone conference
                    "SCREEN" /    ; Screen sharing
                    "VIDEO" /     ; Video capability
                    x-name /      ; Experimental type
                    iana-token)   ; Other IANA registered type

infoparam = "LABEL" "=" param-value

LABEL="Web video chat, access code=76543";
