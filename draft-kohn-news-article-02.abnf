format=flowed" as specified in [RFC2646] and SHOULD preserve flowed
fields          =/ *( newsgroups /
                         path /
                         followup-to /
                         expires /
                         control /
                         distribution /
                         summary /
                         approved /
                         organization /
                         xref /
                         supersedes )

message-id      =  "Message-ID:" msg-id CRLF

msg-id          =  [CFWS] msg-id-core [CFWS]

msg-id-core     =  "<" id-left "@" id-right ">"
                      ; maximum length is 250 octets

id-left         =  dot-atom-text / no-fold-quote / obs-id-left

id-right        =  dot-atom-text / no-fold-literal / obs-id-right

no-fold-quote   =  DQUOTE *( qtext / no-space-qp ) DQUOTE

no-fold-literal =  "[" *( htext / no-space-qp ) "]"

no-space-qp     =  ( "\" ptext ) / obs-qp

ptext           =  %d33-61 /  ; Printable characters excluding ">"
                      %d63-126 /
                      obs-text

htext           =  HEXDIG /  ; hexadecimal digits, case-insensitive
                      "." /     ; IPv4 separator
                      ":"       ; IPv6 separator

newsgroups      =  "Newsgroups:" newsgroup-list CRLF

newsgroup-list  =  [FWS] newsgroup-name
                      *( "," [FWS] newsgroup-name ) [FWS]

newsgroup-name  =  component *( "." component ) ; 71 character max

component       =  plain-component

plain-component =  component-start *29component-rest

component-start =  ALPHA / DIGIT

component-rest  =  ALPHA / DIGIT / "+" / "-" / "_"

path            =  "Path:" [FWS]
                      *( path-host [FWS] path-delimiter [FWS] )
                      path-host [FWS] CRLF

path-host       =  ( ALPHA / DIGIT )
                      *( ALPHA / DIGIT / "-" / "." / ":" / "_" )

path-delimiter  =  "!"

followup-to     =  "Followup-To:" ( newsgroup-list / poster-text )
                      CRLF

poster-text     =  [FWS] %d112.111.115.116.101.114 [FWS]
                      ; "poster" in lower-case

expires         =  "Expires:" date-time CRLF

control         =  "Control:" verb *( FWS argument ) CRLF

distribution    =  "Distribution:" dist-name *( "," dist-name ) CRLF

dist-name       =  [FWS] ALPHA / DIGIT
                      *( ALPHA / DIGIT / "+" / "-" / "_" ) [FWS]

summary         =  "Summary:" unstructured CRLF
approved        =  "Approved:" mailbox-list CRLF

organization    =  "Organization:" unstructured CRLF

xref            =  "Xref:" [CFWS] path-host
                      1*( CFWS location ) [CFWS]

location        =  newsgroup-name ":" 1*16DIGIT

supersedes      =  "Supersedes:" 1*( [FWS] msg-id-core ) CRLF

