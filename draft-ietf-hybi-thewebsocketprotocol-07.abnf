xOo=".  This value would then be echoed in the header |Sec-WebSocket-
ws-frame                = frame-fin
                             frame-rsv1
                             frame-rsv2
                             frame-rsv3
                             frame-opcode
                             frame-masked
                             frame-payload-length
                             [ frame-masking-key ]
                             frame-payload-data

frame-fin               = %x0 ; more frames of this message follow
                           / %x1 ; final frame of message

frame-rsv1              = %x0 ; 1 bit, MUST be 0

frame-rsv2              = %x0 ; 1 bit, MUST be 0

frame-rsv3              = %x0 ; 1 bit, MUST be 0

frame-opcode            = %x0 ; continuation frame
                           / %x1 ; text frame
                           / %x2 ; binary frame
                           / %3-7 ; reserved for further non-control frames
                           / %x8 ; connection close
                           / %x9 ; ping
                           / %xA ; pong
                           / %xB-F ; reserved for further control frames

frame-masked            = %x0 ; frame is not masked, no frame-masking-key
                           = %x1 ; frame is masked, frame-masking-key present

frame-payload-length    = %x00-7D
                           / %x7E frame-payload-length-16
                           / %x7F frame-payload-length-63

frame-payload-length-16 = %x0000-FFFF

frame-payload-length-63 = %x0000000000000000-7FFFFFFFFFFFFFFF

frame-masking-key       = <4>( %0x00-FF ) ; present only if frame-masked is 1

frame-payload-data      = (frame-masked-extension-data
                              frame-masked-application-data)   ; frame-masked 1
                           / (frame-unmasked-extension-data
                              frame-unmasked-application-data) ; frame-masked 0

frame-masked-extension-data     = *( %x00-FF ) ; to be defined later

frame-masked-application-data   = *( %x00-FF )

frame-unmasked-extension-data   = *( %x00-FF ) ; to be defined later

frame-unmasked-application-data = *( %x00-FF )

j                   = i MOD 4
transformed-octet-i = original-octet-i XOR masking-key-octet-j


extension-list = 1#extension
extension = extension-token *( ";" extension-param )
extension-token = registered-token | private-use-token
registered-token = token
private-use-token = "x-" token
extension-param = token [ "=" ( token | quoted-string ) ]

