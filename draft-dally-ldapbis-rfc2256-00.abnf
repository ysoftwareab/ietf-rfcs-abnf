delivery-value = pdm / ( pdm whsp "$" whsp delivery-value )

pdm = "any" / "mhs" / "physical" / "telex" / "teletex" /
                "g3fax" / "g4fax" / "ia5" / "videotex" / "telephone"

EnhancedGuide = woid whsp "#" whsp criteria whsp "#" whsp subset

subset = "baseobject" / "oneLevel" / "wholeSubtree"


guide-value = [ object-class "#" ] criteria

object-class = woid

criteria = criteria-item / criteria-set / ( "!" criteria )

criteria-set = ( [ "(" ] criteria "&" criteria-set [ ")" ] ) /
                     ( [ "(" ] criteria "|" criteria-set [ ")" ] )

criteria-item = [ "(" ] attributetype "$" match-type [ ")" ]

match-type = "EQ" / "SUBSTR" / "GE" / "LE" / "APPROX"

teletex-id = ttx-term  0*("$" ttx-param)

ttx-term   = printablestring

ttx-param  = ttx-key ":" ttx-value

ttx-key    = "graphic" / "control" / "misc" / "page" / "private"

ttx-value  = octetstring

telex-number  = actual-number "$" country "$" answerback

actual-number = printablestring

country       = printablestring

answerback    = printablestring

