rulelist =/ 1*(
          named-rulelist / rulelist-import / rule/ (*c-wsp c-nl) )
        ; this is really a backward compatible replacement definition

named-rulelist =
          rulelist-name *WSP "{" rulelist "}" c-nl

rulelist-name = rulename

rulelist-import =
          (basic-import / named-import) c-nl

basic-import = "@" docname
        ; imports all the ABNF from the named document

docname = char-val
        ; format and resolution of the document from the docname
        ; is defined by the specific tools used.
        ; At least common URI types should be supported
        ; E.g. "urn:ietf:rfc:5234",
        ;      "urn:ietf:id:draft-ietf-foo-bar-01",
        ;      "file:foo.xml"

named-import = rulelist-name basic-import
        ; (E.g. CORE@"urn:ietf:rfc:5234")
        ; This is shorthand for importing into a new named
        ; rulelist with the given name.

element =/ scoped-rulename

scoped-rulename = 1*(rulelist-name ".") rulename



