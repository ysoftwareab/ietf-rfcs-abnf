type-name = restricted-name
subtype-name = restricted-name

restricted-name = restricted-name-first *126restricted-name-chars
restricted-name-first  = ALPHA / DIGIT
restricted-name-chars  = ALPHA / DIGIT / "!" / "#" /
                            "$" / "&" / "-" / "^" / "_"
restricted-name-chars =/ "." ; Characters before first dot always
                                ; specify a facet name
restricted-name-chars =/ "+" ; Characters after last plus always
                                ; specify a structured syntax suffix

       Figure 2: ABNF for type and subtype, as defined from RFC 6838

Media-Type-Name = type-name "/" subtype-name

                  Figure 3: Definition of Media-Type-Name

Content-Type   = Media-Type-Name *( *SP ";" *SP parameter )
parameter      = token "=" ( token / quoted-string )

token          = 1*tchar
tchar          = "!" / "#" / "$" / "%" / "&" / "'" / "*"
                  / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~"
                  / DIGIT / ALPHA
quoted-string  = %x22 *qdtext %x22
qdtext         = SP / %x21 / %x23-5B / %x5D-7E

                    Figure 5: Definition of Content-Type

content-coding   = token

           Figure 6: Definition of content-coding as in RFC 2616

Content-Format = "0" / (POS-DIGIT *DIGIT)
Content-Format-String   = Content-Type ["@" content-coding]

               Figure 7: Definition of Content-Format/-String

DIGIT     =  %x30-39           ; 0 – 9
POS-DIGIT =  %x31-39           ; 1 – 9
ALPHA     =  %x41-5A / %x61-7A ; A – Z / a – z
SP        =  %x20

                  Figure 8: Commonly Used ABNF Definitions

