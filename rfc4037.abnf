message = name [SP anonym-parameters]
             [CRLF named-parameters CRLF]
             [CRLF payload CRLF]
             ";" CRLF

anonym-parameters = value *(SP value)               ; space-separated
named-parameters  = named-value *(CRLF named-value) ; CRLF-separated
list-items        = value *("," value)              ; comma-separated

payload = data

named-value = name ":" SP value

value     = structure / list / atom
structure = "{" [anonym-parameters] [CRLF named-parameters CRLF] "}"
list      = "(" [ list-items ] ")"
atom      = bare-value / quoted-value

name = ALPHA *safe-OCTET
bare-value = 1*safe-OCTET
quoted-value = DQUOTE data DQUOTE
data = size ":" *OCTET                   ; exactly size octets

safe-OCTET = ALPHA / DIGIT / "-" / "_"
size = dec-number                        ; 0-2147483647
dec-number = 1*DIGIT                     ; no leading zeros or signs

