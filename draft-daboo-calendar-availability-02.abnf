availabilityc  = "BEGIN" ":" "VAVAILABILITY" CRLF
                           availabilityprop *availablec
                           "END" ":" "VAVAILABILITY" CRLF

availabilityprop  = *(
                            ;
                            ; the following are REQUIRED,
                            ; but MUST NOT occur more than once
                            ;
                            dtstamp / uid
                            ;
                            ; the following are OPTIONAL,
                            ; but MUST NOT occur more than once
                            ;
                            busytype / class / created / description /
                            dtstart / last-mod / organizer / seq /
                            summary / url /
                            ;
                            ; either 'dtend' or 'duration' MAY appear
                            ; once, but 'dtend' and 'duration' MUST
                            ; NOT occur in the same 'availabilityprop'.
                            ;
                            dtend / duration /
                            ;
                            ; the following are OPTIONAL,
                            ; and MAY occur more than once
                            ;
                            categories / comment / contact /
                            x-prop / iana-prop
                            ;
                            )

availablec  = "BEGIN" ":" "AVAILABLE" CRLF
                        availableprop
                        "END" ":" "AVAILABLE" CRLF

availableprop  = *(
                         ;
                         ; the following are REQUIRED,
                         ; but MUST NOT occur more than once
                         ;
                         dtstamp / dtstart / uid /
                         ;
                         ; either 'dtend' or 'duration' MAY appear
                         ; once, but 'dtend' and 'duration' MUST NOT
                         ; occur in the same 'availableprop'.
                         ; 'duration' MUST NOT be present if 'dtstart'
                         ; is not present
                         ;
                         dtend / duration /
                         ;
                         ; the following are OPTIONAL,
                         ; but MUST NOT occur more than once
                         ;
                         created / description / geo / last-mod /
                         location / recurid / rrule / summary /
                         ;
                         ; the following are OPTIONAL,
                         ; and MAY occur more than once
                         ;
                         categories / comment / contact / exdate /
                         rdate / x-prop / iana-prop

                         )
busytype      = "BUSYTYPE" busytypeparam ":" busytypevalue CRLF

busytypeparam = *(";" other-param)

busytypevalue = "BUSY" / "BUSY-UNAVAILABLE" /
                        "BUSY-TENTATIVE" / iana-token / x-name
                        ; Default is "BUSY-UNAVAILABLE".

