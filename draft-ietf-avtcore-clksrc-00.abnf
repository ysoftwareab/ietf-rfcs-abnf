timestamp-refclk = "a=ts-refclk:" clksrc [ SP sync-confidence ] CRLF

clksrc = ntp / ptp / gps / gal / local / private

ntp             =  "ntp=" ntp-server-addr
ntp-server-addr =  host [ ":" port ]
ntp-server-addr =/ "traceable" )

ptp             =  "ptp=" ptp-version ":" ptp-gmid [":" ptp-domain]
ptp-version     =  "IEEE1588-2002"
ptp-version     =/ "IEEE1588-2008"
ptp-version     =/ "IEEE802.1AS-2011"
ptp-gmid        =  EUI64
ptp-gmid        =/ "traceable"
ptp-domain      =  ptp-domain-name / ptp-domain-nmbr
ptp-domain-name =  "domain-name=" 16ptp-domain-char
ptp-domain-char =  %x21-7E / %x00
                       ; allowed characters: 0x21-0x7E (IEEE 1588-2002)
ptp-domain-nmbr =  "domain-nmbr=" %x00-7f
                       ; allowed number range: 0-127 (IEEE 1588-2008)

gps      =  "gps"
gal      =  "gal"
local    =  "local"
private  =  "private" [ ":" "traceable" ]


sync-confidence = sync-timestamp [SP sync-frequency]

sync-timestamp  = sync-date SP sync-time SP sync-UTCoffset

sync-date       = 4DIGIT "-" 2DIGIT "-" 2DIGIT
                      ; yyyy-mm-dd (e.g., 1982-12-02)

sync-time       = 2DIGIT ":" 2DIGIT ":" 2DIGIT "." 3DIGIT
                      ; 00:00:00.000 - 23:59:59.999

sync-UTCoffset  = ( "+" / "-" ) 2DIGIT ":" 2DIGIT
                      ; +HH:MM or -HH:MM

sync-frequency  = 2HEXDIG
                      ; If N is the field value, HZ=2^(N-127)


host          = hostname / IPv4address / IPv6reference

hostname      =  *( domainlabel "." ) toplabel [ "." ]
toplabel      =  ALPHA / ALPHA *( alphanum / "-" ) alphanum
domainlabel   =  alphanum
                  =/ alphanum *( alphanum / "-" ) alphanum

IPv4address   =  1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6reference =  "[" IPv6address "]"
IPv6address   =  hexpart [ ":" IPv4address ]
hexpart       =  hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
hexseq        =  hex4 *( ":" hex4)
hex4          =  1*4HEXDIG

port = 1*DIGIT

EUI64 = 7(2HEXDIG "-") 2HEXDIG


           Figure 1: Timestamp Reference Clock Source Signalling

v=0
o=jdoe 2890844526 2890842807 IN IP4 10.47.16.5
s=SDP Seminar
i=A Seminar on the session description protocol
u=http://www.example.com/seminars/sdp.pdf
e=j.doe@example.com (Jane Doe)
c=IN IP4 224.2.17.12/127
t=2873397496 2873404696
a=recvonly
a=ts-refclk:ntp=traceable
m=audio 49170 RTP/AVP 0
m=video 51372 RTP/AVP 99
a=rtpmap:99 h263-1998/90000

v=0
o=jdoe 2890844526 2890842807 IN IP4 10.47.16.5
s=SDP Seminar
i=A Seminar on the session description protocol
u=http://www.example.com/seminars/sdp.pdf
e=j.doe@example.com (Jane Doe)
c=IN IP4 224.2.17.12/127
t=2873397496 2873404696
a=recvonly
a=ts-refclk:local
m=audio 49170 RTP/AVP 0
a=ts-refclk:ntp=203.0.113.10 2011-02-19 21:03:20.345+01:00
a=ts-refclk:ntp=198.51.100.22
m=video 51372 RTP/AVP 99
a=rtpmap:99 h263-1998/90000
a=ts-refclk:ptp=IEEE802.1AS-2011:39-A7-94-FF-FE-07-CB-D0

v=0
o=jdoe 2890844526 2890842807 IN IP4 10.47.16.5
s=SDP Seminar
i=A Seminar on the session description protocol
u=http://www.example.com/seminars/sdp.pdf
e=j.doe@example.com (Jane Doe)
c=IN IP4 224.2.17.12/127
t=2873397496 2873404696
a=recvonly
a=ts-refclk:local
m=audio 49170 RTP/AVP 0
m=video 51372 RTP/AVP 99
a=rtpmap:99 h263-1998/90000
a=ssrc:12345 ts-refclk:ptp=IEEE802.1AS-2011:39-A7-94-FF-FE-07-CB-D0

a=mediaclk:sender

a=mediaclk:offset=<offset>[ rate=<rate numerator>/<rate
a=mediaclk:rtp=<connection address>:<port> <CNAME>

a=mediaclk:IEEE1722=<StreamID>

timestamp-mediaclk = "a=mediaclk:" mediaclock

mediaclock = refclk / rtp / streamid / sender

refclk = "offset=" 1*DIGIT [ SP "rate=" 1*DIGIT "/" 1*DIGIT ]

rtp = "rtp=" nettype SP addrtype SP connection-address SP port SP cname

streamid = "IEEE1722=" EUI64
sender = "sender"

cname = non-ws-string

nettype = token
           ;typically "IN"

addrtype = token
           ;typically "IP4" or "IP6"

token-char = %x21 / %x23-27 / %x2A-2B / %x2D-2E / %x30-39 / %x41-5A
                / %x5E-7E

token = 1*(token-char)

connection-address =  multicast-address / unicast-address

unicast-address = IP4-address / IP6-address / FQDN / extn-addr

multicast-address = IP4-multicast / IP6-multicast / FQDN / extn-addr

IP4-multicast = m1 3( "." decimal-uchar ) "/" ttl [ "/" integer ]
           ; IPv4 multicast addresses may be in the
           ; range 224.0.0.0 to 239.255.255.255

m1 = ("22" ("4"/"5"/"6"/"7"/"8"/"9")) / ("23" DIGIT )

IP6-multicast = hexpart [ "/" integer ]
           ; IPv6 address starting with FF

FQDN = 4*(alpha-numeric / "-" / ".")
           ; fully qualified domain name as specified
           ; in RFC 1035 (and updates)

IP4-address = b1 3("." decimal-uchar)

b1 = decimal-uchar
           ; less than "224"

IP6-address = hexpart [ ":" IP4-address ]

hexpart = hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]

hexseq = hex4 *( ":" hex4)

hex4 = 1*4HEXDIG
extn-addr = non-ws-string

non-ws-string = 1*(VCHAR/%x80-FF)
           ;string of visible characters

port = 1*DIGIT

EUI64 = 7(2HEXDIG "-") 2HEXDIG

                  Figure 5: Media Clock Source Signalling

v=0
o=- 1311738121 1311738121 IN IP4 192.168.1.1
c=IN IP4 239.0.0.2/255
s=
t=0 0
m=audio 5004 RTP/AVP 96
a=rtpmap:96 L24/48000/8
a=sendonly
a=ts-refclk:ptp=IEEE1588-2008:39-A7-94-FF-FE-07-CB-D0:0
a=mediaclk:offset=963214424

v=0
o=- 1311738121 1311738121 IN IP4 192.168.1.1
c=IN IP4 239.0.0.2/255
s=
t=0 0
m=audio 5004 RTP/AVP 96
a=rtpmap:96 L24/44100/2
a=sendonly
a=ts-refclk:ptp=IEEE1588-2008:39-A7-94-FF-FE-07-CB-D0:0
a=mediaclk:offset=963214424 rate=1000/1001

v=0
o=- 1311738121 1311738121 IN IP4 192.168.1.1
c=IN IP4 224.2.228.230/32
s=
t=0 0
m=audio 5004 RTP/AVP 96
a=rtpmap:96 L24/48000/2
a=sendonly
a=ts-refclk:ptp=IEEE1588-2008:39-A7-94-FF-FE-07-CB-D0:0
a=mediaclk:rtp=IN IP4 239.0.0.1 5004 00:60:2b:20:12:if

v=0
o=- 1311738121 1311738121 IN IP4 192.168.1.1
c=IN IP4 224.2.228.230/32
s=
t=0 0
m=audio 5004 RTP/AVP 96
a=rtpmap:96 L24/48000/2
a=sendonly
a=ts-refclk:ptp=IEEE1588-2008:39-A7-94-FF-FE-07-CB-D0:0
a=mediaclk:IEEE1722=38-D6-6D-8E-D2-78-13-2F

