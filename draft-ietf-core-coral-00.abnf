document = body

body = [*(link / form / representation / directive)]

link = [2, relation-type, link-target, ?body]

relation-type = text

link-target = CoRI / literal

CoRI = <Defined in Section X of RFC XXXX>

literal = bool / int / float / time / bytes / text / null

form = [3, operation-type, submission-target, ?form-fields]

operation-type = text

submission-target = CoRI

form-fields = [*(form-field-type, form-field-value)]

form-field-type = text

form-field-value = CoRI / literal

representation = [0, bytes, ?representation-metadata]

representation-metadata = [*(metadata-name, metadata-value)]

metadata-name = text

metadata-value = CoRI / literal

directive = base-directive
base-directive = [1, base]

base = CoRI

token = identifier / iri / iriref / literal / punctuator

identifier = START *CONTINUE *(MEDIAL 1*CONTINUE)

START = <Any character with the XID_Start property>
CONTINUE = <Any character with the XID_Continue property>

MEDIAL = "-" / "." / "~" / %x58A / %xF0B

MEDIAL =/ %x2010 / %x2027 / %x30A0 / %x30FB

iri = "<" IRI ">"

iriref = "<" IRI-reference ">"

IRI = <Defined in Section 2.2 of RFC 3987>

IRI-reference = <Defined in Section 2.2 of RFC 3987>

literal = boolean / integer / float / datetime / bytes / text

literal =/ null

boolean = "true" / "false"

integer = ["+" / "-"] (decimal / binary / octal / hexadecimal)

decimal = 1*DIGIT

binary = %x30 (%x42 / %x62) 1*BINDIG

octal = %x30 (%x4F / %x6F) 1*OCTDIG

hexadecimal = %x30 (%x58 / %x78) 1*HEXDIG

DIGIT = %x30-39

BINDIG = %x30-31

OCTDIG = %x30-37

HEXDIG = %x30-39 / %x41-46 / %x61-66

float = ["+" / "-"] 1*DIGIT [fraction] [exponent]

fraction = "." 1*DIGIT
exponent = (%x45 / %x65) ["+" / "-"] 1*DIGIT

float =/ "NaN"

float =/ ["+" / "-"] "Infinity"

datetime = %x64.74 SQUOTE date-time SQUOTE

date-time = <Defined in Section 5.6 of RFC 3339>

SQUOTE = %x27

bytes = base16 / base32 / base64

base16 = (%x68 / %x62.31.36) SQUOTE <Base16 encoded data> SQUOTE

base32 = %x62.33.32 SQUOTE <Base32 encoded data> SQUOTE

base64 = %x62.36.34 SQUOTE <Base64 encoded data> SQUOTE
text = DQUOTE *(char / %x5C escape) DQUOTE

char = <Any character except %x22, %x5C, and line terminators>

escape = simple-escape / hexadecimal-escape / unicode-escape

simple-escape = %x30 / %x62 / %x74 / %x6E / %x76

simple-escape =/ %x66 / %x72 / %x22 / %x27 / %x5C

hexadecimal-escape = (%x78 / %x58) 2HEXDIG

unicode-escape = %x75 4HEXDIG / %x55 8HEXDIG

DQUOTE = %x22

null = "null" / "_"

punctuator = "#" / ":" / "*" / "[" / "]" / "{" / "}" / "=" / "->"

document = body

body = *(link / form / representation / directive)

link = relation-type link-target ["{" body "}"]

relation-type = iri / simple-name / qualified-name

simple-name = identifier

qualified-name = identifier ":" identifier

link-target = iriref / literal

form = operation-type "->" submission-target ["[" form-fields "]"]

operation-type = iri / simple-name / qualified-name

submission-target = iriref

form-fields = *(form-field-type form-field-value)

form-field-type = iri / simple-name / qualified-name

form-field-value = iriref / literal

representation = "*" bytes ["[" representation-metadata "]"]

representation-metadata = *(metadata-name metadata-value)

metadata-name = iri / simple-name / qualified-name

metadata-value = iriref / literal

directive = base-directive / using-directive

base-directive = "#" "base" base

base = iriref

using-directive = "#" "using" [identifier "="] iri

CoRAL-Document = document

CoRAL-Link = link

CoRAL-Form = form

