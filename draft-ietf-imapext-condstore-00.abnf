capability          =/ "CONDSTORE"

status              = "STATUS" SP mailbox SP 
                          "(" status-att-req *(SP status-att-req) ")"
                          ;; redefine STATUS command syntax defined in [IMAP4]

status-att-req      = status-att / "HIGHESTMODSEQ"

mailbox-data        =/ "STATUS" SP mailbox SP "("
                           [status-rsp-info *(SP status-rsp-info)] ")"

status-rsp-info     = status-att SP number /
                          "HIGHESTMODSEQ" SP mod-sequence-value

store               = "STORE" SP set store-modifiers SP store-att-flags

store-modifiers     = [ SP "(" store-modifier *(SP store-modifier) ")" ]

store-modifier      = "UNCHANGEDSINCE" SP mod-sequence-valzer
                          ;; Only single "UNCHANGEDSINCE" may be specified
                          ;; in a STORE operation

fetch-att           =/ fetch-mod-sequence
                           ;; modifies original IMAP4 fetch-att

fetch-mod-sequence  = "MODSEQ"

fetch-mod-resp      = "MODSEQ" SP "(" permsg-modsequence ")"

search-key          =/ search-modsequence
                           ;; modifies original IMAP4 search-key

search-modsequence  = "MODSEQ" [search-modseq-ext] SP mod-sequence-valzer

search-modseq-ext   = SP entry-name SP entry-type-req

resp-text-code      =/ "HIGHESTMODSEQ" SP mod-sequence-value /
                           "MODIFIED" SP set

entry-name          = '"' "/message/flags/" attr-flag '"'
                          ;; each system or user defined flag <flag>
                          ;; is mapped to "/message/flags/<flag>".
                          ;;
                          ;; <entry-name> follows the escape rules used
                          ;; by "quoted" string as described in Section
                          ;; 4.3 of [IMAP4], e.g. for the flag \Seen
                          ;; the corresponding <entry-name> is
                          ;; "/message/flags/\\seen", and for the flag
                          ;; $MDNSent, the corresponding <entry-name>
                          ;; is "/message/flags/$mdnsent".

entry-type-resp     = "private" | "shared"
                          ;; metadata item type

entry-type-req      = entry-type-resp | "all"
                          ;; perform SEARCH operation on private
                          ;; metadata item, shared metadata item or both

permsg-modsequence  = mod-sequence-value
                          ;; per message mod-sequence

mod-sequence-value  = 1*DIGIT
                          ;; Positive unsigned 64-bit integer (mod-sequence)
                          ;; (1 <= n < 18,446,744,073,709,551,615)

mod-sequence-valzer = "0" | mod-sequence-value

search_sort_mod_seq = "(" "MODSEQ" SP mod-sequence-value ")"

sort-key            =/ "MODSEQ"

mailbox-data        =/ "SEARCH" [SP nz-number *(SP nz-number) search_sort_mod_seq] /
                           "SORT" [SP nz-number *(SP nz-number) search_sort_mod_seq]

attr-flag           = "\\Answered" / "\\Flagged" / "\\Deleted" /
                          "\\Seen" / "\\Draft" / attr-flag-keyword /
                          attr-flag-extension
                          ;; Does not include "\Recent"

attr-flag-extension = "\\" atom
                          ;; Future expansion.  Client implementations
                          ;; MUST accept flag-extension flags.  Server
                          ;; implementations MUST NOT generate
                          ;; flag-extension flags except as defined by
                          ;; future standard or standards-track
                          ;; revisions of this specification.

attr-flag-keyword   = atom


