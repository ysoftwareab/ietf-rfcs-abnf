CHAR        =  <any UTF-8 character (1 to 6 octets)>
CTL         =  <any ASCII control           ; (  0- 37,  0.- 31.)
                   character and DEL>          ; (    177,     127.)
CR          =  <ASCII CR, carriage return>  ; (     15,      13.)
LF          =  <ASCII LF, linefeed>         ; (     12,      10.)
SPACE       =  <ASCII SP, space>            ; (     40,      32.)
HTAB        =  <ASCII HT, horizontal-tab>   ; (     11,       9.)
CRLF        =  CR LF
WS          =  1*(SPACE / HTAB)

dqstring    =  <"> *(dqtext/quoted-pair) <">
dqtext      =  <any CHAR except <">, "\", and CTLs>
sqstring    =  <'> *(dqtext/quoted-pair) <'>
sqtext      =  <any CHAR except <'>, "\", and CTLs>
quoted-pair =  "\" CHAR

atom        =  1*<any CHAR except SPACE, CTLs, <'>, <">, and "\">
string      =  *<dqstring / sqstring / quoted-pair>
word        =  *<atom / string>
description =  *<word / WS>
text        =  *<word / WS>

command     = cmd-word *<WS cmd-param>
cmd-word    = atom
cmd-param   = database / strategy / word
database    = atom
strategy    = atom

capabilities =  ["<" msg-atom *("." msg-atom) ">"]
msg-atom     =  1*<any CHAR except SPACE, CTLs,
                                "<", ">", ".", and "\">
msg-id       =  "<" spec ">"            ; Unique message id
spec         =  local-part "@" domain
local-part   =  msg-atom *("." msg-atom)
domain       =  msg-atom *("." msg-atom)

