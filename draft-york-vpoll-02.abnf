statvalue-poll = "IN-PROCESS"
                            / "COMPLETED"  ; Poll has closed,
                                           ; nothing has been chosen yet
                            / "CONFIRMED"  ; Poll has closed and
                                           ;winning items confirmed
                            / "CANCELLED"

public-comment-param = "PUBLIC-COMMENT=" DQUOTE text DQUOTE

responseparam = "RESPONSE=" integer
             ; integer value 0..100

stayinformedparam = "STAY-INFORMED"  "=" ("TRUE" / "FALSE")
                       ; Default is FALSE

acceptresponse = "ACCEPT-RESPONSE" "="
                          acceptresponseparams ":"
                          iana-token ("," iana-token) CRLF

acceptresponseparams = *(";" other-param)

pollitemid = "POLL-ITEM-ID" pollitemdparams ":"
                        integer CRLF

pollitemidparams = *(
                         (";" responseparam) /
                         (";" stayinformedparam) /
                         (";" public-comment-param) /
                         (";" other-param)
                  )


POLL=ITEM-ID.
pollmode = "POLL-MODE" pollmodeparams ":"
                 ("BASIC" / iana-token / other-token) CRLF

pollmodeparams = *(";" other-param)


pollproperties = "POLL-PROPERTIES" pollpropparams ":"
                 text *("," text) CRLF

pollpropparams = *(";" other-param)


pollwinner = "POLL-WINNER" pollwinnerparams ":"
                       integer CRLF

pollwinnerparams = *(";" other-param)

voter = "VOTER" voterparams ":" cal-address CRLF

voterparam   = *(
                       ;
                       ; The following are OPTIONAL,
                       ; but MUST NOT occur more than once.
                       ;
                       (";" cutypeparam) / (";" memberparam) /
                       (";" roleparam) /
                       (";" rsvpparam) / (";" deltoparam) /
                       (";" delfromparam) / (";" sentbyparam) /
                       (";" cnparam) / (";" dirparam) /
                       (";" languageparam) /
                       (";" responseparam) /
                       (";" stayinformedparam) /

                       ;
                       ; The following are OPTIONAL, but MUST NOT occur
                       ; more than once. They are defined in RFC6638
                       ;
                       (";" scheduleagentparam) /
                       (";" scheduleforcesendparam) /
                       (";" schedulestatusparam) /

                       ;
                       ; The following is OPTIONAL,
                       ; and MAY occur more than once.
                       ;
                       (";" other-param)
                       ;
                 )


pollc    = "BEGIN" ":" "VPOLL" CRLF
                  pollprop
                  *eventc *todoc *journalc *freebusyc
                  *availabilityc *alarmc *iana-comp *x-comp
                  "END" ":" "VPOLL" CRLF

pollprop = *(
                ;
                ; The following are REQUIRED,
                ; but MUST NOT occur more than once.
                ;
                dtstamp / uid / organizer /
                ;
                ; The following are OPTIONAL,
                ; but MUST NOT occur more than once.
                ;
                acceptresponse / class / created / completed /
                description / dtstart / last-mod / pollmode /
                pollproperties / priority / seq / status /
                summary / url /
                ;
                ; Either 'dtend' or 'duration' MAY appear in
                ; a 'pollprop', but 'dtend' and 'duration'
                ; MUST NOT occur in the same 'pollprop'.
                ; 'duration' MUST only occur when 'dtstart'
                ; is present
                ;
                dtend / duration /
                ;
                ; The following are OPTIONAL,
                ; and MAY occur more than once.
                ;
                attach / categories / comment /
                contact / rstatus / related /
                resources / voter / x-prop / iana-prop
                ;
                ; The following is OPTIONAL, it SHOULD appear
                ; once for the confirmation of a BASIC mode
                ; VPOLL. Other modes may define differing
                ; requirements.
                ;
                pollwinner /
                ;
                )
RSVP=TRUE is used by the organizer to force the voter to reset
end="20121205T000000Z"/>
