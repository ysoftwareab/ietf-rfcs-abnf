body-extension  =/ number64

body-fld-lines  = number64

body-fld-octets = number64

capability =/ "APPENDLIMIT" ["=" number64]
               ;; capability is defined in RFC 3501.
               ;; APPENDLIMIT capability is defined in RFC 7889.
fetch-att       =/ "BODY" section [partial] /
                     "BODY.PEEK" section [partial] /
                    ; When BINARY extension is supported:
                     "BINARY" [".PEEK"] section-binary [partial]

literal         = "{" number64 ["+"] "}" CRLF *CHAR8
                    ; number64 represents the number of CHAR8s.
                    ; NOTE: "+" can only present when LITERAL+/LITERAL-
                    ; is also advertised

literal8        = "~{" number64 ["+"] "}" CRLF *OCTET
                    ;; Updating RFC 4466 version.
                    ;; A string that might contain NULs.
                    ;; <number> represents the number of OCTETs
                    ;; in the response string.
                    ;; The "+" is only allowed when both LITERAL+/LITERAL-
                    ;; and BINARY extensions are supported by the server
                    ;; [RFC7888]

msg-att-static  =/ "RFC822.SIZE" SP number64

number64    = 1*DIGIT
                ; Unsigned 63-bit integer
                ; (0 <= n <= 9,223,372,036,854,775,807)

nz-number64 = digit-nz *DIGIT
                ; Unsigned 63-bit integer
                ; (0 < n <= 9,223,372,036,854,775,807)

partial-range  = number64 ["." nz-number64]
                    ; Updates definition from RFC 5092 (IMAP URL).
                    ; This also affect URL-PARTIAL (RFC 5550).

partial        = "<" number64 "." nz-number64 ">"
                    ; Partial FETCH request. 0-based offset of
                    ; the first octet, followed by the number of octets
                    ; in the fragment.


quota_resource = atom SP resource-usage SP resource-limit
                    ; Updates definition in RFC 2087.

setquota_resource = atom SP resource-limit
                    ; Updates definition in RFC 2087.

resource-limit = number64

resource-usage = number64
search-key      =/ "LARGER" SP number64 / "SMALLER" SP number64

status-att-val =/ "APPENDLIMIT" SP (number64 / nil)
               ;; status-att-val is defined in RFC 4466
               ;; APPENDLIMIT status data item is defined in RFC 7889.


CHAR8   = <defined in RFC 3501>

