uvml                   = ballot / query
ballot                 = [T] 1*(vote [T]) [signature] [T]
vote                   = SLASH ( opinion / choice )
query                  = Q [timeframe] [sample] SLASH votes
opinion                = topic [ undecided ] value
topic                  = specific / universal
specific               = name
name                   = *9(subgroup-name PERIOD) target-name
universal              = SLASH
undecided              = QUESTION
value                  = quality / importance / outlook
value                  =/ support-opposition / likelihood
choice                 = contest selections
contest                = name
selections             = *HWS SLASH first
                          [*HWS SLASH second]
                          [*HWS SLASH third]
                          [*HWS SLASH fourth]
                          [*HWS SLASH fifth]
                          [*HWS SLASH sixth]
                          [*HWS SLASH seventh]
                          [*HWS SLASH eighth]
                          [*HWS SLASH ninth]
                          [*HWS SLASH tenth]
first                  = selection
second                 = selection
third                  = selection
fourth                 = selection
fifth                  = selection
sixth                  = selection
seventh                = selection
eighth                 = selection
ninth                  = selection
tenth                  = selection
selection              = opinion
signature              = 3SLASH profile 3SLASH
profile                = [ birth-year *SEP] gender [ location]
birth-year             = 4DIGIT
gender                 = iso-5218-gender
location               = *SEP [ country *SEP] region-code
country                = iso-3166-country
region-code            = 2*9LETTERDIGIT
votes                  = (opinions / choices)
timeframe              = count granularity
sample                 = HASH count
count                  = 1*2DIGIT
granularity            = YEAR / QUARTER / MONTH / WEEK / DAY
opinions               = opinion [voters]
voters                 = population
population             = 3SLASH [age-range *SEP] [ gender *SEP]
                          [location] 3SLASH
age-range              = 1*3DIGIT SEP 1*3DIGIT
choices                = contest [range] [voters]
range                  = HASH [ first-rank HYPHEN] last-rank
first-rank             = count
last-rank              = count
target-name            = valid-name
subgroup-name          = valid-name
valid-name             = NAME-EDGE *60NAME-INNER NAME-EDGE
NAME-EDGE              = LETTERDIGIT / UPPER-ASCII / UNICODE
NAME-INNER             = NAME-EDGE / WSP / AMPERSAND / APOSTROPHE
NAME-INNER             =/ HYPHEN / UNDERSCORE
quality                = none-better / very-good / good / fair
quality                =/ poor / very-poor / none-worse
importance             = life-and-death / very-important / important
importance             =/ unimportant / irrelevant
outlook                = never-more-optimistic / very-optimistic /
                          optimistic
outlook                =/ pessimistic / very-pessimistic /
                          never-more-pessimistic
support-opposition     = strongly-support / support /
                          somewhat-support
support-opposition     =/ somewhat-oppose / oppose / strongly-oppose
likelihood             = almost-certain / very-likely / likely
likelihood             =/ unlikely / very-unlikely / unimaginable
none-better            = 5*10STAR
very-good              = 4STAR
good                   = 3STAR
fair                   = 2STAR
poor                   = 1STAR
very-poor              = STAR MINUS
none-worse             = STAR 2*9MINUS
life-and-death         = 3*10BANG
very-important         = 2BANG
important              = BANG
unimportant            = BANG MINUS
irrelevant             = BANG 2*9MINUS
never-more-optimistic  = 3*10CURRENCY
very-optimistic        = 2CURRENCY
optimistic             = CURRENCY
pessimistic            = CURRENCY MINUS
very-pessimistic       = CURRENCY 2MINUS
never-more-pessimistic = CURRENCY 3*9MINUS
CURRENCY               = DOLLAR / EURO / POUND / YUAN-YEN
strongly-support       = 3*10PLUS
support                = 2PLUS
somewhat-support       = PLUS
somewhat-oppose        = MINUS
oppose                 = 2MINUS
strongly-oppose        = 3*10MINUS
almost-certain         = 3*10PERCENT
very-likely            = 2PERCENT
likely                 = PERCENT
unlikely               = PERCENT MINUS
very-unlikely          = PERCENT 2MINUS
unimaginable           = PERCENT 3*9MINUS
iso-5218-gender        = MALE / FEMALE
iso-3166-country       = 2LETTER
iso-3166-state         = 1*3LETTERDIGIT
MALE                   = M
FEMALE                 = F
QUESTION               = %x3F
STAR                   = %x2A
PLUS                   = %x2B
MINUS                  = %x2D
BANG                   = %x21
PERCENT                = %x25
DOLLAR                 = %x24
EURO                   = %x80
POUND                  = %xA3
YUAN-YEN               = %xA5
YEAR                   = Y
QUARTER                = Q
MONTH                  = M
WEEK                   = W
DAY                    = D
Q                      = "Q" / "q"
P                      = "P" / "p"
M                      = "M" / "m"
F                      = "F" / "f"
Y                      = "Y" / "y"
W                      = "W" / "w"
D                      = "D" / "d"
LETTERDIGIT            = LETTER / DIGIT
LETTER                 = "A" / "B" / "C" / "D" / "E" / "F" / "G" /
                          "H" / "I" / "J"
LETTER                 =/ "K" / "L" / "M" / "N" / "O" / "P" / "Q" /
                          "R" / "S" / "T"
LETTER                 =/ "U" / "V" / "W" / "X" / "Y" / "Z"
LETTER                 =/ "a" / "b" / "c" / "d" / "e" / "f" / "g" /
                          "h" / "i" / "j"
LETTER                 =/ "k" / "l" / "m" / "n" / "o" / "p" / "q" /
                          "r" / "s" / "t"
LETTER                 =/ "u" / "v" / "w" / "x" / "y" / "z"
UPPER-ASCII            = %xC0-FF
UNICODE                = %x100-10FFFF
SEP                    = HWS / HYPHEN
EQUAL                  = %x3D
AMPERSAND              = %x26
APOSTROPHE             = %x27
HYPHEN                 = %x2D
HASH                   = %x23
UNDERSCORE             = %x5F
PERIOD                 = %x2E
SLASH                  = %x2F

                    Table 1: UVML Syntax Specification

