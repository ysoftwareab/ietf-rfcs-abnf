unstructured    =  1*( [FWS] utext ) [FWS]

fields          =/ *( newsgroups /
                         path /
                         injection-date /
                         followup-to /
                         expires /
                         control /
                         supersedes /
                         distribution /
                         summary /
                         approved /
                         organization /
                         xref /
                         archive /
                         user-agent /
                         injection-info )

from            =  "From:" SP mailbox-list CRLF


orig-date       =  "Date:" SP date-time CRLF
zone            =  (( "+" / "-" ) 4DIGIT) / "UT" / "GMT"

message-id      =  "Message-ID:" SP msg-id CRLF

msg-id          =  [FWS] msg-id-core [FWS]

msg-id-core     =  "<" id-left "@" id-right ">"
                      ; maximum length is 250 octets

id-left         =  dot-atom-text / no-fold-quote

id-right        =  dot-atom-text / no-fold-literal

no-fold-quote   =  DQUOTE
                         *( mqtext / "\\" / "\" DQUOTE )
                         mqspecial
                         *( mqtext / "\\" / "\" DQUOTE )
                         DQUOTE

mqtext          =  NO-WS-CTL /      ; all of <text> except
                      %d33 /           ; SP, HTAB, "\", ">"
                      %d35-61 /        ; and DQUOTE
                      %d63-91 /
                      %d93-126

mqspecial       =  "(" / ")" /      ; same as specials except
                      "<" /            ; "\" and DQUOTE quoted
                      "[" / "]" /      ; and ">" omitted
                      ":" / ";" /
                      "@" / "\\" /
                      "," / "." /
                      "\" DQUOTE

no-fold-literal =  "[" *( mdtext / "\[" / "\]" / "\\" ) "]"

mdtext          =  NO-WS-CTL /      ; Non white space controls
                      %d33-61 /        ; The rest of the US-ASCII
                      %d63-90 /        ; characters not including
                      %d94-126         ; ">", "[", "]", or "\"

subject         =  "Subject:" SP unstructured CRLF


newsgroups      =  "Newsgroups:" SP newsgroup-list CRLF

newsgroup-list  =  [FWS] newsgroup-name
                      *( [FWS] "," [FWS] newsgroup-name ) [FWS]

newsgroup-name  =  component *( "." component )

component       =  1*component-char

component-char  =  ALPHA / DIGIT / "+" / "-" / "_"
      NOTE: Observe that the syntax does not allow comments within the
      Newsgroups header; this is to simplify processing by relaying and
      serving agents which have a requirement to process this header
      extremely rapidly.

path            =  "Path:" SP path-list CRLF

path-list       =  [FWS]
                      *( path-identity [FWS] path-delimiter [FWS] )
                      tail-entry [FWS]

path-identity   =  ( ALPHA / DIGIT )
                      *( ALPHA / DIGIT / "-" / "." / ":" / "_" )

tail-entry      =  path-identity

path-delimiter  =  "!" ; possible other delimiters TBD

injection-date  =  "Injection-Date:" SP date-time CRLF
sender          =  "Sender:" SP mailbox CRLF

reply-to        =  "Reply-To:" SP address-list CRLF

comments        =  "Comments:" SP unstructured CRLF

keywords        =  "Keywords:" SP phrase *("," phrase) CRLF

references      =  "References:" SP 1*( [CFWS] msg-id-core) [CFWS]
                      CRLF


followup-to     =  "Followup-To:" SP ( newsgroup-list / poster-text )
                      CRLF

poster-text     =  [FWS] %d112.111.115.116.101.114 [FWS]
                      ; "poster" in lower-case

expires         =  "Expires:" SP date-time CRLF

control         =  "Control:" SP [FWS] control-message [FWS] CRLF

control-message =  verb *( [FWS] argument )

verb            =  token

argument        =  value

supersedes      =  "Supersedes:" SP [CFWS] msg-id-core [CFWS] CRLF

distribution    =  "Distribution:" SP distribution-list CRLF

dist-list       =  [FWS] dist-name *( "," [FWS] dist-name ) [FWS]

dist-name       =  ALPHA / DIGIT
                      *( ALPHA / DIGIT / "+" / "-" / "_" )

summary         =  "Summary:" SP unstructured CRLF


approved        =  "Approved:" SP mailbox-list CRLF

organization    =  "Organization:" SP unstructured CRLF

xref            =  "Xref:" SP [CFWS] server-name
                      1*( CFWS location ) [CFWS] CRLF

server-name     =  path-identity

location        =  newsgroup-name ":" article-locator

article-locator =  1*( %x21-27 / %x29-3A / %x3C-7E )
                      ; US-ASCII printable characters
                      ; except '(' and ';'

archive         =  "Archive:" SP [CFWS] ("no" / "yes")
                      *( [CFWS] ";" archive-param ) CRLF

archive-param   =  "filename=" value

user-agent      =  "User-Agent:" SP 1*product CRLF

product         =  [CFWS] token [CFWS] [ "/" product-version ]

product-version =  [CFWS] token [CFWS]

injection-info  =  "Injection-Info:" SP [CFWS] path-identity [CFWS]
                      *( ";" inj-info-para ) CRLF

inj-info-param  =  post-host-param /
                      post-account-param /
                      sender-param /
                      logging-param

post-host-param =  "posting-host=" host-value

host-value      =  dot-atom / [ dot-atom ":" ]
                      ( IPv4address / IPv6address ) ;  see [RFC 2373]

post-acct-param =  "posting-account=" value

sender-param    =  "sender=" sender-value

sender-value    =  mailbox / "verified"

logging-param   =  "logging-data=" value
lines           =  "Lines" ":" SP [CFWS] 1*DIGIT [CFWS] CRLF

