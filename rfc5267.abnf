capability          =/ "CONTEXT=SEARCH" / "CONTEXT=SORT" / "ESORT"
       ;; <capability> from [IMAP]

command-select      =/ "CANCELUPDATE" 1*(SP quoted)
       ;; <command-select> from [IMAP]

context-position      = number
       ;; Context position may be 0 for SEARCH result additions.
       ;; <number> from [IMAP]

modifier-context    = "CONTEXT"

modifier-partial    = "PARTIAL" SP partial-range

partial-range       = nz-number ":" nz-number
       ;; A range 500:400 is the same as 400:500.
       ;; This is similar to <seq-range> from [IMAP],
       ;; but cannot contain "*".

modifier-update     = "UPDATE"

search-return-opt   =/ modifier-context / modifier-partial /
                          modifier-update
       ;; All conform to <search-return-opt>, from [IMAP-ABNF]

resp-text-code      =/ "NOUPDATE" SP quoted
       ;; <resp-text-code> from [IMAP]

ret-data-addto      = "ADDTO"
                          SP "(" context-position SP sequence-set
                          *(SP context-position SP sequence-set)
                          ")"
       ;; <sequence-set> from [IMAP]

ret-data-partial    = "PARTIAL"
                         SP "(" partial-range SP partial-results ")"
       ;; <partial-range> is the requested range.

partial-results     = sequence-set / "NIL"
       ;; <sequence-set> from [IMAP]
       ;; NIL indicates no results correspond to the requested range.
ret-data-removefrom = "REMOVEFROM"
                          SP "(" context-position SP sequence-set
                          *(SP context-position SP sequence-set)
                          ")"
       ;; <sequence-set> from [IMAP]

search-return-data  =/ ret-data-partial / ret-data-addto /
                          ret-data-removefrom
       ;; All conform to <search-return-data>, from [IMAP-ABNF]

sort                =/ extended-sort
       ;; <sort> from [SORT]

extended-sort       = ["UID" SP] "SORT" search-return-opts
                         SP sort-criteria SP search-criteria
       ;; <search-return-opts> from [IMAP-ABNF]
       ;; <sort-criteria> and <search-criteria> from [SORT]

