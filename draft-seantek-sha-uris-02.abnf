sha1uri   = "sha1:" ( 40HEXDIG / 40*relaxed ) [ length ]

sha256uri = "sha256:" ( 64HEXDIG / 64*relaxed ) [ length ]

relaxed   = HEXDIG / ":" / "." / "~" / "-" / "_" /
                     "%20" / "%0D" / "%0A" / "%09"

length    = ";" wholenum [ bits ]

wholenum  = "0" / ( %x31-39 *DIGIT )

bits      = "b" ( %x31-37 )   ; 1 - 7

sha1msguri   = "sha1msg:" ( 40HEXDIG / 40*relaxed )
                                   [ msglength ] [ "/" content-id ]

sha256msguri = "sha256msg:" ( 64HEXDIG / 64*relaxed )
                                     [ msglength ] [ "/" content-id ]

msglength    = ";" wholenum

content-id   = id-left "@" id-right

id-left = pchar-dot-atom-text

pchar-dot-atom-text = 1*pchar-atext *("." 1*pchar-atext)

pchar-atext = ALPHA / DIGIT / "!" / "$" / "&" / "'" /
                       "*" / "+" / "-" / "/" / "=" / "_" / "~" /
                       pct-encoded

id-right = pchar-dot-atom-text / pchar-no-fold-literal

pchar-no-fold-literal = unreserved / sub-delims / ":" /
                                 pct-encoded

                   Figure 2: ABNF of SHA Message URIs



 




