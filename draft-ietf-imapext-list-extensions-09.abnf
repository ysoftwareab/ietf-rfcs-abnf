child-mbox-flag    = "\HasChildren" / "\HasNoChildren"
                        ; attributes for CHILDREN return option, at most one
                        ; possible per LIST response

list               = "LIST" [SP list-select-opts] SP mailbox SP mbox_or_pat
                        [SP list-return-opts]

list-select-opts   = "(" [list-select-option *(SP list-select-option)] ")"
                        ; list selection options, e.g. REMOTE

list-return-opts   = "RETURN" SP "(" [return-option *(SP return-option)] ")"
                        ; list return options, e.g. CHILDREN

mparent-mbox-flag  = "\PlaceHolder" / "\HasSubmailboxes"
                        ; attributes for MATCHPARENT selection option,
                        ; at most one possible per LIST response

mailbox-list       = "(" [mbx-list-flags] ")" SP
                        (DQUOTE QUOTED-CHAR DQUOTE / nil) SP mailbox
                        [SP mbox-list-extended]

mbox-list-extended = "(" [mbox-list-extended-item
                        *(SP mbox-list-extended-item)] ")"

mbox-list-extended-item = "(" mbox-list-extended-item-data ")"

mbox-list-extended-item-data =  mbox-list-extended-item-tag SP nstring-list

mbox-list-extended-item-tag  = astring
                        ; The content MUST conform to either "eitem-vendor-tag" or
                        ; "eitem-standard-tag" ABNF productions.
                        ; A tag registration template is described in section
                        ; 8.5 of this document.

mbox_or_pat        = list-mailbox / patterns

patterns           = "(" list-mailbox *(list-mailbox) ")"

eitem-vendor-tag   = vendor-tag
                        ; a vendor specific tag for extended list data

eitem-standard-tag = atom
                        ; a tag for extended list data defined in a Standard
                        ; Track or Experimental RFC.

nstring-list       = nstring /
                        "(" [nstring-list *(SP nstring-list)] ")"
                        ;; a recursive list definition

mbox-list-oflag    = child-mbox-flag / mparent-mbox-flag / "\NonExistent" / 
                        / "\Subscribed" / "\Remote"

list-select-option = "SUBSCRIBED" / "REMOTE" / "MATCHPARENT" /
                        option-extension
                        ; An option registration template is described in
                        ; section 8.3 of this document.

return-option      = "SUBSCRIBED" / "CHILDREN" / 
                        option-extension

option-extension   = option-vendor-tag / option-standard-tag

option-vendor-tag  = vendor-tag
                        ; a vendor specific option

option-standard-tag= atom
                        ; an option defined in a Standard Track or
                        ; Experimental RFC

vendor-tag         = vendor-token "-" atom


