presence-id	= word-presence ":" local-part "@" domain
im-id		= word-im ":" local-part "@" domain
local-part	= dotted-string / quoted-string
domain		= 1*domain-label *("." 1*domain-label)
domain-label	= (ALPHA / DIGIT)
			  [*(ALPHA / DIGIT / "-") (ALPHA / DIGIT)]
dotted-string	= 1*atext *("." 1*atext)
quoted-string   = DQUOTE *qcontent DQUOTE
qcontent	= qtext / quoted-pair
quoted-pair     = "\" (%x1-9 / %xB-C / %xE-7E)
qtext		= %x1-8 / %xB-C / %xD-1F / %x20-21 / %x23-5B
			/ %x5D-7E
atext		= "!" / "#" / "$" / "%" / "&" / "'" / "*"
			/ "+" / "-" / "/" / "=" / "?" /"^" / "_"
			/ "`" / "{" / "|" / "}" / "~" / ALPHA / DIGIT
			/ %x1-8 / %xB-C / %xD-1F / %x7F
scheme		= presence-scheme / im-scheme
word-presence	= %x70.72.65.73.65.6E.63.65
word-im		= %x69.6D

pmessage	= mech / nop / command / response
mech		= "=" word-mech *(" " mechname) CRLF
mechname	= 1*20(ALPHA / DIGIT / "-" / "_")
nop		= "=" word-nop CRLF
command		= ">" id 1*WSP method content
response	= "<" id 1*WSP status *WSP "(" method ")"
			  content
content		= CRLF *header CRLF [payload]
header		= header-name ":" [header-value] CRLF
header-value	= *([fws] printable) *WSP
payload		= *OCTET

fws		= [*WSP CRLF] 1*WSP
id		= 1*(ALPHA / DIGIT)
method		= 1*(ALPHA / DIGIT)
status		= word-ok / word-error
header-name	= 1*(%x21-39 / %x3B-7E) ; ASCII, no colons
printable	= %x21-7E / %x80-D7FF / %xE000-FFFD
			  / %x10000-310FFFF
word-ok		= %x6F.6B
word-error	= %x6E.72.72.6F.72
word-nop	= %x6E.6F.70
word-mech	= %x6D.65.63.38

url-hdr		= [fws] url *WSP

origin-hdr	= [fws] (domain / url) *WSP

idpattern-hdr	= [fws] ("*" / our-ptrn / generic-ptrn)
			  *WSP
our-ptrn	= (word-presence / word-im) ":"
			  ("*" / "*@" ["*."] domain-part /
			   local-part "@" domain-part)
generic-ptrn	= scheme ":" ("*" / scheme-specific-part)

astrength-hdr	= [fws] astrength *WSP
astrength	= %x73.74.72.6F.6E.67		; "strong"
			/ %x6D.65.64.69.75.6D		; "medium"
			/ %x77.65.61.6B			; "weak"
			/ %x6E.6F.6E.65			; "none"

mapping-hdr	= [fws] positive-digit *DIGIT *WSP
positive-digit	= %x31-39			; 1-9

mechanism-hdr	= [fws] mechname *WSP

subscription-hdr= [fws] connection-id [fws] "/" [fws] url *WSP
connection-id	= *(ALPHA / DIGIT)

visited-hdr	= *([fws] domain) *WSP

