header = "Authentication-Results:" [CFWS] authserv-id
              [CFWS] [version] *(";" methodspec *propspec [CFWS])


authserv-id = dot-atom-text
                 ; see below for a description of this element;
                 ; "dot-atom-text" is defined in section 3.2.4 of [MAIL]


version = "version=" 1*DIGIT [CFWS]
             ; indicates which version of this specification is in use;
             ; this draft is version "1"


methodspec = [CFWS] method [CFWS] "=" [CFWS] result
                ; indicates which authentication method was evaluated
propspec = [CFWS] ptype [CFWS] "." [CFWS] property [CFWS] "=" value
              ; an indication of which properties of the message
              ; were evaluated by the authentication scheme being
              ; applied to yield the reported result


method = token [ "/" version ]
            ; a method indicates which method's result is
            ; represented by "value", and is one of the methods
            ; explicitly defined as valid in this document
            ; or is an extension method as defined below


version = 1*( ALPHA / DIGIT ) 0*( "." 1*( ALPHA / DIGIT ) )
             ; indicates which version of the method was applied


result = "pass" / "hardfail" / "softfail" / "neutral" /
              "temperror" / "permerror"
            ; an indication of the results of the attempt to
            ; authenticate the message


ptype = "smtp" / "header" / "body" / "policy"
           ; indicates whether the property being evaluated was
           ; a parameter to an [SMTP] command, or was a value taken
           ; from a message header field, or was some property of
           ; the message body, or some other property evaluated by
           ; the receiving MTA


property = token
             ; if "ptype" is "smtp", this indicates which [SMTP]
             ; command provided the value which was evaluated by the
             ; authentication scheme being applied; if "ptype" is
             ; "header", this indicates from which header field the
             ; value being evaluated was extracted; if "ptype" is
             ; "body", this indicates the offset into the body at which
             ; content of interest was detected; if "ptype" is "policy"
             ; then this indicates the name of the policy which caused
             ; this header field to be added (see below)


value = [CFWS] token [CFWS] / mailbox
           ; the value extracted from the message property defined
           ; by the "ptype.property" construction; if the value
           ; identifies a mailbox, then it is a "mailbox"
           ; as defined in section 3.4 of [MAIL];
           ; "mailbox" allows CFWS

foo=pass bar.baz=blob (2 of 3 tests OK)
spf=pass smtp.mail=sender@example.com
auth=pass (cram-md5) smtp.mail=sender@example.com;
spf=pass smtp.mail=sender@example.com
sender-id=pass header.from=sender@example.com
sender-id=hardfail header.from=sender@example.com;
dkim=pass (good signature) header.i=sender@example.com
auth=pass (cram-md5) smtp.mail=sender@example.com;
spf=hardfail smtp.mail=sender@example.com
t=1188964191; c=simple/simple;
h=From:Date:To:Message-Id:Subject;
bh=sEuZGD/pSr7ANysbY3jtdaQ3Xv9xPQtS0m70;
b=EToRSuvUfQVP3Bkz ... rTB0t0gYnBVCM=
dkim=pass (good signature) header.i=@mail-router.example.net;
dkim=hardfail (bad signature) header.i=@newyork.example.com
d=mail-router.example.net; t=1188964198; c=relaxed/simple;
h=From:Date:To:Message-Id:Subject:Authentication-Results;
bh=ftA9J6GtX8OpwUECzHnCkRzKw1uk6FNiLfJl5Nmv49E=;
b=oINEO8hgn/gnunsg ... 9n9ODSNFSDij3=
dkim=pass (good signature) header.i=@newyork.example.com
t=1188964191; c=simple/simple;
h=From:Date:To:Message-Id:Subject;
bh=sEu28nfs9fuZGD/pSr7ANysbY3jtdaQ3Xv9xPQtS0m7=;
b=EToRSuvUfQVP3Bkz ... rTB0t0gYnBVCM=
