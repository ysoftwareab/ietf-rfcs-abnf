document = [*element]

element = link / form / directive

directive = base-directive

base-directive = [1, baseURI]

baseURI = CoRI

CoRI = <Defined in Section XX of RFC XXXX>

IRI = CoRI

link = [2, relation-type, link-target, ?[*element]]

relation-type = text

link-target = IRI / literal / null

literal = bool / int / float / time / bytes / text

form = [3, operation-type, submission-target, ?[*form-field]]

operation-type = text

submission-target = IRI

form-field = (form-field-type, form-field-value, ?[*element])

form-field-type = text

form-field-value = IRI / literal / null

token = identifier / IRIref / boolean / integer / float

token =/ datetime / bytes / text / null / punctuator

identifier = START *CONTINUE *(MEDIAL 1*CONTINUE)

START = <Any character with the XID_Start property>

CONTINUE = <Any character with the XID_Continue property>

MEDIAL = "-" / "." / "~" / %x58A / %xF0B

MEDIAL =/ %x2010 / %x2027 / %x30A0 / %x30FB

IRIref = "<" IRI-reference ">"

IRI-reference = <Defined in Section 2.2 of RFC 3987>

boolean = "true" / "false"

integer = ["+" / "-"] (decimal / binary / octal / hexadecimal)

decimal = 1*DIGIT

binary = %x30 (%x42 / %x62) 1*BINDIG

octal = %x30 (%x4F / %x6F) 1*OCTDIG

hexadecimal = %x30 (%x58 / %x78) 1*HEXDIG

DIGIT = %x30-39

BINDIG = %x30-31

OCTDIG = %x30-37

HEXDIG = %x30-39 / %x41-46 / %x61-66

float = ["+" / "-"] 1*DIGIT [fraction] [exponent]

fraction = "." 1*DIGIT

exponent = (%x45 / %x65) ["+" / "-"] 1*DIGIT

float =/ "NaN"

float =/ ["+" / "-"] "Infinity"

datetime = %x64.74 SQUOTE date-time SQUOTE

date-time = <Defined in Section 5.6 of RFC 3339>

SQUOTE = %x27

bytes = base16 / base32 / base64

base16 = (%x68 / %x62.31.36) SQUOTE <Base16 encoded data> SQUOTE

base32 = %x62.33.32 SQUOTE <Base32 encoded data> SQUOTE

base64 = %x62.36.34 SQUOTE <Base64 encoded data> SQUOTE

text = DQUOTE *(char / %x5C escape) DQUOTE

char = <Any character except DQUOTE, %x5C, and line terminators>

escape = simple-escape / hexadecimal-escape / unicode-escape

simple-escape = %x30 / %x62 / %x74 / %x6E / %x76

simple-escape =/ %x66 / %x72 / %x22 / %x27 / %x5C

hexadecimal-escape = (%x78 / %x58) 2HEXDIG

unicode-escape = %x75 4HEXDIG / %x55 8HEXDIG

DQUOTE = %x22

null = "null" / "_"

punctuator = "#" / ":" / "=" / "@" / "[" / "]" / "{" / "}" / "->"

document = *element

element = link / form / directive

directive = base-directive / using-directive

base-directive = "#" "base" baseIRI

baseIRI = IRIref

using-directive = "#" "using" [identifier "="] IRIref

IRI = IRIref / simple-name / qualified-name / predefined-name

simple-name = identifier

qualified-name = identifier ":" identifier

predefined-name = "@" identifier

link = relation-type link-target ["{" *element "}"]

relation-type = IRI

link-target = IRI / literal / null

literal = boolean / integer / float / datetime / bytes / text

form = operation-type "->" submission-target ["[" *form-field "]"]

operation-type = IRI

submission-target = IRI

form-field = form-field-type form-field-value ["{" *element "}"]

form-field-type = IRI

form-field-value = IRI / literal / null

CoRAL-Document = document

CoRAL-Link = link

CoRAL-Form = form

