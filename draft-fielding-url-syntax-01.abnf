alpha    = lowalpha | hialpha

lowalpha = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" |
                 "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" |
                 "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"

hialpha  = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" |
                 "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" |
                 "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

digit    = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" |
                 "8" | "9"

alphanum = alpha | digit

urlchar     = reserved | unreserved | escaped

reserved    = ";" | "/" | "?" | ":" | "@" | "&" | "=" | "+"

unreserved  = alpha | digit | mark

mark        = "$" | "-" | "_" | "." | "!" | "~" |
                    "*" | "'" | "(" | ")" | ","

escaped     = "%" hex hex
hex         = digit | "A" | "B" | "C" | "D" | "E" | "F" |
                            "a" | "b" | "c" | "d" | "e" | "f"

excluded    = control | space | delims | unwise | national

control     = <US-ASCII coded characters 00-1F and 7F hexadecimal>

space       = <US-ASCII coded character 20 hexadecimal>

delims      = "<" | ">" | "#" | "%" | <">
   
unwise      = "{" | "}" | "|" | "\" | "^" | "[" | "]" | "`"

national    = <Any character not in the reserved, unreserved,
                     control, space, delims, or unwise sets>

URL-reference  =  [ absoluteURL | relativeURL ] [ "#" fragment ]

fragment  =  *urlchar

scheme  =  1*( alpha | digit | "+" | "-" | "." )

absoluteURL = generic-URL | opaque-URL

opaque-URL  = scheme ":" *urlchar

generic-URL = scheme ":" relativeURL

relativeURL = net_path | abs_path | rel_path

net_path    = "//" server [ abs_path ]
abs_path    = "/"  rel_path
rel_path    = [ path ] [ "?" query ]

server         = [ [ user [ ":" password ] "@" ] hostport ]

user           = *[ unreserved | escaped |
                          ";" | "?" | "&" | "=" | "+" ]

password       = *[ unreserved | escaped |
                          ";" | "?" | "&" | "=" | "+" ]

hostport     = host [ ":" port ]
host         = hostname | hostnumber
hostname     = *( domainlabel "." ) toplabel
domainlabel  = alphanum | alphanum *( alphanum | "-" ) alphanum
toplabel     = alpha | alpha *( alphanum | "-" ) alphanum
hostnumber   = 1*digit "." 1*digit "." 1*digit "." 1*digit
port         = *digit

path        = segment *( "/" segment )
segment     = *pchar *( ";" param )
param       = *pchar

pchar       = unreserved | escaped | ":" | "@" | "&" | "=" | "+"

query       = *urlchar

Content-Base      = "Content-Base" ":" absoluteURL

Content-Location  = "Content-Location" ":"
                          ( absoluteURL | relativeURL )

type=d> but you can probably pick it up from <ftp://ds.in
g             =  http://a/b/c/g
