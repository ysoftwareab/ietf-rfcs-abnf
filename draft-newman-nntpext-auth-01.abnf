authinfo-capability   = "AUTHINFO" [SP "USER"] CRLF
sasl-capability       = "SASL" 1*(SP sasl-mech) CRLF
authinfo-user-command = "AUTHINFO" 1*WSP "USER" 1*WSP username
                             *WSP CRLF
authinfo-pass-command = "AUTHINFO" 1*WSP "PASS" 1*WSP password
                             *WSP CRLF
authinfo-sasl-command = "AUTHINFO" 1*WSP "SASL" 1*WSP sasl-mech
                             [1*WSP base64 *WSP] *(CRLF base64) CRLF
                    ; client waits for server response after each CRLF
resp-sasl-data        = ("351" / "251") SP base64

sasl-mech             = 1*20mech-char
mech-char             = %x41-5A / DIGIT / "-" / "_"
                    ; mech names restricted to uppercase letters,
                    ; digits, "-" and "_"
username              = 1*UTF8-SAFE
password              = 1*PASS-DATA
PASS-DATA             = %x01-09 / %x0B-0C / %x0E-FF
UTF8-SAFE             = %x01-09 / %x0B-0C / %x0E-7F / UTF8-2 /
                             UTF8-3 / UTF8-4 / UTF8-5 / UTF8-6
UTF8-1                = %x80-BF
UTF8-2                = %xC0-DF UTF8-1
UTF8-3                = %xE0-EF 2UTF8-1
UTF8-4                = %xF0-F7 3UTF8-1
UTF8-5                = %xF8-FB 4UTF8-1
UTF8-6                = %xFC-FD 5UTF8-1
base64                = *(4base64-char) [base64-terminal]
base64-char           = ALPHA / DIGIT / "+" / "/"
                    ; case sensitive
base64-terminal       = (2base64-char "==") / (3base64-char "=")

