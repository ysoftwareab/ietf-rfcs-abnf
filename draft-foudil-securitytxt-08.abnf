body             =  signed / unsigned

signed           =  sign-header unsigned sign-footer

sign-header      =  < headers and line from section 7 of [RFC4880] >

sign-footer      =  < OpenPGP signature from section 7 of [RFC4880] >

unsigned         =  *line [can-field eol]
                       *line (contact-field eol)
                       *line [lang-field eol] *line
                       ; the order of elements is not important

line             =  (field / comment) eol

eol              =  *WSP [CR] LF

field            =  ack-field /
                       contact-field /
                       encryption-field /
                       hiring-field /
                       policy-field /
                       ext-field

fs               =  ":"

comment          =  "#" *(WSP / VCHAR / %x80-FFFFF)

ack-field        =  "Acknowledgments" fs SP uri

can-field        =  "Canonical" fs SP uri

contact-field    =  "Contact" fs SP uri

lang-tag         =  < Language-Tag from section 2.1 of [RFC5646] >
uri              =  < URI as per [RFC3986] >

encryption-field =  "Encryption" fs SP uri

hiring-field     =  "Hiring" fs SP uri

policy-field     =  "Policy" fs SP uri

lang-field       =  "Preferred-Languages" fs SP lang-values

lang-values      =  lang-tag *(*WSP "," *WSP lang-tag)

ext-field        =  field-name fs SP unstructured

field-name       =  < imported from section 3.6.8 of [RFC5322] >

unstructured     =  < imported from section 3.2.5 of [RFC5322] >

