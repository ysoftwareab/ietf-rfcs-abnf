till=31.8.2000"

TID = structured-tid | unstructured-tid

structured-tid = [character-set ":"] "urn" ":" namespace ":"
                 1*([key "="] match ",")
                     ; <character-set> is encoded with UTF-8, while the
                     ; others are encoded with the character set
                     ; indicated by character-set, or with the default
                     ; character set UTF-8 if character set is not
                     ; indicated;
                     ; The "," MUST be omitted from the end of
                     ; <structured-tid>

character-set = "UTF-8" | "UTF-16" | "UTF-32" | "ISO-8859-1" |
                "ISO-8859-2" | "ISO-8859-5" | "ISO-8859-7" |"GB2312" |
                "Big5"| cset-name

cset-name = 1*(alpha | digit | "-" | ".")   ; any UTF-8 coded national
                                            ; or international standard
                                            ; character-set name

alpha = <any UTF-8 alphabetic character> ; (a-z, A-Z)

digit = <any UTF-8 decimal digit>        ; (0-9)

namespace = 1*<any character-set character, excluding reserved>

reserved = ";" | ":" | "=" | ","    ; coded according to character-set

key = 1*<any character-set character, excluding reserved>

match = 1*<any character-set character, excluding reserved>

unstructured-tid = [character-set ":"] "uri" ":" namespace ":" NSS
                               ; NSS is defined in RFC2141;   
                               ; <character-set> is encoded with UTF-8,
                               ; while the others are encoded with the
                               ; character set indicated by
                               ; character-set, or with the default
                               ; character set UTF-8 if character set is
                               ; not indicated;

TAD = structured-tad | unstructured-tad

structured-tad = [character-set ":"] scheme ":" user-part "@" host
                 [":" ["transport="] transport-protocol]
                 [":" port ] [";" 1*([key "="] match ",")]
                            ; <character-set> ":" is encoded with UTF-8,
                            ; while the others are encoded with the
                            ; character set indicated by <character-
                            ; set>, or with the default character set
                            ; UTF-8 if character set is not indicated;
                            ; udp is the default transport protocol if
                            ; not indicated;
                            ; The "," MUST be omitted from the end of
                            ; <structured-tad>

scheme = "slop"

user-part = 1*([user-key "="] user-match ",")
                            ; the "," MUST be omitted from
                            ; the end of <user-part>

user-key = 1*<any character-set character, excluding reserved and "@">

user-match = 1*<any character-set character, excluding reserved and "@">

transport-protocol = "udp" | "tcp" | <any feasible transport protocol>

host = fqdn | ip4-address | ip6-address

port = 1*<any character-set digit>      ; (0-9)

fqdn = <any fully qualified domain name>
ip4-address = <any qualified IPv4 address>

ip6-address = "[" IPv6address "]"  ; IPv6address is defined in RFC2373,
                                   ; while coded with character-set

unstructured-tad = [character-set ":"] "uri" ":" scheme ":" opaque_part
                               ; opaque_part is defined in RFC2396,
                               ; while its character set is that
                               ; indicated by <character-set>;
                               ; <character-set> ":" is encoded with
                               ; UTF-8, while the others are encoded
                               ; with the character set indicated by
                               ; <character-set>, or with the default
                               ; character set UTF-8 if character set is
                               ; not indicated;


