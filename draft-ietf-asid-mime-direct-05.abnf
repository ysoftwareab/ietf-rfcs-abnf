profile = x-token / iana-token

x-token = <The two characters "X-" or "x-" followed,
                  with no intervening white space, by any atom,
                  where atom is from Section 3.3 of RFC 822>

iana-token = <a publicly-defined extension token, registered
                     with IANA, as specified in Section 9 of this
                     document>


contentline = [group.]type [";" [ws] parameterlist] ":" [ws] valuespec

group = atom     ; as defined in Section 3.3 of RFC 822

type = x-name
            / iana-type

x-name = <the two characters "X-" or "x-" followed, with no
                 intervening white space, by any atom>
iana-type = <a publicly-defined extension token, registered
                    with IANA, as specified in Section 11 of this
                    document>

ws = *(HTAB / SPACE)    ; ASCII decimal 9 and 32

parameterlist = parameter *(";" [ws] parameter)

parameter = encodingparm
                 / valuetypeparm
                 / languageparm
                 / contextparm
                 / [parmtype "="] parmvalues

encodingparm = "encoding" "=" encodingtype

encodingtype = "b"            ; from RFC 2047

valuetypeparm = "value" "=" valuetype

valuetype = "uri"                  ; generic uri from RFC 1738
                 / "text"
                 / "date"
                 / "time"
                 / "date-time"            ; date time
                 / "integer"
                 / "boolean"
                 / "float"
                 / x-name
                 / iana-valuetype

languageparm = "language" "=" language ; as defined in RFC 1766

contextparm = "context" "=" context

context = iana-token ; a token registered with IANA
               / x-name

parmtype = x-name
                / iana-parmtype

iana-parmtype = <a publicly-defined extension token, registered
                        with IANA, as defined in Section 12 of this
                        document>
parmvalues = parmvalue *("," parmvalue)

parmvalue = x-name
                 / iana-parmvalue

iana-parmvalue = <a publicly-defined extension token, registered
                         with IANA, as defined in Section 13 of this
                         document, conforming to the qtext or quoted-text
                         productions in section 3.3 of RFC 822>

valuespec =  text-spec
                 / date-spec
                 / time-spec
                 / date-time-spec
                 / boolean
                 / integer
                 / float
                 / iana-valuespec

text-spec = text *("," [ws] text)

text =  <bytes whose syntax depends on type and the
               the encoding parameter. If the value contains
               a CRLF sequence (ASCII 10 followed by 13), it must
               be encoded using either the "b" or \n methods.>

date-spec = date *("," [ws] date)

time-spec = time *("," [ws] time)

date-time-spec = date "T" time

boolean = "TRUE" / "FALSE"

integer = [sign] 1*digit *("," [ws] [sign] 1*digit)

float = [sign] 1*digit ["." *digit] *("," float)

sign = "+" / "-"

digit = "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"

date = date-fullyear ["-"] date-month ["-"] date-mday

date-fullyear = 4digit

date-month = 2digit     ;01-12
date-mday = 2digit      ;01-28, 01-29, 01-30, 01-31
                              ;based on month/year

time = time-hour [":"] time-minute [":"] time-second [time-secfrac]
              [time-zone]

time-hour = 2digit      ;00-23

time-minute = 2digit    ;00-59

time-second = 2digit    ;00-59

time-secfrac = "," 1*digit

time-zone = "Z" / time-numzome

time-numzome = sign time-hour [":"] time-minute

iana-valuespec = <a publicly-defined valuetype format, registered
                         with IANA, as defined in section 15 of this
                         document>

charset="iso-8859-1";
profile="vCard"
mOHZIKi4hlPXBOhcUQ==

boundary=woof;
type="text/directory";
start="<id5@host.com>"
name="myvoice.au";
site="myhost.com";
access-type=ANON-FTP;
directory="pub/myname";
mode="image"

