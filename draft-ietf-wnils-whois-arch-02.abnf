SEARCH=   {exact | lstring }                      LOCAL/GLOBAL

FORMAT=   {full | abridged | handle | summary }   GLOBAL

MAXHITS=  { 1-<max-allowed> }                     GLOBAL

SEARCH=       { regex | fuzzy | substring | <X-format> } LOCAL/GLOBAL

CASE=         { ignore | consider }                      LOCAL/GLOBAL

FORMAT=       { servers-to-ask | <X-format> }            GLOBAL

MAXFULL=      { 1-<max-allowed> }                        GLOBAL

AUTHENTICATE= password                                   GLOBAL

NAME=         <string>                                   GLOBAL

PASSWORD=     <string>                                   GLOBAL

INCHARSET=    { us-ascii | iso-8859-* }                  GLOBAL

LANGUAGE=     <As defined in ISO 639:1988>               GLOBAL

IGNORE=       {attributelist}                            GLOBAL

INCLUDE=      {attributelist}                            GLOBAL


author=chris and template=user

value=phone;search=substring

search-all=Peter ; search=substring;case=consider

whois-command   =   ( system-command [":" "hold"]
                              / terms [":" globalcnstrnts] ) NL

system-command  =   "constraints"
                              / "describe"
                              / "commands"
                              / "polled-by"
                              / "polled-for"
                              / "version"
                              / "list"
                              / "show" [1*SP string]
                              / "help" [1*SP string]
                              / "?" [string]

terms           =   and-expr *("or" and-expr)

and-expr        =   not-expr *("and" not-expr)

not-expr        =   ["not"] (term / ( "(" terms ")" ))

term            =   generalterm / specificterm
                              / shorthandle / combinedterm

generalterm     =   string *(";" localcnstrnt)

specificterm    =   specificname "=" string
                              *(";" localcnstrnt)

specificname    =   "handle" / "value"

shorthandle     =   "!" string *(";" localcnstrnt)

combinedterm    =   string "=" string *(";" localcnstrnt)

globalcnstrnts  =   globalcnstrnt *(";" globalcnstrnt)

globalcnstrnt   =   localcnstrnt
                              / "format" "=" format
                              / "maxfull" "=" 1*digit
                              / "maxhits" "=" 1*digit
                              / opt-globalcnst

opt-globalcnst  =   "hold"
                              / "authenticate" "=" auth-method
                              / "name" "=" string
                              / "password" "=" string
                              / "language" "=" language
                              / "incharset" "=" characterset
                              / "ignore" "=" string
                              / "include" "=" string
 
format          =   "full" / "abridged" / "handle" / "summary"
                              / "servers-to-ask"

language        = <The two-letter language code defined in
                              ISO 639-1988 can be used as language constraint.
                              In these the case of the letters are
                              insignificant. Other language codes shall be
                              interpreted according to the Internet standard
                              for language codes in RFC 822/MIME messages, if
                              and when such a standard is adopted>

characterset    =   "us-ascii" / "iso-8859-1" / "iso-8859-2" /
                              "iso-8859-3" / "iso-8859-4" / "iso-8859-5" /
                              "iso-8859-6" / "iso-8859-7" / "iso-8859-8" /
                              "iso-8859-9" / "iso-8859-10" / charset-value

charset-value   =   1*char

localcnstrnt    =   "search" "=" searchvalue /
                              "case" "=" casevalue

searchvalue     =   "exact" / "substring" / "regex" / "fuzzy"
                              / "lstring"

casevalue       =   "ignore" / "consider"

auth-method     =   "password"

string          =   0*char

char            =   "\" specialchar
                              / <Characters 0-255 (decimal) except specialchar>


specialchar     =   " " / <tab> / "=" / "," / ":" / ";" / "\" /
                              "*" / "." / "(" / ")" / "[" / "]" / "^" /
                              "$" / "!" / "?"

digit           =   "0" / "1" / "2" / "3" / "4" /
                              "5" / "6" / "7" / "8" / "9"

NL              =   <CR LF (decimal 13 10)>


