capability        =/ "METADATA" / "METADATA-SERVER"
                           ; defines the capabilities for this extension

command-auth      =/ setmetadata / getmetadata
                           ; adds to original IMAP command

entries           = entry /
                           "(" entry *(SP entry) ")"
                           ; entry specifiers

entry             = astring
                           ; slash-separated path to entry
                           ; MUST NOT contain "*" or "%"

entry-value       = entry SP value

entry-values      = "(" entry-value *(SP entry-value) ")"

entry-list        = entry *(SP entry)
                           ; list of entries used in unsolicited
                           ; METADATA response

getmetadata       = "GETMETADATA" SP mailbox SP entries
                           ; empty string for mailbox implies
                           ; server annotation.

metadata-resp     = "METADATA" SP mailbox SP
                           (entry-values / entry-list)
                           ; empty string for mailbox implies
                           ; server annotation.

response-payload  =/ metadata-resp
                           ; adds to original IMAP data responses

resp-text-code    =/ "METADATA" SP ("MAXSIZE" SP number /
                                           "TOOMANY" / "NOPRIVATE")
                           ; new response codes for SETMETADATA
                           ; failures

setmetadata       = "SETMETADATA" SP mailbox
                                         SP entry-values
                           ; empty string for mailbox implies
                           ; server annotation.

value             = nstring / literal8
