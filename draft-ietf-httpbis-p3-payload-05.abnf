ALPHA          = <ALPHA, defined in [Part1], Section 2.2>
DIGIT          = <DIGIT, defined in [Part1], Section 2.2>
OCTET          = <OCTET, defined in [Part1], Section 2.2>


quoted-string  = <quoted-string, defined in [Part1], Section 2.2>
token          = <token, defined in [Part1], Section 2.2>
OWS            = <OWS, defined in [Part1], Section 2.2>





absolute-URI   = <absolute-URI, defined in [Part1], Section 3.2>
Content-Length = <Content-Length, defined in [Part1], Section 8.2>
relativeURI    = <relativeURI, defined in [Part1], Section 3.2>
message-header = <message-header, defined in [Part1], Section 4.2>


Last-Modified  = <Last-Modified, defined in [Part4], Section 7.6>


Content-Range  = <Content-Range, defined in [Part5], Section 6.2>


Expires        = <Expires, defined in [Part6], Section 16.3>


charset = token





content-coding   = token

media-type = type "/" subtype *( OWS ";" OWS parameter )
type       = token
subtype    = token

parameter      = attribute "=" value
attribute      = token
value          = token / quoted-string

qvalue         = ( "0" [ "." 0*3DIGIT ] )
                    / ( "1" [ "." 0*3("0") ] )

language-tag  = primary-tag *( "-" subtag )
primary-tag   = 1*8ALPHA
subtag        = 1*8ALPHA

entity-header  = Content-Encoding         ; Section 6.5
                    / Content-Language         ; Section 6.6
                    / Content-Length           ; [Part1], Section 8.2
                    / Content-Location         ; Section 6.7
                    / Content-MD5              ; Section 6.8
                    / Content-Range            ; [Part5], Section 6.2
                    / Content-Type             ; Section 6.9
                    / Expires                  ; [Part6], Section 16.3
                    / Last-Modified            ; [Part4], Section 7.6
                    / extension-header

extension-header = message-header

entity-body    = *OCTET

Accept   = "Accept" ":" OWS Accept-v
Accept-v = #( media-range [ accept-params ] )

media-range    = ( "*/*"
                      / ( type "/" "*" )
                      / ( type "/" subtype )
                      ) *( OWS ";" OWS parameter )
accept-params  = OWS ";" OWS "q=" qvalue *( accept-ext )
accept-ext     = OWS ";" OWS token
                      [ "=" ( token / quoted-string ) ]





Accept-Charset   = "Accept-Charset" ":" OWS
             Accept-Charset-v
Accept-Charset-v = 1#( ( charset / "*" )
                            [ OWS ";" OWS "q=" qvalue ] )

Accept-Encoding    = "Accept-Encoding" ":" OWS
                        Accept-Encoding-v
Accept-Encoding-v  =
                        #( codings [ OWS ";" OWS "q=" qvalue ] )
codings            = ( content-coding / "*" )

q=1.

Accept-Language   = "Accept-Language" ":" OWS
                       Accept-Language-v
Accept-Language-v =
                       1#( language-range [ OWS ";" OWS "q=" qvalue ] )
language-range    =
               <language-range, defined in [RFC4647], Section 2.1>

Content-Encoding   = "Content-Encoding" ":" OWS Content-Encoding-v
Content-Encoding-v = 1#content-coding

Content-Language   = "Content-Language" ":" OWS Content-Language-v
Content-Language-v = 1#language-tag

Content-Location   = "Content-Location" ":" OWS
                       Content-Location-v
Content-Location-v =
                       absolute-URI / relativeURI

Content-MD5   = "Content-MD5" ":" OWS Content-MD5-v
Content-MD5-v = <base64 of 128 bit MD5 digest as per [RFC1864]>

Content-Type   = "Content-Type" ":" OWS Content-Type-v
Content-Type-v = media-type

MIME-Version   = "MIME-Version" ":" OWS MIME-Version-v
MIME-Version-v = 1*DIGIT "." 1*DIGIT

content-disposition = "Content-Disposition" ":" OWS
                           content-disposition-v
content-disposition-v = disposition-type
                             *( OWS ";" OWS disposition-parm )
disposition-type = "attachment" / disp-extension-token
disposition-parm = filename-parm / disp-extension-parm
filename-parm = "filename" "=" quoted-string
disp-extension-token = token
disp-extension-parm = token "=" ( token / quoted-string )

