smpte=10:12:33:20-
smpte=10:07:33-
smpte=10:07:00-10:07:33:05.01
smpte-25=10:07:00-10:07:33:05.01



npt=123.45-125
npt=12:05:35.3-
npt=now-




        The syntax conforms to ISO 8601 [34]. The npt-sec notation
        is optimized for automatic generation, the ntp-hhmmss
        notation for consumption by human readers. The "now"
        constant allows clients to request to receive the live feed
        rather than the stored or time-delayed version. This is
        needed since neither absolute time nor zero time are
        appropriate for this case.

Sd=Send, Opt: Optional, Req: Required, Rec:  Recommended

        Note on Table 7: GET_PARAMETER is recommended, but not
        required. For example, a fully functional server can be
        built to deliver media without any parameters.
        SET_PARAMETER is required however due to its usage for
        keep-alive.  PAUSE is now required due to that it is the
        only way of getting out of the state machines play state
        without terminating the whole session.

v=0
o=mhandley 2890844526 2890842807 IN IP4 126.16.64.4
s=SDP Seminar
i=A Seminar on the session description protocol
u=http://www.example.com/lectures/sdp.ps
e=seminar@example.com (Seminar Management)
c=IN IP4 224.2.17.12/127
t=2873397496 2873404696
a=recvonly
m=audio 3456 RTP/AVP 0
m=video 2232 RTP/AVP 31
m=application 32416 UDP WB
a=orient:portrait



src_addr="192.0.2.241:6256"/"192.0.2.241:6257";
ssrc=2A3F93ED
ssrc=0D12F123:seq=14783;rtptime=2345962545

npt=0-. If a PLAY request is received without a Range header when
ssrc=0D12F123:seq=14783;rtptime=2345962545
ssrc=0D12F123:seq=53745;rtptime=484589019




ssrc=0D12F123:seq=5712;rtptime=934207921,
url="rtsp://example.com/fizzle/videotrack"
ssrc=4FAD8726:seq=57654;rtptime=2792482193
ssrc=0D12F123:seq=5712;rtptime=934207921,
url="rtsp://example.com/fizzle/videotrack"
ssrc=789DAF12:seq=57654;rtptime=2792482193
time= parameter indicates the wall clock time by when the redirection
ssrc=0D12F123:seq=232433;rtptime=972948234

r=response. Note: It is allowed for all error messages 4xx and 5xx to
max-age=0 directive, to revalidate its own cache entry, and
rtptime=12345678,url="rtsp://example.com/foo/video"
ssrc=9A9DE123:seq=30211;rtptime=29567112
interleaved=4-5 in cases where the transport choice for the
v=0
o=- 2890844526 2890842807 IN IP4 192.16.24.202
s=RTSP Session
e=adm@example.com
a=range:npt=0-1:49:34
t=0 0
m=audio 0 RTP/AVP 0
a=control:rtsp://audio.example.com/twister/audio.en
m=video 0 RTP/AVP 31
a=control:rtsp://video.example.com/twister/video

src_addr="192.0.2.5:5000"/"192.0.2.5:5001"
src_addr="192.0.2.5:5002"/"192.0.2.5:5003"
ssrc=A17E189D:seq=12312232;rtptime=78712811
ssrc=3D124F01:seq=876655;rtptime=1032181
v=0
o=- 2890844256 2890842807 IN IP4 172.16.2.93
s=RTSP Session
i=An Example of RTSP Session Usage
e=adm@example.com
a=control: *
a=range: npt=0-0:10:34.10
t=0 0
m=audio 0 RTP/AVP 0
a=control: trackID=1
m=video 0 RTP/AVP 26
a=control: trackID=4

src_addr="192.0.2.5:9000"/"192.0.2.5:9001"
ssrc=93CB001E
src_addr="192.0.2.5:9002"/"192.0.2.5:9003";
ssrc=A813FC13
ssrc=0D12F123:seq=12345;rtptime=3450012,
url="rtsp://example.com/twister.3gp/trackID=1";
ssrc=4F312DD8:seq=54321;rtptime=2876889
ssrc=0D12F123:seq=12555;rtptime=6330012,
url="rtsp://example.com/twister.3gp/trackID=1"
ssrc=4F312DD8:seq=55021;rtptime=3132889




v=0
o=- 872653257 872653257 IN IP4 172.16.2.187
s=mu-law wave file
i=audio test
t=0 0
a=control: *
m=audio 0 RTP/AVP 0
a=control:streamid=0

dest_addr=":6970"/":6971";mode="PLAY"
src_addr="192.0.2.5:6970"/"192.0.2.5:6971";
mode="PLAY";ssrc=EAB98712
ssrc=0D12F123:seq=981888;rtptime=3781123
src="rtsp://live.example.com/concert/audio">
v=0
o=- 2890844526 2890842807 IN IP4 192.16.24.202
s=RTSP Session
m=audio 3456 RTP/AVP 0
c=IN IP4 224.2.0.1/16
a=control: rtsp://live.example.com/concert/audio
a=range:npt=0-

ssrc=0D12F123:seq=1473; rtptime=80000



src_addr="192.0.2.5:5000"/"192.0.2.5:5001"
OCTET      =  %x00-FF ; any 8-bit sequence of data
CHAR       =  %x01-7F ; any US-ASCII character (octets 1 - 127)
UPALPHA    =  %x41-5A ; any US-ASCII uppercase letter "A".."Z"
LOALPHA    =  %x61-7A ;any US-ASCII lowercase letter "a".."z"
ALPHA      =  UPALPHA / LOALPHA
DIGIT      =  %x30-39 ; any US-ASCII digit "0".."9"
CTL        =  %x00-1F / %x7F ; any US-ASCII control character
                 ; (octets 0 - 31) and DEL (127)
CR         =  %x0D ; US-ASCII CR, carriage return (13
LF         =  %x0A ; US-ASCII LF, linefeed (10)
SP         =  %x20 ; US-ASCII SP, space (32)
HT         =  %x09 ; US-ASCII HT, horizontal-tab (9)
DQUOTE     =  %x22 ; US-ASCII double-quote mark (34)
BACKSLASH  =  %x5C ; US-ASCII backslash (92)
CRLF       =  CR LF



LWS            =  [CRLF] 1*( SP / HT )
SWS            =  [LWS] ; sep whitespace
HCOLON         =  *( SP / HTAB ) ":" SWS
TEXT           =  %x20-7D / %x80-FF ; any OCTET except CTLs
tspecials      =  "(" / ")" / "<" / ">" / "@"
                  /  "," / ";" / ":" / BACKSLASH / DQUOTE
                  /  "/" / "[" / "]" / "?" / "="
                  /  "{" / "}" / SP / HT
token          =  1*(%x21 / %x23-27 / %x2A-2B / %x2D-2E / %x30-39
                  /  %x41-5A / %x5E-7A / %x7C / %x7E)
                     ; 1*<any CHAR except CTLs or tspecials>
quoted-string  =  ( DQUOTE *qdtext DQUOTE )
qdtext         =  %x20-21 / %x23-7D / %x80-FF ; any TEXT except <">
quoted-pair    =  BACKSLASH CHAR
ctext          =  %x20-27 / %x2A-7D
                  /  %x80-FF ; any OCTET except CTLs, "(" and ")"
generic-param  =  token [ EQUAL gen-value ]
gen-value      =  token / host / quoted-string



safe             =  "$" / "-" / "_" / "." / "+"
extra            =  "!" / "*" / "'" / "(" / ")" / ","
rtsp-extra       =  "!" / "*" / "'" / "(" / ")"
HEX              =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
                    /  "a" / "b" / "c" / "d" / "e" / "f"
LHEX             =  DIGIT / %x61-66 ;lowercase a-f
reserved         =  ";" / "/" / "?" / ":" / "@" / "&" / "="
unreserved       =  ALPHA / DIGIT / safe / extra
rtsp-unreserved  =  ALPHA / DIGIT / safe / rtsp-extra
base64           =  *base64-unit [base64-pad]
base64-unit      =  4base64-char
base64-pad       =  (2base64-char "==") / (3base64-char "=")
base64-char      =  ALPHA / DIGIT / "+" / "/"



SLASH          =  SWS "/" SWS ; slash
EQUAL          =  SWS "=" SWS ; equal
LPAREN         =  SWS "(" SWS ; left parenthesis
RPAREN         =  SWS ")" SWS ; right parenthesis
COMMA          =  SWS "," SWS ; comma
SEMI           =  SWS ";" SWS ; semicolon
COLON          =  SWS ":" SWS ; colon
LDQUOT         =  SWS DQUOTE ; open double quotation mark
RDQUOT         =  DQUOTE SWS ; close double quotation mark
RAQUOT         =  ">" SWS ; right angle quote
LAQUOT         =  SWS "<" ; left angle quote
TEXT-UTF8char  =  %x21-7E / UTF8-NONASCII
UTF8-NONASCII  =  %xC0-DF 1UTF8-CONT
                  /  %xE0-EF 2UTF8-CONT
                  /  %xF0-F7 3UTF8-CONT
                  /  %xF8-FB 4UTF8-CONT
                  /  %xFC-FD 5UTF8-CONT
UTF8-CONT      =  %x80-BF


URI-reference  =  RTSP-URI / relative-ref
relative-ref   =  < As defined in RFC 3986 [6]>
RTSP-URI       =  rtsp-uri-def / rtsps-uri-def / rtspu-uri-def
rtsp-uri-def   =  "rtsp:" rtsp-uri-rest
rtsps-uri-def  =  "rtsps:" rtsp-uri-rest
rtspu-uri-def  =  "rtspu:" rtsp-uri-rest
rtsp-uri-rest  =  "//" host [":" port] [abs-path-def] [frag-def]
abs-path-def   =  abs-path ["?" query]
frag-def       =  "#" fragment
host           =  <As defined by RFC 3986 [6]>
abs-path       =  <As defined by RFC 3986 [6]>
port           =  *DIGIT ; Is expected to be 1*5DIGIT
query          =  <As defined by RFC 3986 [6]>
fragment       =  <As defined by RFC 3986 [6]>
absolute-URI   =  <As defined by RFC 3986 [6]>
IPv4address    =  1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6address    =  hexpart [ ":" IPv4address ]
hexpart        =  hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
hexseq         =  hex4 *( ":" hex4)
hex4           =  1*4HEXDIG



smpte-range           =  smpte-type "=" smpte-range-spec
                            ;Section 3.4
smpte-range-spec      =  ( smpte-time "-" [ smpte-time ] )
                         /  ( "-" smpte-time )
smpte-type            =  "smpte" / "smpte-30-drop"
                         /  "smpte-25" / smpte-type-extension
                            ; other timecodes may be added
smpte-type-extension  =  token
smpte-time            =  1*2DIGIT ":" 1*2DIGIT ":" 1*2DIGIT
                            [ ":" 1*2DIGIT [ "." 1*2DIGIT ] ]



npt-range       =  "npt=" npt-range-spec ; Section 3.5
npt-range-spec  =  ( npt-time "-" [ npt-time ] ) / ( "-" npt-time )
npt-time        =  "now" / npt-sec / npt-hhmmss
npt-sec         =  1*DIGIT [ "." *DIGIT ]
npt-hhmmss      =  npt-hh ":" npt-mm ":" npt-ss [ "." *DIGIT ]
npt-hh          =  1*DIGIT ; any positive number
npt-mm          =  1*2DIGIT ; 0-59
npt-ss          =  1*2DIGIT ; 0-59



utc-range       =  "clock=" utc-range-spec ; Section 3.6
utc-range-spec  =  ( utc-time "-" [ utc-time ] ) / ( "-" utc-time )
utc-time        =  utc-date "T" utc-clock "Z"
utc-date        =  8DIGIT ; < YYYYMMDD >
utc-clock       =  6DIGIT [ "." fraction ]; < HHMMSS.fraction >
fraction        =  1*DIGIT



feature-tag       =  token
session-id        =  8*( ALPHA / DIGIT / safe )
extension-header  =  header-name HCOLON header-value
header-name       =  token
header-value      =  *(TEXT-UTF8char / UTF8-CONT / LWS)


RTSP-message  =   Request / Response ; RTSP/2.0 messages
Request       =   Request-Line        ; Section 6.1
                      *(  general-header      ; Section 5
                      /   request-header      ; Section 6.2
                      /   entity-header )     ; Section 8.1
                          CRLF
                          [ message-body ]    ; Section 4.3
Response      =   Status-Line         ; Section 7.1
                      *(  general-header      ; Section 5
                      /   response-header     ; Section 7.1.2
                      /   entity-header )     ; Section 8.1
                          CRLF
                          [ message-body ]    ; Section 4.3



Request-Line  =  Method SP Request-URI SP RTSP-Version CRLF
Status-Line   =  RTSP-Version SP Status-Code SP Reason-Phrase CRLF



Method            =  "DESCRIBE"        ; Section 11.2
                     /  "GET_PARAMETER"   ; Section 11.7
                     /  "OPTIONS"         ; Section 11.1
                     /  "PAUSE"           ; Section 11.5
                     /  "PLAY"            ; Section 11.4
                     /  "REDIRECT"        ; Section 11.9
                     /  "SETUP"           ; Section 11.3
                     /  "SET_PARAMETER"   ; Section 11.8
                     /  "TEARDOWN"        ; Section 11.6
                     /  extension-method
extension-method  =  token



Request-URI             =  "*" / RTSP-URI
RTSP-Version            =  "RTSP/" 1*DIGIT "." 1*DIGIT
message-body = 1*OCTET



Status-Code     =  "100" ; Continue
                   /  "200" ; OK
                   /  "201" ; Created
                   /  "250" ; Low on Storage Space
                   /  "300" ; Multiple Choices
                   /  "301" ; Moved Permanently
                   /  "302" ; Moved Temporarily
                   /  "303" ; See Other
                   /  "304" ; Not Modified
                   /  "305" ; Use Proxy
                   /  "400" ; Bad Request
                   /  "401" ; Unauthorized
                   /  "402" ; Payment Required
                   /  "403" ; Forbidden
                   /  "404" ; Not Found
                   /  "405" ; Method Not Allowed
                   /  "406" ; Not Acceptable
                   /  "407" ; Proxy Authentication Required
                   /  "408" ; Request Time-out
                   /  "410" ; Gone
                   /  "411" ; Length Required
                   /  "412" ; Precondition Failed
                   /  "413" ; Request Entity Too Large
                   /  "414" ; Request-URI Too Large
                   /  "415" ; Unsupported Media Type
                   /  "451" ; Parameter Not Understood
                   /  "452" ; reserved
                   /  "453" ; Not Enough Bandwidth
                   /  "454" ; Session Not Found
                   /  "455" ; Method Not Valid in This State
                   /  "456" ; Header Field Not Valid for Resource
                   /  "457" ; Invalid Range
                   /  "458" ; Parameter Is Read-Only
                   /  "459" ; Aggregate operation not allowed
                   /  "460" ; Only aggregate operation allowed
                   /  "461" ; Unsupported transport
                   /  "462" ; Destination unreachable
                   /  "463" ; Destination Prohibited
                   /  "470" ; Connection Authorization Required
                   /  "471" ; Connection Credentials not accepted
                   /  "500" ; Internal Server Error
                   /  "501" ; Not Implemented
                   /  "502" ; Bad Gateway
                   /  "503" ; Service Unavailable
                   /  "504" ; Gateway Time-out
                   /  "505" ; RTSP Version not supported
                   /  "551" ; Option not supported
                   /  extension-code
extension-code  =  3DIGIT
Reason-Phrase   =  *TEXT



general-header  =  Cache-Control     ; Section 14.10
                   /  Connection        ; Section 14.11
                   /  CSeq              ; Section 14.19
                   /  Date              ; Section 14.20
                   /  Proxy-Supported   ; Section 14.32
                   /  Supported         ; Section 14.43
                   /  Timestamp         ; Section 14.44
                   /  Via               ; Section 14.49
                   /  extension-header



request-header  =  Accept              ; Section 14.1 and [H14.1]
                   /  Accept-Credentials  ; Section 14.2
                   /  Accept-Encoding     ; Section 14.3 and [H14.3]
                   /  Accept-Language     ; Section 14.4 and [H14.4]
                   /  Authorization       ; Section 14.7 and [H14.8]
                   /  Bandwidth           ; Section 14.8
                   /  Blocksize           ; Section 14.9
                   /  From                ; Section 14.23
                   /  If-Match            ; Section 14.25
                   /  If-Modified-Since   ; Section 14.26 and [H14.25]
                   /  If-None-Match       ; Section 14.27
                   /  Proxy-Require       ; Section 14.31
                   /  Range               ; Section 14.34
                   /  Referer             ; Section 14.35
                   /  Require             ; Section 14.37
                   /  Scale               ; Section 14.39
                   /  Session             ; Section 14.42
                   /  Speed               ; Section 14.40
                   /  Supported           ; Section 14.43
                   /  Transport           ; Section 14.45
                   /  User-Agent          ; Section 14.47
                   /  extension-header



response-header  =  Accept-Credentials  ; Section 14.2
                    /  Accept-Ranges       ; Section 14.5
                    /  Connection-Creds    ; Section 14.12
                    /  ETag                ; Section 14.21
                    /  Location            ; Section 14.29
                    /  Proxy-Authenticate  ; Section 14.30
                    /  Public              ; Section 14.33
                    /  Range               ; Section 14.34
                    /  Retry-After         ; Section 14.36
                    /  RTP-Info            ; Section 14.38
                    /  Scale               ; Section 14.39
                    /  Session             ; Section 14.42
                    /  Server              ; Section 14.41
                    /  Speed               ; Section 14.40
                    /  Transport           ; Section 14.45
                    /  Unsupported         ; Section 14.46
                    /  Vary                ; Section 14.48
                    /  WWW-Authenticate    ; Section 14.50
                    /  extension-header



entity-header  =  Allow             ; Section 14.6
                  /  Content-Base      ; Section 14.13
                  /  Content-Encoding  ; Section 14.14
                  /  Content-Language  ; Section 14.15
                  /  Content-Length    ; Section 14.16
                  /  Content-Location  ; Section 14.17
                  /  Content-Type      ; Section 14.18
                  /  Expires           ; Section 14.22 and [H14.21]
                  /  Last-Modified     ; Section 14.28
                  /  extension-header


Accept              =  Accept-name HCOLON
                          [ accept-range *(COMMA accept-range) ]
accept-range        =  media-range *(SEMI accept-param)
media-range         =  ( "*/*"
                       /  ( m-type SLASH "*" )
                       /  ( m-type SLASH m-subtype )
                          ) *( SEMI m-parameter )
accept-param        =  ("q" EQUAL qvalue) / generic-param
qvalue              =  ( "0" [ "." *3DIGIT ] )
                       /  ( "1" [ "." *3("0") ] )
Accept-Credentials  =  "Accept-Credentials" HCOLON cred-decision CRLF
cred-decision       =  ("User" COMMA [cred-info])
                       /  "Proxy"
                       /  "Any"
                       /  token ; For future extensions
cred-info           =  cred-info-data *(COMMA cred-info-data)
cred-info-data      =  DQUOTE RTSP-URI DQUOTE SEMI base64
Accept-Encoding     =  "Accept-Encoding" HCOLON
                          [ encoding *(COMMA encoding) ]
encoding            =  codings *(SEMI accept-param)
codings             =  content-coding / "*"
content-coding      =  token
Accept-Language     =  "Accept-Language" HCOLON
                          [ language *(COMMA language) ]
language            =  language-range *(SEMI accept-param)
language-range      =  ( ( 1*8ALPHA *( "-" 1*8ALPHA ) ) / "*" )
Accept-Ranges       =  "Accept-Ranges" HCOLON acceptable-ranges CRLF
acceptable-ranges   =  (range-unit *(COMMA range-unit))
                       /  "none"
range-unit          =  "NPT" / "SMPTE" / "UTC" / extension-format
extension-format    =  token
Allow               =  "Allow" HCOLON [Method *(COMMA Method)]
Authorization       =  "Authorization" HCOLON credentials
credentials         =  ("Digest" LWS digest-response)
                       /  other-response
digest-response     =  dig-resp *(COMMA dig-resp)
dig-resp            =  username / realm / nonce / digest-uri
                       /  dresponse / algorithm / cnonce
                       /  opaque / message-qop
                       /  nonce-count / auth-param
username            =  "username" EQUAL username-value
username-value      =  quoted-string
digest-uri          =  "uri" EQUAL LDQUOT digest-uri-value RDQUOT
digest-uri-value    =  Request-URI
                          ; by HTTP/1.1
message-qop         =  "qop" EQUAL qop-value
cnonce              =  "cnonce" EQUAL cnonce-value
cnonce-value        =  nonce-value
nonce-count         =  "nc" EQUAL nc-value
nc-value            =  8LHEX
dresponse           =  "response" EQUAL request-digest
request-digest      =  LDQUOT 32LHEX RDQUOT
auth-param          =  auth-param-name EQUAL
                          ( token / quoted-string )
auth-param-name     =  token
other-response      =  auth-scheme LWS auth-param
                          *(COMMA auth-param)
auth-scheme         =  token
Bandwidth           =  "Bandwidth" HCOLON 1*DIGIT CRLF
Blocksize           =  "Blocksize" HCOLON 1*DIGIT CRLF



Cache-Control          =  "Cache-Control" HCOLON cache-directive CRLF
                             *(COMMA cache-directive)
cache-directive        =  cache-rqst-directive
                          /  cache-rspns-directive
cache-rqst-directive   =  "no-cache"
                          /  "max-stale" [EQUAL delta-seconds]
                          /  "min-fresh" EQUAL delta-seconds
                          /  "only-if-cached"
                          /  cache-extension
cache-rspns-directive  =  "public"
                          /  "private"
                          /  "no-cache"
                          /  "no-transform"
                          /  "must-revalidate"
                          /  "proxy-revalidate"
                          /  "max-age" EQUAL delta-seconds
                          /  cache-extension
cache-extension        =  token [EQUAL (token / quoted-string)]
delta-seconds          =  1*DIGIT



Connection-Creds    =  "Connection-Credentials" HCOLON cred-info CRLF
Connection          =  "Connection" HCOLON (connection-token)
                          *(COMMA connection-token) CRLF
connection-token    =  token
Content-Base        =  "Content-Base" HCOLON URI-reference CRLF
Content-Encoding    =  "Content-Encoding" HCOLON
                          content-coding *(COMMA content-coding)
Content-Language    =  "Content-Language" HCOLON
                          language-tag *(COMMA language-tag)
language-tag        =  primary-tag *( "-" subtag )
primary-tag         =  1*8ALPHA
subtag              =  1*8ALPHA
Content-Length      =  "Content-Length" HCOLON 1*DIGIT
Content-Location    =  "Content-Location" HCOLON URI-reference
Content-Type        =  ( "Content-Type" / "c" ) HCOLON media-type
media-type          =  m-type SLASH m-subtype *(SEMI m-parameter)
m-type              =  discrete-type / composite-type
discrete-type       =  "text" / "image" / "audio" / "video"
                       /  "application" / extension-token
composite-type      =  "message" / "multipart" / extension-token
extension-token     =  ietf-token / x-token
ietf-token          =  token
x-token             =  "x-" token
m-subtype           =  extension-token / iana-token
iana-token          =  token
m-parameter         =  m-attribute EQUAL m-value
m-attribute         =  token
m-value             =  token / quoted-string
CSeq                =  "Cseq" HCOLON 1*DIGIT CRLF
Date                =  "Date" HCOLON RTSP-date
RTSP-date           =  rfc1123-date ; HTTP-date
rfc1123-date        =  wkday "," SP date1 SP time SP "GMT"
date1               =  2DIGIT SP month SP 4DIGIT
                          ; day month year (e.g., 02 Jun 1982)
time                =  2DIGIT ":" 2DIGIT ":" 2DIGIT
                          ; 00:00:00 - 23:59:59
wkday               =  "Mon" / "Tue" / "Wed"
                       /  "Thu" / "Fri" / "Sat" / "Sun"
month               =  "Jan" / "Feb" / "Mar" / "Apr"
                       /  "May" / "Jun" / "Jul" / "Aug"
                       /  "Sep" / "Oct" / "Nov" / "Dec"
ETag                =  "ETag" HCOLON entity-tag
Expires             =  "Expires" HCOLON delta-seconds
From                =  "From" HCOLON from-spec
from-spec           =  ( name-addr / addr-spec ) *( SEMI from-param )
name-addr           =  [ display-name ] LAQUOT addr-spec RAQUOT
addr-spec           =  RTSP-URI / absolute-URI
display-name        =  *(token LWS)/ quoted-string
from-param          =  tag-param / generic-param
tag-param           =  "tag" EQUAL token
If-Match            =  "If-Match" HCOLON ( "*" / entity-tag-list)
entity-tag-list     =  entity-tag *(COMMA entity-tag)
entity-tag          =  [ weak ] opaque-tag
weak                =  "W/"
opaque-tag          =  quoted-string
If-Modified-Since   =  "If-Modified-Since" HCOLON RTSP-date
If-None-Match       =  "If-None-Match" HCOLON ("*" / entity-tag-list)
Last-Modified       =  "Last-Modified" HCOLON RTSP-date
Location            =  "Location" HCOLON RTSP-URI
Proxy-Authenticate  =  "Proxy-Authenticate" HCOLON challenge
challenge           =  ("Digest" LWS digest-cln *(COMMA digest-cln))
                          / other-challenge
other-challenge     =  auth-scheme LWS auth-param
                          *(COMMA auth-param)
digest-cln          =  realm / domain / nonce
                       /  opaque / stale / algorithm
                       /  qop-options / auth-param
realm               =  "realm" EQUAL realm-value
realm-value         =  quoted-string
domain              =  "domain" EQUAL LDQUOT URI
                          *( 1*SP URI ) RDQUOT
URI                 =  RTSP-URI / abs-path
nonce               =  "nonce" EQUAL nonce-value
nonce-value         =  quoted-string
opaque              =  "opaque" EQUAL quoted-string
stale               =  "stale" EQUAL ( "true" / "false" )
algorithm           =  "algorithm" EQUAL ("MD5" / "MD5-sess" / token)
qop-options         =  "qop" EQUAL LDQUOT qop-value
                          *("," qop-value) RDQUOT
qop-value           =  "auth" / "auth-int" / token
Proxy-Require       =  "Proxy-Require" HCOLON feature-tag CRLF
                          *(COMMA feature-tag)
Proxy-Supported     =  "Proxy-Supported" HCOLON feature-tag
                          *(COMMA feature-tag) CRLF
Public              =  "Public" HCOLON Method *(COMMA Method) CRLF
Range               =  "Range" HCOLON ranges-list [exec-time] CRLF
ranges-list         =  ranges-spec *(COMMA ranges-spec)
exec-time           =  SEMI "time" EQUAL utc-time
ranges-spec         =  npt-range / utc-range / smpte-range
Referer             =  "Referer" HCOLON URI-reference
Require             =  "Require" HCOLON feature-tag-list CRLF
feature-tag-list    =  feature-tag *(COMMA feature-tag)



RTP-Info        =  "RTP-Info" HCOLON rtsp-info-spec
                      *(COMMA rtsp-info-spec) CRLF
rtsp-info-spec  =  stream-url 1*ssrc-parameter
stream-url      =  "url" EQUAL DQUOTE URI-reference DQUOTE
ssrc-parameter  =  LWS "ssrc" EQUAL ssrc HCOLON
                      ri-parameter *(SEMI ri-parameter)
ri-parameter    =  "seq" EQUAL 1*DIGIT
                   /  "rtptime" EQUAL 1*DIGIT
Retry-After     =  "Retry-After" HCOLON delta-seconds
                      [ comment ] *( SEMI retry-param )
retry-param     =  ("duration" EQUAL delta-seconds)
                   /  generic-param



Scale            =  "Scale" HCOLON ["-"] 1*DIGIT [ "." *DIGIT ] CRLF
Speed            =  "Speed" HCOLON 1*DIGIT [ "." *DIGIT ] CRLF
Server           =  "Server" HCOLON ( product / comment )
                       *(LWS (product / comment)) CRLF
product          =  token [SLASH product-version]
product-version  =  token
comment          =  LPAREN *( ctext / quoted-pair) RPAREN
Session          =  "Session" HCOLON session-id
                       [ SEMI "timeout" EQUAL delta-seconds ] CRLF
Supported        =  "Supported" HCOLON [feature-tag-list] CRLF



Timestamp        =  "Timestamp" HCOLON timestamp-value LWS [delay]
timestamp-value  =  *DIGIT [ "." *DIGIT ]
delay            =  *DIGIT [ "." *DIGIT ]
Transport        =  "Transport" HCOLON transport-spec
                       *(COMMA transport-spec) CRLF
transport-spec   =  transport-id *tr-parameter
transport-id     =  trans-id-rtp / other-trans
trans-id-rtp     =  "RTP/" profile ["/" lower-transport]
                       ; no LWS is allowed inside transport-id
other-trans      =  token *("/" token)



profile          =  "AVP" / "SAVP" / "AVPF" / token
lower-transport  =  "TCP" / "UDP" / token
tr-parameter     =  SEMI ( "unicast" / "multicast" )
                    /  SEMI "interleaved" EQUAL channel [ "-" channel ]
                    /  SEMI "append"
                    /  SEMI "ttl" EQUAL ttl
                    /  SEMI "layers" EQUAL 1*DIGIT
                    /  SEMI "ssrc" EQUAL ssrc *(SLASH ssrc)
                    /  SEMI "client_ssrc" EQUAL ssrc
                    /  SEMI "mode" EQUAL mode-spec
                    /  SEMI "dest_addr" EQUAL addr-list
                    /  SEMI "src_addr" EQUAL addr-list
                    /  SEMI trn-param-ext
trn-param-ext    =  par-name EQUAL trn-par-value
par-name         =  token
trn-par-value    =  *(rtsp-unreserved / DQUOTE *TEXT DQUOTE)
ttl              =  1*3DIGIT ; 0 to 255
ssrc             =  8HEX
channel          =  1*3DIGIT
mode-spec        =  ( DQUOTE mode *(COMMA mode) DQUOTE )
mode             =  "PLAY" / "RECORD" / token
addr-list        =  quoted-addr *(SLASH quoted-addr)
quoted-addr      =  DQUOTE (host-port / extension-addr) DQUOTE
host-port        =  host [":" port]
                    /  ":" port
extension-addr   =  1*qdtext



Unsupported       =  "Unsupported" HCOLON feature-tag-list CRLF
User-Agent        =  "User-Agent" HCOLON ( product / comment )
                        0*(LWS (product / comment)) CRLF
Vary              =  "Vary" HCOLON ( "*" / field-name-list)
field-name-list   =  field-name *(COMMA field-name)
field-name        =  token
Via               =  "Via" HCOLON via-parm *(COMMA via-parm)
via-parm          =  sent-protocol LWS sent-by *( SEMI via-params )
via-params        =  via-ttl / via-maddr
                     /  via-received / via-branch
                     /  via-extension
via-ttl           =  "ttl" EQUAL ttl
via-maddr         =  "maddr" EQUAL host
via-received      =  "received" EQUAL (IPv4address / IPv6address)
via-branch        =  "branch" EQUAL token
via-extension     =  generic-param
sent-protocol     =  protocol-name SLASH protocol-version
                        SLASH transport-prot
protocol-name     =  "RTSP" / token
protocol-version  =  token
transport-prot    =  "UDP" / "TCP" / "TLS" / other-transport
other-transport   =  token
sent-by           =  host [ COLON port ]
WWW-Authenticate  =  "WWW-Authenticate" HCOLON challenge


control-attribute  =  "a=control:" *SP RTSP-URI
a-range-def        =  "a=range:" ranges-spec CRLF
a-etag-def         =  "a=etag:" etag-string CRLF
etag-string        =  1*(%x01-09/%x0B-0C/%x0E-FF)


ssrc=0D12F123:seq=0;rtptime=0



ssrc=0D12F123:seq=50;rtptime=40100



ssrc=0D12F123:seq=0;rtptime=0



ssrc=0D12F123:seq=0;rtptime=0



ssrc=0D12F123:seq=4;rtptime=164400



a=control:rtsp://example.com/foo



m=audio 0 RTP/AVP 31



a=range:npt=0-34.4368
a=range:clock=19971113T2115-19971113T2203
a=range:npt=0-



a=etag:158bb3e7c7fd62ce67f12b533f06b83a




        One could argue that the "o=" field provides identical
        functionality. However, it does so in a manner that would
        put constraints on servers that need to support multiple
        session description types other than SDP for the same piece
        of media content.

v=0
o=- 2890844256 2890842807 IN IP4 204.34.34.32
s=I came from a web page
e=adm@example.com
c=IN IP4 0.0.0.0
t=0 0
m=video 8002 RTP/AVP 31
a=control:rtsp://audio.com/movie.aud
m=audio 8004 RTP/AVP 3
a=control:rtsp://video.com/movie.vid



v=0
o=- 2890844256 2890842807 IN IP4 204.34.34.32
s=I contain
i=<more info>
e=adm@example.com
c=IN IP4 0.0.0.0
t=0 0
a=control:*
m=video 8002 RTP/AVP 31
a=control:trackID=1
m=audio 8004 RTP/AVP 3
a=control:trackID=2



