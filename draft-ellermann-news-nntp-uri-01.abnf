nntpURL        = "nntp:" server "/" group [ "/" article-number ]
server         = "//" authority                     ; see RFC 3986
group          = 1*( group-char / pct-encoded )
article-number = 1*16DIGIT
group-char     = ( ALPHA / DIGIT / "-" / "+" / "_" / "." )

newsURL        = "news:" [ server "/" ] ( article / newsgroups )
article        = mid-left "@" mid-right
newsgroups     = 1*( group-char / pct-encoded / "*" )

mid-left       = 1*( mid-atext / "." /  ; <dot-atom-text>
                         "(" / ")" /           ; allowed sub-delims
                         "," / ";" /           ; allowed sub-delims
                         ":" /                 ; allowed pchar
                         "%22" /               ; encoded DQUOTE
                         "%3C" / "%40" /       ; encoded "<" / "@"
                         "%5B" / "%5D" /       ; encoded "[" / "]"
                         "%5C%5C" / "%5C%22" ) ; encoded "\\" / '\"'

mid-atext      = ALPHA / DIGIT /        ; RFC 2822 <atext>
                        "!" / "$" /            ; allowed sub-delims
                        "&" / "'" /            ; allowed sub-delims
                        "*" / "+" / "=" /      ; allowed sub-delims
                        "-" / "_" / "~" /      ; allowed unreserved
                        "%23" / "%25" /        ; encoded "#" / "%"
                        "%2F" / "%3F" /        ; encoded "/" / "?"
                        "%5E" / "%60" /        ; encoded "^" / "`"
                        "%7B" / "%7C" /        ; encoded "{" / "|"
                        "%7D"                  ; encoded "}"

mid-right      = 1*( mid-atext / "." /  ; <dot-atom-text>
                         "(" / ")" /           ; allowed sub-delims
                         "," / ";" /           ; allowed sub-delims
                         ":" /                 ; allowed pchar
                         "%22" /               ; encoded DQUOTE
                         "%3C" / "%40" /       ; encoded "<" / "@"
                         "%5B" / "%5D" /       ; encoded "[" / "]"
                         "%5C%5C" /            ; encoded "\\"
                         "%5C%5B" / "%5C%5D" ) ; encoded "\[" / "\]"


