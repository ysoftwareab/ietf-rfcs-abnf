command      =  keyword *(SP param) CRLF    ;255 octets maximum
keyword      =  3*4VCHAR
param        =  1*VCHAR

capa-resp    =  single-line *capability "." CRLF
capa-tag     =  1*cchar
capability   =  capa-tag *(SP param)        ;512 octets maximum
cchar        =  %x21-2D / %x2F-7F
                          ;printable ASCII, excluding "."
dot-stuffed  =  *CHAR CRLF                  ;must be dot-stuffed
gchar        =  %x21-3B / %x3D-7F
                          ;printable ASCII, excluding "<"
greeting     =  "+OK" [resp-code] *gchar [timestamp] *gchar
                          ;512 octets maximum
multi-line   =  single-line *dot-stuffed "." CRLF
rchar        =  %x21-2E / %x30-5C / %x5E-7F
                          ;printable ASCII, excluding "/" and "]"
resp-code    =  "[" resp-level *("/" resp-level) "]"
resp-level   =  1*rchar
response     =  greeting / single-line / capa-resp / multi-line
single-line  =  status SP [text] CRLF          ;512 octets maximum
status       =  "+OK" / "-ERR"
text         =  [resp-code] *CHAR
timestamp    =  "<" *VCHAR ">"
                           ;MUST conform to RFC-822 msg-id


