BWS           = <BWS, see [Semantics], Section 5.7.3>
OWS           = <OWS, see [Semantics], Section 5.7.3>
RWS           = <RWS, see [Semantics], Section 5.7.3>
absolute-URI  = <absolute-URI, see [RFC3986], Section 4.3>
absolute-path = <absolute-path, see [Semantics], Section 4>
authority     = <authority, see [RFC3986], Section 3.2>
comment       = <comment, see [Semantics], Section 5.7.5>
field-name    = <field-name, see [Semantics], Section 5.4.3>
field-value   = <field-value, see [Semantics], Section 5.4.4>
obs-text      = <obs-text, see [Semantics], Section 5.7.4>
port          = <port, see [RFC3986], Section 3.2.3>
query         = <query, see [RFC3986], Section 3.4>
quoted-string = <quoted-string, see [Semantics], Section 5.7.4>
token         = <token, see [Semantics], Section 5.7.2>
uri-host      = <host, see [RFC3986], Section 3.2.2>

HTTP-message   = start-line CRLF
                      *( field-line CRLF )
                      CRLF
                      [ message-body ]

start-line     = request-line / status-line

HTTP-version  = HTTP-name "/" DIGIT "." DIGIT
HTTP-name     = %s"HTTP"

request-line   = method SP request-target SP HTTP-version

method         = token

request-target = origin-form
                    / absolute-form
                    / authority-form
                    / asterisk-form

origin-form    = absolute-path [ "?" query ]

absolute-form  = absolute-URI

authority-form = authority

asterisk-form  = "*"

status-line = HTTP-version SP status-code SP [reason-phrase]

status-code    = 3DIGIT

reason-phrase  = 1*( HTAB / SP / VCHAR / obs-text )

field-line   = field-name ":" OWS field-value OWS

obs-fold     = OWS CRLF RWS
                  ; obsolete line folding

message-body = *OCTET

Transfer-Encoding = #transfer-coding

transfer-coding = token *( OWS ";" OWS transfer-parameter )

transfer-parameter = token BWS "=" BWS ( token / quoted-string )

chunked-body   = *chunk
                      last-chunk
                      trailer-section
                      CRLF

chunk          = chunk-size [ chunk-ext ] CRLF
                      chunk-data CRLF
chunk-size     = 1*HEXDIG
last-chunk     = 1*("0") [ chunk-ext ] CRLF

chunk-data     = 1*OCTET ; a sequence of chunk-size octets
chunk-ext      = *( BWS ";" BWS chunk-ext-name
                         [ BWS "=" BWS chunk-ext-val ] )

chunk-ext-name = token
chunk-ext-val  = token / quoted-string

trailer-section   = *( field-line CRLF )

BWS = <BWS, see [Semantics], Section 5.7.3>

HTTP-message = start-line CRLF *( field-line CRLF ) CRLF [
    message-body ]
HTTP-name = %x48.54.54.50 ; HTTP
HTTP-version = HTTP-name "/" DIGIT "." DIGIT

OWS = <OWS, see [Semantics], Section 5.7.3>

RWS = <RWS, see [Semantics], Section 5.7.3>

Transfer-Encoding = [ transfer-coding *( OWS "," OWS transfer-coding
    ) ]

absolute-URI = <absolute-URI, see [RFC3986], Section 4.3>
absolute-form = absolute-URI
absolute-path = <absolute-path, see [Semantics], Section 4>
asterisk-form = "*"
authority = <authority, see [RFC3986], Section 3.2>
authority-form = authority

chunk = chunk-size [ chunk-ext ] CRLF chunk-data CRLF
chunk-data = 1*OCTET
chunk-ext = *( BWS ";" BWS chunk-ext-name [ BWS "=" BWS chunk-ext-val
    ] )
chunk-ext-name = token
chunk-ext-val = token / quoted-string
chunk-size = 1*HEXDIG
chunked-body = *chunk last-chunk trailer-section CRLF
comment = <comment, see [Semantics], Section 5.7.5>

field-line = field-name ":" OWS field-value OWS
field-name = <field-name, see [Semantics], Section 5.4.3>
field-value = <field-value, see [Semantics], Section 5.4.4>

last-chunk = 1*"0" [ chunk-ext ] CRLF

message-body = *OCTET
method = token

obs-fold = OWS CRLF RWS
obs-text = <obs-text, see [Semantics], Section 5.7.4>
origin-form = absolute-path [ "?" query ]

port = <port, see [RFC3986], Section 3.2.3>

query = <query, see [RFC3986], Section 3.4>
quoted-string = <quoted-string, see [Semantics], Section 5.7.4>

reason-phrase = 1*( HTAB / SP / VCHAR / obs-text )
request-line = method SP request-target SP HTTP-version
request-target = origin-form / absolute-form / authority-form /
    asterisk-form

start-line = request-line / status-line
status-code = 3DIGIT
status-line = HTTP-version SP status-code SP [ reason-phrase ]

token = <token, see [Semantics], Section 5.7.2>
trailer-section = *( field-line CRLF )
transfer-coding = token *( OWS ";" OWS transfer-parameter )
transfer-parameter = token BWS "=" BWS ( token / quoted-string )

uri-host = <host, see [RFC3986], Section 3.2.2>

