field         = 1*name-char colon [ space ] *any-char cr lf
colon         = %x003A ; U+003A COLON (:)
space         = %x0020 ; U+0020 SPACE
cr            = %x000D ; U+000D CARRIAGE RETURN (CR)
lf            = %x000A ; U+000A LINE FEED (LF)
name-char     = %x0000-0009
                    / %x000B-000C
                    / %x000E-0039
                    / %x003B-10FFFF
                    ; a Unicode character other than
                    ; U+000A LINE FEED (LF),
                    ; U+000D CARRIAGE RETURN (CR),
                    ; or U+003A COLON (:)
any-char      = %x0000-0009 / %x000B-000C / %x000E-10FFFF
                    ; a Unicode character other than
                    ; U+000A LINE FEED (LF)
                    ; or U+000D CARRIAGE RETURN (CR)

ws-frame               = frame-more
                               frame-rsv1
                               frame-rsv2
                               frame-rsv3
                               frame-opcode
                               frame-rsv4
                               frame-length
                               frame-extension
                               application-data;

frame-more             = %x0 ; final frame of message
                             / %x1 ; more frames of this message follow

frame-rsv1             = %x0 ; 1 bit, must be 0

frame-rsv2             = %x0 ; 1 bit, must be 0

frame-rsv3             = %x0 ; 1 bit, must be 0

frame-opcode           = %x0 ; continuation frame
                             / %x1 ; connection close
                             / %x2 ; ping
                             / %x3 ; pong
                             / %x4 ; text frame
                             / %x5 ; binary frame
                             / %x6-F ; reserved

frame-rsv4             = %x0 ; 1 bit, must be 0

frame-length           = %x00-7D
                             / %x7E frame-length-16
                             / %x7F frame-length-63

frame-length-16        = %x0000-FFFF

frame-length-63        = %x0000000000000000-7FFFFFFFFFFFFFFF

frame-extension        = *( %x00-FF ) ; to be defined later

application-data       = *( %x00-FF )

