IRI-reference  = [ absoluteIRI | relativeIRI ] [ "#" ifragment ]
absoluteIRI    = scheme ":" ( ihier_part | iopaque_part )
relativeIRI    = ( inet_path | iabs_path | irel_path )
                            [ "?" iquery ]
ihier_part     = ( inet_path | iabs_path ) [ "?" iquery ]
iopaque_part   = iric_no_slash *iric
iric_no_slash  = iunreserved | escaped | ";" | "?" | ":" | "@" |
                           "&" | "=" | "+" | "$" | ","
inet_path      = "//" iauthority [ iabs_path ]
iabs_path      = "/"  ipath_segments
irel_path      = irel_segment [ iabs_path ]
irel_segment   = 1*( iunreserved | escaped |
                               ";" | "@" | "&" | "=" | "+" | "$" | "," )
iauthority     = iserver | ireg_name
ireg_name      = 1*( iunreserved | escaped | "$" | "," |
                               ";" | ":" | "@" | "&" | "=" | "+" )
iserver        = [ [ userinfo "@" ] ihostport ]
iuserinfo      = *( iunreserved | escaped |
                              ";" | ":" | "&" | "=" | "+" | "$" | "," )
ihostport      = ihost [ ":" port ]
ihost          = ihostname | IPv4address | IPv6reference
ihostname      = << as specified by [IDNA] >>
ipath_segments = isegment *( "/" isegment )
isegment       = *ipchar *( ";" iparam )
iparam         = *ipchar
ipchar         = iunreserved | escaped |
                             ":" | "@" | "&" | "=" | "+" | "$" | ","
iquery         = *iric
ifragment      = *iric
iric           = reserved | iunreserved | escaped
iunreserved    = ichar | unreserved
ichar          = << allowed character of the UCS [ISO10646] >> | 
reserved      = ";" | "/" | "?" | ":" | "@" | "&" | "=" | "+" |
                           "$" | "," | "[" | "]"
unreserved    = alphanum | mark
mark          = "-" | "_" | "." | "!" | "~" | "*" | "'" |
                           "(" | ")"
escaped       = "%" hex hex
hex           = digit | "A" | "B" | "C" | "D" | "E" | "F" |
                                   "a" | "b" | "c" | "d" | "e" | "f"
IPv6reference = "[" IPv6address "]"
IPv6address   = hexpart [ ":" IPv4address ]
IPv4address   = 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
hexpart       = hexseq | hexseq "::" [ hexseq ] | "::"
                           [ hexseq ]
hexseq        = hex4 *( ":" hex4)
hex4          = 1*4hex
port          = *DIGIT
scheme        = alpha *( alpha | digit | "+" | "-" | "." )
alphanum      = alpha | digit
alpha         = lowalpha | upalpha
lowalpha      = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | 
upalpha       = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | 
digit         = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" |
                           "8" | "9"
space         = << US-ASCII coded character 20 hexadecimal >>
delims        = "<" | ">" | "#" | "%" | <">
unwise        = "{" | "}" | "|" | "\" | "^" | "`"


