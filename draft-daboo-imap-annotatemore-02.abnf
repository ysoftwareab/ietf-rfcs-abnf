getannotation     = "GETANNOTATION" SP entries SP attribs
                        ; new command

setannotation     = "SETANNOTATION" SP setentryatt
                        ; new command

annotate-data     = "ANNOTATION" SP entry-list
                        ; new response

entries           = entry-match / "(" entry-match *(SP entry-match) ")"
                        ; entry specifiers that can include wildcards

attribs           = attrib-match / "(" attrib-match *(SP attrib-match) ")"
                        ; attribute specifiers that can include wildcards

entry-list        = entry-att *(SP entry-att) /
                        "(" entry *(SP entry) ")"
                        ; entry attribute-value pairs list for 
                        ; GETANNOTATION response, or
                        ; parenthesised entry list for unsolicited
                        ; notification of annotation changes

setentryatt       = entry-att / "(" entry-att *(SP entry-att) ")"
entry-att         = entry SP "(" att-value *(SP att-value) ")"
att-value         = attrib SP value

entry             = string
                        ; slash-separated path to entry
                        ; begins with "/server/" or "/mailbox/"
                        ; MUST NOT contain "*" or "%"
entry-match       = string
                        ; slash-separated path to entry
                        ; begins with "/server/" or "/mailbox/"
                        ; MAY contain "*" or "%" for use as wildcards 

attrib            = string
                        ; dot-separated attribute name
                        ; MUST NOT contain "*" or "%"
attrib-match      = string
                        ; dot-separated attribute name
                        ; MAY contain "*" or "%" for use as wildcards 

value             = nstring
    
    
