uric          = reserved / unreserved / escaped

reserved    = "[" / "]" / ";" / "/" / "?" /
                    ":" / "@" / "&" / "=" / "+" / "$" / ","

unreserved  = ALPHA / DIGIT / mark

mark        = "-" / "_" / "." / "!" / "~" / "*" / "'" / "(" / ")"

escaped     = "%" HEXDIG HEXDIG


delims      = "<" / ">" / "#" / "%" / DQUOTE

unwise      = "{" / "}" / "|" / "\" / "^" / "`"
absolute-URI  = scheme ":" ( hier-part / opaque-part )

hier-part     = [ net-path / abs-path ] [ "?" query ]

net-path      = "//" authority [ abs-path ]

abs-path      = "/"  path-segments

opaque-part   = uric-no-slash *uric

uric-no-slash = unreserved / escaped / "[" / "]" / ";" / "?" /
                      ":" / "@" / "&" / "=" / "+" / "$" / ","
scheme        = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )

authority     = server / reg-name

reg-name      = 1*( unreserved / escaped / ";" /
                          ":" / "@" / "&" / "=" / "+" / "$" / "," )


server        = [ [ userinfo "@" ] hostport ]

userinfo      = *( unreserved / escaped / ";" /
                         ":" / "&" / "=" / "+" / "$" / "," )

hostport      = host [ ":" port ]
host          = IPv6reference / IPv4address / hostname
port          = *DIGIT

hostname      = domainlabel qualified
qualified     = *( "." domainlabel ) [ "." toplabel "." ]
domainlabel   = alphanum [ 0*61( alphanum | "-" ) alphanum ]
toplabel      = alpha    [ 0*61( alphanum | "-" ) alphanum ]
alphanum      = ALPHA / DIGIT

IPv4address = dec-octet "." dec-octet "." dec-octet "." dec-octet
dec-octet   = DIGIT /                         ; 0-9
                    ( %x31-39 DIGIT ) /             ; 10-99
                    ( "1" 2DIGIT ) /                ; 100-199
                    ( "2" %x30-34 DIGIT ) /         ; 200-249
                    ( "25" %x30-35 )                ; 250-255
IPv6reference = "[" IPv6address "]"

IPv6address   = (                          6( h4 ":" ) ls32 )
                    / (                     "::" 5( h4 ":" ) ls32 )
                    / ( [              h4 ] "::" 4( h4 ":" ) ls32 )
                    / ( [ *1( h4 ":" ) h4 ] "::" 3( h4 ":" ) ls32 )
                    / ( [ *2( h4 ":" ) h4 ] "::" 2( h4 ":" ) ls32 )
                    / ( [ *3( h4 ":" ) h4 ] "::"    h4 ":"   ls32 )
                    / ( [ *4( h4 ":" ) h4 ] "::"             ls32 )
                    / ( [ *5( h4 ":" ) h4 ] "::"             h4   )
                    / ( [ *6( h4 ":" ) h4 ] "::"                  )

ls32          = ( h4 ":" h4 ) / IPv4address
                    ; least-significant 32 bits of address

h4            = 1*4HEXDIG


path          = [ abs-path / opaque-part ]

path-segments = segment *( "/" segment )
segment       = *pchar

pchar         = unreserved / escaped / ";" /
                      ":" / "@" / "&" / "=" / "+" / "$" / ","

query         = *( pchar / "/" / "?" )

URI-reference = [ absolute-URI / relative-URI ] [ "#" fragment ]

absolute-URI-reference = absolute-URI [ "#" fragment ]

fragment      = *( pchar / "/" / "?" )

relative-URI  = [ net-path / abs-path / rel-path ] [ "?" query ]

rel-path      = rel-segment [ abs-path ]

rel-segment   = 1*( unreserved / escaped / ";" /
                          "@" / "&" / "=" / "+" / "$" / "," )

result = ""

scheme    = $2
authority = $4
path      = $5
query     = $7
fragment  = $9

g             =  http://a/b/c/g
