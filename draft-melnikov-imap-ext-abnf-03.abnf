append          = "APPEND" SP mailbox 1*append-message
                       ;; only a single append-message may appear
                       ;; if MULTIAPPEND [MULTIAPPEND] capability
                       ;; is not present
     
append-message  = append-opts SP append-data
     
append-ext      = <for extension only>
                       ;; This rule exists solely to be augmented by
                       ;; extensions via incremental alternative
                       ;; ("=/") rules.  It is strongly recommended
                       ;; that such extensions match a subset of the
                       ;; tagged-ext rule syntax
     
append-data     = literal / literal8
                       ;; IMAP extensions extending append-data
                       ;; should use the tagged-ext syntax,
                       ;; i.e. a mandatory label followed
                       ;; by parameters.
     
append-opts     = [SP flag-list] [SP date-time] *(SP append-ext)
     
     
create          = "CREATE" SP mailbox [SP partition]
                       [create_params]
                       ;; Use of INBOX gives a NO error
     
create_params   = SP "(" create_param *( SP create_param) ")"
     
create_param_name = tagged-ext-label
     
create_param      = create_param_name SP create_param_value
     
create_param_value= <for extension only>
                       ;; This rule exists solely to be augmented by
                       ;; extensions via incremental alternative
                       ;; ("=/") rules.  It is strongly recommended
                       ;; that such extensions match a subset of the
                       ;; tagged-ext-val rule syntax
     
examine         = "EXAMINE" SP mailbox [select-params]
                       ;; modifies the original IMAP EXAMINE command
                       ;; to accept optional parameters
     
fetch           = "FETCH" SP sequence-set SP ("ALL" / "FULL" /
                       "FAST" / fetch-att /
                       "(" fetch-att *(SP fetch-att) ")")
                       [SP fetch-modifiers]
                       ;; modifies the original IMAP4 FETCH command to
                       ;; accept optional modifiers
     
fetch-modifiers = "(" fetch-modifier *(SP fetch-modifier) ")"
     
fetch-modifier  = fetch-modifier-name [ SP fetch-modif-params ]
                       ;; Note that the original syntax defined
                       ;; in CONDSTORE was extended to allow
                       ;; for "()"
     
fetch-modif-params  = <for extension only>
                       ;; This rule exists solely to be augmented by
                       ;; extensions via incremental alternative
                       ;; ("=/") rules.  It is strongly recommended
                       ;; that such extensions match a subset of the
                       ;; tagged-ext-val rule syntax
     
fetch-modifier-name = tagged-ext-label
     
literal8        = "~{" number "}" CRLF *OCTET
                        ;; A string that might contain NULs.
                        ;; <number> represents the number of OCTETs
                        ;; in the response string.
     
rename          = "RENAME" SP mailbox SP mailbox
                       [SP partition] [rename_params]
                       ;; Use of INBOX as a destination gives
                       ;; a NO error
     
rename_params     = SP "(" rename_param *( SP rename_param) ")"
     
rename_param_name = tagged-ext-label
     
create_param      = rename_param_name SP rename_param_value
     
rename_param_value= <for extension only>
                       ;; This rule exists solely to be augmented by
                       ;; extensions via incremental alternative
                       ;; ("=/") rules.  It is strongly recommended
                       ;; that such extensions match a subset of the
                       ;; tagged-ext-val rule syntax
     
response-data   = "*" SP response-payload CRLF
     
response-payload= resp-cond-state / resp-cond-bye /
                       mailbox-data / message-data / capability-data
     
search          = "SEARCH" [search-return-opts]
                       [SP "CHARSET" SP astring] 1*(SP search-key)
                       ;; CHARSET argument to SEARCH MUST be
                       ;; registered with IANA
     
search-return-opts = "RETURN" SP "(" [search-return-opt
                          *(SP search-return-opt)] ")"
                          ;; SEARCH return options, e.g. MIN
     
search-return-opt = search-modifier-name
                            [SP search-mod-params]
     
search-modifier-name = tagged-ext-label
     
search-mod-params = <for extension only>
                       ;; This rule exists solely to be augmented by
                       ;; extensions via incremental alternative
                       ;; ("=/") rules.  It is strongly recommended
                       ;; that such extensions match a subset of the
                       ;; tagged-ext-val rule syntax
     
select          = "SELECT" SP mailbox [select-params]
                       ;; modifies the original IMAP SELECT command to
                       ;; accept optional parameters
     
select-params   = SP "(" select-param *(SP select-param) ")"
     
select-param    = select_param_name SP select_param_value
select_param_name= tagged-ext-label
     
select_param_value= <for extension only>
                       ;; This rule exists solely to be augmented by
                       ;; extensions via incremental alternative
                       ;; ("=/") rules.  It is strongly recommended
                       ;; that such extensions match a subset of the
                       ;; tagged-ext-val rule syntax
     
status-att-list = status-att-val *(SP status-att-val)
                       ;; Redefines status-att-list from RFC 3501
                       ;; status-att-val is defined in RFC 3501 errata
     
status-att-val  = ("MESSAGES" SP number) /
                       ("RECENT" SP number) /
                       ("UIDNEXT" SP nz-number) /
                       ("UIDVALIDITY" SP nz-number) /
                       ("UNSEEN" SP number)
                       ;; Extensions to the STATUS responses
                       ;; should extend this production.
     
store           = "STORE" SP sequence-set store-modifiers
                       SP store-att-flags
                       ;; extend [IMAP4] STORE command syntax
                       ;; to allow for optional store-modifiers
     
store-modifiers = [ SP "(" store-modifier *(SP store-modifier)
                         ")" ]
     
store-modifier  = store-modifier-name [SP store-modif-params]
     
store-modif-params = <for extension only>
                       ;; This rule exists solely to be augmented by
                       ;; extensions via incremental alternative
                       ;; ("=/") rules.  It is strongly recommended
                       ;; that such extensions match a subset of the
                       ;; tagged-ext-val rule syntax
     
store-modifier-name = tagged-ext-label
     
tagged-ext          = tagged-ext-label SP tagged-ext-val
                            ;; recommended overarching syntax for
                            ;; extensions
     
tagged-ext-label    = atom
                           ;; <<or should this be astring?>>
     
tagged-ext-comp     = astring /
                           tagged-ext-comp *(SP tagged-ext-comp) /
                           "(" tagged-ext-comp ")"
                            ;; extensions that follow this general
                            ;; syntax should use nstring instead of
                            ;; astring when appropriate in the context
                            ;; of the extension
     
tagged-ext-val      = astring / "(" [tagged-ext-comp] ")"
     
