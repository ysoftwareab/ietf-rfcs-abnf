field-def = name ["(" short-name ")"]
                 [":" sp ((length [";" sp (bool-expr /
                 (bool-expr ";" sp presence-constraint))]) /
                 (bool-expr [(";" sp presence-constraint)]) /
                 presence-constraint)] "."
presence-constraint = "present only when " bool-expr
constant = %x31-39 *(%x30-39)  ; natural numbers without leading 0s
short-name = ALPHA *(ALPHA / DIGIT / "-" / "_")
name = short-name *(" " short-name)
sp = [" "] ; optional space in expression
bool-expr = "(" sp bool-expr sp ")" /
              "!" sp bool-expr /
              bool-expr sp bool-op sp bool-expr /
              bool-expr sp "?" sp expr sp ":" sp expr /
              expr sp cmp-op sp expr
bool-op = "&&" / "||"
cmp-op = "==" / "!=" / "<" / "<=" / ">" / ">="
expr = "(" sp expr sp ")" /
         expr sp "^" sp expr /
         expr sp muldiv-op sp expr /
         expr sp addsub-op sp expr /
         bool-expr "?" expr ":" expr /
         name / short-name "." short-name /
         short-name "#" "Size" /
         constant
muldiv-op = "*" / "/" / "%"
addsub-op = "+" / "-"
length = expr " " unit / "[" sp name sp "]"
unit = %s"bit" / %s"bits" / %s"byte" / %s"bytes" / name

