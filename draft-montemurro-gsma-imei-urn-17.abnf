gsma-urn = "urn:" gsma-NID ":" gsma-NSS
gsma-NID = "gsma"
gsma-NSS = imei-specifier / future-gsma-specifier
imei-specifier = "imei:" imeival [ ";" sw-version-param ]
                                         [ ";" imei-version-param ]
sw-version-param = "svn=" software-version-string
imei-version-param = "vers=" imei-version-val
software-version-string = 2DIGIT
imei-version-val = DIGIT
future-gsma-specifier = gsma-defined-nonempty-string ;GSMA defined
                                                             ;and IETF consensus
                                                             ;required
gsma-defined-nonempty-string = 1*gsma-urn-char
gsma-urn-char  = ALPHA / DIGIT / "-" / "." / "_" / "%"/":"/";"/"="
imeival =  tac "-" snr "-" spare
tac      = 8DIGIT
snr      = 6DIGIT
spare    = DIGIT


