calname       = "CALENDAR-NAME" calnameparam ":" text CRLF

calnameparam  = *(
                           ;
                           ; The following are OPTIONAL,
                           ; but MUST NOT occur more than once.
                           ;
                           (";" altrepparam) / (";" languageparam) /
                           ;
                           ; The following is OPTIONAL,
                           ; and MAY occur more than once.
                           ;
                           (";" other-param)
                           ;
                           )

caldesc       = "CALENDAR-DESCRIPTION" caldescparam ":" text
                          CRLF

caldescparam  = *(
                           ;
                           ; The following are OPTIONAL,
                           ; but MUST NOT occur more than once.
                           ;
                           (";" altrepparam) / (";" languageparam) /
                           ;
                           ; The following is OPTIONAL,
                           ; and MAY occur more than once.
                           ;
                           (";" other-param)
                           ;
                           )

caluid       = "CALENDAR-UID" caluidparam ":" text CRLF

caluidparam  = *(";" other-param)

calurl       = "CALENDAR-URL" calurlparam ":" url CRLF

calurlparam  = *(";" other-param)

caltzid       = "CALENDAR-TZID" caltzidparam ":" [tzidprefix]
                          text CRLF

caltzidparam  = *(";" other-param)
calrefesh      = "CALENDAR-REFRESH-INTERVAL" calrefreshparam
                           ":" dur-value CRLF
                           ;consisting of a positive duration of time.

calrefeshparam = *(";" other-param)

calcolor       = "CALENDAR-COLOR" calcolorparam ":"
                           calcolorvalue CRLF

calcolorparam  = *(";" other-param)

calcolorvalue  = integer ";" integer ";" integer
                           ; Red, green, and blue values in the range
                           ; 0 - 255.

calimg     = "CALENDAR-IMAGE" calimgparam ( ":" uri ) /
                       (
                         ";" "ENCODING" "=" "BASE64"
                         ";" "VALUE" "=" "BINARY"
                         ":" binary
                       )
                       CRLF

calimgparam = *(
                         ;
                         ; The following is OPTIONAL for a URI value,
                         ; RECOMMENDED for a BINARY value,
                         ; and MUST NOT occur more than once.
                         ;
                         (";" fmttypeparam) /
                         ;
                         ; The following is OPTIONAL,
                         ; and MOST NOT occur more than once.
                         ;
                         (";" displayparam)
                         ;
                         ; The following is OPTIONAL,
                         ; and MAY occur more than once.
                         ;
                         (";" other-param)
                         ;
                         )
image      = "IMAGE" imageparam ( ":" uri ) /
                       (
                         ";" "ENCODING" "=" "BASE64"
                         ";" "VALUE" "=" "BINARY"
                         ":" binary
                       )
                       CRLF

imageparam = *(
                        ;
                        ; The following is OPTIONAL for a URI value,
                        ; RECOMMENDED for a BINARY value,
                        ; and MUST NOT occur more than once.
                        ;
                        (";" fmttypeparam) /
                        ;
                        ; The following is OPTIONAL,
                        ; and MOST NOT occur more than once.
                        ;
                        (";" displayparam)
                        ;
                        ; The following is OPTIONAL,
                        ; and MAY occur more than once.
                        ;
                        (";" other-param)
                        ;
                        )

displayparam = "DISPLAY" "="
                     ("BADGE" /       ; A small "badge" image
                      "BACKGROUND" /  ; Use as a background image
                      "OVERLAY" /     ; Use as an overlay image
                      "BANNER" /      ; Use as a "banner" across the top
                      x-name /        ; Experimental type
                      iana-token)     ; Other IANA registered type
                                      ;
                                      ; Default is BADGE

