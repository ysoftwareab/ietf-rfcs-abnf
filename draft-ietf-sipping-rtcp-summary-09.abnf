VQReportEvent  =  AlertReport /  SessionReport / IntervalReport

SessionReport = "VQSessionReport" [HCOLON CallTerm] CRLF
                LocalMetrics [CRLF RemoteMetrics]
                [DialogID]

IntervalReport = "VQIntervalReport" [HCOLON CallTerm] CRLF
                LocalMetrics [CRLF RemoteMetrics]
                [DialogID]

LocalMetrics  = "LocalMetrics" HCOLON CRLF Metrics

RemoteMetrics = "RemoteMetrics" HCOLON CRLF Metrics

AlertReport   = "VQAlertReport" HCOLON
          MetricType WSP Severity WSP Direction CRLF
          LocalMetrics [CRLF RemoteMetrics]
          [DialogID]

Metrics = TimeStamps CRLF

      [SessionDescription CRLF]
       CallID CRLF
       FromID CRLF
       ToID CRLF
       OrigID CRLF
       LocalAddr CRLF
       RemoteAddr CRLF
       [LocalMACAddr CRLF]
       [RemoteMACAddr CRLF]
       [JitterBuffer CRLF]
       [PacketLoss CRLF]
       [BurstGapLoss CRLF]
       [Delay CRLF]
       [Signal CRLF]
       [QualityEstimates CRLF]
       *(Extension CRLF)

TimeStamps = "Timestamps" HCOLON StartTime WSP StopTime
StartTime  = "START" EQUAL date-time
StopTime   = "STOP" EQUAL date-time

SessionDescription  = "SessionDesc" HCOLON
       [PayloadType WSP]
       [MediaSubtype WSP]
       [PayloadDesc WSP]
       [SampleRate WSP]
       [PacketsPerSecond WSP]
       [FrameDuration WSP]
       [FrameOctets WSP]
       [FramesPerPacket WSP]
       [FmtpOptions WSP]
       [PacketLossConcealment WSP]
       [SilenceSuppressionState]
       *(WSP Extension)

PayloadType  = "PT" EQUAL (1*3DIGIT)

PayloadDesc  = "PD" EQUAL (word / DQUOTE word-plus DQUOTE)

SampleRate = "SR" EQUAL (1*6DIGIT) *(SEMI (1*66DIGIT))

FrameDuration = "FD" EQUAL (1*4DIGIT)

FrameOctets  = "FO" EQUAL (1*5DIGIT)

FramesPerPacket = "FPP" EQUAL (1*2DIGIT)

PacketsPerSecond = "PPS" EQUAL (1*5DIGIT)

FmtpOptions = "FMTP" EQUAL DQUOTE word-plus DQUOTE

PacketLossConcealment  = "PLC" EQUAL ("0" / "1" / "2" / "3")

SilenceSuppressionState  = "SSUP" EQUAL ("on" / "off")

CallID  =  "CallID" HCOLON Call-ID-Parm

FromID = "FromID" HCOLON (name-addr/addr-spec)

ToID = "ToID" HCOLON (name-addr/addr-spec)

OrigID = "OrigID" HCOLON (name-addr/addr-spec)

LocalAddr   = "LocalAddr" HCOLON IPAddress WSP Port WSP Ssrc

RemoteAddr  = "RemoteAddr" HCOLON IPAddress WSP Port WSP Ssrc

LocalMACAddr   = "LocalMAC" HCOLON hex2 *(":" hex2)

RemoteMACAddr   = "RemoteMAC" HCOLON hex2 *(":" hex2)

IPAddress   = "IP" EQUAL IPv6address / IPv4address
Port        = "PORT" EQUAL 1*DIGIT
Ssrc        = "SSRC" EQUAL ( ''0x'' 1*8HEXDIG)
JitterBuffer = "JitterBuffer" HCOLON

       [JitterBufferAdaptive WSP]
       [JitterBufferRate WSP]
       [JitterBufferNominal WSP]
       [JitterBufferMax WSP]
       [JitterBufferAbsMax]
       *(WSP Extension)

JitterBufferAdaptive  = "JBA" EQUAL ("0" / "1" / "2" / "3")

JitterBufferRate      = "JBR" EQUAL (1*2DIGIT) ;0-15
JitterBufferNominal   = "JBN" EQUAL (1*5DIGIT) ;0-65535
JitterBufferMax       = "JBM" EQUAL (1*5DIGIT) ;0-65535
JitterBufferAbsMax    = "JBX" EQUAL (1*5DIGIT) ;0-65535

PacketLoss = "PacketLoss" HCOLON
               [NetworkPacketLossRate WSP]
               [JitterBufferDiscardRate]
               *(WSP Extension)

NetworkPacketLossRate =

      "NLR" EQUAL (1*3DIGIT ["." 1*2DIGIT]) ;percentage

JitterBufferDiscardRate =

      "JDR" EQUAL (1*3DIGIT ["." 1*2DIGIT]) ;percentage

BurstGapLoss = "BurstGapLoss" HCOLON

       [BurstLossDensity WSP]
       [BurstDuration WSP]
       [GapLossDensity WSP]
       [GapDuration WSP]
       [MinimumGapThreshold]
       *(WSP Extension)

BurstLossDensity =
     "BLD" EQUAL (1*3DIGIT ["." 1*2DIGIT]) ;percentage

BurstDuration =
     "BD" EQUAL (1*7DIGIT) ;0-3,600,000 -- milliseconds

GapLossDensity =
     "GLD" EQUAL (1*3DIGIT ["." 1*2DIGIT]) ;percentage

GapDuration =
     "GD" EQUAL (1*7DIGIT) ;0-3,600,000 -- milliseconds

MinimumGapThreshold =
     "GMIN" EQUAL (1*3DIGIT) ;1-255

Delay = "Delay" HCOLON
       [RoundTripDelay WSP]
       [EndSystemDelay WSP]
       [OneWayDelay WSP]
       [SymmOneWayDelay WSP]
       [InterarrivalJitter WSP]
       [MeanAbsoluteJitter]
       *(WSP Extension)

RoundTripDelay = "RTD" EQUAL (1*5DIGIT) ;0-65535

EndSystemDelay = "ESD" EQUAL (1*5DIGIT) ;0-65535

OneWayDelay = "OWD" EQUAL (1*5DIGIT) ;0-65535

SymmOneWayDelay = "SOWD" EQUAL (1*5DIGIT); 0-65535

InterarrivalJitter = "IAJ" EQUAL (1*5DIGIT) ;0-65535 ms

MeanAbsoluteJitter = "MAJ" EQUAL (1*5DIGIT);0-65535

Signal = "Signal" HCOLON
       [SignalLevel WSP]
       [NoiseLevel WSP]
       [ResidualEchoReturnLoss]
       *(WSP Extension)

SignalLevel = "SL" EQUAL (["-"] 1*2DIGIT)

NoiseLevel  = "NL" EQUAL (["-"] 1*2DIGIT)

ResidualEchoReturnLoss = "RERL" EQUAL (1*3DIGIT)

QualityEstimates  = "QualityEst" HCOLON
       [ListeningQualityR WSP]
       [RLQEstAlg WSP]
       [ConversationalQualityR WSP]
       [RCQEstAlg WSP]
       [ExternalR-In WSP]
       [ExtRInEstAlg WSP]
       [ExternalR-Out WSP]
       [ExtROutEstAlg WSP]
       [MOS-LQ WSP]
       [MOSLQEstAlg WSP]
       [MOS-CQ WSP]
       [MOSCQEstAlg WSP]
       [QoEEstAlg]
       *(WSP Extension)

ListeningQualityR = "RLQ" EQUAL (1*3DIGIT) ; 0 - 120

RLQEstAlg = "RLQEstAlg" EQUAL word ; "P.564", or other

ConversationalQualityR = "RCQ" EQUAL (1*3DIGIT) ; 0 - 120

RCQEstAlg = "RCQEstAlg" EQUAL word ; "P.564", or other

ExternalR-In = "EXTRI" EQUAL (1*3DIGIT) ; 0 - 120

ExtRInEstAlg = "ExtRIEstAlg" EQUAL word ; "P.564" or other

ExternalR-Out = "EXTRO" EQUAL (1*3DIGIT) ; 0 - 120
ExtROutEstAlg = "ExtROEstAlg" EQUAL word ; "P.564" or other

MOS-LQ = "MOSLQ" EQUAL (DIGIT ["." 1*3DIGIT]) ; 0.0 - 4.9

MOSLQEstAlg = "MOSLQEstAlg" EQUAL word ; "P.564" or other

MOS-CQ = "MOSCQ" EQUAL (DIGIT ["." 1*3DIGIT])  ; 0.0 - 4.9

MOSCQEstAlg = "MOSCQEstAlg" EQUAL word ; "P.564" or other

QoEEstAlg = "QoEEstAlg" EQUAL word ; "P.564" or other

DialogID  = "DialogID" COLON Call-ID-Parm *(SEMI did-parm)

did-parm  = to-tag / from-tag / word

to-tag    = "to-tag" EQUAL token

from-tag  = "from-tag" EQUAL token

MetricType = "Type" EQUAL "RLQ" / "RCQ" / "EXTR" /
       "MOSLQ" / "MOSCQ" /
       "BD" / "NLR" / "JDR" /
       "RTD" / "ESD" / "IAJ" /
       "RERL" / "SL" / "NL" / Extension

Direction = "Dir" EQUAL "local" / "remote"
Severity  = "Severity" EQUAL "Warning" / "Critical" /
       "Clear"

Call-ID-Parm =  word [ "@" word ]

CRLF =  %x0D.0A
DIGIT =  %x30-39
WSP   =  SP / HTAB ; white space
SP    =  " "
HTAB  =  %x09 ; horizontal tab
HEXDIG =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F" /
                 "a" / "b" / "c" / "d" / "e" / "f"
DQUOTE  =  %x22 ; " (Double Quote)
ALPHA   =  %x41-5A / %x61-7A   ; A-Z / a-z

alphanum  =  ALPHA / DIGIT
LWS  =  [*WSP CRLF] 1*WSP ; linear whitespace
SWS  =  [LWS] ; sep whitespace
SEMI =  SWS ";" SWS ; semicolon
EQUAL   =  SWS "=" SWS ; equal
COLON   =  SWS ":" SWS ; colon
HCOLON  =  *( SP / HTAB ) ":" SWS

token       =  1*(alphanum / "-" / "." / "!" / "%" / "*"
                      / "_" / "+" / "`" / "'" / "~" )

IPv4address   =  1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6address   =  hexpart [ ":" IPv4address ]
hexpart       =  hexseq / hexseq "::" [ hexseq ] / "::"
                          [ hexseq ]
hexseq        =  hex4 *( ":" hex4)
hex4          =  1*4HEXDIG
hex2          =  2HEXDIG

date-fullyear   = 4DIGIT ; e.g. 2006
date-month      = 2DIGIT ; e.g. 01 or 11
date-mday       = 2DIGIT ; e.g. 02 or 22
time-hour       = 2DIGIT ; e.g. 01 or 13
time-minute     = 2DIGIT ; e.g. 03 or 55
time-second     = 2DIGIT ; e.g. 01 or 59
time-secfrac    = "." 1*DIGIT
time-numoffset  = ("+" / "-") time-hour ":" time-minute
time-offset     = "Z" / time-numoffset
partial-time = time-hour ":" time-minute ":" time-second
full-date    = date-fullyear "-" date-month "-" date-mday
full-time    = partial-time time-offset
date-time    = full-date "T" full-time
Extension = word-plus

word  =  1*(alphanum / "-" / "." / "!" / "%" / "*" /
       "_" / "+" / "`" / "'" / "~" /
       "(" / ")" / "<" / ">" /
       ":" / "\" / DQUOTE /
       "/" / "[" / "]" / "?" )

word-plus =  1*(alphanum /  "-"  /  "."  /  "!"  / "%" / "*" /
       "_"  /  "+"  /  "`"  /  "'"  /  "~"  /
       "("  /  ")"  /  "<"  /  ">"  /  ":"  /
       "\"  /  "/"  /  "["  /  "]"  /  "?"  /
       "{"  /  "}"  /  "="  /  " ")


PLC=3 SSUP=on
QoEEstAlg=P.564
PLC=3 SSUP=on
QoEEstAlg=P.564
from-tag=9123dh311


FMTP="annexb=no" PLC=3 SSUP=on
FMTP="annexb=no" PLC=3 SSUP=on
from-tag=9123dh311

FMTP="annexb=no" PLC=3 SSUP=on
QoEEstAlg=P.564

FMTP="annexb=no" PLC=3 SSUP=on
from-tag=9123dh311

PLC=3 SSUP=on
QoEEstAlg=P.564

PLC=3 SSUP=on
QoEEstAlg=P.564
from-tag=9123dh3111
