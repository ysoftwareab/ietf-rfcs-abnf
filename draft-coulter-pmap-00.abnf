command-line = verb * ( SP argument ) CRLF

response-line = status-char ( SP error-keyword /
         * ( SP success-parameter ) ) [ SP comment ] [ CRLF
         multi-line-list-reply ] CRLF

success-response-line = "+" * ( SP success-parameter )
         [ SP comment ] CRLF

failure-response-line = "-" SP ( "SYN" / "GEN" / "ID" / "AUTH" /
         "MAX" ) [ SP comment ] CRLF

command = "PMAP" CRLF

failure-response = ( "421" / "500" / "502" ) [ SP comment ] CRLF
success-response = "+" SP digest-context [ SP comment ] CRLF

command = "AUTH" SP username SP ( password / digest ) CRLF

success-response = "+" [ SP comment ] CRLF

failure-response = "-" SP "AUTH" [ SP comment ] CRLF

command = "NEW" CRLF

success-response = "+" SP proxy-id [ SP comment ] CRLF

failure-response = "-" SP "MAX" [ SP comment ] CRLF

command = "DEL" SP proxy-id CRLF
failure-response = "-" SP "ID" [ SP comment ] CRLF

success-response = "+" [ SP comment ] CRLF

command = "SUS" SP proxy-id CRLF

success-response = "+" [ SP comment ] CRLF

failure-response = "-" SP "ID" [ SP comment ] CRLF

command = "REM" SP proxy-id SP remark CRLF

success-response = "+" [ SP comment ] CRLF

failure-response = "-" SP "ID" [ SP comment ] CRLF

command = "STAT" [ SP proxy-id ] CRLF

account-success-response = "+" SP mailbox SP owned-proxies
         SP max-proxies [ SP comment ] CRLF

proxy-success-response = "+" SP suspended SP remark
         [ SP comment ] CRLF

failure-response = "-" SP "ID" [ SP comment ] CRLF

command = "LIST" CRLF

failure-response = "-" SP ( "SYN" / "GEN" / "AUTH" )
         [ SP comment ] CRLF

success-response = "+" [ SP comment ] * ( CRLF proxy-id ) CRLF

command = "DONE" CRLF

success-response = "220" [ SP comment ] CRLF

failure-response = "-" SP "SYN" [ SP comment ] CRLF

alphanum = ALPHA / DIGIT

proxy-id = 8 alphanum

remark = 1*64 VCHAR / DQUOTE *64 vqchar DQUOTE

vqchar = "\" DQUOTE / "\\" / %d32-33 / %d35-91 / %d93-126

command = ( open-session / authenticate / new-proxy /
      delete-proxy / toggle-suspension / set-remark /
      get-account-status / get-proxy-status / list-proxies /
      close-session ) CRLF

open-session = "PMAP"  ; valid in SMTP session only
authenticate = "AUTH" SP username SP ( password / digest )

new-proxy = "NEW"

delete-proxy = "DEL" SP proxy-id

toggle-suspension = "SUS" SP proxy-id

set-remark = "REM" SP proxy-id SP remark

get-account-status = "STAT"

get-proxy-status = "STAT" SP proxy-id

list-proxies = "LIST"

close-session = "DONE"

username = 1* VCHAR

password = 1* VCHAR

digest = 16 HEXDIG

response = ( success / failure ) CRLF

success = ( "+" ( open-session-success / authenticate-success /
      new-proxy-success / delete-proxy-success /
      toggle-suspension-success / set-remark-success /
      get-account-status-success / get-proxy-status-success /
      list-proxies-success ) ) / close-session-success

failure = ( open-session-failure / ( "-"
      SP ( authenticate-failure / new-proxy-failure /
      delete-proxy-failure / toggle-suspension-failure /
      set-remark-failure / get-account-status-failure /
      get-proxy-status-failure / list-proxies-failure /
      close-session-failure ) ) ) [ SP comment ]

open-session-success = SP digest-context [ SP comment ]

open-session-failure = "421" / "500" / "502"
      ; generated in SMTP session

authenticate-success = [ SP comment ]

authenticate-failure = syntax-error / general-error /
      authentication-error
new-proxy-success = SP proxy-id [ SP comment ]

new-proxy-failure = syntax-error / general-error /
      authentication-error / proxy-limit-error

delete-proxy-success = [ SP comment ]

delete-proxy-failure = syntax-error / general-error /
      identifier-error / authentication-error

toggle-suspension-success = [ SP comment ]

toggle-suspension-failure = syntax-error / general-error /
      identifier-error / authentication-error

set-remark-success = [ SP comment ]

set-remark-failure = syntax-error / general-error /
      identifier-error / authentication-error

get-account-status-success = SP mailbox SP owned-proxies
      SP max-proxies [ SP comment ]

get-account-status-failure = syntax-error / general-error /
      authentication-error

get-proxy-status-success = SP suspended SP remark [ SP comment ]

get-proxy-status-failure = syntax-error / general-error /
      identifier-error / authentication-error

list-proxies-success = [ SP comment ] * ( CRLF proxy-id )

list-proxies-failure = syntax-error / general-error /
      authentication-error

close-session-success = "220" [ SP comment ]
      ; generated in SMTP session

close-session-failure = syntax-error

comment = * ncchar

digest-context = 64 VCHAR

proxy-id = 8 alphanum

mailbox = <standard Internet mail address>

proxies = number

max-proxies = number
suspended = BIT

number = 1* DIGIT

ncchar = SP / VCHAR

syntax-error = "SYN"

general-error = "GEN"

identifier-error = "ID"

authentication-error = "AUTH"

proxy-limit-error = "MAX"

