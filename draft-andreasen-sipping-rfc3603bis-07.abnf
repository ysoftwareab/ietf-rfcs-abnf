P-DCS-Trace-Party-ID = "P-DCS-Trace-Party-ID" HCOLON name-addr *1(SEMI
timestamp-param      = "timestamp=" 1*DIGIT ["." 1*DIGIT]
trace-param          = generic-param ;generic-param defined in RFC3261
   This document adds the following entry to Table 2 of [RFC3261]:

   Header field         where proxy  ACK  BYE  CAN  INV  OPT  REG  PUB
   ------------         ----- -----  ---  ---  ---  ---  ---  ---  ---
   P-DCS-Trace-Party-ID   R    dmr    -    -    -    o    -    -    -
                                     SUB  NOT  REF  INF  UPD  PRA  MSG
                                     ---  ---  ---  ---  ---  ---  ---
                                      -    -    -    -    -    -    -


   The addr-spec contained in name-addr contains a URL that identifies
   the remote endpoint.  Addr-spec typically contains a tel URL or SIP
   URI giving the identity of the remote endpoint, as provided in the
   signaling messages that established the session to be traced.

   The timestamp-param contains the value of the time the UA determines
   it received the session initiation request of the call requested to
   be traced.  The timestamp-param is populated using the Network Time
   Protocol timestamp format defined in RFC 1305 [RFC1305] and used by
   the Simple Network Time Protocol [RFC4330].  The timestamp SHOULD be
   encoded in UTF-8 Format per [RFC3629].The trace-param is a generic
   parameter for future extensions.

   An example of the P-DCS-Trace-Party-ID header is shown as follows:

   P-DCS-Trace-Party-ID: sip:+12345678912@domain.com; user=phone;
timestamp=3434688831.2327

P-DCS-OSPS      = "P-DCS-OSPS" HCOLON OSPS-Tag
OSPS-Tag        = "BLV" / "EI" / "RING" / token

P-DCS-Billing-Info      = "P-DCS-Billing-Info" HCOLON
                              Billing-Correlation-ID "/" FEID
                              *(SEMI Billing-Info-param)
Billing-Correlation-ID  = 1*48(HEXDIG)
FEID                    = 1*16(HEXDIG) "@" host
Billing-Info-param      = RKS-Group-ID-param / Charge-param /
                             Calling-param / Called-param /
                             Routing-param / Loc-Routing-param /
                             JIP-param / generic-param
RKS-Group-ID-param      = "rksgroup" EQUAL RKS-Group-ID
RKS-Group-ID            = token
Charge-param            = "charge" EQUAL Acct-Charge-URI
Acct-Charge-URI         = LDQUOT addr-spec RDQUOT
Calling-param           = "calling" EQUAL Acct-Calling-URI
Acct-Calling-URI        = LDQUOT addr-spec RDQUOT
Called-param            = "called" EQUAL Acct-Called-URI
Acct-Called-URI         = LDQUOT addr-spec RDQUOT
Routing-param           = "routing" EQUAL Acct-Routing-URI
Acct-Routing-URI        = LDQUOT addr-spec RDQUOT
Loc-Routing-param       = "locroute" EQUAL Acct-Loc-Routing-URI
Acct-Loc-Routing-URI    = LDQUOT addr-spec RDQUOT
JIP-param               = "jip" EQUAL jip
jip                     = LDQUOT 1*phonedigit-hex jip-context RDQUOT
jip-context             = ";jip-context=" jip-descriptor
jip-descriptor          = global-hex-digits
global-hex-digits       = "+" 1*3(phonedigit) *phonedigit-hex
phonedigit              = DIGIT / [ visual-separator ]
phonedigit-hex          = HEXDIG / "*" / "#" / [ visual-separator ]
visual-separator        = "-" / "." / "(" / ")"

P-DCS-LAES        = "P-DCS-LAES" HCOLON Laes-sig
                          *(SEMI Laes-param)
Laes-sig          = hostport
Laes-param        = Laes-content / Laes-cccid
                         Laes-bcid / generic-param
Laes-content      = "content" EQUAL hostport

Laes-bcid         = "bcid" EQUAL 1*48(HEXDIG)
Laes-cccid        = "cccid" EQUAL 1*8(HEXDIG)

P-DCS-Redirect    = "P-DCS-Redirect" HCOLON Called-ID
                         *(SEMI redir-params)
Called-ID         = LDQUOT addr-spec RDQUOT
redir-params      = redir-uri-param / redir-count-param /
                         generic-param
redir-uri-param   = "redirector-uri" EQUAL Redirector
Redirector        = LDQUOT addr-spec RDQUOT
redir-count-param = "count" EQUAL Redir-count
Redir-count       = 1*DIGIT

