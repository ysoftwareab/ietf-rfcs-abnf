OWS           = <OWS, defined in [Part1], Section 3.2.1>
quoted-string = <quoted-string, defined in [Part1], Section 3.2.4>
token         = <token, defined in [Part1], Section 3.2.4>

field-name    = <field-name, defined in [Part1], Section 3.2>
HTTP-date     = <HTTP-date, defined in [Part2], Section 8>
port          = <port, defined in [Part1], Section 2.7>
pseudonym     = <pseudonym, defined in [Part1], Section 6.2>
uri-host      = <uri-host, defined in [Part1], Section 2.7>

delta-seconds  = 1*DIGIT

response_is_fresh = (freshness_lifetime > current_age)

apparent_age = max(0, response_time - date_value);

response_delay = response_time - request_time;
corrected_age_value = age_value + response_delay;

corrected_initial_age = max(apparent_age, corrected_age_value);

resident_time = now - response_time;
current_age = corrected_initial_age + resident_time;

Age = delta-seconds

Cache-Control   = 1#cache-directive

cache-directive = cache-request-directive
        / cache-response-directive

cache-extension = token [ "=" ( token / quoted-string ) ]

cache-request-directive =
          "no-cache"
        / "no-store"
        / "max-age" "=" delta-seconds
        / "max-stale" [ "=" delta-seconds ]
        / "min-fresh" "=" delta-seconds
        / "no-transform"
        / "only-if-cached"
        / cache-extension

cache-response-directive =
          "public"
        / "private" [ "=" DQUOTE 1#field-name DQUOTE ]
        / "no-cache" [ "=" DQUOTE 1#field-name DQUOTE ]
        / "no-store"
        / "no-transform"
        / "must-revalidate"
        / "proxy-revalidate"
        / "max-age" "=" delta-seconds
        / "s-maxage" "=" delta-seconds
        / cache-extension

Expires = HTTP-date

Pragma           = 1#pragma-directive
pragma-directive = "no-cache" / extension-pragma
extension-pragma = token [ "=" ( token / quoted-string ) ]

Vary = "*" / 1#field-name

Warning       = 1#warning-value

warning-value = warn-code SP warn-agent SP warn-text
                                           [SP warn-date]

warn-code  = 3DIGIT
warn-agent = ( uri-host [ ":" port ] ) / pseudonym
                     ; the name or pseudonym of the server adding
                     ; the Warning header field, for use in debugging
warn-text  = quoted-string
warn-date  = DQUOTE HTTP-date DQUOTE

Age = delta-seconds

Cache-Control = *( "," OWS ) cache-directive *( OWS "," [ OWS
    cache-directive ] )

Expires = HTTP-date

HTTP-date = <HTTP-date, defined in [Part2], Section 8>

OWS = <OWS, defined in [Part1], Section 3.2.1>

Pragma = *( "," OWS ) pragma-directive *( OWS "," [ OWS
    pragma-directive ] )

Vary = "*" / ( *( "," OWS ) field-name *( OWS "," [ OWS field-name ]
    ) )

Warning = *( "," OWS ) warning-value *( OWS "," [ OWS warning-value ]
    )

cache-directive = cache-request-directive / cache-response-directive
cache-extension = token [ "=" ( token / quoted-string ) ]
cache-request-directive = "no-cache" / "no-store" / ( "max-age="
    delta-seconds ) / ( "max-stale" [ "=" delta-seconds ] ) / (
    "min-fresh=" delta-seconds ) / "no-transform" / "only-if-cached" /
    cache-extension
cache-response-directive = "public" / ( "private" [ "=" DQUOTE *( ","
    OWS ) field-name *( OWS "," [ OWS field-name ] ) DQUOTE ] ) / (
    "no-cache" [ "=" DQUOTE *( "," OWS ) field-name *( OWS "," [ OWS
    field-name ] ) DQUOTE ] ) / "no-store" / "no-transform" /
    "must-revalidate" / "proxy-revalidate" / ( "max-age=" delta-seconds
    ) / ( "s-maxage=" delta-seconds ) / cache-extension

delta-seconds = 1*DIGIT

extension-pragma = token [ "=" ( token / quoted-string ) ]

field-name = <field-name, defined in [Part1], Section 3.2>

port = <port, defined in [Part1], Section 2.7>
pragma-directive = "no-cache" / extension-pragma
pseudonym = <pseudonym, defined in [Part1], Section 6.2>

quoted-string = <quoted-string, defined in [Part1], Section 3.2.4>

token = <token, defined in [Part1], Section 3.2.4>

uri-host = <uri-host, defined in [Part1], Section 2.7>

warn-agent = ( uri-host [ ":" port ] ) / pseudonym
warn-code = 3DIGIT
warn-date = DQUOTE HTTP-date DQUOTE
warn-text = quoted-string
warning-value = warn-code SP warn-agent SP warn-text [ SP warn-date
    ]

