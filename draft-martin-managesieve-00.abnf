QUOTED-CHAR           = SAFE-UTF8-CHAR / "

QUOTED-SPECIALS       = <"> / "

SAFE-UTF8-CHAR        = SAFE-CHAR / UTF8-2 / UTF8-3 / UTF8-4 /
                            UTF8-5 / UTF8-6

auth-type             = <"> auth-type-name <">

auth-type-name        = iana-token
                            ;; as defined in SASL [SASL]

command               = command-authenticate / command-logout / command-getscript /
                            command-setactive / command-listscripts / command-deletescript /
                            command-putscript / command-capability / command-havespace /
                            command-starttls

command-authenticate  = "AUTHENTICATE" SP auth-type [SP string]  *(CRLF string)

command-capability    = "CAPABILITY" CRLF

command-deletescript  = "DELETESCRIPT" SP sieve-name CRLF

command-getscript     = "GETSCRIPT" SP sieve-name CRLF

command-havespace     = "HAVESPACE" SP sieve-name SP number CRLF

command-listscripts   = "LISTSCRIPTS" CRLF

command-logout        = "LOGOUT" CRLF

command-putscript     = "PUTSCRIPT" SP sieve-name SP string CRLF

command-setactive     = "SETACTIVE" SP sieve-name CRLF

command-starttls      = "STARTTLS" CRLF

literal               = "{" number [ "+" ] "}" CRLF *OCTET
                            ;; The number represents the number of octets
                            ;; MUST be literal-utf8 except for values

number                = *DIGIT
                            ;; A 32-bit unsigned number.
                            ;; (0 <= n < 4,294,967,296)

quoted                = <"> *QUOTED-CHAR <">
                            ;; limited to 1024 octets between the <">s

response              = response-authenticate / response-logout / response-getscript /
                            response-setactive / response-listscripts / response-deletescript /
                            response-putscript / response-capability / response-havespace /
                            response-starttls

response-authenticate = *(string CRLF / response-okno / response-special-ok)

response-capability   = *(string [SP string] CRLF) response-okno

response-deletescript = response-okno

response-getscript    = [string CRLF] response-okno

response-havespace    = response-okno
response-listscripts  = *(sieve-name [SP "ACTIVE"] CRLF) response-okno

response-logout       = response-okno

response-okno         = ("OK" / "NO") [SP string] CRLF

response-putscript    = response-okno

response-setactive    = response-okno

response-special-ok   = "OK" SP string SP string CRLF

response-starttls     = response-okno

sieve-name            =  string

string                = quoted / literal









