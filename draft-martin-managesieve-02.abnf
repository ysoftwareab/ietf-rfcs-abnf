SAFE-CHAR             = %x01-09 / %x0B-0C / %x0E-21 / %x23-5B / %x5D-7F
                            ;; any TEXT-CHAR except QUOTED-SPECIALS

QUOTED-CHAR           = SAFE-UTF8-CHAR / "

QUOTED-SPECIALS       = <"> / "

SAFE-UTF8-CHAR        = SAFE-CHAR / UTF8-2 / UTF8-3 / UTF8-4 /
                            UTF8-5 / UTF8-6

UTF8-1                = %x80-BF

UTF8-2                = %xC0-DF UTF8-1

UTF8-3                = %xE0-EF 2UTF8-1

UTF8-4                = %xF0-F7 3UTF8-1

UTF8-5                = %xF8-FB 4UTF8-1

UTF8-6                = %xFC-FD 5UTF8-1

auth-type             = <"> auth-type-name <">

auth-type-name        = iana-token
                            ;; as defined in SASL [SASL]

command               = command-authenticate / command-logout / command-getscript /
                            command-setactive / command-listscripts / command-deletescript /
                            command-putscript / command-capability / command-havespace /
                            command-starttls

command-authenticate  = "AUTHENTICATE" SP auth-type [SP string]  *(CRLF string)

command-capability    = "CAPABILITY" CRLF

command-deletescript  = "DELETESCRIPT" SP sieve-name CRLF

command-getscript     = "GETSCRIPT" SP sieve-name CRLF

command-havespace     = "HAVESPACE" SP sieve-name SP number CRLF

command-listscripts   = "LISTSCRIPTS" CRLF

command-logout        = "LOGOUT" CRLF

command-putscript     = "PUTSCRIPT" SP sieve-name SP string CRLF
command-setactive     = "SETACTIVE" SP sieve-name CRLF

command-starttls      = "STARTTLS" CRLF

literal               = "{" number  "+}" CRLF *OCTET
                            ;; The number represents the number of octets
                            ;; MUST be literal-utf8 except for values

number                = *DIGIT
                            ;; A 32-bit unsigned number.
                            ;; (0 <= n < 4,294,967,296)

quoted                = <"> *QUOTED-CHAR <">
                            ;; limited to 1024 octets between the <">s

resp-code             = "AUTH-TOO-WEAK" / "ENCRYPT-NEEDED" /
                            "QUOTA" / resp-code-sasl /
                            "TRANSITION-NEEDED" / "TRYLATER" /
                            resp-code-ext

resp-code-sasl        = "SASL" SP string

resp-code-ext         = iana-token [SP extension-data]
                            ;; unknown codes MUST be tolerated by the client

response              = response-authenticate / response-logout / response-getscript /
                            response-setactive / response-listscripts / response-deletescript /
                            response-putscript / response-capability / response-havespace /
                            response-starttls

response-authenticate = *(string CRLF) (response-okno)

response-capability   = *(string [SP string] CRLF) response-okno

response-deletescript = response-okno

response-getscript    = [string CRLF] response-okno

response-havespace    = response-okno

response-listscripts  = *(sieve-name [SP "ACTIVE"] CRLF) response-okno

response-logout       = response-okno

response-okno         = ("OK" / "NO") [SP "(" resp-code ")"] [SP string] CRLF

response-putscript    = response-okno
response-setactive    = response-okno

response-starttls     = response-okno

sieve-name            =  string

string                = quoted / literal









