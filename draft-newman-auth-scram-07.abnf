generic-message = attr-val *("," attr-val)

attr-val        = ALPHA "=" value

value           = *(value-char)

value-safe-char = %20-2B / %2D-3C / %3E-7E /
                        UTF8-2 / UTF-3 / UTF8-4
                        ;; UTF8-char except CTL, "=", and ",".

value-char      = value-safe-char / "="

base64-char     = ALPHA / DIGIT / "/" / "+"

base64-4        = 4*4(base64-char)

base64-3        = 3*3(base64-char) "="

base64-2        = 2*2(base64-char) "=="

base64          = *(base64-4) [base64-3 / base64-2]

saslname        = 1*(value-safe-char / "=2C" / "=3D")
                        ;; Conforms to <value>
authzid         = "a=" saslname
                        ;; Protocol specific.

username        = "n=" saslname
                        ;; Usernames are prepared using SASLPrep.

mandatory-exts  = "m=" 1*(value-char)
                        ;; Mandatory extensions. The exact syntax
                        ;; will be defined in the future.

channel-binding = "c=" base64

proof           = "p=" base64

nonce           = "r=" value [value]
                        ;; Second part provided by server.

salt            = "s=" base64

verifier        = "v=" base64
                        ;; base-64 encoded ServerSignature.

iteration-count = "i=" (%x31-39) *DIGIT
                        ;; A positive number

client-first-message =
                        [mandatory-exts ","] [authzid ","] username ","
                        nonce ["," extensions]

server-first-message =
                        [mandatory-exts ","] nonce "," salt ","
                        iteration-count ["," extensions]

client-final-message-without-proof =
                        nonce "," channel-binding ["," extensions]

client-final-message =
                        client-final-message-without-proof "," proof

server-final-message =
                        verifier ["," extensions]

extensions =
                        <One or more attribute=value separated by
                        commas.
                        All extensions are optional, i.e. unrecognized
                        attributes not defined in this document
                        MUST be ignored.>
