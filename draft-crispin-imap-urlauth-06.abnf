changekey       = "CHANGEKEY" SP mailbox [SP mechansim]

command-auth    =/ changekey / signurl / urlfetch

mechanism       = "HMAC-MD5" / "INTERNAL" / atom
                   ; new mechanisms MUST be registered with IANA

resp-text-code  =/ "MAILBOXKEY" SP mechanism SP base64

signurl         = "SIGNURL" 1*(SP url SP mechanism)

signurl-data    = "*" SP "SIGNURL" 1*(SP url)

url             = astring
                   ; contains imapurl as defined below

urlfetch        = "URLFETCH" SP url

urlfetch-data   = "*" SP "URLFETCH" SP url SP nstring

   The following modifications are made to the Formal Syntax in [IMAPURL]:

enc-urlauth     = 32*HEXDIG

imessagepart    = enc_mailbox [uidvalidity] iuid [isection] [iurlauth]

iurlauth        = iauth [iexpire] ";URLAUTH=" mechanism ":" enc-urlauth

iauth           = (";AUTHID=" iuserauth) / (";AUTHROLE=" iauthrole)

iauthrole       =  ("submit" [":" iuserauth]) / "authuser" / "anonymous"

iexpire         = ";EXPIRE=" date-time
                    ; date-time defined in [DATETIME]


