challenge = auth-scheme 1*SP 1#auth-param

auth-scheme = token

auth-param = token "=" (token | quoted-string)

realm = "realm" "=" realm-value

realm-value = quoted-string

credentials = auth-scheme #auth-param

sasl-token      = SASL 1*SP 1#sasl-parameter
sasl-parameter  = sasl-mechanism | sasl-sid | realm
                     | sasl-challenge | sasl-credential
        ; Note: realm is defined in [RFC2617]

SASLCHAR        = UPALPHA | DIGIT | "-" | "_"
        ; Characters allowed in SASL mechanism name

sasl-mech-name  = 1*20 SASLCHAR
        ; Name must be from IANA set of registered SASL mechanisms,
        ; e.g. "SECURID"

sasl-mechanism  = "mechanism" "=" <"> 1#sasl-mech-name <">

sasl-sid        = "id" "=" quoted-string

sasl-challenge  =  "challenge" "=" base64-string

sasl-credential = "credential" "=" base64-string | "*"

base64-string   = *BASE64 *2"="
        ; Encoding must be in accordance with [RFC2045], except not
        ; limited to 76 chars/line

BASE64          = DIGIT | ALPHA | "+" | "/"

