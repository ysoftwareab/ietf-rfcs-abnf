IPAddress      = IPv4Address / IPv6Address / "0"
IPv4Address    = 1*3DIGIT 3("." 1*3DIGIT)
IPv6Address    = 1*4HEXDIG 7( ":" 1*4HEXDIG)
Port           = 1*DIGIT
MID            = 1*DIGIT                    ; Message ID number

BID            = 1*DIGIT                    ; Binding ID number

PT             = "UDP" / "TCP" / "ICMP" / "ANY" ; IP protocol type
Authentication = 1*VCHAR
Challenge      = 1*VCHAR
Message        = 1*VCHAR
Timeout        = 1*DIGIT                    ; timeout in seconds
TPS            = IPAddress WSP Port WSP PT  ; transport param. set
TPS_in         = TPS                        ; internal TPS
TPS_ex         = TPS                        ; external TPS
Version        = "SNFC/1.0"

request =  "open"     WSP MID WSP Version WSP Authentication CRLF
request =/ "close"    WSP MID CRLF
request =/ "bind_in"  WSP MID WSP BID WSP TPS WSP Timeout CRLF
request =/ "bind_out" WSP MID WSP BID WSP TPS WSP Timeout CRLF

Reply =/ "410" WSP MID CRLF         ; syntax Error
Reply =/ "411" WSP MID CRLF         ; unknown or illegal request
Reply =/ "510" WSP Message CRLF     ; illegal message

Reply =  "220" WSP MID CRLF         ; OK
Reply =/ "420" WSP MID CRLF         ; protocol version mismatch
Reply =/ "421" WSP MID WSP Challenge CRLF  ; authentication failed
Reply =/ "520" WSP Message CRLF     ; session closed

Reply =/ "231" WSP MID WSP BID WSP TPS
                    WSP Timeout CRLF     ; OK for uni-direct. binding
Reply =/ "232" WSP MID WSP BID WSP TPS_in WSP TPS_ex
                    WSP Timeout CRLF     ; OK for full binding
Reply =/ "233" WSP MID WSP BID CRLF ; binding removed
Reply =/ "430" WSP MID CRLF         ; unknown or illegal BID
Reply =/ "431" WSP MID CRLF         ; binding Refused
Reply =/ "432" WSP MID CRLF         ; illegal IP Address
Reply =/ "433" WSP MID CRLF         ; protocol type not supported
Reply =/ "434" WSP MID CRLF         ; illegal port number
Reply =/ "435" WSP MID CRLF         ; cannot modify binding
Reply =/ "530" WSP BID CRLF         ; binding timed out
