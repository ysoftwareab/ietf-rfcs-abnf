availabilityc  = "BEGIN" ":" "VAVAILABILITY" CRLF
                           availabilityprop *availablec
                           "END" ":" "VAVAILABILITY" CRLF

availabilityprop  = *(

                            ; the following are REQUIRED,
                            ; but MUST NOT occur more than once

                            dtstamp / uid

                            ; the following are OPTIONAL,
                            ; but MUST NOT occur more than once

                            busytype / created / dtstart / last-mod /
                            organizer / seq / summary / url /

                            ; either 'dtend' or 'duration' may appear
                            ; in a 'availabilityprop', but 'dtend' and
                            ; 'duration' MUST NOT occur in the same
                            ; 'availabilityprop'

                            dtend / duration /

                            ; the following are OPTIONAL,
                            ; and MAY occur more than once

                            categories / comment / contact / x-prop

                            )

availablec  = "BEGIN" ":" "AVAILABLE" CRLF
                        availableprop
                        "END" ":" "AVAILABLE" CRLF

availableprop  = *(

                         ; the following are REQUIRED,
                         ; but MUST NOT occur more than once

                         dtstamp / dtstart / uid /

                         ; either a 'dtend' or a 'duration' is required
                         ; in a 'availableprop', but 'dtend' and
                         ; 'duration' MUST NOT occur in the same
                         ; 'availableprop', and each MUST NOT occur more
                         ; than once

                         dtend / duration /

                         ; the following are OPTIONAL,
                         ; but MUST NOT occur more than once

                         created / last-mod / recurid / rrule /
                         summary /

                         ; the following are OPTIONAL,
                         ; and MAY occur more than once

                         categories / comment / contact / exdate /
                         rdate / x-prop

                         )
busytype      = "BUSYTYPE" busytypeparam ":" busytypevalue CRLF

busytypeparam = *(";" xparam)

busytypevalue = "BUSY" / "BUSY-UNAVAILABLE" /
                        "BUSY-TENTATIVE" / iana-token / x-name
                        ; Default is "BUSY-UNAVAILABLE"

