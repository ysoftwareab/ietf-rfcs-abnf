ldif-file            = ldif-content / ldif-changes
ldif-content         = version-spec 1*(1*SEP ldif-attrval-record)

ldif-changes         = version-spec 1*(1*SEP ldif-change-record)

ldif-attrval-record  = dn-spec SEP 1*attrval-spec

ldif-change-record   = dn-spec SEP *control changerecord

version-spec         = "version:" FILL version-number

version-number       = 1*DIGIT  ; version-number MUST be "1" for the
                                   ; LDIF format described in this document.

dn-spec              = "dn:" (FILL dn / ":" FILL base64-dn)

dn                   = <a distinguished name, as defined in RFC 2253 [3]>

base64-dn            = <a dn which has been base-64 encoded, as
                          defined in RFC 1521 [5]>

rdn                  = <a relative distinguished name, as defined in RFC
                          2253 [3]>

base64-rdn           = <an rdn which has been base-64 encoded, as
                          defined in RFC 1521 [5]>

control              = "control: FILL ldap-oid            ; controlType
                          0*1 (FILL ("true" / "false"))      ; criticality
                          0*1(value-spec)                    ; controlValue
                          SEP
                          ; (See note 10, below)

ldap-oid             = <An LDAPOID, as defined in [4]>

attrval-spec         = attribute-description value-spec

value-spec           = ":" (    FILL 0*1(value) /
                               ":" FILL base64-value /
                               "<" FILL url) SEP

url                  = <a Uniform Resource Locator, as defined in [6]>
                                  ; (See Note 6, below)

attribute-description = <an attribute description, as defined in [4]>

value                = safe-initval *safe
                                  ; (See Note 9, below)
safe                 = <any value except NUL, CR or LF>

safe-initval         = <any value except NUL, CR, LF, colon (":", ASCII 58
                          decimal), SPACE, and less-than ("<" , ASCII 60
                          decimal)>

base64-value         = <base-64-encoded value, as defined in RFC 1521 [5]>

changerecord         = "changetype:" FILL (change-add / change-delete /
                                              change-modify / change-moddn)

change-add           = "add"                SEP 1*attrval-spec

change-delete        = "delete"             SEP

change-moddn         = ("modrdn" / "moddn") SEP
                          "newrdn:" (     FILL rdn /
                                      ":" FILL base-64-rdn) SEP
                          "deleteoldrdn:" FILL ("0" / "1")  SEP
                          0*1("newsuperior:"     FILL (dn /
                                             ":" FILL base-64-dn) SEP)

change-modify        = "modify"             SEP *mod-spec

mod-spec             = (mod-add-spec / mod-delete-spec / mod-replace-spec)
                          "-" SEP

mod-add-spec         = "add:"     FILL attribute-description SEP
                          *attrval-spec

mod-delete-spec      = "delete:"  FILL attribute-description SEP
                          *attrval-spec

mod-replace-spec     = "replace:" FILL attribute-description SEP
                          *attrval-spec

SPACE                = <ASCII SP, space>

FILL                 = 1*SPACE

SEP                  = (CR LF / LF)

CR                   = <ASCII CR, carriage return>

LF                   = <ASCII LF, line feed>

NUL                  = <ASCII NUL (0 decimal)>
DIGIT                = <any ASCII decimal digit (ASCII 48 - 57 decimal)>


