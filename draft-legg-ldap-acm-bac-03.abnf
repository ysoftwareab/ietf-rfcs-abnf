ACIItem = "{" sp aci-identificationTag ","
                 sp aci-precedence ","
                 sp aci-authenticationLevel ","
                 sp aci-itemOrUserFirst
                 sp "}"
aci-identificationTag   = id-identificationTag   msp
                                DirectoryString
aci-precedence          = id-precedence          msp Precedence
aci-authenticationLevel = id-authenticationLevel msp
                                AuthenticationLevel
aci-itemOrUserFirst     = id-itemOrUserFirst     msp
                                ItemOrUserFirst
id-identificationTag    = %x69.64.65.6E.74.69.66.69.63.61.74.69.6F
                                %x6E.54.61.67 ; "identificationTag"
id-precedence           = %x70.72.65.63.65.64.65.6E.63.65
                                ; "precedence"
id-authenticationLevel  = %x61.75.74.68.65.6E.74.69.63.61.74.69.6F
                                %x6E.4C.65.76.65.6C
                                ; "authenticationLevel"
id-itemOrUserFirst      = %x69.74.65.6D.4F.72.55.73.65.72.46.69.72
                                %x73.74 ; "itemOrUserFirst"

Precedence = INTEGER-0-MAX ; MUST be less than 256

AuthenticationLevel = al-basicLevels / al-other
al-basicLevels      = id-basicLevels ":" BasicLevels
al-other            = id-other       ":" EXTERNAL
id-basicLevels      = %x62.61.73.69.63.4C.65.76.65.6C.73
                            ; "basicLevels"
id-other            = %x6F.74.68.65.72 ; "other"

BasicLevels = "{"      sp bl-level
                    [ "," sp bl-localQualifier ]
                    [ "," sp bl-signed         ]
                          sp "}"

bl-level          = id-level          msp Level
bl-localQualifier = id-localQualifier msp INTEGER
bl-signed         = id-signed         msp BOOLEAN
Level             = id-none / id-simple / id-strong
id-level          = %x6C.65.76.65.6C ; "level"
id-localQualifier = %x6C.6F.63.61.6C.51.75.61.6C.69.66.69.65.72
                          ; "localQualifier"
id-signed         = %x73.69.67.6E.65.64 ; "signed"
id-none           = %x6E.6F.6E.65       ; "none"
id-simple         = %x73.69.6D.70.6C.65 ; "simple"
id-strong         = %x73.74.72.6F.6E.67 ; "strong"

ItemOrUserFirst    = ( id-itemFirst ":" ItemFirst ) /
                        ( id-userFirst ":" UserFirst )
id-itemFirst       = %x69.74.65.6D.46.69.72.73.74 ; "itemFirst"
id-userFirst       = %x75.73.65.72.46.69.72.73.74 ; "userFirst"
ItemFirst          = "{" sp if-protectedItems ","
                            sp if-itemPermissions
                            sp "}"
if-protectedItems  = id-protectedItems  msp ProtectedItems
if-itemPermissions = id-itemPermissions msp ItemPermissions
id-protectedItems  = %x70.72.6F.74.65.63.74.65.64.49.74.65.6D.73
                           ; "protectedItems"
id-itemPermissions = %x69.74.65.6D.50.65.72.6D.69.73.73.69.6F.6E
                           %x73 ; "itemPermissions"

UserFirst          = "{" sp uf-userClasses ","
                            sp uf-userPermissions
                            sp "}"
uf-userClasses     = id-userClasses     msp UserClasses
uf-userPermissions = id-userPermissions msp UserPermissions
id-userClasses     = %x75.73.65.72.43.6C.61.73.73.65.73
                           ; "userClasses"
id-userPermissions = %x75.73.65.72.50.65.72.6D.69.73.73.69.6F.6E
                           %x73 ; "userPermissions"

ItemPermissions     = "{" [ sp ItemPermission
                            *( "," sp ItemPermission ) ] sp "}"
ItemPermission      = "{" [ sp ip-precedence "," ]
                               sp ip-userClasses ","
                               sp ip-grantsAndDenials
                               sp "}"
ip-precedence       = id-precedence       msp Precedence
ip-userClasses      = id-userClasses      msp UserClasses
ip-grantsAndDenials = id-grantsAndDenials msp GrantsAndDenials
id-grantsAndDenials = %x67.72.61.6E.74.73.41.6E.64.44.65.6E.69.61
                            %x6C.73 ; "grantsAndDenials"

UserClasses  = "{"     [ sp uc-allUsers ]
                      [ sep sp uc-thisEntry ]
                      [ sep sp uc-name ]
                      [ sep sp uc-userGroup ]
                      [ sep sp uc-subtree ]
                            sp "}"
uc-allUsers  = id-allUsers  msp NULL
uc-thisEntry = id-thisEntry msp NULL
uc-name      = id-name      msp NameAndOptionalUIDs
uc-userGroup = id-userGroup msp NameAndOptionalUIDs
uc-subtree   = id-subtree   msp SubtreeSpecifications
id-allUsers  = %x61.6C.6C.55.73.65.72.73    ; "allUsers"
id-thisEntry = %x74.68.69.73.45.6E.74.72.79 ; "thisEntry"
id-name      = %x6E.61.6D.65                ; "name"
id-userGroup = %x75.73.65.72.47.72.6F.75.70 ; "userGroup"
id-subtree   = %x73.75.62.74.72.65.65       ; "subtree"
NameAndOptionalUIDs = "{" sp NameAndOptionalUID
                            *( "," sp NameAndOptionalUID ) sp "}"
NameAndOptionalUID  = "{"      sp nu-dn
                            [ "," sp nu-uid ]
                                  sp "}"
nu-dn               = id-dn  msp DistinguishedName
nu-uid              = id-uid msp UniqueIdentifier
UniqueIdentifier    = BIT-STRING
id-dn               = %x64.6E    ; "dn"
id-uid              = %x75.69.64 ; "uid"

SubtreeSpecifications = "{" sp SubtreeSpecification
                              *( "," sp SubtreeSpecification ) sp "}"

UserPermissions     = "{" [ sp UserPermission
                            *( "," sp UserPermission ) ] sp "}"
UserPermission      = "{" [ sp up-precedence "," ]
                               sp up-protectedItems ","
                               sp up-grantsAndDenials
                               sp "}"
up-precedence       = id-precedence       msp Precedence
up-protectedItems   = id-protectedItems   msp ProtectedItems
up-grantsAndDenials = id-grantsAndDenials msp GrantsAndDenials

ProtectedItems = "{"     [ sp pi-entry ]
                        [ sep sp pi-allUserAttributeTypes ]
                        [ sep sp pi-attributeType ]
                        [ sep sp pi-allAttributeValues ]
                        [ sep sp pi-allUserTypesAndValues ]
                        [ sep sp pi-attributeValue ]
                        [ sep sp pi-selfValue ]
                        [ sep sp pi-rangeOfValues ]
                        [ sep sp pi-maxValueCount ]
                        [ sep sp pi-maxImmSub ]
                        [ sep sp pi-restrictedBy ]
                        ; contexts omitted
                        [ sep sp pi-classes ]
                              sp "}"

pi-entry                 = id-entry msp NULL
pi-allUserAttributeTypes = id-allUserAttributeTypes msp NULL
pi-attributeType         = id-attributeType msp AttributeTypes
pi-allAttributeValues    = id-allAttributeValues msp
                                 AttributeTypes
pi-allUserTypesAndValues = id-allUserAttributeTypesAndValues msp
                                 NULL
pi-attributeValue        = id-attributeValue msp
                                 AttributeTypeAndValues
pi-selfValue             = id-selfValue msp AttributeTypes
pi-rangeOfValues         = id-rangeOfValues msp Filter
pi-maxValueCount         = id-maxValueCount msp MaxValueCounts
pi-maxImmSub             = id-maxImmSub msp INTEGER
pi-restrictedBy          = id-restrictedBy msp RestrictedValues
pi-classes               = id-classes msp Refinement
id-entry                 = %x65.6E.74.72.79 ; "entry"
id-allUserAttributeTypes = %x61.6C.6C.55.73.65.72.41.74.74.72.69
                                 %x62.75.74.65.54.79.70.65.73
                                 ; "allUserAttributeTypes"
id-attributeType         = %x61.74.74.72.69.62.75.74.65.54.79.70
                                 %x65 ; "attributeType"
id-allAttributeValues    = %x61.6C.6C.41.74.74.72.69.62.75.74.65
                                 %x56.61.6C.75.65.73
                                 ; "allAttributeValues"
id-attributeValue        = %x61.74.74.72.69.62.75.74.65.56.61.6C
                                 %x75.65 ; "attributeValue"
id-selfValue             = %x73.65.6C.66.56.61.6C.75.65
                                 ; "selfValue"
id-rangeOfValues         = %x72.61.6E.67.65.4F.66.56.61.6C.75.65
                                 %x73 ; "rangeOfValues"
id-maxValueCount         = %x6D.61.78.56.61.6C.75.65.43.6F.75.6E
                                 %x74 ; "maxValueCount"
id-maxImmSub             = %x6D.61.78.49.6D.6D.53.75.62
                                 ; "maxImmSub"
id-restrictedBy          = %x72.65.73.74.72.69.63.74.65.64.42.79
                                 ; "restrictedBy"
id-classes               = %x63.6C.61.73.73.65.73 ; "classes"

id-allUserAttributeTypesAndValues = %x61.6C.6C.55.73.65.72.41.74
                              %x74.72.69.62.75.74.65.54.79.70.65.73
                              %x41.6E.64.56.61.6C.75.65.73
                              ; "allUserAttributeTypesAndValues"

AttributeTypes = "{" sp AttributeType
                       *( "," sp AttributeType ) sp "}"

AttributeTypeAndValues = "{" sp AttributeTypeAndValue
                               *( "," sp AttributeTypeAndValue )
                               sp "}"

AttributeTypeAndValue = "{" sp atav-type ","
                               sp atav-value
                               sp "}"
atav-type  = id-type  msp AttributeType
atav-value = id-value msp Value
id-type    = %x74.79.70.65    ; "type"
id-value   = %x76.61.6C.75.65 ; "value"
MaxValueCounts = "{" sp MaxValueCount
                       *( "," sp MaxValueCount ) sp "}"
MaxValueCount  = "{" sp mvc-type ","
                        sp mvc-maxCount
                        sp "}"
mvc-type       = id-type msp AttributeType
mvc-maxCount   = id-maxCount msp INTEGER
id-maxCount    = %x6D.61.78.43.6F.75.6E.74 ; "maxCount"

RestrictedValues = "{" sp RestrictedValue
                         *( "," sp RestrictedValue ) sp "}"
RestrictedValue  = "{" sp rv-type ","
                          sp rv-valuesin
                          sp "}"
rv-type          = id-type     msp AttributeType
rv-valuesin      = id-valuesin msp AttributeType
id-valuesin      = %x76.61.6C.75.65.73.69.6E ; "valuesin"

GrantsAndDenials = "{" [ sp grantOrDeny
                         *( "," sp grantOrDeny ) ] sp "}"
grantOrDeny = id-grantAdd
                 / id-denyAdd
                 / id-grantDiscloseOnError
                 / id-denyDiscloseOnError
                 / id-grantRead
                 / id-denyRead
                 / id-grantRemove
                 / id-denyRemove
                 / id-grantBrowse
                 / id-denyBrowse
                 / id-grantExport
                 / id-denyExport
                 / id-grantImport
                 / id-denyImport
                 / id-grantModify
                 / id-denyModify
                 / id-grantRename
                 / id-denyRename
                 / id-grantReturnDN
                 / id-denyReturnDN
                 / id-grantCompare
                 / id-denyCompare
                 / id-grantFilterMatch
                 / id-denyFilterMatch
                 ; grantInvoke omitted
                 ; denyInvoke omitted

id-grantAdd     = %x67.72.61.6E.74.41.64.64 ; "grantAdd"
id-denyAdd      = %x64.65.6E.79.41.64.64 ; "denyAdd"
id-grantBrowse  = %x67.72.61.6E.74.42.72.6F.77.73.65
                        ; "grantBrowse"
id-denyBrowse   = %x64.65.6E.79.42.72.6F.77.73.65 ; "denyBrowse"
id-grantCompare = %x67.72.61.6E.74.43.6F.6D.70.61.72.65
                        ; "grantCompare"
id-denyCompare  = %x64.65.6E.79.43.6F.6D.70.61.72.65
                        ; "denyCompare"

id-grantDiscloseOnError = %x67.72.61.6E.74.44.69.73.63.6C.6F.73.65
                                %x4F.6E.45.72.72.6F.72
                                ; "grantDiscloseOnError"
id-denyDiscloseOnError  = %x64.65.6E.79.44.69.73.63.6C.6F.73.65.4F
                                %x6E.45.72.72.6F.72
                                ; "denyDiscloseOnError"

id-grantExport      = %x67.72.61.6E.74.45.78.70.6F.72.74
                            ; "grantExport"
id-denyExport       = %x64.65.6E.79.45.78.70.6F.72.74
                            ; "denyExport"
id-grantFilterMatch = %x67.72.61.6E.74.46.69.6C.74.65.72.4D.61.74
                            %x63.68 ; "grantFilterMatch"
id-denyFilterMatch  = %x64.65.6E.79.46.69.6C.74.65.72.4D.61.74.63
                            %x68 ; "denyFilterMatch"
id-grantImport      = %x67.72.61.6E.74.49.6D.70.6F.72.74
                            ; "grantImport"
id-denyImport       = %x64.65.6E.79.49.6D.70.6F.72.74
                            ; "denyImport"
id-grantModify      = %x67.72.61.6E.74.4D.6F.64.69.66.79
                            ; "grantModify"
id-denyModify       = %x64.65.6E.79.4D.6F.64.69.66.79
                            ; "denyModify"
id-grantRead        = %x67.72.61.6E.74.52.65.61.64 ; "grantRead"
id-denyRead         = %x64.65.6E.79.52.65.61.64 ; "denyRead"
id-grantRemove      = %x67.72.61.6E.74.52.65.6D.6F.76.65
                            ; "grantRemove"
id-denyRemove       = %x64.65.6E.79.52.65.6D.6F.76.65
                            ; "denyRemove"
id-grantRename      = %x67.72.61.6E.74.52.65.6E.61.6D.65
                            ; "grantRename"
id-denyRename       = %x64.65.6E.79.52.65.6E.61.6D.65
                            ; "denyRename"
id-grantReturnDN    = %x67.72.61.6E.74.52.65.74.75.72.6E.44.4E
                            ; "grantReturnDN"
id-denyReturnDN     = %x64.65.6E.79.52.65.74.75.72.6E.44.4E
                            ; "denyReturnDN"

Filter      = filter-item / filter-and / filter-or / filter-not
filter-item = id-item ":" FilterItem
filter-and  = id-and  ":" SetOfFilter
filter-or   = id-or   ":" SetOfFilter
filter-not  = id-not  ":" Filter
id-and      = %x61.6E.64    ; "and"
id-item     = %x69.74.65.6D ; "item"
id-not      = %x6E.6F.74    ; "not"
id-or       = %x6F.72       ; "or"

SetOfFilter = "{" [ sp Filter *( "," sp Filter ) ] sp "}"

FilterItem = fi-equality
                / fi-substrings
                / fi-greaterOrEqual
                / fi-lessOrEqual
                / fi-present
                / fi-approximateMatch
                / fi-extensibleMatch
                ; contextPresent omitted

fi-equality         = id-equality ":" AttributeValueAssertion
fi-substrings       = id-substrings ":" SubstringsAssertion
fi-greaterOrEqual   = id-greaterOrEqual ":"
                            AttributeValueAssertion
fi-lessOrEqual      = id-lessOrEqual ":" AttributeValueAssertion
fi-present          = id-present ":" AttributeType
fi-approximateMatch = id-approximateMatch ":"
                            AttributeValueAssertion
fi-extensibleMatch  = id-extensibleMatch ":" MatchingRuleAssertion
id-equality         = %x65.71.75.61.6C.69.74.79 ; "equality"
id-substrings       = %x73.75.62.73.74.72.69.6E.67.73
                            ; "substrings"
id-greaterOrEqual   = %x67.72.65.61.74.65.72.4F.72.45.71.75.61.6C
                            ; "greaterOrEqual"
id-lessOrEqual      = %x6C.65.73.73.4F.72.45.71.75.61.6C
                            ; "lessOrEqual"
id-present          = %x70.72.65.73.65.6E.74 ; "present"
id-approximateMatch = %x61.70.70.72.6F.78.69.6D.61.74.65.4D.61.74
                            %x63.68 ; "approximateMatch"
id-extensibleMatch  = %x65.78.74.65.6E.73.69.62.6C.65.4D.61.74.63
                            %x68 ; "extensibleMatch"

AttributeValueAssertion = "{" sp ava-type ","
                                 sp ava-assertion
                                 ; assertedContexts omitted
                                 sp "}"

ava-type      = id-type      msp AttributeType
ava-assertion = id-assertion msp Value
id-assertion  = %x61.73.73.65.72.74.69.6F.6E ; "assertion"

SubstringsAssertion = "{" sp sa-type ","
                             sp sa-strings
                             sp "}"

sa-type    = id-type    msp AttributeType
sa-strings = id-strings msp Substrings
id-strings = %x73.74.72.69.6E.67.73 ; "strings"

Substrings = "{" [ sp Substring *( "," sp Substring ) ] sp "}"
Substring  = ss-initial
                / ss-any
                / ss-final
                ; control omitted
ss-initial = id-initial ":" Value
ss-any     = id-any     ":" Value
ss-final   = id-final   ":" Value
id-initial = %x69.6E.69.74.69.61.6C ; "initial"
id-any     = %x61.6E.79             ; "any"
id-final   = %x66.69.6E.61.6C       ; "final"

MatchingRuleAssertion = "{"      sp mra-matchingRule
                              [ "," sp mra-type ]
                                "," sp mra-matchValue
                              [ "," sp mra-dnAttributes ]
                                    sp "}"

mra-matchingRule = id-matchingRule msp MatchingRuleIds
mra-type         = id-type         msp AttributeType
mra-matchValue   = id-matchValue   msp Value
mra-dnAttributes = id-dnAttributes msp BOOLEAN
id-matchingRule  = %x6D.61.74.63.68.69.6E.67.52.75.6C.65
                         ; "matchingRule"
id-matchValue    = %x6D.61.74.63.68.56.61.6C.75.65 ; "matchValue"
id-dnAttributes  = %x64.6E.41.74.74.72.69.62.75.74.65.73
                         ; "dnAttributes"
MatchingRuleIds = "{" sp MatchingRuleId *( "," sp MatchingRuleId ) sp "}"
MatchingRuleId  = OBJECT-IDENTIFIER

