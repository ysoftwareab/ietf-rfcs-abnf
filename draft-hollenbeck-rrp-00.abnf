space = %x20 ; " "
dot = %x2E ; "."
dash = %x2D ; "-"
underscore = %x5F ; "_"
colon = %x3A ; ":"
cr = %x0D ; ASCII carriage return
lf = %x0A ; ASCII linefeed
crlf = cr lf
alpha = %x41-5A / %x61-7A ; A-Z / a-z
digit = %x30-39 ; 0-9
dns-char = alpha / digit / dash
id-char = alpha / digit / underscore / dash
id-prefix = alpha / digit
id-word = id-prefix *id-char
printable-char = %x20-7E ; ASCII " " - "~"

year = 4digit
month = 2digit
day = 2digit
ymd = year dash month dash day
hour = 2digit
minute = 2digit
second = 2digit
milli-second = 3digit
hms = hour colon minute colon second dot milli-second
time-stamp = ymd space hms
ip-address = 1*3digit dot 1*3digit dot 1*3digit dot 1*3digit
password = 4*16printable-char
option-name = 1*128id-word
option-tag = dash option-name
option-value = 1*128id-word
attribute-name = 1*128id-word
attribute-value = 1*128printable-char
attribute-line = attribute-name colon attribute-value crlf
response = 3digit space 1*printable-char crlf
version-number = "RRP" space 1*digit dot 1*digit dot 1*digit
label = id-prefix *61dns-char id-prefix
sldn = label dot label
servername = *(label dot) sldn
period = %x31-39 / (%x31-39 %x30-39) ; "1" - "9" or "10" - "99"
period-option = dash "Period" colon period crlf
yesno = "Yes" / "No"

rrp = add / check / delete / describe / mod / quit / renew /
        session / status / transfer
add = add-request add-response
check = check-request check-response
delete = del-request del-response
describe = describe-request describe-response
mod = mod-request mod-response
quit = quit-request quit-response
renew = renew-request renew-response
session = session-request session-response
status = status-request status-response
transfer = transfer-request transfer-response

add-request = add-domain-request / add-nameserver-request
add-response = add-domain-response / add-nameserver-response
add-domain-request = "add" crlf
   "EntityName" colon "Domain" crlf
   "DomainName" colon sldn crlf
   [period-option]
   0*13("NameServer" colon servername crlf)
   dot crlf
add-nameserver-request = "add" crlf
   "EntityName" colon "NameServer" crlf
   "NameServer" colon servername crlf
   1*("IPAddress" colon ip-address crlf)
   dot crlf
add-domain-response = response
   "RegistrationExpirationDate" colon time-stamp crlf
   "status" colon 1*digit crlf
   dot crlf
add-nameserver-response = response
   dot crlf

check-request = check-domain-request / check-nameserver-request
check-response = check-domain-response / check-nameserver-response
check-domain-request = "check" crlf
   "EntityName" colon "Domain" crlf
   "DomainName" colon sldn crlf
   dot crlf
check-nameserver-request = "check" crlf
   "EntityName" colon "NameServer" crlf
   "NameServer" colon servername crlf
   dot crlf
check-domain-response = response
   dot crlf
check-nameserver-response = available-check-nameserver-response /
                              notavailable-check-nameserver-response
available-check-nameserver-response = response
   dot crlf
notavailable-check-nameserver-response = response 1*("IPAddress" colon ip-address crlf)
   dot crlf

del-request = del-domain-request / del-nameserver-request
del-response = response
   dot crlf
del-domain-request = "del" crlf
   "EntityName" colon "Domain" crlf
   "DomainName" colon sldn crlf
   dot crlf
del-nameserver-request = "del" crlf
   "EntityName" colon "NameServer" crlf
   "NameServer" colon servername crlf
   dot crlf

describe-request = "describe" crlf
   [target-option]
   *(option-tag colon option-value crlf)
   dot crlf
describe-response = response
   "Protocol" colon version-number crlf
   *attribute-line
   dot crlf
target-option = dash "Target" colon "Protocol" crlf

mod-request = mod-domain-request / mod-nameserver-request
mod-response = response
   *attribute-line
   dot crlf
mod-domain-request = "mod" crlf
   "EntityName" colon "Domain" crlf
   "DomainName" colon sldn crlf
   *(add-attribute-value-line /
   remove-attribute-value-line /
   replace-attribute-value-line)
   dot crlf
mod-nameserver-request = "mod" crlf
   "EntityName" colon "NameServer" crlf
   "NameServer" colon servername crlf
   ["NewNameServer" colon attribute-value crlf]
   *(add-attribute-value-line /
   remove-attribute-value-line /
   replace-attribute-value-line)
   dot crlf
add-attribute-value-line =
   attribute-name colon new-attribute-value
remove-attribute-value-line =
   attribute-name colon old-attribute-value "="
replace-attribute-value-line =
   attribute-name colon old-attribute-value "="
   new-attribute-value
old-attribute-value = attribute-value
new-attribute-value = attribute-value

quit-request = "quit" crlf
   dot crlf
quit-response = response
   dot crlf

renew-request = "renew" crlf
   "EntityName" colon "Domain" crlf
   "DomainName" colon sldn crlf
   [renew-period-option]
   dot crlf
expiration-year-option = dash "CurrentExpirationYear" colon year crlf
renew-period-option = period-option expiration-year-option /
                        expiration-year-option period-option
renew-response = response
   "RegistrationExpirationDate" colon time-stamp crlf
   dot crlf

session-request = "session" crlf
   registrar-id-option
   registrar-password-option
   [registrar-newpassword-option]
   dot crlf
session-response = response
   dot crlf
registrar-id-option = dash "Id" colon option-value crlf
registrar-password-option =
   dash "Password" colon password crlf
registrar-newpassword-option =
   dash "NewPassword" colon password crlf

status-request = status-domain-request /
                   status-nameserver-request
status-response = response
   *attribute-line
   dot crlf
status-domain-request = "status" crlf
   "EntityName" colon "Domain" crlf
   "DomainName" colon sldn crlf
   dot crlf
status-nameserver-request = "status" crlf
   "EntityName" colon "NameServer" crlf
   "NameServer" colon servername crlf
   dot crlf

transfer-request = "transfer" crlf
   [approve-option]
   "EntityName" colon "Domain" crlf
   "DomainName" colon sldn crlf
   dot crlf
transfer-response = response
   "RegistrationExpirationDate" colon time-stamp crlf
   dot crlf
approve-option = dash "Approve" colon yesno crlf

