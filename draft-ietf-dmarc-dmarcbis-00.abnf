MSA = Mail Submission Agent
MDA = Mail Delivery Agent

dmarc-uri       = URI [ "!" 1*DIGIT [ "k" / "m" / "g" / "t" ] ]
                       ; "URI" is imported from [RFC3986]; commas (ASCII
                       ; 0x2C) and exclamation points (ASCII 0x21)
                       ; MUST be encoded; the numeric portion MUST fit
                       ; within an unsigned 64-bit integer

dmarc-record    = dmarc-version dmarc-sep
                       [dmarc-request]
                       [dmarc-sep dmarc-srequest]
                       [dmarc-sep dmarc-auri]
                       [dmarc-sep dmarc-furi]
                       [dmarc-sep dmarc-adkim]
                       [dmarc-sep dmarc-aspf]
                       [dmarc-sep dmarc-ainterval]
                       [dmarc-sep dmarc-fo]
                       [dmarc-sep dmarc-rfmt]
                       [dmarc-sep dmarc-percent]
                       [dmarc-sep]
                       ; components other than dmarc-version and
                       ; dmarc-request may appear in any order

dmarc-version   = "v" *WSP "=" *WSP %x44 %x4d %x41 %x52 %x43 %x31

dmarc-sep       = *WSP %x3b *WSP

dmarc-request   = "p" *WSP "=" *WSP
                       ( "none" / "quarantine" / "reject" )

dmarc-srequest  = "sp" *WSP "=" *WSP
                       ( "none" / "quarantine" / "reject" )

dmarc-auri      = "rua" *WSP "=" *WSP
                       dmarc-uri *(*WSP "," *WSP dmarc-uri)

dmarc-furi      = "ruf" *WSP "=" *WSP
                       dmarc-uri *(*WSP "," *WSP dmarc-uri)

dmarc-adkim     = "adkim" *WSP "=" *WSP
                       ( "r" / "s" )

dmarc-aspf      = "aspf" *WSP "=" *WSP
                       ( "r" / "s" )

dmarc-ainterval = "ri" *WSP "=" *WSP 1*DIGIT

dmarc-fo        = "fo" *WSP "=" *WSP
                       ( "0" / "1" / "d" / "s" )
                       *(*WSP ":" *WSP ( "0" / "1" / "d" / "s" ))
dmarc-rfmt      = "rf"  *WSP "=" *WSP Keyword *(*WSP ":" Keyword)
                       ; registered reporting formats only

dmarc-percent   = "pct" *WSP "=" *WSP
                       1*3DIGIT

ruf=mailto:auth-reports@example.com"

ruf=mailto:auth-reports@thirdparty.example.net"

rua=mailto:dmarc-feedback@example.com"

boundary="----=_NextPart_000_024E_01CC9B0A.AFE54C00"
filename="mail.receiver.example!example.com!
                   1013662812!1013749130.gz"

