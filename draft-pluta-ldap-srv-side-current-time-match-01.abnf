DurationValue  = DQUOTE designations DQUOTE
designations   =   P week-fraction
                       / P year-fraction
                       / P [ year ] month-fraction
                       / P [ year ] [ month ] day-fraction
                       / P [ year ] [ month ] [ day ] hours-mins-sec

P              = %x50 ; "P"

hours-mins-sec =   T hours-fraction
                       / T [ hours ] mins-fraction
                       / T [ hours ] [ mins ] sec-fraction

T              = %x54 ; "T"

week-fraction  = number [ fraction ] %x57 ; "W"
year-fraction  = number [ fraction ] %x59 ; "Y"
year           = number %x59 ; "Y"
month-fraction = number [ fraction ] %x4D ; "M"
month          = number %x4D ; "M"
day-fraction   = number [ fraction ] %x4D ; "D"
day            = number %x44 ; "D"
hours-fraction = number [ fraction ] %x48 ; "H"
hours          = number %x48 ; "H"
mins-fraction  = number [ fraction ] %x4D ; "M"
mins           = number %x4D ; "M"
sec-fraction   = number [ fraction ] %x53 ; "S"

number         = %x30 / ( %x31-39 *( %x30-39 ) )
fraction       = ( DOT / COMMA ) 1*( %x30-39 )


