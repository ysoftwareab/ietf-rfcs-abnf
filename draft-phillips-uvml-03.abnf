ballot                 = [text] 1*(vote [text]) [signature] [text]
vote                   = rating / ranking
rating                 = HASH target score
ranking                = 2HASH contest selections
signature              = 3HASH profile
target                 = name / this
score                  = [undecided] valuation
name                   = tag *( PERIOD tag )
this                   = T H I S
contest                = name
selections             = 1*25selection
selection              = *HWS [rank] HASH name [[undecided]
                          valuation]
rank                   = DIGIT / "1" DIGIT / "2" ZEROTOFIVE
undecided              = QUESTION
valuation              = quality / importance / outlook
valuation              =/ support-opposition / likelihood
profile                = [age [HYPHEN]] gender [jurisdiction]
age                    = 1*3DIGIT
gender                 = iso-5218-gender
jurisdiction           = [country] [region] [area-code]
country                = [HYPHEN] iso-3166-country
region                 = [HYPHEN] iso-3166-subdivision
area-code              = [HYPHEN] 3*4DIGIT
tag                    = name-begin *70name-inner name-end
name-begin             = LETTER / UPPER-ASCII / UNICODE
name-inner             = name-begin / DIGIT
name-end               = name-inner
quality                = among-the-very-best / very-good
quality                =/ good / fair / poor
quality                =/ very-poor / among-the-very-worst
importance             = highest-importance / very-important
importance             =/ important / unimportant / irrelevant
outlook                = never-more-optimistic / very-optimistic
outlook                =/ optimistic / pessimistic
outlook                =/ very-pessimistic / never-more-pessimistic
support-opposition     = strongly-support / support
support-opposition     =/ somewhat-support / somewhat-oppose
support-opposition     =/ oppose / strongly-oppose
likelihood             = definitely / very-likely / likely
likelihood             =/ unlikely / very-unlikely / definitely-not
among-the-very-best    = 5*10STAR
very-good              = 4STAR
good                   = 3STAR
fair                   = 2STAR
poor                   = 1STAR
very-poor              = STAR MINUS
among-the-very-worst   = STAR 2*9MINUS
highest-importance     = 3*10BANG
very-important         = 2BANG
important              = BANG
unimportant            = BANG MINUS
irrelevant             = BANG 2*9MINUS
never-more-optimistic  = 3*10CURRENCY
very-optimistic        = 2CURRENCY
optimistic             = CURRENCY
pessimistic            = CURRENCY MINUS
very-pessimistic       = CURRENCY 2MINUS
never-more-pessimistic = CURRENCY 3*9MINUS
CURRENCY               = DOLLAR / EURO / POUND / YUAN-YEN
strongly-support       = 3*10PLUS
support                = 2PLUS
somewhat-support       = PLUS
somewhat-oppose        = MINUS
oppose                 = 2MINUS
strongly-oppose        = 3*10MINUS
definitely             = 3*10PERCENT
very-likely            = 2PERCENT
likely                 = PERCENT
unlikely               = PERCENT MINUS
very-unlikely          = PERCENT 2MINUS
definitely-not         = PERCENT 3*9MINUS
iso-5218-gender        = male / female
iso-3166-country       = 2LETTER
iso-3166-subdivision   = 1*2DIGIT / 2*3LETTER
male                   = M
female                 = F
BANG                   = %x21
HASH                   = %x23
DOLLAR                 = %x24
PERCENT                = %x25
AMPERSAND              = %x26
APOSTROPHE             = %x27
STAR                   = %x2A
PLUS                   = %x2B
MINUS                  = %x2D
HYPHEN                 = %x2D
PERIOD                 = %x2E
SLASH                  = %x2F
QUESTION               = %x3F
UNDERSCORE             = %x5F
EURO                   = %x80
POUND                  = %xA3
YUAN-YEN               = %xA5
M                      = "M" / "m"
F                      = "F" / "f"
T                      = "T" / "t"
H                      = "H" / "h"
I                      = "I" / "i"
S                      = "S" / "s"
LETTERDIGIT            = LETTER / DIGIT
LETTER                 = "A" / "B" / "C" / "D" / "E" / "F" / "G"
LETTER                 =/ "H" / "I" / "J" / "K" / "L" / "M" / "N"
LETTER                 =/ "O" / "P" / "Q" / "R" / "S" / "T" / "U"
LETTER                 =/ "V" / "W" / "X" / "Y" / "Z"
LETTER                 =/ "a" / "b" / "c" / "d" / "e" / "f" / "g"
LETTER                 =/ "h" / "i" / "j" / "k" / "l" / "m" / "n"
LETTER                 =/ "o" / "p" / "q" / "r" / "s" / "t" / "u"
LETTER                 =/ "v" / "w" / "x" / "y" / "z"
ONETONINE              = "1" / "2" / "3" / "4" / "5" / "6" / "7" /
                          "8" / "9"
ZEROTOFIVE             = "0" / "1" / "2" / "3" / "4" / "5"
UPPER-ASCII            = %xC0-FF
UNICODE                = PLANE0
PLANE0                 = %x0100-D7FF / %xE000-FDCF
PLANE0                 =/ %xFDF0-FFFD
