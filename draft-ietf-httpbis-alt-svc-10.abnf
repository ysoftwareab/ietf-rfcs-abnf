DIGIT         = <DIGIT, see [RFC5234], Appendix B.1>
OWS           = <OWS, see [RFC7230], Section 3.2.3>
delta-seconds = <delta-seconds; see [RFC7234], Section 1.2.1>
port          = <port, see [RFC7230], Section 2.7>
quoted-string = <quoted-string, see [RFC7230], Section 3.2.6>
token         = <token, see [RFC7230], Section 3.2.6>
uri-host      = <uri-host, see [RFC7230], Section 2.7>

Alt-Svc       = clear / 1#alt-value
clear         = %x63.6C.65.61.72; "clear", case-sensitive
alt-value     = alternative *( OWS ";" OWS parameter )
alternative   = protocol-id "=" alt-authority
protocol-id   = token ; percent-encoded ALPN protocol name
alt-authority = quoted-string ; containing [ uri-host ] ":" port
parameter     = token "=" ( token / quoted-string )

ma = delta-seconds; see [RFC7234], Section 1.2.1

persist = 1DIGIT

Alt-Used     = uri-host [ ":" port ]

