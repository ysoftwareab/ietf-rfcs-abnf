absolute-IRI-reference  = absolute-IRI [ "#" ifragment ]

IRI-reference  = [ absolute-IRI / relative-IRI ]
    	                    [ "#" ifragment ]
absolute-IRI   = scheme ":" ( ihier-part / iopaque-part )
relative-IRI   = [ inet-path / iabs-path / irel-path ]
    	                    [ "?" iquery ]

ihier-part     = [ inet-path / iabs-path ] [ "?" iquery ]
iopaque-part   = iric-no-slash *iric

iric-no-slash  = iunreserved / escaped / "[" / "]" / ";" / "?" /
                         ":" / "@" / "&" / "=" / "+" / "$" / ","

inet-path      = "//" iauthority [ iabs-path ]
iabs-path      = "/"  ipath-segments
irel-path      = irel-segment [ iabs-path ]

irel-segment   = 1*( iunreserved / escaped / ";" /
                            "@" / "&" / "=" / "+" / "$" / "," )

iauthority     = iserver / ireg-name

ireg-name      = 1*( iunreserved / escaped / ";" /
                            ":"  / "@" / "&" / "=" /  "+" / "$" / "," )

iserver        = [ [ iuserinfo "@" ] ihostport ]
iuserinfo      = *( iunreserved / escaped / ";" /
                           ":" / "&" / "=" / "+" / "$" / "," )

ihostport      = ihost [ ":" port ]
ihost          = IPv6reference / IPv4address / ihostname

ihostname      = << as specified by [RFCXXXX] >>

ipath          = [ iabs-path / iopaque-part ]
ipath-segments = isegment *( "/" isegment )
isegment       = *ipchar

ipchar         = iunreserved / escaped / ";" /
                         ":" / "@" / "&" / "=" / "+" / "$" / ","
iquery         = *( ipchar / "/" / "?" )
ifragment      = *( ipchar / "/" / "?" )

iric           = reserved / iunreserved / escaped
iunreserved    = ichar / unreserved
ichar          = idelims / ucschar / " " / "{" / "}" / "|"
    	                  / "\" / "^" / "`"
idelims        = "<" / ">" / DQUOTE
ucschar        = %xA0-D7FF / %xF900-FDCF / %xFDF0-FFEF /
                         / %x10000-1FFFD / %x20000-2FFFD / %x30000-3FFFD
                         / %x40000-4FFFD / %x50000-5FFFD / %x60000-6FFFD
                         / %x70000-7FFFD / %x80000-8FFFD / %x90000-9FFFD
                         / %xA0000-AFFFD / %xB0000-BFFFD / %xC0000-CFFFD
                         / %xD0000-DFFFD / %xE1000-EFFFD

scheme        = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )
port          = *DIGIT
alphanum      = ALPHA / DIGIT

IPv4address   = dec-octet 3( "." dec-octet )
dec-octet     = DIGIT /                         ; 0-9
                        ( %x31-39 DIGIT ) /             ; 10-99
                        ( "1" 2*DIGIT ) /               ; 100-199
                        ( "2" %x30-34 DIGIT ) /         ; 200-249
                        ( "25" %x30-35 )                ; 250-255

IPv6reference = "[" IPv6address "]"
IPv6address   = (                       7( h4 ":" )   h4   ) /
                        (                "::" 0*6( h4 ":" ) [ h4 ] ) /
                        ( h4             "::" 0*5( h4 ":" ) [ h4 ] ) /
                        ( h4    ":" h4   "::" 0*4( h4 ":" ) [ h4 ] ) /
                        ( h4 2( ":" h4 ) "::" 0*3( h4 ":" ) [ h4 ] ) /
                        ( h4 3( ":" h4 ) "::" 0*2( h4 ":" ) [ h4 ] ) /
                        ( h4 4( ":" h4 ) "::" 0*1( h4 ":" ) [ h4 ] ) /
                        (                       6( h4 ":" ) IPv4address )/
                        (                "::" 0*5( h4 ":" ) IPv4address )/
                        ( h4             "::" 0*4( h4 ":" ) IPv4address )/
                        ( h4    ":" h4   "::" 0*3( h4 ":" ) IPv4address )/
                        ( h4 2( ":" h4 ) "::" 0*2( h4 ":" ) IPv4address )/
                        ( h4 3( ":" h4 ) "::" 0*1( h4 ":" ) IPv4address )

h4            = 1*4HEXDIG
reserved      = "[" / "]" / ";" / "/" / "?" /
                              ":" / "@" / "&" / "=" / "+" / "$" / "," /
unreserved    = ALPHA / DIGIT / mark
mark          = "-" / "_" / "." / "!" / "~" / "*" / "'" /
                        "(" / ")"

escaped       = "%" HEXDIG HEXDIG


