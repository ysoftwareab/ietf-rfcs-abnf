name = ALPHA *( ALPHA / DIGIT / "_" )


                                 Figure 7
grammar         = 1*(rule / directive) *c-wsp

rule            = rulename definition
definition      = *c-wsp ( value-rule /
                              member-rule /
                              array-rule /
                              object-rule /
                              group-rule )

rulename        = *c-wsp ALPHA *(ALPHA / DIGIT / "-" / "_")

float           = [ "-" ] int [ frac ] [ exp ]
integer         = [ "-" ] int [ exp ]
exp             = ( "e" / "E" ) [ "+" / "-" ] 1*DIGIT
frac            = "." 1*DIGIT
int             = "0" / ( %x31-39 *DIGIT )

regex-char      = %x21-2E / %x30-5D / %x5E-7E / WSP /
                     CR / LF / "\/" / "\\"

q-string        = %x20-21 / %x23-5B / %x5D-10FFFF / "\" (
                       %x22 /      ; "  u+0022
                       %x5C /      ; \  u+005C
                       %x2F /      ; /  u+002F
                       %x62 /      ; BS u+0008
                       %x66 /      ; FF u+000C
                       %x6E /      ; LF u+000A
                       %x72 /      ; CR u+000D
                       %x74 /      ; HT u+0009
                       ( %x75 4HEXDIG ) ) ; uXXXX u+XXXX

enum-items      = float / integer /
                     "1" / "0" / "true" / "false" /
                     "null" /
                     q-string

boolean-type    = "boolean"
null-type       = "null"
integer-type    = "integer" [ 1*c-wsp integer ".." integer ]
float-type      = "float"   [ 1*c-wsp float   ".." float   ]
string-type     = "string"  [ *c-wsp "/" *regex-char "/" ]
uri-type        = "uri"     [ URI ] ; URI defined in RFC 3986
ip-type         = "ip4" / "ip6"
dns-type        = "fqdn" / "idn"
date-type       = "date-time" / "full-date" / "full-time"
email-type      = "email"
phone-type      = "phone"
base64-type     = "base64"
enum-type       = "<" 1*( 1*c-wsp enum-items) 1*c-wsp ">"
any-type        = "any"

value-rule      = ":" *c-wsp type-rule
type-rule       = boolean-type /
                     null-type /
                     integer-type /
                     float-type /
                     string-type /
                     uri-type /
                     ip-type /
                     dns-type /
                     date-type /
                     email-type /
                     phone-type /
                     base64-type /
                     enum-type /
                     any-type

inline-rule     = *c-wsp ( rulename / definition )

member-rule     = (
                       ( "^" %x22.22 ) /
                       ( %x22 *q-string %x22 )
                     ) inline-rule

and-or          = ( "," / "/" )
object-rule     = "{" object-member *(
                                          *c-wsp
                                          and-or
                                          object-member
                                        ) *c-wsp "}"

object-items    = ( rulename / member-rule / group-rule )
object-member   = *c-wsp ["?"] *c-wsp object-items

array-rule      = "[" array-member *(
                                         *c-wsp
                                         and-or
                                         array-member
                                       ) *c-wsp "]"

array-count     = *c-wsp [
                              [int]
                              *c-wsp
                              "*"
                              *c-wsp
                              [int]
                              *c-wsp
                            ]
array-member    = array-count (
                                   rulename /
                                   value-rule /
                                   object-rule /
                                   group-rule )
                     [ *c-wsp "/" array-member ]

group-rule      = "(" group-member *(
                                          1*c-wsp
                                          and-or
                                          group-member
                                       ) *c-wsp ")"

group-member    = *c-wsp [ ("?" / array-count ] inline-rule

directive       = *c-wsp "#" *( VCHAR / WSP / %x7F-10FFFF ) EOL

c-wsp           = WSP / c-nl
c-nl            = comment / EOL
comment         =  ";" *(WSP / VCHAR) EOL
EOL             = 1*( CR / LF )

ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z
CR             =  %x0D
DIGIT          =  %x30-39
HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
LF             =  %x0A
VCHAR          =  %x21-7E
WSP            =  SP / HTAB
SP             =  %x20
HTAB           =  %x09

                          JSON Content Rules ABNF

