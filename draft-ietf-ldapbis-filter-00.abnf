filter         = "(" filtercomp ")"
filtercomp     = and / or / not / item
and            = "&" filterlist
or             = "|" filterlist
not            = "!" filter
filterlist     = 1*filter
item           = simple / present / substring / extensible
simple         = attr filtertype assertionvalue
filtertype     = equal / approx / greater / less
equal          = "="
approx         = "~="
greater        = ">="
less           = "<="
extensible     = attr [":dn"] [":" matchingrule] ":=" assertionvalue
                         / [":dn"] ":" matchingrule ":=" assertionvalue
present        = attr "=*"
substring      = attr "=" [initial] any [final]
initial        = value
any            = "*" *(value "*")
final          = value
attr           = <AttributeDescription from Section 4.1.5 of [RFC2251]>
matchingrule   = <MatchingRuleId from Section 4.1.9 of [RFC2251]>
value          = <AttributeValue from Section 4.1.6 of [RFC2251]
                          encoded using the valueencoding rule>
assertionvalue = <AssertionValue from Section 4.1.7 of [RFC2251]
                          encoded using the valueencoding rule>
valueencoding  = 0*(normal / escaped)
normal         = %x01-27 / %x2b-5b / %x5d-7f
escaped        = " 0" / "2" ( "8" / "9" / "a" ) / "5c"


