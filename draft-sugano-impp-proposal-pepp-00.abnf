PePP-Address      = "pepp:" "//" host [":"port]] abs_path

host              = <FQDN or IP address (in dotted-decimal form),
                         as defined by Section 2.1 of RFC 1123>

port              = 1*DIGIT

abs_path          = <defined in [URI]>

PePP-Message      = PePP-Request | PePP-Response
PePP-Request      = PePP-Command SP Request-ID SP PePP-Version CRLF
                         *(( PePP-Header ) CRLF)
                         CRLF
                         [message-body]

PePP-Response     = PePP-Status-Line
                         *(( PePP-Header ) CRLF)
                         CRLF
                         [message-body]
PePP-Status-Line  = PePP-Version SP Request-ID SP Status-Code SP
                         Reason-Phrase CRLF

PePP-Version      = "PePP" "/" 1*DIGIT "." 1*DIGIT

Request-ID        = token


PePP-Command  = LOGIN           ; section 7.1
                   | LOGOUT          ; section 7.2
                   | SUBSCRIBE       ; section 7.3
                   | UNSUBSCRIBE     ; section 7.4
                   | REQUESTNOTIFY   ; section 7.5
                   | CHANGE          ; section 7.6
                   | CANCEL          ; section 7.7
                   | FETCH           ; section 7.8
                   | NOTIFY          ; section 7.9
                   | PULL            ; section 7.10
                   | SEND            ; section 7.11
                   | RECEIVE         ; section 7.12
                   | CALLBACK        ; section 7.13
                   | REDIRECT        ; section 7.14
                   | SETACL          ; section 7.15
                   | GETACL          ; section 7.16
                   | CREATESECTION   ; section 7.17
                   | DELETESECTION   ; section 7.18
                   | PING            ; section 7.19
                   | STARTTLS        ; section 7.20
                   | CONNECT         ; section 7.21


PePP-Header   = From                 ; section 6.1
                   | Connection-Mode      ; section 6.2
                   | Max-Content-Length   ; section 6.3
                   | Subscription-Mode    ; section 6.4
                   | Subscription-ID      ; section 6.5
                   | Regarding            ; section 6.6
                   | Change-Mode          ; section 6.7
                   | Cancel-Type          ; section 6.8
                   | Duration             ; section 6.9
                   | Last-Modified        ; section 6.10
                   | Section-ID           ; section 6.11
                   | Section-Name         ; section 6.12
                   | Location             ; section 6.13
                   | Content-Type         ; section 6.14
                   | Content-Length       ; section 6.15
                   | Auth-State           ; section 6.16
                   | SASL-Mechanism       ; section 6.17
                   | Message-ID           ; section 6.18
                   | Conversation-ID      ; section 6.19

From = "From" ":" PePP-Address



Connection-Mode = "Connection-Mode" ":" ("server" | "client" |
                                                "direct")

Max-Content-Length = "Max-Content-Length" ":" 1*DIGIT
Subscription-Mode = "Subscription-Mode" ":" ("notify" | "pull" |
                                                   "renew")

Subscription-ID = "Subscription-ID" ":" token

Regarding = "Regarding" ":" ("value" | "control" )

Change-Mode = "Change-Mode" ":" ("lease" | "permanent" | "renew" |
                                       "revert" )

Cancel-Type = "Cancel-Type" ":" ("cancel" | "retry")

Duration = "Duration" ":" 1*DIGIT



Last-Modified = "Last-Modified" ":" date "T" time "Z"

date = 4DIGIT "-" 2DIGIT "-" 2DIGIT
            ; year-month-day
time = 2DIGIT ":" 2DIGIT ":" 2DIGIT
            ; hour:minute:second (00:00:00 - 23:59:59)

Section-ID = "Section-ID" ":" token

Section-Name = "Section-Name" ":" token


Location = "Location" ":" PePP-Address
Content-Type = "Content-Type" ":" type "/" subtype *(";" parameter)


Content-Length = "Content-Length" ":" 1*DIGIT


Auth-State = "Auth-State" ":" ("init" | "continue" | "abort")


SASL-Mechanism = "SASL-Mechanism" ":" mechanism *(LWS mechanism)


Message-ID = "Message-ID" ":" token


Conversation-ID = "Conversation-ID" ":" token




information       = "<information>" 1*subscription "</information>"
subscription      = "<subscription>" subscription-ID subscriber
                         created mode regarding "</subscription>"
subscription-ID   = "<subscription-ID>" token "</subscription-ID>"
subscriber        = "<subscriber>" PePP-Address "</subscriber>"
created           = "<created>" date "T" time "Z" "</created>"
mode              = "<mode>" ("notify" / "pull") "</mode>"
regarding         = "<regarding>" ("value" / "control") "</regarding>"

acl             = "<acl>" subscribe *other-command "</acl>"
subscribe       = "<subscribe>" [deny-list] disclose-list "</subscribe>"
disclose-list   = "<disclose>" 1*section "</disclose>"
section         = "<section>" section-body "</section>"
section-body    = section-id (default / all / user-group-list)
user-group-list = *(user / group)
default         = "<default/>"
other-command   = "<command>" command-body  "</command>"
command-body    = command-name [reverse-priority] [allow-list] [deny-
command-name    = "<name>" command-token "</name>"
command-token   = "SEND" / "FETCH" / "CHANGE" / "CANCEL" / "REDIRECT"
reverse-priority        = "<priority-allow/>"
allow-list      = "<allow>" (all / user-group-list) "</allow>"
deny-list       = "<deny>" user-group-list "</deny>"
user            = "<user>" user-id "</user>"
group           = "<group>" group-name "</group>"
all             = "<all/>"

