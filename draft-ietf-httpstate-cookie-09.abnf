OWS            = *( [ obs-fold ] WSP )
                    ; "optional" whitespace
obs-fold       = CRLF

set-cookie-header = "Set-Cookie:" SP set-cookie-string
set-cookie-string = cookie-pair *( ";" SP cookie-av )
cookie-pair       = cookie-name "=" cookie-value
cookie-name       = token
cookie-value      = token
token             = <token, defined in [RFC 2616], Section 2.2>

cookie-av         = expires-av / max-age-av / domain-av /
                    path-av / secure-av / httponly-av /
                    extension-av
expires-av        = "Expires=" sane-cookie-date
sane-cookie-date  = <rfc1123-date, defined in [RFC 2616], Section 3.3.1>
                    ; Note that RFC 2616 uses a different grammatical
                    ; notation than this document (which uses ABNF
                    ; from [RFC5234]).
max-age-av        = "Max-Age=" 1*DIGIT
domain-av         = "Domain=" domain-value
domain-value      = <subdomain, defined in [RFC 1034], Section 3.5>
path-av           = "Path=" path-value
path-value        = <any CHAR except CTLs or ";">
secure-av         = "Secure"
httponly-av       = "HttpOnly"
extension-av      = <any CHAR except CTLs or ";">
   The semantics of the cookie-value are not defined by this document.

   To maximize compatibility with user agents, servers that wish to
   store non-ASCII data in a cookie-value SHOULD encode that data using
   a printable ASCII encoding.

   The portions of the set-cookie-string produced by the cookie-av term
   are known as attributes.  To maximize compatibility with user agents,
   servers SHOULD NOT produce two attributes with the same name in the
   same set-cookie-string.

   Servers SHOULD NOT include more than one Set-Cookie header fields in
   the same response with the same cookie-name.

   If a server sends multiple responses containing Set-Cookie headers
   concurrently to the user agent (e.g., when communicating with the
   user agent over multiple sockets), these responses create a "race
   condition" that can lead to unpredictable behavior.

   NOTE: Some user agents represent dates using 32-bit UNIX time_t
   values.  Some of these user agents might contain bugs that cause them
   process dates after the year 2038 incorrectly.  Servers wishing to
   interoperate with these user agents might wish to use dates before
   2038.

cookie-header = "Cookie:" OWS cookie-string OWS
cookie-string = cookie-pair *( ";" SP cookie-pair )

cookie-date     = *delimiter date-token-list *delimiter
date-token-list = date-token *( 1*delimiter date-token )
delimiter       = %x09 / %x20-2F / %x3B-40 / %x5B-60 / %x7B-7E
date-token      = day-of-month / month / year / time / mystery
day-of-month    = 1*2DIGIT
month           = "jan" [ mystery ] / "feb" [ mystery ] /
                     "mar" [ mystery ] / "apr" [ mystery ] /
                     "may" [ mystery ] / "jun" [ mystery ] /
                     "jul" [ mystery ] / "aug" [ mystery ] /
                     "sep" [ mystery ] / "oct" [ mystery ] /
                     "nov" [ mystery ] / "dec" [ mystery ]
year            = 1*4DIGIT
time            = time-field ":" time-field ":" time-field
time-field      = 1*2DIGIT
CTLwoHTAB       = %x00-08 / %x0A-1F / %x7F
                     ; CTL except HTAB
mystery         = CTLwoHTAB / ":" / ALPHA / DIGIT / %x80-FF
                     ; any OCTET except a delimiter

