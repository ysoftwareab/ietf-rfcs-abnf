header-content  = "Header-Type:" [FWS] content-code [FWS]
                     *( ";" parameter ) CRLF
                                   ; parameter is defined on RFC 2045
content-code    = "UTF8" / "ASCII" / "Downgraded"

header-language-value = Language-Tag [CFWS]
                           *("," [CFWS] Language-Tag [CFWS])
                           ; Language-Tag is defined in RFC4646

required-fields-value = field-name [FWS]
                           *("," [FWS] field-name [FWS])
                           ; the term <field-name> is defined in RFC2822

UTF8-xtra-char  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   %xC2-DF UTF8-tail

UTF8-3          =   %xE0 %xA0-BF UTF8-tail /
                       %xE1-EC 2(UTF8-tail) /
                       %xED %x80-9F UTF8-tail /
                       %xEE-EF 2(UTF8-tail)

UTF8-4          =   %xF0 %x90-BF 2(UTF8-tail) /
                       %xF1-F7 3(UTF8-tail)

UTF8-tail       =   %x80-BF

ctext   =  NO-WS-CTL /     ; all of <text> except
              %d33-39 /       ; SP, HTAB, "(", ")"
              %d42-91 /       ; and "\"
              %d93-126 /
              UTF8-xtra-char

qtext   =       NO-WS-CTL /     ; all of <text> except
              %d33 /               ; The rest of the US-ASCII
              %d35-91 /        ; characters not including "\"
              %d93-126 /       ; or the quote character
              UTF8-xtra-char

text    =  %d1-9 /         ; all UTF-8 characters except
              %d11-12 /       ; US-ASCII NUL, CR and LF
              %d14-127 /
              UTF8-xtra-char

utext   =  NO-WS-CTL /     ; Non white space controls
              %d33-126 /      ; The rest of US-ASCII
              UTF8-xtra-char

utf8-atext   =  ALPHA / DIGIT /
                   "!" / "#" /     ; Any character except
                   "$" / "%" /     ; controls, SP, and specials.
                   "&" / "'" /     ; Used for atoms
                   "*" / "+" /
                   "-" / "/" /
                   "=" / "?" /
                   "^" / "_" /
                   "`" / "{" /
                   "|" / "}" /
                   "~" /
                   UTF8-xtra-char

utf8-atom     = [CFWS] 1*utf8-atext [CFWS]

utf8-dot-atom = [CFWS] utf8-dot-atom-text [CFWS]

utf8-dot-atom-text = 1*utf8-atext *("." 1*utf8-atext)

angle-addr     =  [CFWS] "<" utf8-addr-spec [alt-address] ">" [CFWS]

utf8-addr-spec =  utf8-local-part "@" utf8-domain

utf8-local-part=  utf8-dot-atom / quoted-string / obs-local-part

utf8-domain    =  utf8-dot-atom / domain-literal / obs-domain

alt-address    =  [CFWS] "[" addr-spec "]" [CFWS]

value   =       token / quoted-string

