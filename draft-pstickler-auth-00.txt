

Network Working Group                                        P. Stickler
Internet-Draft                                     Nokia Research Center
Expires: July 17, 2002                                  January 16, 2002


     The 'auth:' URI Scheme for Hierarchical Authority Identifiers
                        draft-pstickler-auth-00

Status of this Memo

   This document is an Internet-Draft and is in full conformance with
   all provisions of Section 10 of RFC2026.

   Internet-Drafts are working documents of the Internet Engineering
   Task Force (IETF), its areas, and its working groups.  Note that
   other groups may also distribute working documents as Internet-
   Drafts.

   Internet-Drafts are draft documents valid for a maximum of six months
   and may be updated, replaced, or obsoleted by other documents at any
   time.  It is inappropriate to use Internet-Drafts as reference
   material or to cite them other than as "work in progress."

   The list of current Internet-Drafts can be accessed at http://
   www.ietf.org/ietf/1id-abstracts.txt.

   The list of Internet-Draft Shadow Directories can be accessed at
   http://www.ietf.org/shadow.html.

   This Internet-Draft will expire on July 17, 2002.

Copyright Notice

   Copyright (C) The Internet Society (2002).  All Rights Reserved.

Abstract

   This document describes the 'auth:' Uniform Resource Identifier (URI)
   scheme for hierarchical authority identifiers.

   The 'auth:' URI scheme belongs to the class of Hierarchical URI
   Schemes as defined in RFC 2396 "Uniform Resource Identifiers (URI):
   Generic Syntax".









Stickler                  Expires July 17, 2002                 [Page 1]

Internet-Draft            The auth: URI Scheme              January 2002


Table of Contents

   1. Overview . . . . . . . . . . . . . . . . . . . . . . . . . . . . 3
   2. BNF for the 'auth:' URI Scheme . . . . . . . . . . . . . . . . . 3
   3. Hierarchical Characteristics of 'auth:' URIs . . . . . . . . . . 5
   4. Security Considerations  . . . . . . . . . . . . . . . . . . . . 7
      References . . . . . . . . . . . . . . . . . . . . . . . . . . . 7
      Author's Address . . . . . . . . . . . . . . . . . . . . . . . . 7
      Full Copyright Statement . . . . . . . . . . . . . . . . . . . . 8










































Stickler                  Expires July 17, 2002                 [Page 2]

Internet-Draft            The auth: URI Scheme              January 2002


1. Overview

   The 'auth:' URI scheme is intended for defining hierarchical
   identifiers for authorities, to which ownership, control, and/or
   source of knowledge and information is ascribed.

   The 'auth:' URI scheme belongs to the class of Hierarchical URI
   Schemes as defined in RFC 2396 "Uniform Resource Identifiers (URI):
   Generic Syntax" [7] and as such provides for parsing into distinct
   subcomponents, each constituting a hierarchically distinct level.
   The hierarchical characteristics of 'auth:' URIs are discussed later
   in this document.

   The 'auth:' URI scheme also belongs to the class of URIs known as
   Uniform Resource Values (URV) which are themselves a subclass of
   Uniform Resource Primitives (URP), a class of URI which constitutes a
   "WYSIWYG" URI, one which is not dereferencible to and does not denote
   another web resource, but constitutes a self-contained resource where
   the full realization of that resource is expressed in the URI itself.
   For a full discussion of the properties of URPs and URVs, please see
   [1].  Familiarity with the concepts defined therein will facilitate
   the full understanding of this document.

   Examples:

      auth://abc.com
      auth://abc.com/sales/europe/finland
      auth://cool.net;2001-11-29
      auth://john.doe@widgets.org
      auth://f81d4fae-7dec-11d0-a765-00a0c91e6bf6
      auth://f81d4fae-7dec-11d0-a765-00a0c91e6bf6;1996:2001

   These examples are provided for illustrative purposes only and do not
   necessarily constitute actual URIs.  See the BNF definition below for
   an explicit definition of 'auth:' URI syntax.

2. BNF for the 'auth:' URI Scheme

   This is a BNF-like description of the 'auth:' Uniform Resource
   Identifier syntax, using the conventions of RFC 822[2], except that
   "|" is used to designate alternatives, and brackets [] are used
   around optional or repeated elements.  Briefly, literals are quoted
   with "", optional elements are enclosed in [brackets], and elements
   may be preceded with <n>* to designate n or more repetitions of the
   following element; n defaults to 0.

   This BNF description adopts sub-definitions defined in RFC 1738
   "Uniform Resource Locators (URL)" [3].  The definition of UUID is



Stickler                  Expires July 17, 2002                 [Page 3]

Internet-Draft            The auth: URI Scheme              January 2002


   adopted from the Internet Draft draft-kindel-uuid-uri-00 [6] defining
   the UUID string representation conforming to ISO-11578 [5] and
   adopted by RFC 2518 "HTTP Extensions for Distributed Authoring --
   WEBDAV" [4].


   voc-URI        = "auth://" root-authority [ "/" sub-auth-path ] [ dates ]

   root-authority = ( [ user "@" ] host ) | UUID
   sub-auth-path  = sub-authority *( "/" sub-authority )
   sub-authority  = 1*uchar
   dates          = ";" start-date [ ":" end-date ]
   start-date     = date
   end-date       = date
   date           = year [ "-" month [ "-" day ] ]
   year           = 4digit
   month          = 2digit
   day            = 2digit

   ; Sub-definitions adopted from draft-kindel-uuid-uri-00:

   UUID           = 8hex "-" 4hex "-" 4hex "-" 4hex "-" 12hex

   ; Sub-definitions adopted from RFC 1738:

   host           = hostname | hostnumber
   hostname       = *[ domainlabel "." ] toplabel
   domainlabel    = alphadigit | alphadigit *[ alphadigit | "-" ] alphadigit
   toplabel       = alpha | alpha *[ alphadigit | "-" ] alphadigit
   hostnumber     = digits "." digits "." digits "." digits
   user           = *[ uchar | ";" | "?" | "&" | "=" ]

   lowalpha       = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" |
                    "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" |
                    "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" |
                    "y" | "z"
   hialpha        = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" |
                    "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" |
                    "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
   digit          = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" |
                    "8" | "9"
   alpha          = lowalpha | hialpha
   digits         = 1*digit
   alphadigit     = alpha | digit

   safe           = "$" | "-" | "_" | "." | "+"
   extra          = "!" | "*" | "'" | "(" | ")" | ","
   hex            = digit | "A" | "B" | "C" | "D" | "E" | "F" |



Stickler                  Expires July 17, 2002                 [Page 4]

Internet-Draft            The auth: URI Scheme              January 2002


                    "a" | "b" | "c" | "d" | "e" | "f"
   escape         = "%" hex hex
   unreserved     = alpha | digit | safe | extra
   uchar          = unreserved | escape



3. Hierarchical Characteristics of 'auth:' URIs

   The 'auth:' URI scheme belongs to the class of Hierarchical URI
   Schemes as defined in RFC 2396 "Uniform Resource Identifiers (URI):
   Generic Syntax" [7] and as such provides for parsing into distinct
   subcomponents, each constituting a hierarchically distinct level.

   Each 'auth:' URI containing one or more levels beyond the URI
   authority implies a 'auth:' URI for each level up to and including
   the authority level, though implied 'auth:' URIs need not have any
   properties associated with them nor be meaningful to any particular
   system.

   The optional suffixed dates are not part of the hierarchical
   structure of an 'auth:' URI and serve only to qualify a specific
   'auth:' URI by limiting its validity or significance to a particular
   period of time.  Note that this dating mechanism provides a means to
   qualify assertions made by a given authority identified by an 'auth:'
   URI with a bound period of validity; allowing one to define the
   precise point in time when a given assertion comes into force and
   subsequently expires.  It also allows for the query selection/
   filtering of assertions made by a given authority during a specific
   period of time.

   E.g.  the following 'auth:' URI

      auth://abc.com/sales/europe/finland

   implies all of the following superordinate 'auth:' URIs

      auth://abc.com/sales/europe
      auth://abc.com/sales
      auth://abc.com

   Note that it is not required that a 'auth:' URI have any levels
   beyond the first root-authority level.  Thus, the 'auth:' URI scheme
   is suitable both for "flat" authority identifiers as well as for
   hierarchical identifiers.

   No special meaning or interpretation is ascribed by this
   specification to particular 'auth:' URI levels or to the relations



Stickler                  Expires July 17, 2002                 [Page 5]

Internet-Draft            The auth: URI Scheme              January 2002


   between 'auth:' URI levels other than the following:

      o  each of the levels corresponds to a distinct authority
         identifier

      o  the authority denoted by the 'auth:' URI for a given level is
         subordinate in some manner to all authorities denoted by each
         level 'auth:' URI implied by its hierarchical structure

      o  in a query comparison of two 'auth:' URIs for logical (not
         lexical) equivalence:

            *  A superordinate 'auth:' query URI without dates is deemed
               to match all same level and subordinate target URIs with
               or without dates

            *  An 'auth:' query URI with start-date only is deemed to
               match all same level or subordinate target URIs with a
               start-date only where the target start date is equal or
               later than the query start-date

            *  An 'auth:' query URI with start-date only is deemed to
               match all same level or subordinate target URIs with both
               start-date and end-date where the target start date is
               equal or later than the query start-date and the query
               start-date is prior to the target end-date

            *  An 'auth:' query URI with both start-date and end-date is
               deemed to match all same level or subordinate target URIs
               with only a start-date where the target URI start-date is
               equal or later than the query start-date and prior to the
               query end-date

            *  An 'auth:' query URI with both start-date and end-date is
               deemed to match all same level or subordinate target URIs
               with both a start-date and an end date where the target
               URI start-date is equal or later than the query start-
               date and prior to the query end-date and the target end-
               date is equal or prior to the query end-date

   Other, more specialized, URI schemes which are derived from the
   'auth:' URI scheme may ascribe more specific interpretations to
   particular levels implicit in the URI as well as more specific
   relational semantics between different levels.

   Note that the URI scheme prefix "auth:" is considered to be a valid
   URI denoting this URI scheme, though it is not itself a valid URI
   according to this URI scheme.



Stickler                  Expires July 17, 2002                 [Page 6]

Internet-Draft            The auth: URI Scheme              January 2002


4. Security Considerations

   This document raises no known security issues.

References

   [1]  Stickler, P., "An Extended Class Taxonomy of Uniform Resource
        Identifier Schemes", January 2002, <http://www-nrc.nokia.com/sw/
        draft-pstickler-uri-taxonomy-00.html>.

   [2]  Crocker, D., "STANDARD FOR THE FORMAT OF ARPA INTERNET TEXT
        MESSAGES", RFC 822, August 1982.

   [3]  Berners-Lee, T., Masinter, L. and M. McCahill, "Uniform Resource
        Locators (URL)", RFC 1738, December 1994.

   [4]  Goland, Y., Whitehead, E., Faizi, A., Carter, S. and D. Jensen,
        "HTTP Extensions for Distributed Authoring -- WEBDAV", RFC 2518,
        February 1999.

   [5]  International Organization for Standardization, "ISO/IEC
        11578:1996 Information technology -- Open Systems
        Interconnection -- Remote Procedure Call", August 2001.

   [6]  Kindel, C., "The uuid: URI scheme", November 1997, <http://
        www.globecom.net/ietf/draft/draft-kindel-uuid-uri-00.html>.

   [7]  Berners-Lee, T., Fielding, R. and L. Masinter, "Uniform Resource
        Identifiers (URI): Generic Syntax", RFC 2396, August 1998.


Author's Address

   Patrick Stickler
   Nokia Research Center
   Visiokatu 1
   Tampere  33720
   FI

   EMail: patrick.stickler@nokia.com











Stickler                  Expires July 17, 2002                 [Page 7]

Internet-Draft            The auth: URI Scheme              January 2002


Full Copyright Statement

   Copyright (C) The Internet Society (2002).  All Rights Reserved.

   This document and translations of it may be copied and furnished to
   others, and derivative works that comment on or otherwise explain it
   or assist in its implementation may be prepared, copied, published
   and distributed, in whole or in part, without restriction of any
   kind, provided that the above copyright notice and this paragraph are
   included on all such copies and derivative works.  However, this
   document itself may not be modified in any way, such as by removing
   the copyright notice or references to the Internet Society or other
   Internet organizations, except as needed for the purpose of
   developing Internet standards in which case the procedures for
   copyrights defined in the Internet Standards process must be
   followed, or as required to translate it into languages other than
   English.

   The limited permissions granted above are perpetual and will not be
   revoked by the Internet Society or its successors or assigns.

   This document and the information contained herein is provided on an
   "AS IS" basis and THE INTERNET SOCIETY AND THE INTERNET ENGINEERING
   TASK FORCE DISCLAIMS ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING
   BUT NOT LIMITED TO ANY WARRANTY THAT THE USE OF THE INFORMATION
   HEREIN WILL NOT INFRINGE ANY RIGHTS OR ANY IMPLIED WARRANTIES OF
   MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.

Acknowledgement

   Funding for the RFC Editor function is currently provided by the
   Internet Society.



















Stickler                  Expires July 17, 2002                 [Page 8]

