LOWER-ALPHA     =  %x61-7A   ;; a-z

acl-data        = "ACL" SP mailbox *(SP identifier SP
                        rights)

capability      =/ rights-capa

command-auth    =/ setacl / deleteacl / getacl /
                      listrights / myrights

deleteacl       = "DELETEACL" SP mailbox SP identifier

getacl          = "GETACL" SP mailbox

identifier      = astring

listrights      = "LISTRIGHTS" SP mailbox SP identifier

listrights-data = "LISTRIGHTS" SP mailbox SP identifier
                           SP rights *(SP rights)

mailbox-data    =/ acl-data / listrights-data / myrights-data

mod-rights      = astring
                      ;; +rights to add, -rights to remove
                      ;; rights to replace

myrights        = "MYRIGHTS" SP mailbox

myrights-data   = "MYRIGHTS" SP mailbox SP rights

new-rights      = 1*LOWER-ALPHA
                      ;; MUST include "t", "e", "x" and "k".
                      ;; MUST NOT include standard rights listed
                      ;; in section 3.2

rights          = astring
                      ;; only lowercase ASCII letters and digits
                      ;; are allowed.

rights-capa     = "RIGHTS=" new-rights
                      ;; RIGHTS=... capability

setacl          = "SETACL" SP mailbox SP identifier
                       SP mod-rights


