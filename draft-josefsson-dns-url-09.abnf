dnsurl          = "dns:" [ "//" dnsauthority "/" ] dnsname ["?" dnsquery]

dnsauthority    = hostport
                                ; See RFC 2396 for "hostport" definition.

dnsname         = *pchar
                                ; See RFC 2396 for "pchar" definition.
                                ; NB! Can be empty.

dnsquery        = dnsqueryelement [";" dnsquery]
                                ; First matching element MUST be used.
                                ; E.g., dns:host.example.org?TYPE=A;TYPE=TXT
                                ; means type A.

dnsqueryelement = ( "CLASS=" dnsclassval ) / ( "TYPE=" dnstypeval ) /
                     ( 1*alphanum "=" 1*alphanum )

dnsclassval     = 1*digit / "IN" / "CH" / ...
                                ; Any IANA registered DNS class expressed as
                                ; mnemonic or as decimal integer.

dnstypeval      = 1*digit / "A" / "NS" / "MD" / ...
                                ; Any IANA registered DNS type expressed as
                                ; mnemonic or as decimal integer.

