graphic-string       = *( gs-char / graphic-encoded )
graphic-encoded      = "{" 1* graphic-encoded-char "}"
graphic-encoded-char = 3DIGIT
gs-char              = 1DIGIT / 1ALPHA / " " / "'" / "+" /
                         "," / "-" / "." / ":" / "=" / "?" /
                         "(" / ")"

graphic-string-sequence = graphic-string
                            *( "/" graphic-string )


pathname-attribute  = incomplete-pathname /
                        complete-pathname
incomplete-pathname = graphic-string-sequence
complete-pathname   = "/" graphic-string-sequence


pathname-and-version  = pathname-attribute
                          [ ";" file-version ]
file-version          = graphic-string


general-identifier = object-identifier /
                       ( "/" graphic-string-sequence )
user-identity = graphic-string


related-stored-file   = "Content-Related-Stored-File" ":"
                          file-identifier [ relationship ]
file-identifier       = "path" pathname-and-version
                          cross-reference
cross-reference       = "application" octet-string
                          [ "message" message-reference ]
                          [ "body-part" 1*DIGIT ]
octet-string          = word
message-reference     = 822-address
                          "relative-id" relative-identifier
relative-identifier   = word
relationship          = explicit-relationship /
                          descriptive-relationship
explicit-relationship = "explicit-relationship"
                          ( "Unspecified" / "New-file" /
                            "Replacement" / "Extension" )
descriptive-relationship = "descriptive-relationship"
                             graphic-string

machine = "Content-Machine" ":" general-identifier


operating-system = "Content-Operating-System" ":"
                     object-identifier

pathname = "Content-Pathname" ":" pathname-attribute


permitted-actions      = "Content-Permitted-Actions" ":"
                           1#permitted-actions-item
permitted-actions-item = "Read" / "Insert" / "Replace" /
                           "Extend" / "Erase" /
                           "Read-attribute" /
                           "Change-attribute" /
                           "Delete-file" / "Traversal" /
                           "Reverse-traversal" /
                           "Random-Order"


storage-account = "Content-Storage-Account" ":"
                    graphic-string

creation-date = "Content-Creation-Date" ":" date-time

last-modification-date = "Content-Last-Modification-Date" ":"
                           date-time

last-read-date = "Content-Last-Read-Date" ":" date-time

creator = "Content-Creator" ":" user-identity

last-modifier = "Content-Last-Modifier" ":" user-identity

last-reader = "Content-Last-Reader" ":" user-identity

reader = "Content-Object-Size" ":" 1*DIGIT

reader = "Content-Future-Object-Size" ":" 1*DIGIT

access-control     = "Content-Access-Control" ":"
                       access-control-element
access-control-element = action-list
                           [ ";" concurrency-access ]
                           [ ";" identity ]
                           [ ";" passwords ]
                           [ ";" location ]
action-list        = "actions" #access-request
access-request     = "read" / "insert" / "replace" /
                       "extend" / "erase" /
                       "read-attribute" /
                       "change-attribute" /
                       "delete-file"
concurrency-access = "concurrency"
                       "read" concurrency-key
                       "insert" concurrency-key
                       "replace" concurrency-key
                       "extend" concurrency-key
                       "erase" concurrency-key
                       "read-attribute" concurrency-key
                       "change-attribute" concurrency-key
                       "delete-file" concurrency-key
concurrency-key    = "not-required" / "shared" /
                       "exclusive" / "no-access"
identity           = "identity" user-identity
passwords          = "passwords"
                       "read" password
                       "insert" password
                       "replace" password
                       "extend" password
                       "erase" password
                       "read-attribute" password
                       "change-attribute" password
                       "delete-file" password
password           = graphic-password /
                       octet-password
graphic-password   = "string" graphicstring
octet-password     = "binary" base64
base64             = *<any legal base64
                         encoding character>
location           = "location" base64

legal-qualification = "Content-Legal-Qualifications" ":"
                        *text

