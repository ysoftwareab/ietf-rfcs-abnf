uMailbox = uLocal-part "@" uDomain
             ; Replace Mailbox in RFC 2821, Section 4.1.2

uLocal-part = uDot-string / uQuoted-string
             ; MAY be case-sensitive
             ; Replace Local-part in RFC 2821, Section 4.1.2

uDot-string = uAtom *("." uAtom)
             ; Replace Dot-string in RFC 2821, Section 4.1.2

uAtom = 1*ucharacter
                 ; Replace Atom in RFC 2821, Section 4.1.2

ucharacter = atext / UTF8-non-ascii

atext = <See Section 3.2.4 of RFC 2822>

uQuoted-string = DQUOTE *uqcontent DQUOTE
             ; Replace Quoted-string in RFC 2821, Section 4.1.2

DQUOTE = <See appendix B.1 of RFC 5234>

uqcontent = qcontent / UTF8-non-ascii

qcontent = <See Section 3.2.5 of RFC 2822>

uDomain = (sub-udomain 1*("." sub-udomain)) / address-literal
             ; Replace Domain in RFC 2821, Section 4.1.2

address-literal = <See Section 4.1.2 of RFC 2822>

sub-udomain = uLet-dig [uLdh-str]
             ; Replace sub-domain in RFC 2821, Section 4.1.2

uLet-dig = Let-dig / UTF8-non-ascii

Let-dig = <See Section 4.1.3 of RFC 2821>

uLdh-str = *( ALPHA / DIGIT / "-" / UTF8-non-ascii) uLet-dig
             ; Replace Ldh-str in RFC 2821, Section 4.1.3

UTF8-non-ascii = UTF8-2 / UTF8-3 / UTF8-4

UTF8-2 =  <See Section 4 of RFC 3629>

UTF8-3 =  <See Section 4 of RFC 3629>

UTF8-4 =  <See Section 4 of RFC 3629>





uReturn-path-line = "Return-Path:" FWS uReverse-path <CRLF>
          ; Replaces Return-path-line in Section 4.4 of RFC 2821
          ; uReverse-path is defined in Section 3.3 of this document

uTime-stamp-line = "Received:" FWS uStamp <CRLF>
          ; Replaces Time-stamp-line in Section 4.4 of RFC 2821

uStamp = From-domain By-domain uOpt-info ";"  FWS date-time
          ; Replaces Stamp in Section 4.4 of RFC 2821

uOpt-info = [Via] [With] [ID] [uFor]
          ; Replaces Opt-info in Section 4.4 of RFC 2821
          ; The protocol value for With will allow a UTF8SMTPbis value

uFor = "FOR" ( FWS (uPath / uMailbox) ) CFWS
          ; Replaces For in Section 4.4 of RFC 2821
          ; uPath and uMailbox are defined in Sections 3.4 and
          ; 3.3, respectively, of this document

