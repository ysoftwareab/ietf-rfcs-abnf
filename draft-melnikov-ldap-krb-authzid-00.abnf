authzId               =/ krbAuthzId

KRBCOLON              = %x6B %x72 %x62 %x3a ; "krb:"

krbAuthzId            = KRBCOLON krbPrincipal
                           ; kerberos-principal-name-based authorization id.

krbRealmDelimiter     = %x40
                           ; '@'

krbComponentDelimiter = %x2F
                           ; '/'

krbPrincipal          = krbNameComponents [krbRealmDelimiter krbRealm]

krbNameComponents     = krbNameComponent *(krbComponentDelimiter krbNameComponent)

krbNameComponent      = KerberosString ; *UTF8
                           ; This corresponds to individual "name-string" of
                           ; "PrincipalName" as defined in [KERBEROS].
                           ;
                           ; '/', '\' and '@' characters must be escaped by
                           ; prefixing with \, i.e. "\@"

krbRealm              = KerberosString ; *UTF8
                           ; This corresponds to "Realm" as defined in [KERBEROS]
                           ; The syntax is constrained as described in section 6
                           ; of [KERBEROS]
                           ;
                           ; '/', '\' and '@' characters must be escaped by
                           ; prefixing with \, i.e. "\@"

