sms-uri         =  scheme ":" hier-part [ "?" sms-body ]
scheme          =  "sms"
hier-part       =  sms-recipient *( "," sms-recipient )
sms-recipient   =  sms-number sms-qualifier
sms-number      =  ( global-number / local-number )
global-number   =  "+" local-number
local-number    =  *phonedigit DIGIT *phonedigit
sms-qualifier   =  *( smsc-qualifier / pid-qualifier )
smsc-qualifier  =  ";smsc=" sms-number
pid-qualifier   =  ";pid=" pid-value
sms-body        =  "body=" query
pid-value       =  "IMPL" / "TELEX" / "G3FAX" / "G4FAX" / "VOICE"
                     / "ERMES" / "NATPAG" / "VIDEOTEX" / teletex / "UCI"
                     / reserved / "MH" / "X400" / email / specific
teletex         =  "TELETEX-"
                     ( "UNSPEC" / "PSPDN" / "CSPDN" / "PSTN" / "ISDN" )
email           =  "SMTP:" address
reserved        =  "RES" ( "1" / "2" / "3" / "4" / "5" / "6" / "7" )
specific        =  "SPEC" ( "1" / "2" / "3" / "4" / "5" / "6" / "7" )

   Some illustrative examples using this syntax are given in
   Section 2.4.

   The syntax definition for "phonedigit" is taken from RFC 3966
   [RFC3966].

   The syntax definition for "query" is taken from RFC 3986 [RFC3986],
   please refer to that document.

   The "sms-body" is used to define the body of the SMS message to be
   composed.  It consists of percent-encoded UTF-8 characters.
   Implementations MUST make sure that the sms-body characters are
   converted to a suitable character encoding before sending, the most
   popular being the 7-bit SMS character encoding, another variant
   (though not as universally supported as 7-bit SMS) is the UCS-2
   character encoding (both specified in [SMS-CHAR]).  Implementations
   MAY choose to discard (or convert) characters in the sms-body that
   are not supported by the SMS character set they are using to send the
   SMS message.  If they do discard or convert characters, they MUST
   notify the user.

   It should be noted that both the SMSC as well as the PID qualifier
   may appear only once per sms-recipient.  If multiple SMSC or PID
   qualifiers are present, conforming software MUST interpret the first
   occurrence and ignore all other occurrences.
