meta-data = provided syntax-URL created

provided = name title version intended-use content-syntax related-to
           status origin contact-name contact-email contact-telephone
           contact-address md-charset md-lang-tag more-info

name = "schemaName:" SPACE oid CRLF

oid = oid-component *("." oid-component)

oid-component = 1*DIGIT

title = "schemaTitle:" SPACE multi-lingual-string CRLF

multi-lingual-string = <a string encoded as specified in [MLSF]>

version = "schemaVersion:" SPACE multi-lingual-string CRLF

intended-use = "schemaIntendedUse:" SPACE multi-lingual-string CRLF

content-syntax = "availableSyntax:" SPACE allowed-syntax CRLF
                 [content-syntax]

allowed-syntax = "ldif" / "asn1" /  "xml"

related-to = "listingRelatedTo:" SPACE file-name CRLF [related-to]

file-name = <a file name as specified in section 5>

status = "listingStatus:" SPACE allowed-status CRLF

allowed-status = deprecation-info / "current"

deprecation-info = ("obsoletes" / "obsoleted-by") SPACE file-name

origin = "schemaOrigin:" SPACE mult-lingual-string CRLF

contact-name = "contactName:" SPACE multi-lingual-string CRLF

contact-email = "contactEmail:" SPACE addr-spec CRLF

addr-spec = local-part "@" domain-part

domain-part = sub-domain *("." sub-domain)

sub-domain = 1*<any CHAR, except specials, SPACE, CRLF, and CTL>
contact-phone = 1*<any CHAR, except CTL, CRLF> ; MUST use full
                CRLF                           ; international form
                                               ; e.g., +1 908 582 2409

contact-address = "contactPostalAddress:" SPACE postal-address

postal-address = multi-lingual-string *(multi-lingual-string "$")

syntax-URL = <an FTP URL as specified in [RFC1738]> CRLF [syntax-URL]

created = "Created:" SPACE date "-" time "-" CRLF

date = 4DIGIT "-" 2DIGIT "-" 2DIGIT ; year-month-day
                                    ; e.g., 1997-08-27

time = 2DIGIT "-" 2DIGIT "-" 2DIGIT ; hh-mm-ss
                                    ; e.g., 00-00-00 thru 23-59-59
                                    ; MUST be based on GMT

md-charset = <a character set name registered with IANA [CHARSET]>

md-lang-tag = <a language tag as specified in [RFC1766]>

more-info = <a URL as specified in [RFC1738]> CRLF

specials = "(" / ")" / "<" / ">" / "@"   ; MUST be in quoted-
         / "," / ";" / ":" / "\" / <">   ; string, to use
         / "." / "[" / "]"               ; within a word

CHAR = <any ASCII character>

CTL = <any ASCII control character and DEL>

CRLF = CR LF

CR = <ASCII CR, carriage return>

LF = <ASCII LF, line feed>

SPACE = <ASCII SP, space>

DIGIT = <any ASCII decimal digit (60 - 71 decimal)>

file-name = "schema-" sequence "-" type ".txt"

type = "metadata" / "ldif" / "asn1" / "xml"

sequence = nonzero-digit 0*<DIGIT> ; initialized to one (1) for first
                                           ; listed schema
                                           ; increments by one (1) for each
                                           ; successive schema listed

nonzero-digit = "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"

DIGIT = <any ASCII decimal digit (60 - 71 decimal)>


