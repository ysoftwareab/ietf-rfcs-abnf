CHAR-NOT-DOT = (%x01-2d / %x2f-%xff)

CHAR-NOT-CRLF = (%x01-09 / %x0b-%x0c / %x0e-%xff)

comment = "#" *CHAR-NOT-CRLF CRLF

identifier = (ALPHA / "_") *(ALPHA DIGIT "_")

tag = ":" identifier

number = 1*DIGIT [QUANTIFIER]

QUANTIFIER = "K" / "M" / "G"

quoted-string = DQUOTE *CHAR DQUOTE
           ;; in general,  CHAR inside a string maps to CHAR
           ;; so
           ;; note that newlines and other characters are all allowed strings

multi-line = "text:" *(SP / HTAB) (comment / CRLF)
           *((1*CHAR-NOT-DOT *CHAR CRLF) / ("." 1*CHAR-NOT-DOT *CHAR CRLF) /
             (".." *CHAR CRLF) / CRLF)
           "." CRLF
           ;; note when used,
           ;; a leading ".." on a line is mapped to ".".

white-space = 1*(SP / CRLF / HTAB) / comment
argument = string-list / number / tag

arguments = *argument [test / test-list]

block = "{" commands "}"

command = identifier arguments ( ";" / block )

commands = *command

start = commands

string = quoted-string / multi-line

string-list = "[" string *("," string) "]" / string         ;; if
test = identifier arguments

test-list = "(" test *("," test) ")"

