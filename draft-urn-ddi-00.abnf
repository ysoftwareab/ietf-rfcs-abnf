ddi-urn              = urn separator ddi separator ddi-irdi
urn                  = "urn"
ddi                  = "ddi"
ddi-irdi             = agency-identifier separator
                             resource-identifier separator
                             version-identifier
agency-identifier    = top-level-domain
                               sub-separator ddi-authority-id
                               *(sub-separator ddi-sub-authority-id)
                             ; length limit is 255 characters
                             ; see section 11 of [RFC2181]
top-level-domain     = dns-label
ddi-authority-id     = dns-label
ddi-sub-authority-id = dns-label
dns-label            = (ALPHA / DIGIT)
                               [ *(ALPHA / DIGIT / "-")
                                 (ALPHA / DIGIT) ]
                             ; length limit is 63 characters
                             ; see section 11 of [RFC2181]
resource-identifier  = restricted-string
                             *("/" restricted-string)
version-identifier   = restricted-string
                             *("/" restricted-string)
restricted-string    = 1*(unreserved / sub-delims / "@")
unreserved           = ALPHA / DIGIT / "-" / "." / "_" / "~"
sub-delims           = "!" / "$" / "&" / "'" / "(" / ")" /
                             "*" / "+" / "," / ";" / "="
separator            = ":"
sub-separator        = "."
ALPHA                = %x41-5A /  ; uppercase letters
                             %x61-7A    ; lowercase letters
DIGIT                = %x30-39    ; digits
