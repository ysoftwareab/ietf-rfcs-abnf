UTF8-non-ascii  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   <Defined in Section 4 of RFC3629>

UTF8-3          =   <Defined in Section 4 of RFC3629>
UTF8-4          =   <Defined in Section 4 of RFC3629>

FWS     =  <Defined in Section 3.2.2 of RFC 5322>

CFWS    =  <Defined in Section 3.2.2 of RFC 5322>

ctext   =/  UTF8-non-ascii
               ; Extending ctext in RFC 5322, Section 3.2.2
comment =   "(" *([FWS] uCcontent) [FWS] ")"

word    =   uAtom / uQuoted-String


uText          = %d1-9 /    ; all UTF-8 characters except
                    %d11-12 /  ; US-ASCII NUL, CR, and LF
                    %d14-127 /
                    UTF8-non-ascii

uQuoted-Pair   = ("\" (VCHAR / WSP / UTF8-non-ascii )) / obs-qp

VCHAR          = <Defined in appendix B.1 of RFC 5234>

WSP            = <Defined in appendix B.1 of RFC 5234>

obs-qp         = <Defined in Section 4.1 of RFC 5322>

uQcontent      = uQtext / uQuoted-Pair

DQUOTE         = <Defined in appendix B.1 of RFC 5234>

uCcontent      = ctext / uQuoted-Pair / comment

uQtext         = qtext / UTF8-non-ascii

uAtext         = ALPHA / DIGIT /
                    "!" / "#" /  ; Any character except
                    "$" / "%" /  ; controls, SP, and specials.
                    "&" / "'" /  ; Used for atoms.
                    "*" / "+" /
                    "-" / "/" /
                    "=" / "?" /
                    "^" / "_" /
                    "`" / "{" /
                    "|" / "}" /
                    "~" /
                    UTF8-non-ascii

uAtom          = [CFWS] 1*uAtext [CFWS]

uDot-Atom      = [CFWS] uDot-Atom-text [CFWS]

uDot-Atom-text = 1*uAtext *("." 1*uAtext)


description    = "Content-Description" ":" *uText
                   ; Replace description in RFC 2045, Section 8


mailbox        = name-addr / addr-spec / uAddr-Spec
                    ; Replace mailbox in RFC 5322, Section 3.4

angle-addr     =/ [CFWS] "<" uAddr-Spec ">" [CFWS]
                    ; Extending angle-addr in RFC 5322, Section 3.4

uAddr-Spec     = uLocal-Part "@" uDomain

uLocal-Part    = uDot-Atom / uQuoted-String / obs-local-part
                    ; Replace Local-Part in RFC 5322, Section 3.4.1

uQuoted-String = [CFWS] DQUOTE *([FWS] uQcontent) [FWS] DQUOTE [CFWS]

obs-local-part = <Defined in Section 4.4 of RFC 5322>
uDomain        = uDot-Atom / domain-literal / obs-domain

domain-literal = <Defined in Section 3.4.1 of RFC 5322>


received-token =/ uAddr-Spec

