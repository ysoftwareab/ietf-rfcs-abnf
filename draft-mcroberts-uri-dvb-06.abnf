MPEG-2_TS_URI       = transmission_scheme ":" path

transmission_scheme = "dvb"

path                = dvb_net_path | dvbapp_entity | path-absolute | extension

dvb_net_path        = "//" dvb_entity [ path-absolute ]

dvb_entity            = *( original_network_id | transport_stream | service )

transport_stream      = original_network_id "." transport_stream_id

service               = service_string [ "." component_string
                          [ "$" carousel_id ] ] [ event_string ]

service_string        = textual_service | hier_service

hier_service          = original_network_id [ "." [ transport_stream_id ]
                          "." service_id ]

orginal_network_id    = hex_string

transport_stream_id   = hex_string

network_id            = hex_string

carousel_id           = hex_string

hex_string            = 1 * hex

textual_service       = unreserved_string | quoted_string

unreserved_string     = 1*255 ( unreserved )

quoted_string         = "'" special-string "'"

special_string        = 1*255 ( unreserved | sub-delims )

unreserved            = ALPHA | DIGIT | "-" | "." | "_" | "~"

sub-delims            = "!" | "$" | "&" | "'" | "(" | ")" |
                        "*" | "+" | "," | ";" | "="

HEX                   = DIGIT | "A" | "B" | "C" | "D" | "E" | "F" |
                        "a" | "b" | "c" | "d" | "e" | "f"

DIGIT                 = "0" | "1" | "2" | "3" | "4" |
                        "5" | "6" | "7" | "8" | "9"
   The broadcast_auth_domain, if present, may identify one the
   following:

   o  An original_network_id

   o  A transport stream (through combination of original_network_id and
      transport_stream_id)

   o  A service, either through hexadecimal numeric identifiers or
      through in textual form

   o  An audio, visual, or data component within a service

   o  A carousel contained within a component of a service

   o  An event which occurs within a service

component_string              = component_tag_set | qualified_component_set |
                                fully_qualified_component_set

component_tag_set             = component_tag *( "&" component_tag )

component_tag                 = hex_string

qualified_component_set       = qualified_component
                                *( "&" qualified_component )

qualified_component           = component_type "-" component_id

component_type                = "video" | "audio" | "data" | "subtitle" |
                                "teletext" | "dvbst"

component_id                  = component_tag | iso639_lang_code | "default" |
                                "current" | "hearing_impaired" |
                                "visually_impaired" | "none"

fully_qualified_component_set = fully_qualified_component
                                *( "&" fully_qualified_component )

fully_qualified_component     = "fqc-" stream_and_component_type ","
                                component_type [ "," iso639_lang_code ]

stream_and_component_type     = hex_string

iso639_lang_code              = 3 ( "A-Z" | "a-z" )

event_string     = ";" [ event_id ] [ ";" tva_id ] [ "~" time_duration ]

event_id         = hex_string

tva_id           = hex_string

time_duration    = datetime "--" period

datetime         = date "T" time "Z"

date             = year month day

year             = digit digit digit digit

month            = digit digit

day              = digit digit

time             = hours minutes [ seconds ]

hours            = digit digit

minutes          = digit digit

second           = digit digit

period           = "PT" hours "H" minutes "M" [ seconds "S" ]

   An event may be identified by its event_id, a TV-Anytime tva_id, a
   datetime and duration according to UTC, or any combination of the
   three.

dvbapp_entity             = service_context | service_component_context |
                            ait_string

service_context           = "current" | "original"

service_component_context = "current.audio" | "current.video" | "current.av"

ait_string                = ait_filter ".ait" ait_abs_path

ait_filter                = "current" | service_string

ait_abs_path              = "/app_root" | ait_application

ait_application           = "/" org_id_part "." app_id_part [ "?" ait_params ]

org_id_part               = lowposhex_string

app_id_part               = lowposhex_string

ait_params                = "arg_" 1*digit "=" *uric

lowposhex_string          = LOWPOSHEXDIGIT *LOWHEX

LOWHEX                    = DIGIT | "a" | "b" | "c" | "d" | "e" | "f"

LOWPOSHEX                 = POSDIGIT | "a" | "b" | "c" | "d" | "e" | "f"

POSDIGIT                  = "1" | "2" | "3" | "4" | "5" |
                            "6" | "7" | "8" | "9"

   For further information, please see [DVB-MHP].

extension = *pchar

pchar     = unreserved | pct-encoded | sub-delims | ":" | "@"

