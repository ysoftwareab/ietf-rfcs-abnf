relativeURL = [ absoluteURL | location | abs_path | rel_path ]
                 [ "#" fragment ]

absoluteURL = scheme ":" [ location | abs_path | rel_path ]
location    = "//" net_loc [ "/" rel_path ]
abs_path    = "/"  rel_path
rel_path    = [ path ] [ ";" params ] [ "?" query ]

path        = segment *[ "/" segment ]
segment     = *[ pchar | ";" ]

params      = param *[ ";" param ]
param       = *[ pchar | "/" ]

scheme      = 1*[ alpha | digit | "+" | "-" | "." ]
net_loc     =  *[ pchar | ";" ]
query       =  *[ uchar | reserved | "#" ]
fragment    =  *[ uchar | reserved ]

pchar       = [ uchar | "?" | ":" | "@" | "&" | "=" | "#" ]
uchar       = unreserved | escape
unreserved  = alpha | digit | safe | extra | national

escape      = "%" hex hex
hex         = digit | "A" | "B" | "C" | "D" | "E" | "F" |
		         "a" | "b" | "c" | "d" | "e" | "f"

alpha       = lowalpha | hialpha
lowalpha    = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" |
                 "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" |
                 "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
hialpha     = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | 
                 "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | 
                 "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

digit       = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" |
                 "8" | "9"

safe        = "$" | "-" | "_" | "." | "+"
extra       = "!" | "*" | "'" | "(" | ")" | ","
national    = "{" | "}" | "|" | "\" | "^" | "~" | "[" | "]" | "`"
reserved    = ";" | "/" | "?" | ":" | "@" | "&" | "="
punctuation = "<" | ">" | "#" | "%" | <">


Uses-Netloc = {ftp, http, gopher, nntp, telnet, wais,
                     file, prospero}

Non-Hierarchical = {gopher, wais, mailto, news, telnet, prospero}

Uses-Params = {ftp, http, prospero}

Uses-Query = {http, wais}

Uses-Fragment = {ftp, http, gopher, news, nntp, wais,
                       file, prospero}

g          = <URL:http://a/b/c/g>
