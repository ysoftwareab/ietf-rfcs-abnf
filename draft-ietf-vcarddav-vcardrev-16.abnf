vcard-entity = 1*(vcard)

vcard = "BEGIN" ":" "VCARD" CRLF
           "VERSION" ":" "4.0" CRLF
           1*contentline
           "END" ":" "VCARD" CRLF
     ; A vCard object MUST include the VERSION and FN properties.
     ; VERSION MUST come first.

contentline = [group "."] name *(";" param) ":" value CRLF
     ; When parsing a content line, folded lines must first
     ; be unfolded according to the unfolding procedure
     ; described above.
     ; When generating a content line, lines longer than 75
     ; characters SHOULD be folded according to the folding
     ; procedure described above.

group = 1*(ALPHA / DIGIT / "-")
name  = "SOURCE" / "KIND" / "FN" / "N" / "NICKNAME"
         / "PHOTO" / "BDAY" / "ANNIVERSARY" / "GENDER" / "ADR" / / "TEL"
         / "EMAIL" / "IMPP" / "LANG" / "TZ" / "GEO" / "TITLE" / "ROLE"
         / "LOGO" / "ORG" / "MEMBER" / "RELATED" / "CATEGORIES"
         / "NOTE" / "PRODID" / "REV" / "SOUND" / "UID" / "CLIENTPIDMAP"
         / "URL" / "KEY" / "FBURL" / "CALADRURI" / "CALURI" / "XML"
         / iana-token / x-name
     ; Parsing of the param and value is based on the "name" as
     ; defined in ABNF sections below.
     ; Group and name are case-insensitive.

iana-token = 1*(ALPHA / DIGIT / "-")
     ; identifier registered with IANA

x-name = "x-" 1*(ALPHA / DIGIT / "-")
     ; Names that begin with "x-" or "X-" are
     ; reserved for experimental use, not intended for released
     ; products, or for use in bilateral agreements.

param = language-param / value-param / pref-param / pid-param
         / type-param / geo-param / tz-param / sort-as-param
         / calscale-param / version-param / any-param
     ; Allowed parameters depend on property name.

param-value = *SAFE-CHAR / DQUOTE *QSAFE-CHAR DQUOTE

any-param  = (iana-token / x-name) "=" param-value *("," param-value)

NON-ASCII = %x80-FF
     ; Use is restricted by UTF-8

QSAFE-CHAR = WSP / %x21 / %x23-7E / NON-ASCII
     ; Any character except CTLs, DQUOTE

SAFE-CHAR = WSP / %x21 / %x23-39 / %x3C-7E / NON-ASCII
     ; Any character except CTLs, DQUOTE, ";", ":"

VALUE-CHAR = WSP / VCHAR / NON-ASCII
     ; Any textual character

value = text
           / text-list
           / date-list
           / time-list
           / date-time-list
           / date-and-or-time-list
           / timestamp-list
           / boolean
           / integer-list
           / float-list
           / URI               ; from Section 3 of [RFC3986]
           / iana-valuespec
       ; Actual value type depends on property name and VALUE parameter.

text = *TEXT-CHAR

TEXT-CHAR = "\\" / "\," / "\n"
               / <any VALUE-CHAR except , or \>
        ; Backslashes, commas, and newlines must be encoded.

component = "\\" / "\," / "\;" / "\n" / WSP / NON-ASCII
               / %x21-2B / %x2D-3A / %x3C-5B / %x5D-7E

list-component = list-component-value *("," list-component-value)
list-component-value = "\\" / "\," / "\;" / "\n" / WSP / NON-ASCII
                          / %x21-2B / %x2D-3A / %x3C-5B / %x5D-7E

text-list             = text             *("," text)
date-list             = date             *("," date)
time-list             = time             *("," time)
date-time-list        = date-time        *("," date-time)
date-and-or-time-list = date-and-or-time *("," date-and-or-time)
timestamp-list        = timestamp        *("," timestamp)
integer-list          = integer          *("," integer)
float-list            = float            *("," float)

boolean = "TRUE" / "FALSE"
integer = [sign] 1*DIGIT
float   = [sign] 1*DIGIT ["." 1*DIGIT]

sign = "+" / "-"

year   = 4DIGIT  ; 0000-9999
month  = 2DIGIT  ; 01-12
day    = 2DIGIT  ; 01-28/29/30/31 depending on month and leap year
hour   = 2DIGIT  ; 00-23
minute = 2DIGIT  ; 00-59
second = 2DIGIT  ; 00-58/59/60 depending on leap second
zone   = utc-designator / utc-offset
utc-designator = %d90  ; uppercase "Z"

date          = year    [month  day]
                   / year "-" month
                   / "--"     month [day]
                   / "--"      "-"   day
date-noreduc  = year     month  day
                   / "--"     month  day
                   / "--"      "-"   day
date-complete = year     month  day

time          = hour [minute [second]] [zone]
                   /  "-"  minute [second]  [zone]
                   /  "-"   "-"    second   [zone]
time-notrunc  = hour [minute [second]] [zone]
time-complete = hour  minute  second   [zone]

time-designator = %d84  ; uppercase "T"
date-time = date-noreduc  time-designator time-notrunc
timestamp = date-complete time-designator time-complete

date-and-or-time = date-time / date / time-designator time

utc-offset = sign hour [minute]

Language-Tag = <Language-Tag, defined in [RFC5646], Section 2.1>

iana-valuespec = <value-spec, see Section 12>
                    ; a publicly-defined valuetype format, registered
                    ; with IANA, as defined in section 12 of this
                    ; document

language-param = "LANGUAGE=" Language-Tag
             ; Language-Tag is defined in section 2.1 of RFC 5646

value-param = "VALUE=" value-type

value-type = "text"
                / "uri"
                / "date"
                / "time"
                / "date-time"
                / "timestamp"
                / "boolean"
                / "integer"
                / "float"
                / "language-tag"
                / "utc-offset"
                / iana-token  ; registered as described in section 12
                / x-name

pref-param = "PREF=" (1*2DIGIT / "100")
                                ; An integer between 1 and 100.

altid-param = "ALTID=" param-value
pid-param = "PID=" pid-value *("," pid-value)
pid-value = 1*DIGIT ["." 1*DIGIT]

type-param = "TYPE=" type-value *("," type-value)

type-value = "work" / "home" / type-param-tel
                      / type-param-related / iana-token / x-name
             ; This is further defined in individual property sections.
calscale-param = "CALSCALE=" calscale-value

calscale-value = "gregorian" / iana-token / x-name

sort-as-param = "SORT-AS=" sort-as-value

sort-as-value = param-value *("," param-value)

geo-param = "GEO=" DQUOTE uri DQUOTE
tz-param  = "TZ=" (param-value / DQUOTE uri DQUOTE)

BEGIN-param =  ; no parameter allowed
BEGIN-value = "VCARD"

END-param =  ; no parameter allowed
END-value = "VCARD"

SOURCE-param = "VALUE=uri" / pid-param / pref-param / altid-param
                  / any-param
SOURCE-value = uri

KIND-param = "VALUE=text" / any-param
KIND-value = "individual" / "group" / "org" / "location"
                / iana-token / x-name
XML-param = "VALUE=text" / altid-param
XML-value = text

FN-param = "VALUE=text" / type-param / language-param / altid-param
              / pid-param / pref-param / any-param
FN-value = text

N-param = "VALUE=text" / sort-as-param / language-param
             / altid-param / any-param
N-value = list-component 4(";" list-component)

NICKNAME-param = "VALUE=text" / type-param / language-param
                    / altid-param / pid-param / pref-param / any-param
NICKNAME-value = text-list
PHOTO-param = "VALUE=uri" / altid-param / type-param
PHOTO-value = uri

BDAY-param = BDAY-param-date / BDAY-param-text
BDAY-value = date-and-or-time / text
       ; Value and parameter MUST match.

BDAY-param-date = "VALUE=date-and-or-time"
BDAY-param-text = "VALUE=text" / language-param

BDAY-param =/ altid-param / calscale-param / any-param
       ; calscale-param can only be present when BDAY-value is
       ; date-and-or-time and actually contains a date or date-time.

ANNIVERSARY-param = "VALUE=" ("date-and-or-time" / "text")
ANNIVERSARY-value = date-and-or-time / text
       ; Value and parameter MUST match.

ANNIVERSARY-param =/ altid-param / calscale-param / any-param
       ; calscale-param can only be present when ANNIVERSARY-value is
       ; date-and-or-time and actually contains a date or date-time.

GENDER-param = "VALUE=text" / any-param
GENDER-value = sex [";" text]

sex = "" / "M" / "F" / "O" / "N" / "U"

label-param = "LABEL=" param-value

ADR-param = "VALUE=text" / label-param / language-param / geo-param
               / tz-param / altid-param / pid-param / pref-param
               / type-param / any-param
ADR-value = ADR-component-pobox ";" ADR-component-ext ";"
                 ADR-component-street ";" ADR-component-locality ";"
                 ADR-component-region ";" ADR-component-code ";"
                 ADR-component-country
ADR-component-pobox    = list-component
ADR-component-ext      = list-component
ADR-component-street   = list-component
ADR-component-locality = list-component
ADR-component-region   = list-component
ADR-component-code     = list-component
ADR-component-country  = list-component

TEL-param = TEL-text-param / TEL-uri-param
TEL-value = TEL-text-value / TEL-uri-value
       ; Value and parameter MUST match

TEL-text-param = "VALUE=text"
TEL-text-value = text

TEL-uri-param = "VALUE=uri"
TEL-uri-value = uri

TEL-param =/ type-param / pid-param / pref-param / altid-param
                / any-param

type-param-tel = "text" / "voice" / "fax" / "cell" / "video"
                    / "pager" / "textphone" / iana-token / x-name
       ; type-param-tel MUST NOT be used with a property other than TEL.
EMAIL-param = "VALUE=text" / pid-param / pref-param / type-param
                 / altid-param / any-param
EMAIL-value = text

IMPP-param = "VALUE=uri" / pid-param / pref-param / type-param
                / altid-param / any-param
IMPP-value = uri

LANG-param = "VALUE=language-tag" / pid-param / pref-param
                / altid-param / type-param / any-param
LANG-value = Language-Tag

TZ-param = "VALUE=" ("text" / "uri" / "utc-offset")
TZ-value = text / uri / utc-offset
       ; Value and parameter MUST match

TZ-param =/ altid-param / pid-param / pref-param / type-param
               / any-param

GEO-param = "VALUE=uri" / pid-param / pref-param / type-param
               / altid-param / any-param
GEO-value = uri

TITLE-param = "VALUE=text" / language-param / pid-param
                 / pref-param / altid-param / type-param / any-param
TITLE-value = text

ROLE-param = "VALUE=text" / language-param / pid-param / pref-param
                / type-param / altid-param / any-param
ROLE-value = text

LOGO-param = "VALUE=uri" / language-param / pid-param / pref-param
                / type-param / altid-param / any-param
LOGO-value = uri

ORG-param = "VALUE=text" / sort-as-param / language-param
               / pid-param / pref-param / altid-param / type-param
               / any-param
ORG-value = component *(";" component)

MEMBER-param = "VALUE=uri" / pid-param / pref-param / altid-param
                  / any-param
MEMBER-value = uri
RELATED-param = RELATED-param-uri / RELATED-param-text
RELATED-value = uri / text
       ; Parameter and value MUST match.

RELATED-param-uri = "VALUE=uri"
RELATED-param-text = "VALUE=text" / language-param

RELATED-param =/ pid-param / pref-param / altid-param / type-param
                    / any-param

type-param-related = related-type-value *("," related-type-value)
       ; type-param-related MUST NOT be used with a property other than
       ; RELATED.

related-type-value = "contact" / "acquaintance" / "friend" / "met"
                        / "co-worker" / "colleague" / "co-resident"
                        / "neighbor" / "child" / "parent"
                        / "sibling" / "spouse" / "kin" / "muse"
                        / "crush" / "date" / "sweetheart" / "me"
                        / "agent" / "emergency"

CATEGORIES-param = "VALUE=text" / pid-param / pref-param
                      / type-param / altid-param / any-param
CATEGORIES-value = text-list

NOTE-param = "VALUE=text" / language-param / pid-param / pref-param
                / type-param / altid-param / any-param
NOTE-value = text

PRODID-param = "VALUE=text" / any-param
PRODID-value = text

REV-param = "VALUE=timestamp" / any-param
REV-value = timestamp

SOUND-param = "VALUE=uri" / language-param / pid-param / pref-param
                 / type-param / altid-param / any-param
SOUND-value = uri

UID-param = UID-uri-param / UID-text-param
UID-value = UID-uri-value / UID-text-value
       ; Value and parameter MUST match.

UID-uri-param = "VALUE=uri"
UID-uri-value = uri

UID-text-param = "VALUE=text"
UID-text-value = text

UID-param =/ any-param
CLIENTPIDMAP-param = any-param
CLIENTPIDMAP-value = 1*DIGIT ";" uri

URL-param = "VALUE=uri" / pid-param / pref-param / type-param
               / altid-param / any-param
URL-value = uri

VERSION-param = "VALUE=text" / any-param
VERSION-value = "4.0"

KEY-param = KEY-uri-param / KEY-text-param
KEY-value = KEY-uri-value / KEY-text-value
       ; Value and parameter MUST match.

KEY-uri-param = "VALUE=uri"
KEY-uri-value = uri

KEY-text-param = "VALUE=text"
KEY-text-value = text

KEY-param =/ altid-param / pid-param / pref-param / type-param
                / any-param

FBURL-param = "VALUE=uri" / pid-param / pref-param / type-param
                 / altid-param / any-param
FBURL-value = uri

CALADRURI-param = "VALUE=uri" / pid-param / pref-param / type-param
                     / altid-param / any-param
CALADRURI-value = uri

CALURI-param = "VALUE=uri" / pid-param / pref-param / type-param
                  / altid-param / any-param
CALURI-value = uri

