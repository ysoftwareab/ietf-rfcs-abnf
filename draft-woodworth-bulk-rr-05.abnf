DIGIT  = <as defined in RFC 5234 Appendix B.1>
HEXDIG = <as defined in RFC 5234 Appendix B.1>
DQUOTE = <as defined in RFC 5234 Appendix B.1>

pattern             =   "-" / 1*part / DQUOTE 1*part DQUOTE

part                =   "[" range "]" / string

range               =   number [ "-" number ]

number              =   1*DIGIT / 1*HEXDIG

string              =   1*( %x01-5A / %x5C / %x5E-7F )
                                 ; Any [US-ASCII] character excluding
                                 ; NUL and square bracket characters
                                 ; "[" or "]"

DIGIT  = <as defined in RFC 5234 Appendix B.1>
HEXDIG = <as defined in RFC 5234 Appendix B.1>
DQUOTE = <as defined in RFC 5234 Appendix B.1>

pattern             =   "-" / 1*part / DQUOTE 1*part DQUOTE

part                =   backreference / string

backreference       =   "$" "{" substitution "}"

substitution        =   range 0*( "," range ) [ options ]

substitution        =/  "*" [ options ]

options             =   delimiter [ interval [ padding ] ]

delimiter           =   "|" 0*1( %x01-23 / %x25-7A / %7E-7F )
                                 ; Any single [US-ASCII] character
                                 ; excluding NUL, dollar sign "$",
                                 ; pipe "|" and curly brace characters
                                 ; "{" or "}"

interval            =   "|" *2DIGIT

padding             =   "|" *2DIGIT

range               =   number [ "-" number ]

number              =   1*DIGIT / 1*HEXDIG

string              =   1*( %x01-23 / %x25-7A / %x7C / %7E-7F )
                                 ; Any [US-ASCII] character excluding
                                 ; NUL, dollar sign "$" and curly brace
                                 ; characters "{" or "}"

