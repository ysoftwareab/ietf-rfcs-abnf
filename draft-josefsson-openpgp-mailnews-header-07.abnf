openpgp     = "OpenPGP:" o-params CRLF
                   ; CFWS is defined in RFC 5322.
                   ; CRLF is defined in RFC 5234.

o-params    = (o-parameter *(";" o-parameter))

o-parameter = *CFWS "id" "=" id *CFWS
               / *CFWS "url" "=" url *CFWS
               / *CFWS "preference" "=" preference *CFWS
               / *CFWS parameter *CFWS ; normally unused, for extensions
                   ; parameter is defined in RFC 2045.

id          = 1*(8HEXDIG)
                   ; HEXDIG is defined in RFC 5234.
                   ; Matching of value is case-insensitive.

url         = folded-uri / quoted-url
                   ; If the URL contains the character ";",
                   ; the quoted-url form MUST be used.

quoted-url  = DQUOTE folded-uri DQUOTE
                   ; DQUOTE is defined in RFC 5234.

folded-uri  = <absolute-URI, but free insertion of FWS permitted>
                   ; absoluteURI is defined in RFC 3986.
                   ; FWS is defined in RFC 5234.

preference  = "sign" / "encrypt" / "signencrypt" / "unprotected"
                   ; Matching of values is case-insensitive.

id=12345678 (short key ID)
id=1234567890ABCDEF (long key ID)
id=1234567890abcdef0123456789ABCDEF01234567 (v4 fingerprint)
id=1234567890ABCDEF0123456789ABCDEF (v3 fingerprint, deprecated)

url=http://example.org/pgp.txt
url="http://example.org/funny;name.txt"

preference=sign
preference=unprotected
preference=ENCRYPT
id=B565716F (key stored on non-networked laptop)
id=12345678 (1024 bit RSA Key for Encrypt or Sign)
id=ABCD0123 (created Sun Jan  2 02:25:15 CET 2005)


preference=signencrypt
id=12345678 (this key is only used at the office);
preference=sign (unsigned emails are filtered away)
