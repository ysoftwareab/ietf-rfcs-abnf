uMailbox = uLocal-part "@" ( uDomain / address-literal )
              ; Replace Mailbox in RFC 5321, Section 4.1.2

address-literal = <See Section 4.1.2 of RFC 5321>

uLocal-part = uDot-string / uQuoted-string
              ; MAY be case-sensitive
              ; Replace Local-part in RFC 5321, Section 4.1.2

uDot-string = uAtom *("." uAtom)
              ; Replace Dot-string in RFC 5321, Section 4.1.2

uAtom = 1*ucharacter
              ; Replace Atom in RFC 5321, Section 4.1.2

ucharacter = atext / UTF8-non-ascii

atext = <See Section 3.2.3 of RFC 5322>

uQuoted-string = DQUOTE *uqcontent DQUOTE
              ; Replace Quoted-string in RFC 5321, Section 4.1.2

DQUOTE = <See appendix B.1 of RFC 5234>

uqcontent = qcontent / UTF8-non-ascii

qcontent = <See Section 3.2.4 of RFC 5322>

uDomain = sub-udomain *("." sub-udomain)
              ; Replace Domain in RFC 5321, Section 4.1.2

sub-udomain = uLet-dig [uLdh-str]
              ; Replace sub-domain in RFC 5321, Section 4.1.2

uLet-dig = Let-dig / UTF8-non-ascii

Let-dig = <See Section 4.1.2 of RFC 5321>

uLdh-str = *( ALPHA / DIGIT / "-" / UTF8-non-ascii) uLet-dig
              ; Replace Ldh-str in RFC 5321, Section 4.1.2

UTF8-non-ascii = UTF8-2 / UTF8-3 / UTF8-4

UTF8-2 =  <See Section 4 of RFC 3629>

UTF8-3 =  <See Section 4 of RFC 3629>

UTF8-4 =  <See Section 4 of RFC 3629>





BODY=8BITMIME parameter if that is appropriate given the body content
uReturn-path-line = "Return-Path:" FWS uReverse-path <CRLF>
          ; Replaces Return-path-line in Section 4.4 of RFC 5321

uReverse-path = uPath / "<>"
          ; Replace Reverse-path in RFC 5321, section 4.1.2

uPath = "<" [ A-d-l ":" ] uMailbox ">"
          ; Replace Path in RFC 5321, section 4.1.2
          ; uMailbox is defined in section 3.3 of this document

A-d-l = <See section 4.1.2 of RFC 5321>

uTime-stamp-line = "Received:" FWS uStamp <CRLF>
          ; Replaces Time-stamp-line in Section 4.4 of RFC 5321

uStamp = From-domain By-domain uOpt-info [CFWS] ";" FWS date-time
          ; Replaces Stamp in Section 4.4 of RFC 5321

From-domain = <See section 4.4 of RFC 5321>

By-domain = <See section 4.4 of RFC 5321>

date-time = <See section 3.3 of RFC 5322>

uOpt-info = [Via] [With] [ID] [uFor]
                 [Additional-Registered-Clauses]
          ; Replaces Opt-info in Section 4.4 of RFC 5321
          ; The protocol value for With will allow a UTF8SMTPbis value

Via = <See section 4.4 of RFC 5321>

With = <See section 4.4 of RFC 5321>

ID = <See section 4.4 of RFC 5321>

Additional-Registered-Clauses = <See section 4.4 of RFC 5321>

uFor = CFWS "FOR" FWS ( uPath / uMailbox)
          ; Replaces For in Section 4.4 of RFC 5321
          ; uMailbox is defined in section 3.3 of this document





vrfy = "VRFY" SP ( uLocal-part / uMailbox )
                 [ SP "UTF8REPLY" ] CRLF
              ; uLocal-part and uMailbox are defined in
              ; Section 3.3 of this document.

expn = "EXPN" SP ( uLocal-part / uMailbox )
                 [ SP "UTF8REPLY" ] CRLF
              ; uLocal-part and uMailbox are defined in
              ; Section 3.3 of this document.





