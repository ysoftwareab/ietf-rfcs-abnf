append-ext        =/ att-annotate
                          ; modifies [RFC3501] extension behaviour

att-annotate      = "ANNOTATION" SP "(" entry-att *(SP entry-att) ")"

att-match         = string
                          ; dot-separated attribute name
                          ; MAY contain "*" or "%" for use as wildcards

att-value         = attrib SP value

attrib            = string
                          ; dot-separated attribute name
                          ; MUST NOT contain "*" or "%"

attribs           = att-match /
                          "(" att-match *(SP att-match) ")"

capability        =/ "ANNOTATE"
                          ; defines the capability for this extension

entries           = entry-match /
                          "(" entry-match *(SP entry-match) ")"

entry             = string
                          ; slash-separated path to entry
                          ; MUST NOT contain "*" or "%"

entry-att         = entry SP "(" att-value *(SP att-value) ")"

entry-match       = string
                          ; slash-separated path to entry
                          ; MAY contain "*" or "%" for use as wildcards

fetch-att         =/ "ANNOTATION" SP "(" entries SP attribs ")"
                          ; modifies original IMAP fetch-att

msg-att-dynamic   =/ "ANNOTATION" SP
                             ( "(" entry-att *(SP entry-att) ")" /
                               "(" entry *(SP entry) ")" )
                          ; extends FETCH response with annotation data

resp-text-code    =/ "ANNOTATE" SP "TOOBIG" /
                           "ANNOTATE" SP "TOOMANY" /
                           "ANNOTATESIZE" SP (number / nil)
                          ; new response codes

search-key        =/ "ANNOTATION" SP entry-match SP att-match
                          SP value
                          ; modifies original IMAP search-key

select-param      =/ "ANNOTATE"
                          ; defines the select parameter used with
                          ; ANNOTATE extension

sort-key          =/ "ANNOTATION" SP entry SP attrib
                          ; modifies original sort-key

store-att-flags   =/ att-annotate
                          ; modifies original IMAP STORE command

value             = nstring / literal8


