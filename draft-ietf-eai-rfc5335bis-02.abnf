UTF8-xtra-char  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   %xC2-DF UTF8-tail

UTF8-3          =   %xE0 %xA0-BF UTF8-tail /
                       %xE1-EC 2(UTF8-tail) /
                       %xED %x80-9F UTF8-tail /
                       %xEE-EF 2(UTF8-tail)

UTF8-4          =   %xF0 %x90-BF 2( UTF8-tail ) /
                       %xF1-F3 3( UTF8-tail ) /
                       %xF4 %x80-8F 2( UTF8-tail )

UTF8-tail       =   %x80-BF

FWS     =  <see [RFC5322], folding white space>

CFWS    =  <see [RFC5322], folding white space>

ctext   =/  UTF8-xtra-char

utext   =/  UTF8-xtra-char

comment =   "(" *([FWS] uCcontent) [FWS] ")"

word    =   uAtom / uQuoted-String

uText   =  %d1-9 /         ; all UTF-8 characters except
                  %d11-12 /       ; US-ASCII NUL, CR, and LF
                  %d14-127 /
                  UTF8-xtra-char

uQuoted-Pair   = ("\" uText) / obs-qp

uQcontent      = uQtext / uQuoted-Pair

uQuoted-String = [CFWS]
                        DQUOTE *([FWS] uQcontent) [FWS] DQUOTE
                        [CFWS]

uCcontent =     ctext / uQuoted-Pair / comment
uQtext =        qtext / UTF8-xtra-char


uAtext   =  ALPHA / DIGIT /
                   "!" / "#" /     ; Any character except
                   "$" / "%" /     ; controls, SP, and specials.
                   "&" / "'" /     ; Used for atoms.
                   "*" / "+" /
                   "-" / "/" /
                   "=" / "?" /
                   "^" / "_" /
                   "`" / "{" /
                   "|" / "}" /
                   "~" /
                   UTF8-xtra-char

uAtom     =     [CFWS] 1*uAtext [CFWS]

uDot-Atom =     [CFWS] uDot-Atom-text [CFWS]

uDot-Atom-text = 1*uAtext *("." 1*uAtext)

qcontent      = uQcontent

description   = "Content-Description:" unstructured CRLF

mailbox            =  name-addr / addr-spec / uAddr-Spec

angle-addr         =/ [CFWS] "<" uAddr-Spec">" [CFWS] /
                     obs-angle-addr

uAddr-Spec     =  uLocal-Part "@" uDomain

uLocal-Part    =  uDot-String / uQuoted-String

uQuoted-String =  DQUOTE *uQcontent DQUOTE uDomain
                   =  (sub-uDomain 1*("." sub-uDomain)) / address-literal


   Below are a few examples of possible <mailbox> representations.



      "DISPLAY_NAME" <ASCII@ASCII>
         ; traditional mailbox format
      "DISPLAY_NAME" <non-ASCII@non-ASCII>
          ; message will bounce if UTF8SMTPbis extension is not supported
      <non-ASCII@non-ASCII>
         ; without DISPLAY_NAME and quoted string
         ; message will bounce if UTF8SMTPbis extension is not supported


item-value      =/      uAddr-Spec

