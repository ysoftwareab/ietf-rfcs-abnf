DVB-URL    = dvb-scheme ":" dvb-path

dvb-scheme = "dvb"

dvb-path   = ( "//" ( dvb-net-entity [ path-absolute ] ))
              / ( "//" dvb-app-entity )
              / path-absolute

dvb-net-entity             = transport-stream / service-entity

transport-stream           = original-network-id "." transport-stream-id

service-entity             = dvb-service [ "." component-set [ "$" carousel-id ]]
                             [ event-constraint ]

dvb-service                = ( original-network-id "." [ transport-stream-id ] "." service-id )
                           / ( "'" textual-service-identifier "'" )

original-network-id        = hex-string

transport-stream-id        = hex-string

service-id                 = hex-string

textual-service-identifier = reg-name

carousel-id                = hex-string

hex-string                 = 1*HEXDIG

   The dvb-net-entity, if present, may identify one the following:

   o  A transport stream (through combination of original_network_id and
      transport_stream_id)
   o  A service, either through hexadecimal numeric identifiers or
      through in textual form

   o  An audio, visual, or data component within a service

   o  A carousel contained within a component of a service

   o  An event which occurs within a service

component-set                     = component-tag-set
                                  / qualified-component-set
                                  / fully-qualified-component-set

component-tag-set                 = component-tag *( "&" component-tag )

component-tag                     = hex-string

qualified-component-set           = qualified-component *( "&" qualified-component )

qualified-component               = component-type "=" component-id

component-type                    = "video" / "audio" / "data" / "subtitle"
                                  / "teletext" / "dvbst"

component-id                      = component-tag / iso639-language-code
                                  / "default" / "current"
                                  / "hearing_impaired" / "visually_impaired"
                                  / "none"

fully-qualified-component-set     = fully-qualified-component
                                    *( "&" fully-qualified-component )

fully-qualified-component         = "fqc=" stream-content-and-component-type
                                    "," component-tag
                                    [ "," iso639-language-code ]

stream-content-and-component-type = hex-string

iso639-language-code              = 3ALPHA
event-constraint = ( event-ref [time-constraint] )
                    / time-constraint

event-ref        = ";" [ event-id ] [ ";" tva-id ]

event-id         = hex-string

tva-id           = hex-string

time-constraint  = "~" time-duration

time-duration    = start-time "--" duration

start-time       = date "T" time "Z"

duration         = "PT" hours "H" minutes "M" [ seconds "S" ]

date             = year month day

time             = hours minutes [ seconds ]

year             = DIGIT DIGIT DIGIT DIGIT

month            = DIGIT DIGIT

day              = DIGIT DIGIT

hours            = DIGIT DIGIT

minutes          = DIGIT DIGIT

seconds          = DIGIT DIGIT

dvb-app-entity   = ( "current" [ ( ".audio" / ".video" / ".av" ) ] )
                  / "original"
                  / ( ( "current" / dvb-service ) ".ait/" ait-abs-path )

ait-abs-path     = "app-root" / ait-application

ait-application  = org-id-part "." app-id-part [ "?" ait-params ]

ait-params       = "arg_" 1*DIGIT "=" *uric [ "&" ait-params ]

org-id-part      = lowposhex-string

app-id-part      = lowposhex-string

lowposhex-string = lowposhex *lowhex

lowhex           = DIGIT / "a" / "b" / "c" / "d" / "e" / "f"

posdigit         = "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"

lowposhex        = posdigit / "a" / "b" / "c" / "d" / "e" / "f"

   DVB interactive applications are Java Xlets that are carried in the
   broadcast signal.  Terminals extract and then execute these Xlets.
   They are advertised inside the broadcast signal in the Application
   Information Table (AIT).  The dvb-app-entity can thus refer to such
   application advertisements in its ".ait/" form.  In its other forms,
   the dvb-app-entity refers to a DVB service or components thereof
   which are used in conjunction with the currently running application.
   For further information, please see [DVB-MHP].

