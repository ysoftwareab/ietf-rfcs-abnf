uMailbox = uLocal-part "@" uDomain
                   ; Replace Mailbox in RFC 2821, section 4.1.2

uLocal-part = uDot-string / uQuoted-string
                   ; MAY be case-sensitive
                   ; Replace Local-part in RFC 2821, section 4.1.2

uDot-string = uAtom *("." uAtom)
                   ; Replace Dot-string in RFC 2821, section 4.1.2

uAtom = 1*ucharacter
                   ; Replace Atom in RFC 2821, section 4.1.2

ucharacter = atext / UTF8-xtra-char
                   ; Replace character in RFC 2821, section 4.1.2
                   ; atext is defined in RFC 2822

uQuoted-string = DQUOTE *uqcontent DQUOTE
                   ; Replace Quoted-string in RFC 2821, section 4.1.2
                   ; DQUOTE is Double Quote defined in RFC 4234

uqcontent = qcontent / UTF8-xtra-char
                   ; qcontent is defined in RFC 2822, section 3.2.5

uDomain = (sub-udomain 1*("." sub-udomain)) / address-literal
                   ; Replace Domain in RFC 2821, section 4.1.2
                   ; address-literal is defined in RFC2821 section 4.1.2

sub-udomain = uLet-dig [uLdh-str]
                   ; Replace sub-domain in RFC 2821, section 4.1.2

uLet-dig = Let-dig / UTF8-xtra-char
                   ; Let-dig is defined in RFC 2821, section 4.1.3

uLdh-str = *( ALPHA / DIGIT / "-" / UTF8-xtra-char) uLet-dig
                   ; Replace Ldh-str in RFC 2821, section 4.1.3

UTF8-xtra-char = UTF8-2 / UTF8-3 / UTF8-4
                   ; UTF8-2, UTF8-3 and UTF8-4 are defined in RFC 3629



uReverse-path = uPath
               ; Replace Reverse-path in RFC 2821, section 4.1.2

uForward-path = uPath
               ; Replace Forward-path in RFC 2821, section 4.1.2

uPath = "<" [ A-d-l ":" ] uMailbox ">"
               ; Replace Path in RFC 2821, section 4.1.2
               ; A-d-l is defined in RFC 2821, section 4.1.2
               ; uMailbox is defined in section 2.3 of this document

ALT-ADDRESS-parameter="ALT-ADDRESS=" ALT-ADDRESS-esmtp-value

ALT-ADDRESS-esmtp-value=xtext
               ; Mailbox encoded as xtext.
               ; xtext is defined in RFC 3461 [RFC3461], section 4.2


uReturn-path-line = "Return-Path:" FWS uReverse-path <CRLF>
           ; Replaces Return-path-line in the section 4.4 of [RFC2821]
           ; uReverse-path is defined in Section 2.3

uTime-stamp-line = "Received:" FWS uStamp <CRLF>
           ; Replaces Time-stamp-line in the section 4.4 of [RFC2821]

uStamp = From-domain By-domain uOpt-info ";"  FWS date-time
           ; Replaces Stamp in the section 4.4 of [RFC2821]

uOpt-info = [Via] [With] [ID] [uFor]
           ; Replaces Opt-info in the section 4.4 of [RFC2821]
           ; [With]'s protocol value will allow UTF8SMTP value

uFor = "FOR" 1*( FWS (uPath / uMailbox) ) CFWS
           ; Replaces For in the section 4.4 of [RFC2821]
           ; uPath is defined in section 2.4 of this document
           ; uMailbox is defined in section 2.3 of this document


