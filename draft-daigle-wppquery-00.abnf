whois-command   =   ( system-command [":" "hold"]
                       / terms [":" globalcnstrnts] ) nl

system-command  =   "constraints"
                       / "describe"
                       / "commands"
                       / "polled-by"
                       / "polled-for"
                       / "version"
                       / "list"
                       / "show" [1*sp bytestring]
                       / "help" [1*sp bytestring]
                       / "?" [bytestring]

terms           =   and-expr *("or" and-expr)

and-expr        =   not-expr *("and" not-expr)

not-expr        =   ["not"] (term / ( "(" terms ")" ))

term            =   generalterm / specificterm
                       / combinedterm

generalterm     =   bytestring 

specificterm    =   specificname "=" bytestring

specificname    =   "handle" / "value"

combinedterm    =   attributename "=" bytestring 

globalcnstrnts  =   globalcnstrnt *(";" globalcnstrnt)

globalcnstrnt   =   "format" "=" format
                       / "maxfull" "=" 1*digit
                       / "maxhits" "=" 1*digit
                       / opt-globalcnst

opt-globalcnst  =   "authenticate" "=" auth-method
                       / "language" "=" language
                       / "incharset" "=" characterset
                       / "ignore" "=" bytestring
                       / "include" "=" bytestring

format          =   "full" / "abridged" / "handle" / "summary"
                       / "server-to-ask"

auth-method     =  bytestring

language        = <The language code defined in RFC1766 [ALVE95]>

characterset    =   "us-ascii" / "iso-8859-1" / "iso-8859-2" /
                       "iso-8859-3" / "iso-8859-4" / "iso-8859-5" /
                       "iso-8859-6" / "iso-8859-7" / "iso-8859-8" /
                       "iso-8859-9" / "iso-8859-10" / 
                       "UNICODE-1-1-UTF-8" / "UNICODE-2-0-UTF-8" 
                       "UTF-8"
                         ;"UTF-8" is as defined in [RFC2279].  This is 
                         ;the character set label that should be used 
                         ;for UTF encoded information; the labels 
                         ;"UNICODE-2-0-UTF-8" and "UNICODE-1-1-UTF-8" 
                         ;are retained primarily for compatibility with 
                         ;older Whois++ servers (and as outlined in 
                         ;[RFC2279]).  


searchvalue     =   "exact" / "substring" / "regex" / "fuzzy"
                       / "lstring"

casevalue       =   "ignore" / "consider"

bytestring          =   0*charbyte

attributename   =   1*attrbyte

charbyte        =   "\" specialbyte / normalbyte

normalbyte      =   <%d33-255, except specialbyte>

attrbyte        =   <%d33-127 except specialbyte> /
                       "\" <specialbyte except ":" " " tab nl>

specialbyte     =   " " / tab / "=" / "," / ":" / ";" / "\" /
                       "*" / "." / "(" / ")" / "[" / "]" / "^" /
                       "$" / "!" / "?"

tab             =  %d09        
sp              =  %d32        ; space

digit           =   "0" / "1" / "2" / "3" / "4" /
                       "5" / "6" / "7" / "8" / "9"

nl              =   %d13 %d10   ; CR LF


