text            = %d1-9 /           ; all octets except
                            %d11-12 /         ; US-ASCII NUL, CR and LF
                            %d14-255
ctext           = NO-WS-CTL /       ; all of <text> except
                            %d33-39 /         ; SP, HTAB, "(", ")"
                            %d42-91 /         ; and "\"
                            %d93-255
qtext           = NO-WS-CTL /       ; all of <text> except
                            %d33 /            ; SP, HTAB, "\" and <">
                            %d35-91 /
                            %d93-255
ftext           = %d33-57 /         ; all octets except
                            %d59-126 /        ; CTL, SP and ":"
                            %d128-255
token           = 1*<any ftext except tspecials>
tspecials       = "(" / ")" / "<" / ">" / "@"
                            "," / ";" / ":" / "
                            "/" / "[" / "]" / "?" / "="


ALPHA           = %x41-5A /         ; A-Z
                            %x61-7A           ; a-z
CR              = %x0D              ; carriage return
CRLF            = CR LF
DIGIT           = %x30-39           ; 0-9
HTAB            = %x09              ; horizontal tab
LF              = %x0A              ; line feed
SP              = %x20              ; space
NO-WS-CTL       = %d1-8 /           ; US-ASCII control characters
                            %d11 /            ; which do not include the
                            %d12 /            ; carriage return, line feed,
                            %d14-41 /         ; and whitespace characters
                            %d127
WSP             = SP / HTAB          ; Whitespace characters
FWS             = ([*WSP CRLF] 1*WSP)   ; Folding whitespace
comment         = "(" *([FWS] (ctext / quoted-pair / comment))
                            [FWS] ")"
CFWS            = *([FWS] comment) (([FWS] comment) / FWS )
quoted-pair     = "\" text
quoted-string   = *CFWS <"> *(FWS (qtext / quoted-pair)) <"> *CFWS
unstructured    = *( [FWS] text )


article         = 1*header separator body
header          = header-name ":" SP header-content CRLF
header-name     = 1*name-character *( "-" 1*name-character )
name-character  = ALPHA / DIGIT
header-content  = usenet-header-content / unstructured
separator       = CRLF
body            = *( *998text CRLF )
nonblank-text   = 1*( [FWS] nbtext )
nbtext          = qtext /           ; all of <text> except
                            "\" / <">         ; SP and HTAB

Date-content = date-time  

From-content = mailbox-list

Message-ID-content = msg-id

subject-content = [ back-reference ] pure-subject CRLF
pure-subject = nonblank-text 
back-reference = %x52.65.3A.20        ; which is a case-sensitive
                                                 "Re: "

Newsgroups-content  = newsgroup-name 
                          *( ng-delim *FWS newsgroup-name ) *FWS
newsgroup-name      = component
                          *( "." component )
component           = component-start
                          *( component-start / component-other )
component-start     = Un-lowercase / Un-digit
Un-lowercase        = <Unicode Letter, Lowercase> /
                      <Unicode Letter, Other>
Un-uppercase        = <Unicode Letter, Uppercase> /
                      <Unicode Letter, Titlecase>
Un-digit            = <Unicode Number, Decimal Digit> /
                      <Unicode Number, Other>
component-other     = "+" / "-" / "_"
ng-delim            = ","

          where the <Unicode ...> items are as described in [UNICODE]. 

          An article's Newsgroups header may not contain a duplicated
          newsgroup-name component.

          The inclusion of folding white space within a newsgroup-name
          is a newly introduced feature in this standard. It MUST be
          accepted by all conforming implementations (relaying agents,
          serving agents and reading agents). Posting agents should be
          aware that except for experimental posting to 'test' newsgroups
          or within cooperating subnets, such postings may be rejected by 
          overly-critical old-style relaying agents. When a sufficient 
          number of relay agents are in conformance, posting agents
          SHOULD generate such whitespace in the form of <CRLF WS> so as
          to keep the length of lines in the relevant headers (notably
          Newsgroups and Followup-To) to no more than than 79 characters
          (or other agreed policy limit - see 4.6). Before such critical
          mass occurs, injecting agents MAY reformat such headers by removing
          whitespace inserted by the posting agent, but relaying agents
          MUST NOT do so.

          A newsgroup name consists of one or more components.
          Components MAY contain non-ASCII letters, but these MUST be
          encoded in UTF-8 and not according to RFC-2047. A component
          MUST contain at least one letter (and must, according to the
          syntax, begin and end with a letter or digit). Components
          SHOULD begin with a letter. Composite characters (made by
          overlaying one character with another) and format characters,
          as allowed in certain parts of Unicode and needed by certain
          languages, must use whatever canonical conventions apply to
          those parts of Unicode (such conventions are not
          defined in this Standard). The use of "_" in a component is
          deprecated. Serving agents MAY refuse to accept newsgroups
          using that component.

          NOTE: Components composed entirely of digits would cause
          problems for the commonly used implementation technique of
          using the component as the name of a directory, whilst also
          using sequential numbers to distinguish the articles within a
          group.

          NOTE: Uppercase letters MUST NOT be used. Although converting
          ASCII uppercase letters to their lowercase counterparts is
          straightforward enough, it would be unreasonable to expect
          software to do the same in parts of Unicode for which it was
          not configured (in general, a table lookup would be required).
          Thus software MAY attempt to convert Un-uppercase letters
          according to the mappings defined by [UNICODE], but this
          behaviour is not required.

          Whilst there is no longer any technical reason to limit the
          length of a component (formerly, it was limited to 14
          characters) nor to limit the total length of a newsgroup-name,
          it should be noted that these names are also used in the
          newsgroups line (6.6.1.2) where an overall limit applies, and
          moreover excessively long names can be exceedingly
          inconvenient in practical use.  Agencies responsible for
          individual hierarchies SHOULD therefore, as a matter of
          policy, set reasonable limits for the length of a component
          and of a newsgroup name. In the absence of such explicit
          policies, the default figures are 30 characters and 72
          characters respectively.

          NOTE: The newsgroup-name as encoded in UTF-8 should be
          regarded as the canonical form. Reading agents may convert it
          to whatever character set they are able to display (see 4.5.2)
          and serving agents may possibly need to convert it to some
          form more suitable as a filename. Simple algorithms for both
          kinds of conversion are readily available.

          Posters SHOULD use only the names of existing newsgroups in
          the Newsgroups header, because newsgroups are not created
          simply by being posted to. However, it is legitimate to
          cross-post to newsgroup(s) which do not exist on the posting
          agent's host, provided that at least one of the newsgroups
          DOES exist there, and followup agents MUST accept this
          (posting agents MAY accept it, but SHOULD at least alert the
          poster to the situation and request confirmation). Relaying
          agents MUST NOT rewrite Newsgroups headers in any way, even if
          some or all of the newsgroups do not exist on the relaying
          agent's host.

path-content    =       old-path / new-path
          
old-id          =       1*( ALPHA / digit / "-" | "." | "_")  
old-path        =       old-id *(punctuation old-id)
punctuation     =       LWSP / %x21-2f / %x3a-40 / %x5b-60 / %x7b-7f
                                              ; These are ! " # $ % & ' ( ) *
                                              ;   + , - . / : ; < = > ? @ [ \
                                              ;   ] ^ _ ` { | } ~ DEL
new-delims      =       [FWS] ("@" / "/" / "," ) [FWS]
new-path        =       post-injection "%" pre-injection
delim-plus-id   =       [FWS] "!" [FWS] old-id
                                  / new-delims site-id
post-injection  =       *(site-id 1*new-delims) site-id
pre-injection   =       site-id *delim-plus-id
site-id         =       ALPHA word      ; UUCP name
                                  / ALPHA         ; for "x" tail entry
                                  / "." word      ; other registered name
                                  / <FQDN>        ; as per RFC 1034
                                  / <dotted-quad> ; numeric IP address rep
                                                  ; specified in rfc820 etc.
                                  / "[" dotted-quad "]"
                                  / "[" <ipv6-numeric> "]" ; per RFC1884
word            =       1*(ALPHA / digit / "-" / "_")


Followup-To-content = Newsgroups-content / "poster"


Sender-content = mailbox-list


expires-content = date-time

Reply-To-content = From-content

References-content = msg-id [msg-id...]

Control-content = verb *( FWS argument ) verb = 1*( ALPHA /
newgroup-ctrl         = "newgroup" FWS groupname [ FWS flags ]
flags                 = "moderated" 
groupinfo-body     = descriptor-tag CRLF 1*( description-line CRLF )
descriptor-tag     = %x46.6F.72 SP %x79.6F.75.72 SP 
                               %x6E.65.77.73.67.72.6F.75.70.73 SP 
                               %x66.69.6C.6E.3A
                             ; case sensitive "For your newsgroups file:"    
description-line    = newsgroup-name [ 1*WSP description ]
description         = nonblank-text
moderation-flags    = [ moderated-literal ]
moderated-literal   = %x28.4D.6F.64.65.72.61.74.65.64.29
                                ; case sensitive "(Moderated)"
    

rmgroup-ctrl  = "rmgroup" FWS groupname

mvgroup-ctrl  = "mvgroup" FWS ( mvgrp-groups / mvgrp-hrchy)
mvgrp-groups  = groupname [ FWS groupname ]
mvgrp-hrchy   = groupnamepart ".*" FWS groupnamepart
groupnamepart = groupname    ; syntactically
 
checkgroup-ctrl = "checkgroups" [ FWS chkscope ] [ FWS chksernr ]
chkscope        = 1*( ["!"] newsgroup-name-part ) 
chksernr        = "#" 1*DIGIT

checkgroups-body = *( invalidation CRLF ) 1*( valid-group CRLF )
invalidation     = "!" groupname *( "," *WSP groupname )
valid-group      = description-line 
differences = content-language ;
boundary = nxtlang
cancel-arguments  = 1*( message-id CFWS )
cancel-body       = body


ihave-arguments   = *( message-id space ) relayer-name
sendme-arguments  = ihave-arguments
ihave-body        = *( message-id CRLF )
sendme-body       = ihave-body

Distribution-content = distribution *( dist-delim distribution)
dist-delim = ","
distribution = positive-distribution / negative-distribution
positive-distribution = *FWS distribution-name *FWS
negative-distribution = *FWS "!" distribution-name *FWS
distribution-name = 1*letter

summary-content  =  non-blank-text CRLF 
non-blank-text = 1*(FWS text)

Approved-content = From-content

Lines-content = 1*digit

Xref-content = server 1*( CFWS location ) 
server = server-name
location = newsgroup-name ":" article-locator
article-locator  = 1*
 
organization-content = nonblank-text CRLF

User-Agent = "User-Agent:" SP User-Agent-content
User-Agent-content = product *(CFWS product) [CFWS]

product = token ["/" product-version] product-version = token

batch        = 1*( batch-header article )
batch-header = "#!" SPACE "rnews" SPACE article-size CRLF
article-size = 1*digit

Supersedes-content = message-id *( FWS message-id )

Archive-content = "no" | CFWS

Kristian =?ISO-8859-1?Q?K=F6hntopp?= (KRIS@koehntopp.de)
Peter =?ISO-8859-1?Q?Br=FClls?= (pb@Ecce-Terram.DE)
