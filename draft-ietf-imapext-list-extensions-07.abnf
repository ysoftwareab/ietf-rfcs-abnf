child-mbox-flag    = "\HasChildren" / "\HasNoChildren"
                        ; flags for Child Mailbox Extension, at most one
                        ; possible per LIST response

list               = "LIST" [SP list-options] SP mailbox SP list-mailbox

list-options       = "(" [option *(SP option)] ")"

mailbox-list       = "(" [mbx-list-flags] ")" SP
                        (DQUOTE QUOTED-CHAR DQUOTE / nil) SP mailbox
                        [SP mbox-list-extended]

mbox-list-extended = "(" [mbox-list-extended-item
                        *(SP mbox-list-extended-item)] ")"

mbox-list-extended-item = "(" mbox-list-extended-item-data ")"

mbox-list-extended-item-data =  mbox-list-extended-item-tag SP nstring-list

mbox-list-extended-item-tag  = astring
                        ; The content MUST conform to either "eitem-vendor-tag" or
                        ; "eitem-standard-tag" ABNF productions.
                        ; A tag registration template is described in section
                        ; 8.5 of this document.

eitem-vendor-tag   = vendor-tag
                        ; a vendor specific tag for extended list data

eitem-standard-tag = atom
                        ; a tag for extended list data defined in a Standard
                        ; Track or Experimental RFC.

nstring-list       = nstring /
                        "(" [nstring-list *(SP nstring-list)] ")"
                        ;; a recursive list definition

mbox-list-oflag    = child-mbox-flag / "\NonExistent" / "\PlaceHolder" /
                        "\HasSubmailboxes" / "\Subscribed" / "\Remote"

option             = "SUBSCRIBED" / "CHILDREN" / "REMOTE" / "SUBMAILBOXES" /
                        option-extension
                        ; An option registration template is described in section
                        ; 8.3 of this document.

option-extension   = option-vendor-tag / option-standard-tag

option-vendor-tag  = vendor-tag
                        ; a vendor specific option

option-standard-tag= atom
                        ; an option defined in a Standard Track or
                        ; Experimental RFC

vendor-tag         = vendor-token "-" atom



