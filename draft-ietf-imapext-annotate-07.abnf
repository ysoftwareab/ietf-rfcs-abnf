annotate-param    = "ANNOTATE"
                       ; defines the select parameter used with
                       ; ANNOTATE extension
   
append            = "APPEND" SP mailbox [SP flag-list] [SP date-time]
                       [SP "ANNOTATION" SP att-annotate] SP literal
                       ; modifies original IMAP4 APPEND command

append-message    = [SP flag-list] [SP date-time]
                       [SP "ANNOTATION" SP att-annotate] SP literal
                       ; modifies [MULTIAPPEND] extension behaviour

att-annotate      = "ANNOTATION" SP "(" entry-att *(SP entry-att) ")"

att-match         = string
                       ; dot-separated attribute name
                       ; MAY contain "*" or "%" for use as wildcards 

att-value         = attrib SP value

attrib            = string
                       ; dot-separated attribute name
                       ; MUST NOT contain "*" or "%"

attribs           = att-match /
                       "(" att-match *(SP att-match) ")"

entries           = entry-match /
                       "(" entry-match *(SP entry-match) ")"

entry             = string
                       ; slash-separated path to entry
                       ; MUST NOT contain "*" or "%"

entry-att         = entry SP "(" att-value *(SP att-value) ")"

entry-match       = string
                       ; slash-separated path to entry
                       ; MAY contain "*" or "%" for use as wildcards 

examine            =/ *(SP "(" select-param *(SP select-param) ")"
                       ; modifies the original IMAP4 examine command to
                       ; accept optional parameters
   
fetch-ann-resp    = "ANNOTATION" SP "(" entry-att *(SP entry-att) ")"

fetch-att         =/ "ANNOTATION" SP "(" entries SP attribs ")"
                       ; modifies original IMAP4 fetch-att

resp-text-code    =/ "ANNOTATE" SP "TOOBIG" /
                        "ANNOTATE" SP "TOOMANY" /
                        "ANNOTATESIZE" SP number
                       ; new response codes for STORE failures

search-key        =/ "ANNOTATION" SP entry-match SP att-match
                       SP value
                       ; modifies original IMAP4 search-key

select            =/ *(SP "(" select-param *(SP select-param) ")"
                       ; modifies the original IMAP4 select command to
                       ; accept optional parameters
   
select-param      = astring / "(" astring SP astring *(SP astring) ")"
                       ; parameters to SELECT may contain one or
                       ; more atoms or strings - multiple items
                       ; are always parenthesised
   
sort-key          =/ "ANNOTATION" SP entry SP attrib
                       ; modifies original sort-key [SORT]

store-att-flags   =/ att-annotate
                       ; modifies original IMAP4 STORE command

value             = nstring
   
    
    
