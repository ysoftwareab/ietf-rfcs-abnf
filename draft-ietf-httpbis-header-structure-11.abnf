Foo-Example = sh-dictionary

sh-list       = list-member *( OWS "," OWS list-member )
list-member   = ( sh-item / inner-list ) *parameter
inner-list    = "(" OWS [ sh-item *( SP sh-item ) OWS ] ")"
parameter     = OWS ";" OWS param-name [ "=" param-value ]
param-name    = key
key           = lcalpha *( lcalpha / DIGIT / "_" / "-" )
lcalpha       = %x61-7A ; a-z
param-value   = sh-item

sh-dictionary  = dict-member *( OWS "," OWS dict-member )
dict-member    = member-name "=" member-value
member-name    = key
member-value   = sh-item / inner-list

sh-item = sh-integer / sh-float / sh-string / sh-token / sh-binary
             / sh-boolean

sh-integer = ["-"] 1*15DIGIT

sh-float    = ["-"] (
                DIGIT "." 1*14DIGIT /
               2DIGIT "." 1*13DIGIT /
               3DIGIT "." 1*12DIGIT /
               4DIGIT "." 1*11DIGIT /
               5DIGIT "." 1*10DIGIT /
               6DIGIT "." 1*9DIGIT /
               7DIGIT "." 1*8DIGIT /
               8DIGIT "." 1*7DIGIT /
               9DIGIT "." 1*6DIGIT /
              10DIGIT "." 1*5DIGIT /
              11DIGIT "." 1*4DIGIT /
              12DIGIT "." 1*3DIGIT /
              13DIGIT "." 1*2DIGIT /
              14DIGIT "." 1DIGIT )

sh-string = DQUOTE *(chr) DQUOTE
chr       = unescaped / escaped
unescaped = %x20-21 / %x23-5B / %x5D-7E
escaped   = "\" ( DQUOTE / "\" )

sh-token = ALPHA
              *( ALPHA / DIGIT / "_" / "-" / "." / ":" / "%"
                 / "*" / "/" )

sh-binary = "*" *(base64) "*"
base64    = ALPHA / DIGIT / "+" / "/" / "="

sh-boolean = "?" boolean
boolean    = "0" / "1"

