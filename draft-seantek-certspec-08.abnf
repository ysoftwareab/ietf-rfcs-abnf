certstring = (certspec / multispec) [ "|" pkcsattrs ]

                         Figure 1: certstring ABNF
certspec         = certspec-hash / certspec-content / certspec-el /
                      certspec-path

hexOctet         = 2HEXDIG

certspec-hash    = "SHA-1"   ":" 20hexOctet /
                      "SHA-256" ":" 32hexOctet /
                      "SHA-384" ":" 48hexOctet /
                      "SHA-512" ":" 64hexOctet /
                      certspec-other-hash

certspec-other-hash = certspec-hash-type ":" certspec-hash-value
certspec-hash-type = scheme
certspec-hash-value = 16*hexOctet

certspec-content = ("HEX" / "BASE16") ":" 1*hexOctet /
                      "BASE64"  ":" base64string

base64char       = ALPHA / DIGIT / "+" / "/"
base64string     = 1*(4base64char)
                      [ 3base64char "=" / 2base64char "==" ]

distinguishedName = distinguishedName@[RFC4514]
hexstring         = hexstring@[RFC4514]

certspec-el   = "ISSUERSN" ":" distinguishedName ";" serialNumber /
                   "SUBJECTEXP" ":" distinguishedName ";" certtime /
                   "HOLDEREXP" ":" holder ";" certtime /
                   "SKI"      ":" 1*(hexOctet)

serialNumber  = 1*hexOctet

holder     = distinguishedName ";" serialNumber ["+" certspec-hash] /
                ("SPKI" / "CERT" / numericoid ) "/" certspec-hash /
                hexstring

certtime   = date-fullyear date-month date-mday
                time-hour time-minute time-second %s"Z" /
                date-time-nosecfrac

full-date   = full-date@[RFC3339]
time-hour   = time-hour@[RFC3339]
time-minute = time-minute@[RFC3339]
time-second = time-second@[RFC3339]
time-offset = time-offset@[RFC3339]

date-time-nosecfrac = full-date "T" time-hour ":" time-minute ":"
                         time-second time-offset

certspec-path     = certspec-uri / certspec-file / certspec-reg

URI-reference     = URI-reference@[RFC3986]
URI-Template      = URI-Template@[RFC6570]

certspec-uri      = "URI:" URI-reference / URI-Template

certspec-file     = ("/" / "\" / [A-Z] ":" /
                       ("." / "..") ("/" / "\") / "~" / "%" / "$")
                       *filepathchar

filepathchar      = %x01-29 / %x2B-3B / "=" / %x40-5B /
                       %x5D-7B / %x7D-7F / quoted-fpc / BEYONDASCII

quoted-fpc        = "\" ("*" / "<" / ">" / "?" / "\" / "|")

certspec-reg      = reg-hive 1*("\" reg-key)
                       ["\\" reg-value-name]

reg-hive          = reg-local-hive / reg-remote-hive
reg-local-hive    = "HKEY_LOCAL_MACHINE" / "HKEY_CURRENT_USER" /
                       "HKEY_CLASSES_ROOT" / "HKEY_USERS" /
                       "HKEY_CURRENT_CONFIG" /
                       "HKLM:" / "HKCU:" / "HKCR:" /
                       "HKU:" / "HKCC:"
reg-remote-hive   = "\\" reg-name@[RFC3986] "\" ("HKLM" / "HKU") ":"

reg-key           = 1*(%x20-3B / %x3D / %x3F-5B %x5D-7B / %x7D.7E /
                          "\<" / "\>" / "\|" / BEYONDASCII)
reg-value-name    = *(%x20-3B / %x3D / %x3F-7B / %x7D.7E /
                          "\<" / "\>" / "\|" / BEYONDASCII)

                          Figure 2: certspec ABNF

multispec = 1*("<" certspec ">")

                         Figure 3: multispec ABNF

pkcsattrs = pkcsattr ["," pkcsattrs]
pkcsattr  = pkcsattrType ["=" pkcsattrValues]
pkcsattrType = descr / numericoid
pkcsattrValues = pkcsattrValue ["+" pkcsAttrValues]
pkcsattrValue = hexstring / string /
                   basic-xer-string / asn1-value-string
basic-xer-string = "<" <balanced tags> ">"
asn1-value-string = %x22 <balanced quotation marks> %x22

                         Figure 4: pkcsattrs ABNF

localKeyId=#0402534C,friendlyName=Chubby\F0\9F\90\B0
