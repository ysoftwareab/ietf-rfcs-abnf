BaseStream1 = Element0 *element e

Element0 = i %d0 %d3 %d232 %d1
               ; byte 'i' followed by an INT4 with value 256001

element = [elementName] (simple / array / string)

simple =
           b INT1 /                    ; b-element
           s INT2 /                    ; s-element
           i INT4 /                    ; i-element
           l INT8 /                    ; l-element
           f FLOAT4 /                  ; f-element
           d FLOAT8                    ; d-element

array =
           B size *INT1 /              ; B-element
           S size *INT2 /              ; S-element
           I size *INT4 /              ; I-element
           L size *INT8 /              ; L-element
           F size *FLOAT4 /            ; F-element
           D size *FLOAT8              ; D-element

string =
           U size utf8

utf8 = <an UTF-8 encoded string>

size = shortSize / longSize

shortSize = <an INT1 between 0 and 127 inclusive>

longSize = minus8 longSizeNumber

minus8 = <an INT1 with the value -8>

longSizeNumber = <an INT8 larger than or equal to 128>

elementName = N nameSize nameString

nameSize = <an INT1 between 1 and 127 inclusive>

nameString = ALPHA 0*126digitLetterOrUnderscore

digitLetterOrUnderscore = ALPHA / DIGIT / UNDERSCORE

ALPHA = %x41-5A / %x61-7A       ; A-Z / a-z

UNDERSCORE = %x5F               ; '_'

DIGIT = "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"

e = %x65
N = %x4E
b = %x62
s = %x73
i = %x69
l = %x6C
f = %x66
d = %x64
B = %x42
S = %x53
I = %x49
L = %x4C
F = %x46
D = %x44
U = %x55
INT1 = <previously defined>
INT2 = <previously defined>
INT4 = <previously defined>
INT8 = <previously defined>
FLOAT4 = <previously defined>
FLOAT8 = <previously defined>

