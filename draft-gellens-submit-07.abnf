SUBM   =  "SUBM" [domain]  ; <domain defined in RFC 821>
change-id          = "Change-ID" ":" SP id-parameters

contact            = "Contact" "=" "<" local-part "@" domain ">"

date               = "Date" "=" date-time

date-time          = <date-time as specified in [MESSAGE-FORMAT],
                          specifically RFC 821 section 5.1, as modified
                          by RFC-1123 section 5.2.14, except that
                          4-digit years MUST be used>
domain             = <domain as defined in [SMTP-MTA]>

id-parameters      = date ";" SP msa [";" SP port] ";" SP contact
                         *(";" SP ext-parameter)

local-part         = <defined in [SMTP-MTA]>

msa                = "MSA" "=" [msa-literal "@"] msa-domain

msa-domain         = domain

msa-literal        = quoted-string

port               = "Port" "=" 1*DIGIT


change-history     = "Change-History" ":" SP hist-parameters

action             = "Action" "=" ("Added" / "Changed" / "Expanded"
                         / "Quoted" / "Removed" / "Unquoted")

cause              = "Cause" "=" ("Bad-Syntax" / "Incorrect"
                         / "Missing" / "Alias" / "Policy")

element            = field / envelope

envelope           = "Envelope" "=" ("MAIL" / "RCPT" / "DATA" /
                         ext-parameter)

field              = "Field" "=" ("body" / header-field)

header-field       = <header field as specified in [HEADERS]>

hist-parameters    = element ";" SP action [";" SP cause]
                         [";" SP original] [";" SP result]
                         *(";" SP ext-parameter)

original           = "Original" "=" value

result             = "Result" "=" value

value              = simple-value / quoted-string


alphanumeric       = ALPHA / DIGIT

ext-parameter      = [alphanumeric *(alphanumeric / "." / "-")]
                         alphanumeric

printable-char     = VCHAR / SP

quoted-char        = printable-char / "\" quoted-specials

quoted-specials    = DQUOTE / "\"

quoted-string      = DQUOTE *quoted-char DQUOTE

simple-char        = %x21 / %x23-3A / %x3C-7E
                           ;ASCII character in the range exclamation
                           ;mark through tilde, except quote and
                           ;semicolon

simple-value       = 1*simple-char


MSA=helpful.qualcomm.gork;
Contact=<Postmaster@Qualcomm.Gork>
Original=Foo; Result=Foobar
Original=Foo; Result="Foobar L. Gork"
Original="John Icons Now @$1.99 Doe"

MSA="xyz99abc"@Qualcomm.Gork;
Contact=<admin+msa@Shy.Qualcomm.Gork>;
