header = "Authentication-Results" CFWS ":" CFWS hostname CFWS
             headerspec *(CFWS ";" CFWS method CFWS "=" CFWS result)
             CFWS

hostname = domain
              ; as defined in section 3.4.1 of [MAIL]

method = token
            ; a method indicates which method's result is
            ; is represented by "value", and is one of the methods
            ; explicitly defined as valid in this document
            ; or is an extension method as defined below

result = "pass" / "fail" / "softfail" / "neutral" /
             "temperror" / "permerror"
             ; an indication of the results of the attempt to
             ; authenticate the sender

token = ; as defined in Appendix A of [MIME]

CFWS = ; as defined in section 3.2.3 of [MAIL]

headerspec = ptype CWFS "." CWFS property CWFS "=" CFWS value
                ; an indication of which property of the message
                ; was evaluated by the authentication scheme being
                ; applied to yield the reported result

ptype = "smtp" / "header"
           ; indicates whether the property being evaluated was
           ; a parameter to an SMTP command or was a value taken
           ; from a message header

property = token
              ; if "ptype" is "smtp", this indicates which SMTP command
              ; provided the value which was evaluated by the
              ; authentication scheme being applied, or if "ptype" is
              ; "header", this indicates from which header the value
              ; being evaluated was extracted

value = token / mailbox
           ; the value extracted from the message property defined
           ; by the "ptype.property" construction; if the value is
           ; intended ; to identify a mailbox, then it is a "mailbox"
           ; as defined in section 3.4 of [MAIL]


auth=pass (cram-md5); spf=pass
domainkeys=pass (good signature)
spf=fail
c=simple; q=dns;
b=EToRSuvUfQVP3Bkz ... rTB0t0gYnBVCM=
