mdn-request-header = "Disposition-Notification-To" ":"
              mailbox-list CRLF

Disposition-Notification-Options =
             "Disposition-Notification-Options" ":" [FWS]
                            disposition-notification-parameter-list CRLF

disposition-notification-parameter-list =
             disposition-notification-parameter
             *([FWS] ";" [FWS] disposition-notification-parameter)

disposition-notification-parameter = attribute [FWS] "="
             [FWS] importance [FWS] "," [FWS] value
             *([FWS] "," [FWS] value)

importance = "required" / "optional"

attribute = Atom

value = word

original-recipient-header =
             "Original-Recipient" ":" OWS address-type OWS
             ";" OWS generic-address OWS

OWS = [CFWS]
         ; Optional whitespace.
         ; MDN generators SHOULD use "*WSP"
         ; (Typically a single space or nothing.
         ; It SHOULD be nothing at the end of a field.),
         ; unless an RFC 5322 "comment" is required.
         ;
         ; MDN parsers MUST parse it as "[CFWS]".

disposition-notification-content = [ reporting-ua-field CRLF ]
             [ mdn-gateway-field CRLF ]
             [ original-recipient-field CRLF ]
             final-recipient-field CRLF
             [ original-message-id-field CRLF ]
             disposition-field CRLF
             *( error-field CRLF )
             *( extension-field CRLF )

extension-field = extension-field-name ":" *([FWS] text)

extension-field-name = field-name

address-type = Atom

Atom = <The version from RFC 5321 (not from RFC 5322)
              is used in this document.>

mta-name-type = Atom

reporting-ua-field = "Reporting-UA" ":" OWS ua-name OWS
                        [ ";" OWS ua-product OWS ]

ua-name = *text-no-semi

ua-product = *([FWS] text)

text-no-semi = %d1-9 /         ; "text" characters excluding NUL, CR,
                  %d11 / %d12 / %d14-58 / %d60-127  ; LF, or semi-colon

mdn-gateway-field = "MDN-Gateway" ":" OWS mta-name-type OWS
                       ";" OWS mta-name OWS

mta-name = *text

original-recipient-field =
             "Original-Recipient" ":" OWS address-type OWS
             ";" OWS generic-address OWS

generic-address = *text

final-recipient-field = "Final-Recipient" ":" OWS address-type OWS
                           ";" OWS generic-address OWS

original-message-id-field =
             "Original-Message-ID" ":" msg-id

disposition-field =
             "Disposition" ":" OWS disposition-mode OWS ";"
             OWS disposition-type
             [ OWS "/" OWS disposition-modifier
             *( OWS "," OWS disposition-modifier ) ] OWS

disposition-mode = action-mode OWS "/" OWS sending-mode

action-mode = "manual-action" / "automatic-action"

sending-mode = "MDN-sent-manually" / "MDN-sent-automatically"

disposition-type = "displayed" / "deleted" / "dispatched" /
             "processed"

disposition-modifier = "error" / disposition-modifier-extension

disposition-modifier-extension = Atom
error-field = "Error" ":" *([FWS] text)

OWS = [CFWS]
          ; Optional whitespace.
          ; MDN generators SHOULD use "*WSP"
          ; (Typically a single space or nothing.
          ; It SHOULD be nothing at the end of a field.),
          ; unless an RFC 5322 "comment" is required.
          ;
          ; MDN parsers MUST parse it as "[CFWS]".

mdn-request-header =
           "Disposition-Notification-To" ":" mailbox-list CRLF
Disposition-Notification-Options =
           "Disposition-Notification-Options" ":" [FWS]
                     disposition-notification-parameter-list CRLF

disposition-notification-parameter-list =
                     disposition-notification-parameter
                     *([FWS] ";" [FWS]
                     disposition-notification-parameter)

disposition-notification-parameter = attribute [FWS] "=" [FWS]
                     importance [FWS] "," [FWS] value *([FWS] ","
                     [FWS] value)

importance = "required" / "optional"

attribute = Atom

value = word

original-recipient-header =
           "Original-Recipient" ":" OWS address-type OWS
           ";" OWS generic-address OWS CRLF

disposition-notification-content =
           [ reporting-ua-field CRLF ]
           [ mdn-gateway-field CRLF ]
           [ original-recipient-field CRLF ]
           final-recipient-field CRLF
           [ original-message-id-field CRLF ]
           disposition-field CRLF
           *( error-field CRLF )
           *( extension-field CRLF )

address-type = Atom

mta-name-type = Atom

reporting-ua-field = "Reporting-UA" ":" OWS ua-name OWS [
                         ";" OWS ua-product OWS ]

ua-name = *text-no-semi

ua-product = *([FWS] text)

text-no-semi = %d1-9 /        ; "text" characters excluding NUL, CR,
            %d11 / %d12 / %d14-58 / %d60-127      ; LF, or semi-colon
mdn-gateway-field = "MDN-Gateway" ":" OWS mta-name-type OWS
                        ";" OWS mta-name

mta-name = *text

original-recipient-field =
           "Original-Recipient" ":" OWS address-type OWS
           ";" OWS generic-address OWS

generic-address = *text

final-recipient-field =
           "Final-Recipient" ":" OWS address-type OWS
           ";" OWS generic-address OWS

original-message-id-field = "Original-Message-ID" ":" msg-id

disposition-field =
           "Disposition" ":" OWS disposition-mode OWS ";"
           OWS disposition-type
           [ OWS "/" OWS disposition-modifier
           *( OWS "," OWS disposition-modifier ) ] OWS

disposition-mode = action-mode OWS "/" OWS sending-mode

action-mode = "manual-action" / "automatic-action"

sending-mode = "MDN-sent-manually" / "MDN-sent-automatically"

disposition-type = "displayed" / "deleted" / "dispatched" /
            "processed"

disposition-modifier = "error" / disposition-modifier-extension

disposition-modifier-extension = Atom

error-field = "Error" ":" *([FWS] text)

extension-field = extension-field-name ":" *([FWS] text)

extension-field-name = field-name
boundary="RAA14128.773615765/example.com"

