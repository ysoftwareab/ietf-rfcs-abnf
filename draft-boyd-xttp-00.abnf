XTTP-message   = Request | Response


generic-message = start-line
                               *(message-header CRLF)
                               CRLF
                               [ message-body ]
start-line      = Request-Line | Status-Line


message-header = field-name ":" [ field-value ]
field-name     = token
field-value    = *( field-content | LWS )
field-content  = <the OCTETs making up the field-value
                             and consisting of either *TEXT or
                             combinations of token, separators, and
                             quoted-string>


message-body = entity-body
                         | <entity-body encoded as per
                            transfer encoding>


Request       = Request-Line              ; Section 3.1
                     *(request-header CRLF)    ; Section 3.2
                     CRLF
                     [ message-body ]          ; Section 2.3

Request-Line   = Method SP
                      [request-parameters SP]
                      XTTP-Version CRLF

Method           = extension-method
extension-method = token

request-header = general-header                   ; Section 2.5
Response      = Status-Line               ; Section 4.1
                    *(response-header CRLF)   ; Section 4.2
                    CRLF
                    [ message-body ]          ; Section 5.2

Status-Line = XTTP-Version SP Status-Code SP Reason-Phrase CRLF

entity-header = message-header

entity-body    = *OCTET

OCTET          = <any 8-bit sequence of data>
CHAR           = <any US-ASCII character (octets 0 - 127)>
UPALPHA        = <any US-ASCII uppercase letter "A".."Z">
LOALPHA        = <any US-ASCII lowercase letter "a".."z">
ALPHA          = UPALPHA | LOALPHA
DIGIT          = <any US-ASCII digit "0".."9">
CTL            = <any US-ASCII control character
                       (octets 0 - 31) and DEL (127)>
CR             = <US-ASCII CR, carriage return (13)>
LF             = <US-ASCII LF, linefeed (10)>
SP             = <US-ASCII SP, space (32)>
HT             = <US-ASCII HT, horizontal-tab (9)>
CRLF           = CR LF

LWS            = [CRLF] 1*( SP | HT )

TEXT           = <any OCTET except CTLs,
                       but including LWS>

token          = 1*<any CHAR except CTLs or separators>
separators     = "(" | ")" | "<" | ">" | "@"
                     | "," | ";" | ":" | "\" | <">
                     | "/" | "[" | "]" | "?" | "="
                     | "{" | "}" | SP | HT

quoted-string  = ( <"> *(qdtext | quoted-pair ) <"> )
qdtext         = <any TEXT except <">>

quoted-pair    = "\" CHAR
