jid           = [ localpart "@" ] domainpart [ "/" resourcepart ]
localpart     = 1*1023(localpoint)
                   ;
                   ; a "localpoint" is a UTF-8 encoded
                   ; Unicode code point that conforms to
                   ; the "LocalpartIdentifierClass" subclass
                   ; of the PRECIS IdentifierClass
                   ;
domainpart    = IP-literal / IPv4address / ifqdn
                   ;
                   ; the "IPv4address" and "IP-literal"
                   ; rules are defined in RFC 3986, and
                   ; the first-match-wins (a.k.a. "greedy")
                   ; algorithm described in RFC 3986
                   ; applies to the matching process
                   ;
                   ; note well that reuse of the IP-literal
                   ; rule from RFC 3986 implies that IPv6
                   ; addresses are enclosed in square
                   ; brackets (i.e., beginning with '['
                   ; and ending with ']')
                   ;
ifqdn         = 1*1023(domainpoint)
                   ;
                   ; a "domainpoint" is a UTF-8 encoded
                   ; Unicode code point that conforms to
                   ; RFC 5890
                   ;
resourcepart  = 1*1023(resourcepoint)
                   ;
                   ; a "resourcepoint" is a UTF-8 encoded
                   ; Unicode code point that conforms to
                   ; the "ResourcepartFreeformClass" subclass
                   ; of the PRECIS FreeformClass
                   ;


