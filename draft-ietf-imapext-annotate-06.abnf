append            = "APPEND" SP mailbox [SP flag-list] [SP date-time]
                       [SP "ANNOTATION" SP att-annotate]
                       SP literal
                       ; modifies original IMAP4 APPEND command

att-annotate      = "ANNOTATION" SP "(" entry-att *(SP entry-att) ")"

fetch-att         =/ fetch-annotate
                       ; modifies original IMAP4 fetch-att

fetch-annotate    = "ANNOTATION" SP "(" entries SP attribs ")"
fetch-ann-resp    = "ANNOTATION" SP "(" entry-att *(SP entry-att) ")"

store-att-flags   =/ att-annotate
                       ; modifies original IMAP4 STORE command

search-key        =/ search-annotate
                       ; modifies original IMAP4 search-key

search-annotate   = "ANNOTATION" SP entry-match SP attrib-match
                       SP value

sort-key          =/ sort-annotate
                       ; modifies original
                       ; draft-crispin-imapext-sort-xx.txt sort-key

sort-annotate     = "ANNOTATION" SP entry SP attrib

entries           = entry-match /
                       "(" entry-match *(SP entry-match) ")"
attribs           = attrib-match /
                       "(" attrib-match *(SP attrib-match) ")"
entry-att         = entry SP "(" att-value *(SP att-value) ")"
att-value         = attrib SP value

entry             = string
                       ; slash-separated path to entry
                       ; MUST NOT contain "*" or "%"
entry-match       = string
                       ; slash-separated path to entry
                       ; MAY contain "*" or "%" for use as wildcards 

attrib            = string
                       ; dot-separated attribute name
                       ; MUST NOT contain "*" or "%"
attrib-match      = string
                       ; dot-separated attribute name
                       ; MAY contain "*" or "%" for use as wildcards 

value             = nstring
   
select            =/ *(SP "(" select-param *(SP select-param) ")"
                       ; modifies the original IMAP4 select command to
                       ; accept optional parameters
   
examine            =/ *(SP "(" select-param *(SP select-param) ")"
                       ; modifies the original IMAP4 examine command to
                       ; accept optional parameters
   
select-param      = astring / "(" astring SP astring *(SP astring) ")"
                       ; parameters to SELECT may contain one or
                       ; more atoms or strings - multiple items
                       ; are always parenthesised
   
annotate-param    = "ANNOTATE"
                       ; defines the select parameter used with
                       ; ANNOTATE extension
    
    
