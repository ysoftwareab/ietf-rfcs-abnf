acl2_command    = myrights | "ACL" SP acl2_subcommand

acl2_subcommand = replaceacl | deleteacl | updateacl | getacl | listrights

acl_list_data   = "(" acl_list_tag SP acl_data ")"
                     ;; acl_list_data conforms to the syntax of
                     ;; mbox-list-extended-item from [LISTEXT]

acl_list_tag    = <"> "ACL" <">

ace             = "(" identifier SP rights ")"

acl_data        = "(" [ace *( SP ace )] ")"
                     ;; zero or more parenthesized identifier/rights pairs

always_granted  = rights

deleteacl       = "DELETE" SP mbox_or_pat SP identifier

getacl          = "GET" SP mbox_or_pat

identifier      = astring
                      ;; UTF-8 string with syntax of ident_syntax

ident_syntax    = ident | "-" ident 

ident           = ident_special | ident_user | ident_group |
                     ident_vendor

ident_special   = "anyone" | "authuser" | "owner" | "administrators"

ident_user      = "user=" ident_detail

ident_group     = "group=" ident_detail

ident_vendor    = "vendor=" ident_vname "." ident_detail

ident_detail    = 1*UTF8-CHAR

ident_vname     = 1*UTF8-CHAR
                      ;; MUST NOT contain "."

listrights      = "LIST" SP mbox_or_pat SP identifier

listrights_data = "RIGHTS-INFO" SP mailbox SP identifier
                           SP always_granted *(SP rights)

mod_rights      = quoted
                      ;; +rights to add, -rights to remove

myrights        = "MYRIGHTS" SP mailbox

myrights_data   = "MYRIGHTS" SP mailbox SP rights

myrights_rspcod = "MYRIGHTS" SP rights

replaceacl      = "SET" SP mbox_or_pat *(SP identifier SP rights)

resp-text-code  =/ myrights_data

rights          = quoted
                      ;; MUST NOT contain leading "+" or "-"

updateacl       = "STORE" SP mbox_or_pat SP identifier SP mod_rights

mbox_or_pat     = mailbox / patterns

patterns        = "(" list-mailbox *(list-mailbox) ")"

partialfail_rsp = "ACLFAILED" SP mailbox
                     [SP ["[" resp-text-code "]" SP] text]
                      ;; May contain optional failure reason followed by a
                      ;; human readable text

UTF8-CHAR       = CHAR | UTF8-2 | UTF8-3 | UTF8-4 | UTF8-5 | UTF8-6

CHAR            = %x01-7F

UTF8-loworder   = %x80-BF

UTF8-2          = %xC1-DF UTF8-loworder
                      ;; Disallow overlong sequences beginning with 0xC0.

UTF8-3          = (%xE0 %xA0-BF UTF8-loworder) |
                     (%xE1-EC 2UTF8-loworder) |
                     (%xED %x80-9F UTF8-loworder) |
                     (%xEE 2UTF8-loworder) |
                     (%xEF %x80-BE UTF8-loworder) |
                     (%xEF %xBF %x80-BD)
                      ;; Disallow overlong sequences beginning with 0xE0,
                      ;; disallow encoded surrogate characters, and
                      ;; disallow U+FFFE, U+FFFF.

UTF8-4          = (%xF0 %x90-BF 2UTF8-loworder) |
                     (%xF1-F7 3UTF8-loworder)
                      ;; Disallow overlong sequences beginning with 0xF0.

UTF8-5             = %xF8-FB 4UTF8-1

UTF8-6             = %xFC-FD 5UTF8-1


SETACL          => ACL STORE
DELETEACL       => ACL DELETE
REPLACEACL      => ACL SET
GETACL          => ACL GET
LISTRIGHTS      => ACL LIST

ACL             => Extended LIST response with ACL information
LISTRIGHTS      => RIGHTS-INFO

