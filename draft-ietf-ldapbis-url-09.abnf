ldapurl     = scheme COLON SLASH SLASH [host [COLON port]]
                        [SLASH dn [QUESTION [attributes]
                        [QUESTION [scope] [QUESTION [filter]
                        [QUESTION extensions]]]]]
                                       ; <host> and <port> are defined
                                       ;   in Sections 3.2.2 and 3.2.3
                                       ;   of [RFC2396bis].
                                       ; <filter> is from Section 3 of
                                       ;   [Filters], subject to the
                                       ;   provisions of the
                                       ;   "Percent-Encoding" section
                                       ;   below.

scheme      = "ldap"

dn          = distinguishedName ; From Section 3 of [LDAPDN],
                                       ; subject to the provisions of
                                       ; the "Percent-Encoding"
                                       ; section below.

attributes  = attrdesc *(COMMA attrdesc)
attrdesc    = selector *(COMMA selector)
selector    = attributeSelector ; From Section 4.5.1 of
                                       ; [Protocol], subject to the
                                       ; provisions of the
                                       ; "Percent-Encoding" section
                                       ; below.

scope       = "base" / "one" / "sub"
extensions  = extension *(COMMA extension)
extension   = [EXCLAMATION] extype [EQUALS exvalue]
extype      = oid               ; From section 1.4 of [Models].

exvalue     = LDAPString        ; From section 4.1.2 of
                                       ; [Protocol], subject to the
                                       ; provisions of the
                                       ; "Percent-Encoding" section
                                       ; below.

EXCLAMATION = %x21              ; exclamation mark ("!")
SLASH       = %x2F              ; forward slash ("/")
COLON       = %x3A              ; colon (":")
QUESTION    = %x3F              ; question mark ("?")


c=US?postalAddress

c=US??sub?(cn=Babs%20Jensen)

o=An Example\2C Inc.,c=US

