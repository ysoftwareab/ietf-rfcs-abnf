ecf=192.1.1.3; ecf=192.1.1.4

icid-generated-at=192.0.6.8;
orig-ioi=home1.net
P-Associated-URI       = "P-Associated-URI" HCOLON
                               (p-aso-uri-spec)
                               *(COMMA p-aso-uri-spec)
p-aso-uri-spec         = name-addr *(SEMI ai-param)
ai-param               = generic-param

P-Called-Party-ID      = "P-Called-Party-ID" HCOLON
                               called-pty-id-spec
called-pty-id-spec     = name-addr *(SEMI cpid-param)
cpid-param             = generic-param

P-Visited-Network-ID   = "P-Visited-Network-ID" HCOLON
                                vnetwork-spec
                                *(COMMA vnetwork-spec)
vnetwork-spec          = (token / quoted-string)
                                *(SEMI vnetwork-param)
vnetwork-param         = generic-param

P-Access-Network-Info  = "P-Access-Network-Info" HCOLON
                               access-net-spec
access-net-spec        = access-type *(SEMI access-info)
access-type            = "IEEE-802.11a" / "IEEE-802.11b" /
                               "3GPP-GERAN" / "3GPP-UTRAN-FDD" /
                               "3GPP-UTRAN-TDD" /
                               "3GPP-CDMA2000" / token
access-info            = cgi-3gpp / utran-cell-id-3gpp /
                               extension-access-info
extension-access-info  = gen-value
cgi-3gpp               = "cgi-3gpp" EQUAL
                               (token / quoted-string)
utran-cell-id-3gpp     = "utran-cell-id-3gpp" EQUAL
                               (token / quoted-string)

P-Charging-Addr        = "P-Charging-Function-Addresses" HCOLON
                               charge-addr-params
                               *(SEMI charge-addr-params)
charge-addr-params     = ccf / ecf / generic-param
ccf                    = "ccf" EQUAL gen-value
ecf                    = "ecf" EQUAL gen-value

P-Charging-Vector     = "P-Charging-Vector" HCOLON icid-value
                              *(SEMI charge-params)
charge-params         = icid-gen-addr / orig-ioi /
                              term-ioi / generic-param
icid-value            = "icid-value" EQUAL gen-value
icid-gen-addr         = "icid-generated-at" EQUAL host
orig-ioi              = "orig-ioi" EQUAL gen-value
term-ioi              = "term-ioi" EQUAL gen-value

