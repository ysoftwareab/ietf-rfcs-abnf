getquota =          "GETQUOTA" SP quota-root-name

getquotaroot =      "GETQUOTAROOT" SP mailbox

quota-list =        "(" quota-resource *(SP quota-resource) ")"

quota-resource =    resource-name SP resource-usage SP resource-limit

quota-response =    "QUOTA" SP quota-root-name SP quota-list

quotaroot-response =  "QUOTAROOT" SP mailbox *(SP quota-root-name)

setquota =          "SETQUOTA" SP quota-root-name SP setquota-list

setquota-list =     "(" [setquota-resource *(SP setquota-resource)]
                       ")"

setquota-resource =  resource-name SP resource-limit

quota-root-name =   astring

resource-limit =    number64

resource-name =     "STORAGE" / "MESSAGE" / "MAILBOX" /

                       "ANNOTATION-STORAGE" / resource-name-vnd /

                       resource-name-ext
resource-name-vnd =  "V-" atom

                       ;; Vendor specific, must be registered with IANA.

                       ;; The "V-" prefix should be followed by a domain
                       name

                       ;; under vendor's control.

resource-name-ext =  atom

                       ;; Not starting with V- and defined

                       ;; in a Standard Track or Experimental RFC

resource-names =    "(" [resource-name *(SP resource-name)] ")"

resource-usage =    number64

                       ;; must be less than corresponding resource-limit

capability-quota =  capa-quota-res / "QUOTASET"

                       ;; One or more capa-quota-res must be returned.

                       ;; Also "QUOTASET" can optionally be returned.

capa-quota-res =    "QUOTA=RES-" resource-name

status-att =/       "DELETED" / "DELETED-STORAGE"

                       ;; DELETED status data item MUST be supported

                       ;; when "QUOTA=RES-MESSAGE" capability is

                       ;; advertised.

                       ;; DELETED-STORAGE status data item MUST be

                       ;; supported when "QUOTA=RES-STORAGE" capability

                       ;; is advertised.

status-att-val =/   status-att-deleted /

                       status-att-deleted-storage

status-att-deleted =/  "DELETED" SP number
                       ;; DELETED status data item MUST be supported

                       ;; when "QUOTA=RES-MESSAGE" capability is

                       ;; advertised.

status-att-deleted-storage =/  "DELETED-STORAGE" SP number64

                       ;; DELETED-STORAGE status data item MUST be

                       ;; supported when "QUOTA=RES-STORAGE" capability

                       ;; is advertised.

resp-text-code =/   "OVERQUOTA"

number64 =          <Defined in RFC 9051>

