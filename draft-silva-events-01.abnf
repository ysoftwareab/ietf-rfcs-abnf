impreciseeventc     = "BEGIN" ":" "VIMPRECISEEVENT" CRLF
                                impreciseeventprop *availabilityc
                                *freebusyc
                                "END" ":" "VIMPRECISEEVENT" CRLF

impreciseeventprop  = *(

                              ; the following are REQUIRED,
                              ; but MUST NOT occur more than once

                              dtstamp / uid /

                              ; the following are OPTIONAL,
                              ; but MUST NOT occur more than once

                              class / created / description / due /
                              duration / geo / last-mod / location /
                              min-granularity / max-granularity /
                              organizer / priority / rank / rrule /
                              seq / status / summary / transp / url /

                              ; 'percent' is OPTIONAL,
                              ; but MUST NOT occur more than once and,
                              ; if it occurs, 'duration' MUST occur

                              percent /

                              ; the following are OPTIONAL,
                              ; and MAY occur more than once

                              attach / attendee / categories / comment /
                              contact / rstatus / related / resources /
                              x-prop / iana-prop

                              )

alternativeeventsc     = "BEGIN" ":" "VALTERNATIVEEVENTS" CRLF
                                alternativeeventsprop *eventc
                                *impreciseeventc
                                "END" ":" "VALTERNATIVEEVENTS" CRLF

alternativeeventsprop  = *(

                              ; the following are REQUIRED,
                              ; but MUST NOT occur more than once

                              dtstamp / uid /

                              ; the following are OPTIONAL,
                              ; but MUST NOT occur more than once

                              class / created / description / geo /
                              last-mod / location / organizer /
                              priority / seq / status / summary /
                              transp / url /

                              ; the following are OPTIONAL,
                              ; and MAY occur more than once

                              attach / attendee / categories / comment /
                              contact / rstatus / related / resources /
                              x-prop / iana-prop

                              )

min-granularity  = "MIN-GRANULARITY" min-granulparam ":"
                              dur-value CRLF

min-granulparam  = *(";" other-param)

max-granularity  = "MAX-GRANULARITY" max-granulparam ":"
                              dur-value CRLF

max-granulparam  = *(";" other-param)

rank       = "RANK" rankparam ":" integer CRLF

rankparam  = *(";" other-param)

rankpparam  = "RANK" "=" integer

