IPAddress      = IPv4Address / IPv6Address / "0"
IPv4Address    = 1*3DIGIT 3("." 1*3DIGIT)
IPv6Address    = 1*4HEXDIG 7( ":" 1*4HEXDIG)
Port           = 1*DIGIT
NOSP           = 1*DIGIT            ; Number Of Subsequent Ports
RID            = 1*DIGIT            ; Request ID number

GID            = 1*DIGIT            ; Group ID number
BID            = 1*DIGIT            ; Binding ID number
PT             = "UDP" / "TCP" / "ICMP" / "IP" ; IP protocol type
Authentication = 1*4096VCHAR         ; limited to 4096 Bytes
Challenge      = 1*4096VCHAR         ; limited to 4096 Bytes
Message        = 1*VCHAR
Timeout        = 1*DIGIT             ; timeout in seconds
ADR            = IPAddress WSP Port  ; addresses
SBPS           = PT WSP NOSP WSP ADR
                                          ; single binding parameter set
DBPS           = PT WSP NOSP WSP ADR_src WSP ADR_dst
                                          ; double binding parameter set
ADR_src        = ADR                 ; source ADR
ADR_dst        = ADR                 ; destination ADR
Version        = "SIMCO/1.0"


request =  "open"      WSP RID WSP Version
                            WSP Challenge WSP Authentication CRLF
request =/ "close"     WSP RID CRLF
request =/ "group"     WSP RID WSP GID WSP Timeout CRLF
request =/ "resv"      WSP RID WSP GID WSP BID
                            WSP SBPS WSP Timeout CRLF
                            ; SBPS contains address to be mapped
request =/ "bind"      WSP RID WSP GID WSP BID
                            WSP DBPS WSP Timeout CRLF


Reply =  "410" WSP RID CRLF          ; syntax Error
Reply =/ "411" WSP RID CRLF          ; unknown or illegal request
Reply =/ "510" WSP Message CRLF      ; illegal message

Reply =/ "220" WSP RID CRLF          ; OK
Reply =/ "221" WSP RID WSP Challenge WSP Challenge CRLF
                                          ; Need further Authentication
Reply =/ "222" WSP RID WSP CAP CRLF
                        ; Authentication successful and Capabilities

Reply =/ "420" WSP RID CRLF          ; protocol version mismatch
Reply =/ "421" WSP RID CRLF          ; authentication failed
Reply =/ "520" WSP Message CRLF      ; session closed

Reply =/ "231" WSP RID WSP GID WSP Timeout CRLF
                                          ; OK for binding-group
Reply =/ "233" WSP RID WSP GID CRLF  ; binding-group removed
Reply =/ "430" WSP RID CRLF          ; unknown or illegal GID
Reply =/ "431" WSP RID CRLF          ; binding-group refused
Reply =/ "530" WSP GID CRLF          ; binding-group timed out

Reply =/ "241" WSP RID WSP GID WSP BID
                    WSP SBPS WSP Timeout CRLF
                                          ; OK for reservation
Reply =/ "242" WSP RID WSP GID WSP BID
                    WSP DBPS WSP Timeout CRLF
                                          ; OK for full binding
Reply =/ "243" WSP RID WSP GID WSP BID CRLF
                                          ; binding removed
Reply =/ "440" WSP RID CRLF          ; unknown or illegal BID
Reply =/ "441" WSP RID CRLF          ; binding Refused
Reply =/ "442" WSP RID CRLF          ; illegal IP Address
Reply =/ "443" WSP RID CRLF          ; protocol type not supported
Reply =/ "444" WSP RID CRLF          ; illegal port number
Reply =/ "445" WSP RID CRLF          ; binding modific. rejected
Reply =/ "446" WSP RID CRLF          ; illegal NOSP
Reply =/ "540" WSP BID CRLF          ; binding timed out

CAP      = MAX_TO WSP BOX_TYPE WSP AWC WSP PWC
MAX_TO   = 1*DIGIT         ; maximal timeout granted by the server
BOX_TYPE =  "NAT" / "FW" / "NATFW" / "NAPT" / "NAPTFW"
BOX_TYPE =/ "NAT-PT" / "NAT-PTFW" ; types of the middle box
AWC      = "YES" / "NO"    ; IP address wildcard supported
PWC      = "YES" / "NO"    ; Port number wildcard supported



