UTF8-1-safe    = %x01-2B / %x2D-3C / %x3E-7F
                       ;; As UTF8-1 in RFC 3629 except
                       ;; NUL, "=", and ",".
UTF8-2         = <as defined in RFC 3629 (STD 63)>
UTF8-3         = <as defined in RFC 3629 (STD 63)>
UTF8-4         = <as defined in RFC 3629 (STD 63)>
UTF8-char-safe = UTF8-1 / UTF8-2 / UTF8-3 / UTF8-4

saslname     = 1*(UTF8-char-safe / "=2C" / "=3D")
gs2-authzid  = "a=" saslname
                        ;; GS2 has to transport an authzid since
                        ;; the GSS-API has no equivalent
gs2-std-mech = "F"
                        ;; "F" means the mechanism is NOT is a
                        ;; standard GSS-API mechanism in that the
                        ;; RFC2743 section 3.1 header was missing
gs2-cb-flag  = "n" / "y" / "p"
                        ;; GS2 channel binding (CB) flag
                        ;; "n" -> client does not support CB
                        ;; "y" -> client supports CB, thinks the server
                        ;;           does not
                        ;; "p" -> client supports and used CB
gs2-header   = [gs2-std-mech] gs2-cb-flag [gs2-authzid] ","
                        ;; The GS2 header is gs2-header.
                        ;; gs2-std-mech is present if the GSS-API
                        ;; mechanism's initial context token did not
                        ;; have the standard header defined in
                        ;; [RFC2743] section 3.1.

