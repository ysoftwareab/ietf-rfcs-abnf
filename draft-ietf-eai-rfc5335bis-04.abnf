UTF8-non-ascii  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   <See Section 4 of RFC3629>

UTF8-3          =   <See Section 4 of RFC3629>

UTF8-4          =   <See Section 4 of RFC3629>

FWS     =  <see [RFC5322] Section 3.2.2, folding white space>

CFWS    =  <see [RFC5322] Section 3.2.2>

ctext   =/  UTF8-non-ascii

comment =   "(" *([FWS] uCcontent) [FWS] ")"

word    =   uAtom / uQuoted-String

uText          = %d1-9 /    ; all UTF-8 characters except
                    %d11-12 /  ; US-ASCII NUL, CR, and LF
                    %d14-127 /
                    UTF8-non-ascii

uQuoted-Pair   = ("\" (VCHAR / WSP / UTF8-non-ascii )) / obs-qp

VCHAR          = <See appendix B.1 of RFC 5234>

WSP                = <See appendix B.1 of RFC 5234>

uQcontent      = uQtext / uQuoted-Pair

uQuoted-Pair   = ("\" uText) / obs-qp

DQUOTE         = <See appendix B.1 of RFC 5234>

uCcontent      = ctext / uQuoted-Pair / comment

uQtext         = qtext / UTF8-non-ascii

uAtext         = ALPHA / DIGIT /
                    "!" / "#" /  ; Any character except
                    "$" / "%" /  ; controls, SP, and specials.
                    "&" / "'" /  ; Used for atoms.
                    "*" / "+" /
                    "-" / "/" /
                    "=" / "?" /
                    "^" / "_" /
                    "`" / "{" /
                    "|" / "}" /
                    "~" /
                    UTF8-non-ascii

uAtom          = [CFWS] 1*uAtext [CFWS]

uDot-Atom      = [CFWS] uDot-Atom-text [CFWS]

uDot-Atom-text = 1*uAtext *("." 1*uAtext)

qcontent       =/ uQcontent


description  =  "Content-Description" ":" *uText





                   ; Replace description in Section 8 of [RFC2045]


mailbox         =  name-addr / addr-spec / uAddr-Spec
                      ; Replace mailbox in Section 3.4 of RFC 5322

angle-addr      =/ [CFWS] "<" uAddr-Spec ">" [CFWS]
                      ; Replace angle-addr in Section 3.4 of RFC 5322

uAddr-Spec      =  uLocal-Part "@" ( uDomain / address-literal )

address-literal =  <See Section 4.1.2 of RFC 5321>

uLocal-Part     =  uDot-String / uQuoted-String / obs-Local-Part
                      ; Replace Local-Part in Section 4.1.2 of RFC 5321

uDot-string     =  uAtom *("." uAtom)
                      ; Replace Dot-string in RFC 5321, Section 4.1.2

uDomain         =  (sub-udomain 1*("." sub-uDomain)) /
                      dot-atom / domain-literal / obs-domain
                      ; Replace Domain in Section 4.1.2 of  RFC 5321

sub-udomain     =  uLet-dig [uLdh-str]
                      ; Replace sub-domain in Section 4.1.2 of RFC 5321

uLet-dig        =  Let-dig / UTF8-non-ascii
                      ; Replace uLet-dig in Section 4.1.2 of RFC 5321

Let-dig         =  <See Section 4.1.2 of RFC 5321>

uLdh-str        =  *( ALPHA / DIGIT / "-" / UTF8-non-ascii) uLet-dig
                      ; Replace Ldh-str Section 4.1.2 of RFC 5321





received-token   =/    uAddr-Spec

