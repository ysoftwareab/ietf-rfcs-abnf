capability         =/  "X-DRAFT-W02-QRESYNC"
                      ;; [[Note to RFC Editor: fix before
                      ;; publication]]

select-param     = "QRESYNC" SP "(" uidvalidity SP
                      mod-sequence-value [SP known-uids]
                      [SP seq-match-data] ")"
                      ;; conforms to the generic select-param
                      ;; syntax defined in [IMAPABNF]

seq-match-data   = "(" known-sequence-set SP known-uid-set ")"

uidvalidity      = nz-number
known-uids       = sequence-set
                      ;; sequence of UIDs, "*" is not allowed

known-sequence-set =  sequence-seq
                      ;; set of message numbers corresponding to
                      ;; the UIDs in known-uid-set, in ascending order.
                      ;; * is not allowed.

known-uid-set      =  sequence-seq
                      ;; set of UIDs corresponding to the messages in
                      ;; known-sequence-set, in ascending order.
                      ;; * is not allowed.

message-data       =/  expunged-resp

expunged-resp      =  "VANISHED" [expunge-correlator] SP known-uids

expunge-correlator =  SP "(" single-exp-correlator
                      *(SP single-exp-correlator) ")"
                      ;; Unless explicitly specified otherwise,
                      ;; all correlator types must be specified
                      ;; only once.

single-exp-correlator =  "TAG" SP tag-string
                      ;; Correlator type followed by parameters

rexpunges-fetch-mod =  "VANISHED"
                      ;; VANISHED UID FETCH modifier conforms
                      ;; to the fetch-modifier syntax
                      ;; defined in [IMAPABNF].  It is only
                      ;; allowed in the UID FETCH command.


