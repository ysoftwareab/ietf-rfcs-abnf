examine            = "EXAMINE" SP mailbox [SP view-set-arg]
                           ; redefines [IMAP4] EXAMINE command

exists_resp        = number "EXISTS" SP number
                           ; replaces standard [IMAP4] EXISTS response
                           ; when VIEW SET is in effect

expunge_resp       = nz-number "EXPUNGE" SP uniqueid SP position
                           ; replaces standard IMAP4 EXPUNGE response
                           ; when VIEW SET is in effect

flag               =/ "\View"
                           ; extends [IMAP4] message flags
                           ; new flag is only available when
                           ; VIEW SET is in effect

msg-att-dynamic    =/ "VIEW" SP position
                           ; extends [IMAP4] message data item
                           ; FETCH response

position           = number ["." number]
                           ; position number of messages in the view
                           ; may be hierarchical if sub-command defines
                           ; hierarchic ordering of messages
                           ; 0 means outside of the current view

search-key         =/ "VIEW" / "UNVIEW"
                           ; extends [IMAP4] search criteria
                           ; new search criteria are only available
                           ; when a view is in effect

select             = "SELECT" SP mailbox [SP view-set-arg]
                           ; redefines [IMAP4] SELECT command

view               = "VIEW" SP (view-set / view-reference)

view-mailbox-data  = "VIEW" SP (("SEARCH" *(SP position)) / OTHER)
                           ; OTHER reserved for future expansion

view-msg-data      = nz_number SP "VIEW" SP uniqueid
                         SP position [SP position]

view-reference     = copy / fetch / store / search
                           ; copy, fetch, store, search defined
                           ; in [IMAP4]

view-response      = view-msg-data / view-mailbox-data

view-set           = "SET" [SP view-set-arg]

view-set-arg       = search / sort / thread / OTHER
                           ; search is defined in [IMAP4]
                           ; sort is defined in [SORT]
                           ; thread is defined in [THREAD]
                           ; OTHER reserved for future definition


