filter-mailboxes-other =/  ("subtree-one" SP one-or-more-mailbox)
           ; Update definition from RFC 5465 [NOTIFY]
           ; Add new "subtree-one" selector.

filter-mailboxes-selected =  "selected"
           ; Update definition from RFC 5465 [NOTIFY]
           ; We forbid the use of "selected-delayed".

one-correlator =  ("TAG" SP tag-string) / ("MAILBOX" SP astring) /
           ("UIDVALIDITY" SP nz-number)
           ; Each correlator MUST appear exactly once

scope-option =  scope-option-name [SP scope-option-value]
           ; No options defined here.  Syntax for future extensions.

scope-option-name =  tagged-ext-label
           ; No options defined here.  Syntax for future extensions.
scope-option-value =  tagged-ext-val
           ; No options defined here.  Syntax for future extensions.

scope-options =  scope-option *(SP scope-option)
           ; A given option may only appear once
           ; No options defined here.  Syntax for future extensions.

search =  "SEARCH" [SP search-source-opts] [SP search-return-opts] SP
           search-program
           ; Updates definition in RFC 4466 [IMAPABNF]

search-correlator =  SP "(" one-correlator *(SP one-correlator) ")"
           ; Updates definition in RFC 4466 [IMAPABNF]

search-source-opts =  "IN" SP "(" source-mbox [SP "(" scope-options
           ")"] ")"

source-mbox =  filter-mailboxes *(SP filter-mailboxes)
           ; filter-mailboxes is defined in RFC 5465 [NOTIFY]
           ; See updated definition of filter-mailboxes-other, above.
           ; See updated definition of filter-mailboxes-selected, above.


