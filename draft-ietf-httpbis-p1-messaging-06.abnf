OWS            = *( [ obs-fold ] WSP )
                    ; "optional" whitespace
RWS            = 1*( [ obs-fold ] WSP )
                    ; "required" whitespace
BWS            = OWS
                    ; "bad" whitespace
obs-fold       = CRLF
                    ; see Section 4.2

tchar          = "!" / "#" / "$" / "%" / "&" / "'" / "*"
                    / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~"
                    / DIGIT / ALPHA

token          = 1*tchar

quoted-string  = DQUOTE *( qdtext / quoted-pair ) DQUOTE
qdtext         = *( OWS / %x21 / %x23-5B / %x5D-7E / obs-text )
obs-text       = %x80-FF

quoted-text    = %x01-09 /
                      %x0B-0C /
                      %x0E-FF ; Characters excluding NUL, CR and LF
quoted-pair    = "\" quoted-text

request-header  = <request-header, defined in [Part2], Section 3>
response-header = <response-header, defined in [Part2], Section 5>


entity-body     = <entity-body, defined in [Part3], Section 3.2>
entity-header   = <entity-header, defined in [Part3], Section 3.1>








Cache-Control   = <Cache-Control, defined in [Part6], Section 3.4>
Pragma          = <Pragma, defined in [Part6], Section 3.4>
Warning         = <Warning, defined in [Part6], Section 3.6>


URI           = <URI, defined in [RFC3986], Section 3>
URI-reference = <URI-reference, defined in [RFC3986], Section 4.1>
absolute-URI  = <absolute-URI, defined in [RFC3986], Section 4.3>
relative-part = <relative-part, defined in [RFC3986], Section 4.2>
authority     = <authority, defined in [RFC3986], Section 3.2>
fragment      = <fragment, defined in [RFC3986], Section 3.5>
path-abempty  = <path-abempty, defined in [RFC3986], Section 3.3>
path-absolute = <path-absolute, defined in [RFC3986], Section 3.3>
port          = <port, defined in [RFC3986], Section 3.2.3>
query         = <query, defined in [RFC3986], Section 3.4>
uri-host      = <host, defined in [RFC3986], Section 3.2.2>

partial-URI   = relative-part [ "?" query ]

http-URI = "http:" "//" authority path-abempty [ "?" query ]

HTTP-Version   = HTTP-Prot-Name "/" 1*DIGIT "." 1*DIGIT





HTTP-Prot-Name = %x48.54.54.50 ; "HTTP", case-sensitive

HTTP-date    = rfc1123-date / obsolete-date
obsolete-date = rfc850-date / asctime-date
rfc1123-date = wkday "," SP date1 SP time SP GMT
rfc850-date  = weekday "," SP date2 SP time SP GMT
asctime-date = wkday SP date3 SP time SP 4DIGIT
date1        = 2DIGIT SP month SP 4DIGIT
                    ; day month year (e.g., 02 Jun 1982)
date2        = 2DIGIT "-" month "-" 2DIGIT
                    ; day-month-year (e.g., 02-Jun-82)
date3        = month SP ( 2DIGIT / ( SP 1DIGIT ))
                    ; month day (e.g., Jun  2)
time         = 2DIGIT ":" 2DIGIT ":" 2DIGIT
                    ; 00:00:00 - 23:59:59
wkday        = s-Mon / s-Tue / s-Wed
                  / s-Thu / s-Fri / s-Sat / s-Sun
weekday      = l-Mon / l-Tue / l-Wed
                  / l-Thu / l-Fri / l-Sat / l-Sun
month        = s-Jan / s-Feb / s-Mar / s-Apr
                  / s-May / s-Jun / s-Jul / s-Aug
                  / s-Sep / s-Oct / s-Nov / s-Dec

GMT   = %x47.4D.54 ; "GMT", case-sensitive

s-Mon = %x4D.6F.6E ; "Mon", case-sensitive
s-Tue = %x54.75.65 ; "Tue", case-sensitive





s-Wed = %x57.65.64 ; "Wed", case-sensitive
s-Thu = %x54.68.75 ; "Thu", case-sensitive
s-Fri = %x46.72.69 ; "Fri", case-sensitive
s-Sat = %x53.61.74 ; "Sat", case-sensitive
s-Sun = %x53.75.6E ; "Sun", case-sensitive

l-Mon = %x4D.6F.6E.64.61.79          ; "Monday", case-sensitive
l-Tue = %x54.75.65.73.64.61.79       ; "Tuesday", case-sensitive
l-Wed = %x57.65.64.6E.65.73.64.61.79 ; "Wednesday", case-sensitive
l-Thu = %x54.68.75.72.73.64.61.79    ; "Thursday", case-sensitive
l-Fri = %x46.72.69.64.61.79          ; "Friday", case-sensitive
l-Sat = %x53.61.74.75.72.64.61.79    ; "Saturday", case-sensitive
l-Sun = %x53.75.6E.64.61.79          ; "Sunday", case-sensitive

s-Jan = %x4A.61.6E ; "Jan", case-sensitive
s-Feb = %x46.65.62 ; "Feb", case-sensitive
s-Mar = %x4D.61.72 ; "Mar", case-sensitive
s-Apr = %x41.70.72 ; "Apr", case-sensitive
s-May = %x4D.61.79 ; "May", case-sensitive
s-Jun = %x4A.75.6E ; "Jun", case-sensitive
s-Jul = %x4A.75.6C ; "Jul", case-sensitive
s-Aug = %x41.75.67 ; "Aug", case-sensitive
s-Sep = %x53.65.70 ; "Sep", case-sensitive
s-Oct = %x4F.63.74 ; "Oct", case-sensitive
s-Nov = %x4E.6F.76 ; "Nov", case-sensitive
s-Dec = %x44.65.63 ; "Dec", case-sensitive

transfer-coding         = "chunked" / transfer-extension
transfer-extension      = token *( OWS ";" OWS parameter )

parameter               = attribute BWS "=" BWS value
attribute               = token
value                   = token / quoted-string





Chunked-Body   = *chunk
                      last-chunk
                      trailer-part
                      CRLF

chunk          = chunk-size *WSP [ chunk-ext ] CRLF
                      chunk-data CRLF
chunk-size     = 1*HEXDIG
last-chunk     = 1*("0") *WSP [ chunk-ext ] CRLF

chunk-ext      = *( ";" *WSP chunk-ext-name
                         [ "=" chunk-ext-val ] *WSP )
chunk-ext-name = token
chunk-ext-val  = token / quoted-string
chunk-data     = 1*OCTET ; a sequence of chunk-size octets
trailer-part   = *( entity-header CRLF )

product         = token ["/" product-version]
product-version = token

qvalue         = ( "0" [ "." 0*3DIGIT ] )
                    / ( "1" [ "." 0*3("0") ] )

      Note: "Quality values" is a misnomer, since these values merely
      represent relative degradation in desired quality.


HTTP-message   = Request / Response     ; HTTP/1.1 messages

generic-message = start-line
                       *( message-header CRLF )
                       CRLF
                       [ message-body ]
start-line      = Request-Line / Status-Line

message-header = field-name ":" OWS [ field-value ] OWS
field-name     = token
field-value    = *( field-content / OWS )
field-content  = *( WSP / VCHAR / obs-text )

comment        = "(" *( ctext / quoted-pair / comment ) ")"
ctext          = *( OWS / %x21-27 / %x2A-7E / obs-text )

message-body = entity-body





                  / <entity-body encoded as per Transfer-Encoding>

general-header = Cache-Control            ; [Part6], Section 3.2
                    / Connection               ; Section 8.1
                    / Date                     ; Section 8.3
                    / Pragma                   ; [Part6], Section 3.4
                    / Trailer                  ; Section 8.6
                    / Transfer-Encoding        ; Section 8.7
                    / Upgrade                  ; Section 8.8
                    / Via                      ; Section 8.9
                    / Warning                  ; [Part6], Section 3.6

Request       = Request-Line              ; Section 5.1
                     *(( general-header        ; Section 4.5
                      / request-header         ; [Part2], Section 3
                      / entity-header ) CRLF )  ; [Part3], Section 3.1
                     CRLF
                     [ message-body ]          ; Section 4.3









Request-Line   = Method SP request-target SP HTTP-Version CRLF

Method         = token

request-target = "*"
                    / absolute-URI
                    / ( path-absolute [ "?" query ] )
                    / authority

Response      = Status-Line               ; Section 6.1
                     *(( general-header        ; Section 4.5
                      / response-header        ; [Part2], Section 5
                      / entity-header ) CRLF )  ; [Part3], Section 3.1
                     CRLF
                     [ message-body ]          ; Section 4.3

Status-Line = HTTP-Version SP Status-Code SP Reason-Phrase CRLF

Status-Code    = 3DIGIT
Reason-Phrase  = *( WSP / VCHAR / obs-text )


Connection       = "Connection" ":" OWS Connection-v
Connection-v     = 1#connection-token
connection-token = token

Content-Length   = "Content-Length" ":" OWS 1*Content-Length-v
Content-Length-v = 1*DIGIT

Date   = "Date" ":" OWS Date-v
Date-v = HTTP-date

Host   = "Host" ":" OWS Host-v
Host-v = uri-host [ ":" port ] ; Section 2.1.1

TE        = "TE" ":" OWS TE-v
TE-v      = #t-codings
t-codings = "trailers" / ( transfer-extension [ te-params ] )
te-params = OWS ";" OWS "q=" qvalue *( te-ext )
te-ext    = OWS ";" OWS token [ "=" ( token / quoted-string ) ]

Trailer   = "Trailer" ":" OWS Trailer-v
Trailer-v = 1#field-name

Transfer-Encoding   = "Transfer-Encoding" ":" OWS
                           Transfer-Encoding-v
Transfer-Encoding-v = 1#transfer-coding

Upgrade   = "Upgrade" ":" OWS Upgrade-v
Upgrade-v = 1#product

Via               = "Via" ":" OWS Via-v
Via-v             = 1#( received-protocol RWS received-by
                             [ RWS comment ] )
received-protocol = [ protocol-name "/" ] protocol-version
protocol-name     = token
protocol-version  = token
received-by       = ( uri-host [ ":" port ] ) / pseudonym
pseudonym         = token

BWS = OWS

Cache-Control = <Cache-Control, defined in [Part6], Section 3.4>
Chunked-Body = *chunk last-chunk trailer-part CRLF
Connection = "Connection:" OWS Connection-v
Connection-v = *( "," OWS ) connection-token *( OWS "," [ OWS
    connection-token ] )
Content-Length = "Content-Length:" OWS 1*Content-Length-v
Content-Length-v = 1*DIGIT

Date = "Date:" OWS Date-v
Date-v = HTTP-date

GMT = %x47.4D.54

HTTP-Prot-Name = %x48.54.54.50
HTTP-Version = HTTP-Prot-Name "/" 1*DIGIT "." 1*DIGIT
HTTP-date = rfc1123-date / obsolete-date
HTTP-message = Request / Response
Host = "Host:" OWS Host-v
Host-v = uri-host [ ":" port ]

Method = token

OWS = *( [ obs-fold ] WSP )

Pragma = <Pragma, defined in [Part6], Section 3.4>

RWS = 1*( [ obs-fold ] WSP )
Reason-Phrase = *( WSP / VCHAR / obs-text )
Request = Request-Line *( ( general-header / request-header /
    entity-header ) CRLF ) CRLF [ message-body ]
Request-Line = Method SP request-target SP HTTP-Version CRLF
Response = Status-Line *( ( general-header / response-header /
    entity-header ) CRLF ) CRLF [ message-body ]

Status-Code = 3DIGIT
Status-Line = HTTP-Version SP Status-Code SP Reason-Phrase CRLF

TE = "TE:" OWS TE-v
TE-v = [ ( "," / t-codings ) *( OWS "," [ OWS t-codings ] ) ]
Trailer = "Trailer:" OWS Trailer-v
Trailer-v = *( "," OWS ) field-name *( OWS "," [ OWS field-name ] )
Transfer-Encoding = "Transfer-Encoding:" OWS Transfer-Encoding-v
Transfer-Encoding-v = *( "," OWS ) transfer-coding *( OWS "," [ OWS
    transfer-coding ] )





URI = <URI, defined in [RFC3986], Section 3>
URI-reference = <URI-reference, defined in [RFC3986], Section 4.1>
Upgrade = "Upgrade:" OWS Upgrade-v
Upgrade-v = *( "," OWS ) product *( OWS "," [ OWS product ] )

Via = "Via:" OWS Via-v
Via-v = *( "," OWS ) received-protocol RWS received-by [ RWS comment
    ] *( OWS "," [ OWS received-protocol RWS received-by [ RWS comment ]
    ] )

Warning = <Warning, defined in [Part6], Section 3.6>

absolute-URI = <absolute-URI, defined in [RFC3986], Section 4.3>
asctime-date = wkday SP date3 SP time SP 4DIGIT
attribute = token
authority = <authority, defined in [RFC3986], Section 3.2>

chunk = chunk-size *WSP [ chunk-ext ] CRLF chunk-data CRLF
chunk-data = 1*OCTET
chunk-ext = *( ";" *WSP chunk-ext-name [ "=" chunk-ext-val ] *WSP )
chunk-ext-name = token
chunk-ext-val = token / quoted-string
chunk-size = 1*HEXDIG
comment = "(" *( ctext / quoted-pair / comment ) ")"
connection-token = token
ctext = *( OWS / %x21-27 / %x2A-7E / obs-text )

date1 = 2DIGIT SP month SP 4DIGIT
date2 = 2DIGIT "-" month "-" 2DIGIT
date3 = month SP ( 2DIGIT / ( SP DIGIT ) )

entity-body = <entity-body, defined in [Part3], Section 3.2>
entity-header = <entity-header, defined in [Part3], Section 3.1>

field-content = *( WSP / VCHAR / obs-text )
field-name = token
field-value = *( field-content / OWS )
fragment = <fragment, defined in [RFC3986], Section 3.5>

general-header = Cache-Control / Connection / Date / Pragma / Trailer
    / Transfer-Encoding / Upgrade / Via / Warning
generic-message = start-line *( message-header CRLF ) CRLF [
    message-body ]

http-URI = "http://" authority path-abempty [ "?" query ]

l-Fri = %x46.72.69.64.61.79
l-Mon = %x4D.6F.6E.64.61.79





l-Sat = %x53.61.74.75.72.64.61.79
l-Sun = %x53.75.6E.64.61.79
l-Thu = %x54.68.75.72.73.64.61.79
l-Tue = %x54.75.65.73.64.61.79
l-Wed = %x57.65.64.6E.65.73.64.61.79
last-chunk = 1*"0" *WSP [ chunk-ext ] CRLF

message-body = entity-body /
    <entity-body encoded as per Transfer-Encoding>
message-header = field-name ":" OWS [ field-value ] OWS
month = s-Jan / s-Feb / s-Mar / s-Apr / s-May / s-Jun / s-Jul / s-Aug
    / s-Sep / s-Oct / s-Nov / s-Dec

obs-fold = CRLF
obs-text = %x80-FF
obsolete-date = rfc850-date / asctime-date

parameter = attribute BWS "=" BWS value
partial-URI = relative-part [ "?" query ]
path-abempty = <path-abempty, defined in [RFC3986], Section 3.3>
path-absolute = <path-absolute, defined in [RFC3986], Section 3.3>
port = <port, defined in [RFC3986], Section 3.2.3>
product = token [ "/" product-version ]
product-version = token
protocol-name = token
protocol-version = token
pseudonym = token

qdtext = *( OWS / "!" / %x23-5B / %x5D-7E / obs-text )
query = <query, defined in [RFC3986], Section 3.4>
quoted-pair = "\" quoted-text
quoted-string = DQUOTE *( qdtext / quoted-pair ) DQUOTE
quoted-text = %x01-09 / %x0B-0C / %x0E-FF
qvalue = ( "0" [ "." *3DIGIT ] ) / ( "1" [ "." *3"0" ] )

received-by = ( uri-host [ ":" port ] ) / pseudonym
received-protocol = [ protocol-name "/" ] protocol-version
relative-part = <relative-part, defined in [RFC3986], Section 4.2>
request-header = <request-header, defined in [Part2], Section 3>
request-target = "*" / absolute-URI / ( path-absolute [ "?" query ] )
    / authority
response-header = <response-header, defined in [Part2], Section 5>
rfc1123-date = wkday "," SP date1 SP time SP GMT
rfc850-date = weekday "," SP date2 SP time SP GMT

s-Apr = %x41.70.72
s-Aug = %x41.75.67
s-Dec = %x44.65.63





s-Feb = %x46.65.62
s-Fri = %x46.72.69
s-Jan = %x4A.61.6E
s-Jul = %x4A.75.6C
s-Jun = %x4A.75.6E
s-Mar = %x4D.61.72
s-May = %x4D.61.79
s-Mon = %x4D.6F.6E
s-Nov = %x4E.6F.76
s-Oct = %x4F.63.74
s-Sat = %x53.61.74
s-Sep = %x53.65.70
s-Sun = %x53.75.6E
s-Thu = %x54.68.75
s-Tue = %x54.75.65
s-Wed = %x57.65.64
start-line = Request-Line / Status-Line

t-codings = "trailers" / ( transfer-extension [ te-params ] )
tchar = "!" / "#" / "$" / "%" / "&" / "'" / "*" / "+" / "-" / "." /
    "^" / "_" / "`" / "|" / "~" / DIGIT / ALPHA
te-ext = OWS ";" OWS token [ "=" ( token / quoted-string ) ]
te-params = OWS ";" OWS "q=" qvalue *te-ext
time = 2DIGIT ":" 2DIGIT ":" 2DIGIT
token = 1*tchar
trailer-part = *( entity-header CRLF )
transfer-coding = "chunked" / transfer-extension
transfer-extension = token *( OWS ";" OWS parameter )

uri-host = <host, defined in [RFC3986], Section 3.2.2>

value = token / quoted-string

weekday = l-Mon / l-Tue / l-Wed / l-Thu / l-Fri / l-Sat / l-Sun
wkday = s-Mon / s-Tue / s-Wed / s-Thu / s-Fri / s-Sat / s-Sun


















