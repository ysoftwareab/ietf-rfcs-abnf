jcalobject = component

component = begin-array
               DQUOTE component-name DQUOTE value-separator
               properties-array value-separator
               components-array
               end-array

components-array = begin-array
                      [ component *(value-separator component) ]
                      end-array

property = begin-array
              DQUOTE property-name DQUOTE value-separator
              params-object value-separator
              DQUOTE type-name DQUOTE
              propery-value *(value-separator property-value)
              end-array
properties-array = begin-array
                      [ property *(value-separator property) ]
                      end-array

property-value = simple-prop-value / structured-prop-value
simple-prop-value = string / number / true / false
structured-prop-value =
       begin-array
       [ structured-element *(value-separator structured-element) ]
       end-array
structured-element = simple-prop-value

params-object = begin-object
                   [ params-member *(value-separator params-member) ]
                   end-object
params-member = DQUOTE param-name DQUOTE name-separator param-value
param-value = string / param-multi
param-multi = begin-array
                 [ string *(value-separtor string) ]
                 end-array

type-name = "binary" / "boolean" / "cal-address" / "date" /
               "date-time" / "duration" / "float" / "integer" /
               "period" / "recur" / "text" / "time" / "uri" /
               "utc-offset" / x-type


component-name = lowercase-name
property-name = lowercase-name
param-name = lowercase-name
x-type = lowercase-name
lowercase-name = 1*(%x61-7A / DIGIT / "-")


