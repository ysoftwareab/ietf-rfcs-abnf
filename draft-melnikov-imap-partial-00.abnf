CONTEXT=SEARCH capability, especially if the intent is to walk a
SP                  = <Defined in RFC 5234>
MINUS               = "-"

capability          =/ "PARTIAL"
                          ;; <capability> from [RFC3501]

modifier-partial    = "PARTIAL" SP partial-range

partial-range-first = nz-number ":" nz-number
       ;; Request to search from oldest (lowest UIDs) to
       ;; more recent messages.
       ;; A range 500:400 is the same as 400:500.
       ;; This is similar to <seq-range> from [RFC3501],
       ;; but cannot contain "*".

partial-range-last  = MINUS nz-number ":" MINUS nz-number
       ;; Request to search from newest (highest UIDs) to
       ;; oldest messages.
       ;; A range -500:-400 is the same as -400:-500.

partial-range       = partial-range-first / partial-range-last

search-return-opt   =/ modifier-partial
       ;; All conform to <search-return-opt>, from [IMAP-ABNF]/[RFC9051]

search-return-data  =/ ret-data-partial

ret-data-partial    = "PARTIAL"
                         SP "(" partial-range SP partial-results ")"
       ;; <partial-range> is the requested range.

partial-results     = sequence-set / "NIL"
       ;; <sequence-set> from [RFC3501].
       ;; NIL indicates no results correspond to the requested range.

tagged-ext-simple   =/ partial-range-last



fetch-modifier      =/ modifier-partial



resp-text-code      =/ "MESSAGELIMIT" SP nz-number SP uniqueid
       ;; No more than nz-number messages can be processed
       ;; by any command at a time. The last processed
       ;; UID is uniqueid.

