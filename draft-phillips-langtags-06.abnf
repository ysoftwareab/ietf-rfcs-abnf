Language-Tag = (lang
                   *("-"extlang)
                   ["-"script]
                   ["-"region]
                   *("-"variant)
                   *("-"extension)
                   ["-"privateuse])
                   / privateuse         ; private-use tag
                   / grandfathered      ; grandfathered registrations

lang            = 2*3ALPHA           ; shortest ISO 639 code
                   / registered-lang
extlang         = 3ALPHA             ; reserved for future use
script          = 4ALPHA             ; ISO 15924 code
region          = 2ALPHA             ; ISO 3166 code
                   / 3DIGIT             ; UN country number
variant         = ALPHA(4*7alphanum) ; registered variants
                   / DIGIT(3*7alphanum)
extension       = singleton 1*("-"(2*8alphanum)) ; extension subtag(s)
privateuse      = "x" 1*("-"(1*8alphanum))       ; private use subtag(s)
singleton       = 1ALPHA             ; single letters (except x, which has special meaning)
registered-lang = 4*8ALPHA           ; registered language subtag
grandfathered   = ALPHA*(alphanum / "-")           ; grandfathered registration
alphanum        = (ALPHA / DIGIT)    ; letters and numbers

                      Figure 1: Language Tag ABNF

language-range = language-tag / "*"

