processor="Markdown.pl-1.0.2b8 --html4tags"
EXTCHAR  = <any character outside the US-ASCII range,
                      essentially amounting to any Unicode
                      code point beyond U+007F without requiring
                      Unicode or any particular encoding>

REXTCHAR = <EXTCHAR without spaces (Zs category) or
                      control characters>

                  Figure X: ABNF Used in This Section

flavor-param     = flavor *( *WSP rule ) *WSP

flavor           = registered-fid / unregistered-fid

registered-fid   = fid-char 1*("!" / fid-char)

unregistered-fid = "!" 2*fid-char

fid-char         = %d35-%d42 / %d44 / %d46-%d126 / REXTCHAR

rule             = "+" (should-rule / any-rule)

should-rule      = should-rule-char [ *(should-rule-char / "_")
                                            should-rule-char ]
any-rule         = 1*rule-char 

rule-char        = %d35-%d42 / %d44-%d126 / REXTCHAR

                 Figure X: ABNF of the flavor parameter

processor-param  = processor [ "-" version ]
                         *( 1*WSP argument ) *WSP

processor        = registered-pid / unregistered-pid

registered-pid   = pid-char 1*("!" / pid-char)

unregistered-pid = "!" 2*pid-char

version          = pid-char *("!" / pid-char)

argument         = regular-argument / uri-argument

regular-argument = 1*(regular-char / quoted-chars)

pid-char         = %d35-%d44 / %d46-%d59 / %d61 /
                         %d63-126 / REXTCHAR

regular-char     = %d33 / %d35-%d59 / %d61 / %d63-126 / REXTCHAR
 




quoted-chars     = DQUOTE *pqcontent DQUOTE

pqcontent        = %d1-%d33 / %d35-127 / EXTCHAR / DQUOTE DQUOTE

uri-argument     =  "<" URI-reference ">"         ; from [RFC3986]

                   Figure X: processor parameter ABNF

