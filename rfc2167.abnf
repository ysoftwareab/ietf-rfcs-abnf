alpha = "a".."z" / "A".."Z"
digit = "0".."9"
hex-digit = digit / "a".."f" / "A".. "F"
id-char = alpha / digit / "_" / "-"
any-char = <ASCII 1..255,
              except LF (linefeed) and CR (carriage return)>
dns-char = alpha / digit / "-"
email-char = <see [RFC 822]>
space = " "
tab = <ASCII TAB (tab)>
lf = <ASCII LF (linefeed)>
cr = <ASCII CR (carriage return)>
crlf = cr lf

year = 4digit
month = 2digit
day = 2digit
hour = 2digit
minute = 2digit
second = 2digit
milli-second = 3digit
host-name = dns-char *(dns-char / ".")
ip-address = 1*3digit "." 1*3digit "." 1*3digit "." 1*3digit
email = 1*email-char "@" host-name
authority-area = (dns-char / ".") *(dns-char / "." / "/")
object-id = 1*id-char "." authority-area
host-port = (host-name / ip-address) ":" 1*5digit
class-name = 1*id-char
attribute-name = 1*id-char
attribute-value = 1*any-char
time-stamp = year month day hour minute second milli-second
on-off = "on" / "off"

response = ok-response crlf / error-response crlf / info-response
ok-response = "%ok"
error-response = "%error" space error-code space error-text
error-code = 3digit
error-text = 1*any-char
info-response = "%info" space "on" crlf *(*any-char crlf) "%info"
           space "off" crlf
rwhois-banner = "%rwhois" space version-list space host-name
         [space implementation] crlf
version-list = version *("," version)
version = version-number [":" capability-id]
           / "V-1.5" ":" capability-id
version-number = "V-" 1*digit "." 1*digit
capability-id = response-id ":" extra-id
response-id = 6hex-digit
extra-id = 2hex-digit
implementation = 1*any-char

rwhois-protocol = client-sends / server-returns
client-sends = *(directives / rwhois-query)
server-returns = *(responses / rwhois-query-result)

rwhois-dir = "-rwhois" space version-number [space implementation]
                crlf
rwhois-response = "%rwhois" space version space host-name
           [space implementation] crlf

class-dir = "-class" space authority-area *(space class-name) crlf
class-response = *class-record response
class-record = *class-line "%class" crlf
class-line = "%class" space class-name ":" "description" ":"
                1*any-char crlf
      / "%class" space class-name ":" "version" ":" time-stamp crlf
      / "%class" space class-name ":" meta-field ":" meta-value crlf
meta-field = 1*id-char
meta-value = 1*any-char

directive-dir = "-directive" *(space directive-name) crlf
directive-name = 1*id-char
directive-response = *directive-record response
directive-record = "%directive" space "directive" ":" directive-name
                      crlf *directive-line "%directive" crlf
directive-line = "%directive" space "description" ":" 1*any-char crlf
           / "%directive" space attribute-name ":" attribute-value crlf

display-dir = "-display" crlf
       / "-display" space display-name crlf
display-name = 1*id-char
display-response = *(display-record) response
display-record = "%display" space "name" ":" display-name crlf
display-line = "%display" space attribute-name ":"
                  attribute-value crlf

forward-dir = "-forward" space on-off crlf
forward-response = response

holdconnect-dir = "-holdconnect" space on-off crlf
holdconnect-response = response

limit-dir = "-limit" space 1*digit crlf
limit-response = response

notify-dir = "-notify" space "badref" space referral-query crlf
           / "-notify" space "recurref" space referral-query crlf
      / "-notify" space "update" space host-port ":" authority-area crlf
           / "-notify" space "inssec" space host-port ":"
             authority-area crlf
           / "-notify" space "delsec" space host-port ":"
             authority-area crlf
referral-query = referral-url space [class-name space] query
notify-response = response

quit-dir = "-quit" crlf
quit-response = response

register-dir = register-on space "add" space maintainer-id crlf
                  register-add register-off
           / register-on space "mod" space maintainer-id crlf
             register-mod register-off
           / register-on space "del" space maintainer-id crlf
             register-del register-off
register-on = "-register" space "on"
register-off = "-register" space "off" crlf
register-add = 1*(register-line crlf)
register-mod = 1*(register-line crlf) "_NEW_" crlf
                  1*(register-line crlf)
register-del = 1*(register-line crlf)
maintainer-id = email
register-line = attribute-name ":" attribute-value
register-on-response = response
register-off-response = "%register" space "ID" ":" object-id crlf
                           response
           / "%register" space "Updated" ":" time-stamp crlf response
           / response

      * The server must return the register-on-response for the
        "-register on" directive and the register-off-response for the
        "-register off" directive.
      * The maintainer-id identifies, for maintenance purposes, the
        sender of registration information. The server should not use it
        to authenticate the sender.
      * For the "add" option, the client must send all the required
        attributes for the object, including the Class-Name and Auth-
        Area attributes.  However, the client must not send the ID and
        Updated attributes. These attributes are assigned by the server
        and returned in the response.
      * For the "mod" option, the client must send the identifying
        information for the object to be modified, followed by the
        "_NEW_" separator and the entire replacement object. The
        identifying information must contain the ID and Updated
        attributes; it may contain other attributes, but the server may
        not check them. The ID, Auth-Area, and Class-Name attributes
        must match in both the original object data and the replacement
        object. The original object data is sent before the replacement
        object to enable the server to lock the record in the database.
      * For the "del" option, the client must send the identifying
        information for the object to be deleted. The identifying
        information must contain the ID and Updated attributes; it may
        contain other attributes, but the server may not check them.

schema-dir = "-schema" space authority-area *(space class-name) crlf
schema-response = *schema-record response
schema-record = *schema-line "%schema" crlf
schema-line = "%schema" space class-name ":" attribute-name ":"
                 attribute-value crlf

security-dir = "-security" space "on" space direction space
           security-method [space security-data] crlf
           security-payload ["-security" space "off" crlf]
direction = "request" / "response"
security-method = "password" / "pgp" / 1*id-char
security-data = password-data / pgp-data / 1*any-char
password-data = 1*any-char
pgp-data = "signed" / "encrypt" [space key-id] / "signed-encrypt"
              [space key-id]
security-payload = *(*any-char crlf)
security-response = response

      * The "password" security-method is available in the "request"
        direction only. For password, the security-data is a cleartext
        password.
      * The "pgp" security-method is available in both the "request" and
        "response" directions. For PGP, the security-data indicates how
        to treat the security-payload: signed, encrypted, or signed and
        encrypted. To encrypt the security-payload in the "response"
        direction, the security-data must include the public key ID with
        which to encrypt it.
C =a0sQ
attribute=Organization-Name
S =vZ2x
soa-dir = "-soa" *(space authority-area) crlf
soa-response = *soa-record response
soa-record = *soa-line "%soa" crlf
soa-line = "%soa" space "authority" ":" authority-area crlf
      / "%soa" space "ttl" ":" 1*digit crlf
      / "%soa" space "serial" ":" time-stamp crlf
      / "%soa" space "refresh" ":" 1*digit crlf
      / "%soa" space "increment" ":" 1*digit crlf
      / "%soa" space "retry" ":" 1*digit crlf
      / "%soa" space "tech-contact" ":" email crlf
      / "%soa" space "admin-contact" ":" email crlf
      / "%soa" space "hostmaster" ":" email crlf
      / "%soa" space "primary" ":" host-port crlf
      / "%soa" space attribute-name ":" attribute-value crlf

status-dir = "-status" crlf
status-response = *status-line response
status-line = "%status" space "limit" ":" 1*digit crlf
      / "%status" space "holdconnect" ":" on-off crlf
      / "%status" space "forward" ":" on-off crlf
      / "%status" space "objects" ":" 1*digit crlf
      / "%status" space "display" ":" 1*any-char crlf
      / "%status" space "contact" ":" email crlf
      / "%status" space attribute-name ":" attribute-value crlf

xfer-dir = "-xfer" space authority-area *attribute-def
           [space serial-number] crlf
attribute-def = [space "class=" class-name] *(space "attribute="
                   attribute-name)
serial-number = time-stamp
xfer-response = *xfer-record response
xfer-record = *xfer-line "%xfer" crlf
xfer-line = "%xfer" space class-name ":" attribute-name ":"
               attribute-value crlf

attribute=Organization-Name
x-dir = "-X-" x-directive [space x-arguments] crlf *x-line
x-directive = 1*id-char
x-arguments = *any-char
x-response = *(*any-char crlf) response
x-line = *any-char crlf

rwhois-query = [class-name space] query crlf
query = query-string / attribute-query / query bin-boolean query
query-char = <any-char, except """, space, tab>
quoted-query-char = query-char / space / tab / "
query-string = ["*"] 1*query-char ["*"] / """ ["*"]
           1*quoted-query-char ["*"] """
attribute-query = attribute-name "=" query-string
bin-boolean = "and" / "or"

rwhois-query-result = *(query-record / referral-record) response
query-record = 1*query-line crlf
query-line = class-name ":" attribute-name [";" type-char] ":"
           attribute-value crlf
type-char = "T" / "I" / "S"
referral-record = 1*(referral-line crlf)
referral-line = "%referral" space referral-url
referral-url = "rwhois" ":" "//" host-port "/" "auth-area="
                  authority-area
alpha = "a".."z" / "A".."Z"
digit = "0".."9"
hex-digit = digit / "a".."f" / "A".. "F"
id-char = alpha / digit / "_" / "-"
any-char = <ASCII 1..255,
              except LF (linefeed) and CR (carriage return)>
dns-char = alpha / digit / "-"
email-char = <see [RFC 822]>
space = " "
tab = <ASCII TAB (tab)>
lf = <ASCII LF (linefeed)>
cr = <ASCII CR (carriage return)>
crlf = cr lf

year = 4digit
month = 2digit
day = 2digit
hour = 2digit
minute = 2digit
second = 2digit
milli-second = 3digit
host-name = dns-char *(dns-char / ".")
email = 1*email-char "@" host-name
authority-area = (dns-char / ".") *(dns-char / "." / "/")
object-id = 1*id-char "." authority-area
host-port = (host-name / ip-address) ":" 1*5digit
ip-address = 1*3digit "." 1*3digit "." 1*3digit "." 1*3digit
class-name = 1*id-char
attribute-name = 1*id-char
attribute-value = 1*any-char
time-stamp = year month day hour minute second milli-second
on-off = "on" / "off"

response = ok-response crlf / error-response crlf / info-response
ok-response = "%ok"
error-response = "%error" space error-code space error-text
error-code = 3digit
error-text = 1*any-char
info-response = "%info" space "on" crlf *(*any-char crlf) "%info"
           space "off" crlf

rwhois-banner = "%rwhois" space version-list space host-name
           [space implementation] crlf
version-list = version *("," version)
version = version-number [":" capability-id]
           / "V-1.5" ":" capability-id
version-number = "V-" 1*digit "." 1*digit
capability-id = response-id ":" extra-id
response-id = 6hex-digit
extra-id = 2hex-digit
implementation = 1*any-char

rwhois-protocol = client-sends / server-returns
client-sends = *(directives / rwhois-query)
server-returns = *(responses / rwhois-query-result)

directives = rwhois-dir / class-dir / directive-dir / display-dir /
           holdconnect-dir / limit-dir / notify-dir / quit-dir /
           register-dir / schema-dir / security-dir / soa-dir /
           status-dir / xfer-dir / x-dir
responses = rwhois-response / class-response/ directive-response/
           display-response/ holdconnect-response/ limit-response/
           notify-response/ quit-response/ register-response/
           schema-response / security-response/ soa-response/
           status-response/ xfer-response/ x-response

rwhois-dir = "-rwhois" space version-number [space implementation]
                crlf
rwhois-response = "%rwhois" space version space host-name
           [space implementation] crlf

class-dir = "-class" space authority-area *(space class-name) crlf
class-response = *class-record response
class-record = *class-line "%class" crlf
class-line = "%class" space class-name ":" "description" ":"
                1*any-char crlf
      / "%class" space class-name ":" "version" ":" time-stamp crlf
      / "%class" space class-name ":" meta-field ":" meta-value crlf
meta-field = 1*id-char
meta-value = 1*any-char

directive-dir = "-directive" *(space directive-name)crlf
directive-name = 1*id-char
directive-response = *directive-record response
directive-record = "%directive" space "directive" ":"
           directive-name crlf *directive-line "%directive" crlf
directive-line = "%directive" space "description" ":" 1*any-char crlf
           / "%directive" space attribute-name ":" attribute-value crlf
display-dir = "-display" crlf
      / "-display" space display-name crlf
display-name = 1*id-char
display-response = *display-record response
display-record = "%display" space "name" ":" display-name crlf
display-line = "%display" space attribute-name ":" attribute-value
                  crlf

holdconnect-dir = "-holdconnect" space on-off crlf
holdconnect-response = response

limit-dir = "-limit" space 1*digit crlf
limit-response = response

notify-dir = "-notify" space "badref" space referral-query crlf
           / "-notify" space "recurref" space referral-query crlf
           / "-notify" space "update" space host-port ":" authority-area
             crlf
           / "-notify" space "inssec" space host-port ":" authority-area
             crlf
           / "-notify" space "delsec" space host-port ":" authority-area
             crlf
referral-query = referral-url space [class-name space] query
notify-response = response

quit-dir = "-quit" crlf
quit-response = response
register-dir = register-on space "add" space maintainer-id crlf
           register-add register-off
           / register-on space "mod" space maintainer-id crlf
                   register-mod register-off
           / register-on space "del" space maintainer-id crlf
                   register-del register-off
register-on = "-register" space "on"
register-off = "-register" space "off" crlf
register-add = 1*(register-line crlf)
register-mod = 1*(register-line crlf) "_NEW_" crlf
                  1*(register-line crlf)
register-del = 1*(register-line crlf)
maintainer-id = email
register-line = attribute-name ":" attribute-value
register-on-response = response
register-off-response = "%register" space "ID" ":" object-id crlf
                           response
           / "%register" space "Updated" ":" time-stamp crlf response
           / response

schema-dir = "-schema" space authority-area *(space class-name) crlf
schema-response = *schema-record response
schema-record = *schema-line "%schema" crlf
schema-line = "%schema" space class-name ":" attribute-name ":"
           attribute-value crlf

security-dir = "-security" space "on" space direction space
           security-method [space security-data] crlf security-payload
           ["-security" space "off" crlf]
direction = "request" / "response"
security-method = "password" / "pgp" / 1*id-char
security-data = password-data / pgp-data / 1*any-char
password-data = 1*any-char
pgp-data = "signed" / "encrypt" [space key-id] / "signed-encrypt"
              [space key-id]
security-payload = *(*any-char crlf)
security-response = response
soa-dir = "-soa" *(space authority-area) crlf
soa-response = *soa-record response
soa-record = *soa-line "%soa" crlf
soa-line = "%soa" space "authority" ":" authority-area crlf
      / "%soa" space "ttl" ":" 1*digit crlf
      / "%soa" space "serial" ":" time-stamp crlf
      / "%soa" space "refresh" ":" 1*digit crlf
      / "%soa" space "increment" ":" 1*digit crlf
      / "%soa" space "retry" ":" 1*digit crlf
      / "%soa" space "tech-contact" ":" email crlf
      / "%soa" space "admin-contact" ":" email crlf
      / "%soa" space "hostmaster" ":" email crlf
      / "%soa" space "primary" ":" host-port crlf
      / "%soa" space attribute-name ":" attribute-value crlf

status-dir = "-status" crlf
status-response = *status-line response
status-line = "%status" space "limit" ":" 1*digit crlf
      / "%status" space "holdconnect" ":" on-off crlf
      / "%status" space "forward" ":" on-off crlf
      / "%status" space "authority" ":" 1*digit crlf
      / "%status" space "display" ":" 1*any-char crlf
      / "%status" space "contact" ":" email crlf
      / "%status" space attribute-name ":" attribute-value crlf

xfer-dir = "-xfer" space authority-area *attribute-def
           [space serial-number] crlf
attribute-def = [space "class=" class-name]
                   *(space "attribute=" attribute-name)
serial-number = time-stamp
xfer-response = *xfer-record response
xfer-record = *xfer-line "%xfer" crlf
xfer-line = "%xfer" space class-name ":" attribute-name ":"
               attribute-value crlf

x-dir = "-X-" x-directive [space *[x-arguments]] crlf
x-directive = 1*id-char
x-arguments = *any-char
x-response = *(*any-char crlf) response
rwhois-query = [class-name space] query crlf
query = query-string / attribute-query / query bin-boolean query
query-char = <any-char, except """, space, tab>
quoted-query-char = query-char / space / tab / "
query-string = 1*query-char ["*"] / """ 1*quoted-query-char ["*"] """
attribute-query = attribute-name "=" query-string
bin-boolean = "and" / "or"

rwhois-query-result = *(query-record / referral-record) response
query-record = 1*query-line crlf
query-line = class-name ":" attribute-name [";" type-char] ":"
           attribute-value crlf
type-char = "T" / "I" / "S"
referral-record = 1*(referral-line crlf)
referral-line = "%referral" space referral-url
referral-url = "rwhois" ":" "//" host-port "/" "auth-area="
                  authority-area

error-response = "%error" space error-code space error-text
error-code = 3digit
error-text = 1*any-char

