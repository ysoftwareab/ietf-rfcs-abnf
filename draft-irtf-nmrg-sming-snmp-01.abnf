snmpStatement           = snmpKeyword *1(sep lcIdentifier) optsep
                                 "{" stmtsep
                                 *1(oidStatement stmtsep)
                                 *(nodeStatement stmtsep)
                                 *(scalarsStatement stmtsep)
                                 *(tableStatement stmtsep)
                                 *(notificationStatement stmtsep)
                                 *(groupStatement stmtsep)
                                 *(complianceStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             "}" optsep ";"

nodeStatement           = nodeKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
   			      *1(representsStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 *1(descriptionStatement stmtsep)
                                 *1(referenceStatement stmtsep)
                             "}" optsep ";"

representsStatement     = representsKeyword sep
   			      qucIdentifier optsep ";"

scalarsStatement        = scalarsKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
   			      1*(implementsStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             "}" optsep ";"

tableStatement          = tableKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
                                 anyIndexStatement stmtsep
                                 *1(createStatement stmtsep)
   			      1*(implementsStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             "}" optsep ";"

implementsStatement     = implementsKeyword sep qucIdentifier optsep
                                 "{" stmtsep
                                 1*(implObjectStatement stmtsep)
                             "}" optsep ";"

implObjectStatement     = objectKeyword sep
   			      lcIdentifier sep
   			      attrIdentifier optsep;

notificationStatement   = notificationKeyword sep lcIdentifier
                                 optsep "{" stmtsep
                                 oidStatement stmtsep
   			      signalsStatement stmtsep
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             "}" optsep ";"

signalsStatement        = signalsKeyword sep qattrIdentifier
   			      optsep "{" stmtsep
   			      *(signalsObjectStatement)
   			  "}" optsep ";"

signalsObjectStatement  = objectKeyword sep
   			      qattrIdentifier optsep ";"

groupStatement          = groupKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
                                 membersStatement stmtsep
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                             "}" optsep ";"

complianceStatement     = complianceKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                                 *1(referenceStatement stmtsep)
                                 *1(mandatoryStatement stmtsep)
                                 *(optionalStatement stmtsep)
                                 *(refineStatement stmtsep)
                             "}" optsep ";"
anyIndexStatement       = indexStatement /
                             augmentsStatement /
                             reordersStatement /
                             extendsStatement /
                             expandsStatement

indexStatement          = indexKeyword *1(sep impliedKeyword) optsep
                                 "(" optsep qlcIdentifierList
                                 optsep ")" optsep ";"

augmentsStatement       = augmentsKeyword sep qlcIdentifier
                                 optsep ";"

reordersStatement       = reordersKeyword sep qlcIdentifier
                                 *1(sep impliedKeyword)
                                 optsep "(" optsep
                                 qlcIdentifierList optsep ")"
                                 optsep ";"

extendsStatement        = extendsKeyword sep qlcIdentifier optsep ";"

expandsStatement        = expandsKeyword sep qlcIdentifier
                                 *1(sep impliedKeyword)
                                 optsep "(" optsep
                                 qlcIdentifierList optsep ")"
                                 optsep ";"

createStatement         = createKeyword optsep ";"

membersStatement        = membersKeyword optsep "(" optsep
                                 qlcIdentifierList optsep
                                 ")" optsep ";"

mandatoryStatement      = mandatoryKeyword optsep "(" optsep
                                 qlcIdentifierList optsep
                                 ")" optsep ";"

optionalStatement       = optionalKeyword sep qlcIdentifier optsep
                                 "{" descriptionStatement stmtsep
                             "}" optsep ";"

refineStatement         = refineKeyword sep qlcIdentifier optsep "{"
                                 *1(typeStatement stmtsep)
                                 *1(writetypeStatement stmtsep)
                                 *1(accessStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

typeStatement           = typeKeyword sep
                                 (refinedBaseType / refinedType)
                                 optsep ";"

writetypeStatement      = writetypeKeyword sep
                                 (refinedBaseType / refinedType)
                                 optsep ";"

oidStatement            = oidKeyword sep objectIdentifier optsep ";"

objectIdentifier        = (qlcIdentifier / subid) *127("." subid)

subid                   = decimalNumber

snmpKeyword         =  %x73 %x6e %x6d %x70
nodeKeyword         =  %x6e %x6f %x64 %x65
representsKeyword   =  %x72 %x65 %x70 %x72 %x65 %x73 %x65 %x6e %x74
                          %x73
scalarsKeyword      =  %x73 %x63 %x61 %x6c %x61 %x72 %x73
tableKeyword        =  %x74 %x61 %x62 %x6c %x65
implementsKeyword   =  %x69 %x6d %x70 %x6c %x65 %x6d %x65 %x6e %x74
                          %x73
objectKeyword       =  %x6f %x62 %x6a %x65 %x63 %x74
notificationKeyword =  %x6e %x6f %x74 %x69 %x66 %x69 %x63 %x61 %x74
                          %x69 %x6f %x6e
signalsKeyword      =  %x73 %x69 %x67 %x6e %x61 %x6c %x73
oidKeyword          =  %x6f %x69 %x64
groupKeyword        =  %x67 %x72 %x6f %x75 %x70
complianceKeyword   =  %x63 %x6f %x6d %x70 %x6c %x69 %x61 %x6e %x63
                          %x65
impliedKeyword      =  %x69 %x6d %x70 %x6c %x69 %x65 %x64
indexKeyword        =  %x69 %x6e %x64 %x65 %x78
augmentsKeyword     =  %x61 %x75 %x67 %x6d %x65 %x6e %x74 %x73
reordersKeyword     =  %x72 %x65 %x6f %x72 %x64 %x65 %x72 %x73
extendsKeyword      =  %x65 %x78 %x74 %x65 %x6e %x64 %x73
expandsKeyword      =  %x65 %x78 %x70 %x61 %x6e %x64 %x73
createKeyword       =  %x63 %x72 %x65 %x61 %x74 %x65
membersKeyword      =  %x6d %x65 %x6d %x62 %x65 %x72 %x73
mandatoryKeyword    =  %x6d %x61 %x6e %x64 %x61 %x74 %x6f %x72 %x79
optionalKeyword     =  %x6f %x70 %x74 %x69 %x6f %x6e %x61 %x6c
refineKeyword       =  %x72 %x65 %x66 %x69 %x6e %x65
writetypeKeyword    =  %x77 %x72 %x69 %x74 %x65 %x74 %x79 %x70 %x65
agentcapsStatement = 'agentcaps' sep lcIdentifier
                        optsep '{' stmtsep
                          oidStatement stmtsep
                          releaseStatement stmtsep
                          *1(statusStatement stmtsep)
                          descriptionStatement stmtsep
                          *1(referenceStatement stmtsep)
                          *(includesStatement stmtsep)
                        '}' optsep ';'

includesStatement  = 'includes' sep qlcIdentifier
                        optsep '{' stmtsep
                          *(variationStatement stmtsep)
                        '}' optsep ';'

variationStatement = 'variation' sep qlcIdentifier
                        optsep '{' stmtsep
                          typeStatement stmtsep
                          writetypeStatement stmtsep
                          accessStatement stmtsep
                          createStatement stmtsep
                        '}' optsep ';'

