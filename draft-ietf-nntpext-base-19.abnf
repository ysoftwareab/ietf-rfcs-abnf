wildmat = wildmat-pattern *("," ["!"] wildmat-pattern)

wildmat-pattern = 1*wildmat-item

wildmat-item = wildmat-exact / wildmat-wild

wildmat-exact = %x21-29 / %x2B / %x2D-3E / %x40-5A / %x5E-7E /
         UTF8-non-ascii ; exclude * , ? [ \ ]

wildmat-wild = "*" / "?"

group  = name of newsgroup
number = estimated number of articles in the group
low    = reported low water mark
high   = reported high water mark
n          = article number
message-id = article message-id


n          = article number
message-id = article message-id


number     = Requested article number
n          = Returned article number
message-id = Article message-id

number     = Requested article number
n          = Returned article number
message-id = Article message-id

number     = Requested article number
n          = Returned article number
message-id = Article message-id
number     = Requested article number
n          = Returned article number
message-id = Article message-id

message-id = Article message-id


yyyymmddHHmmss = Current UTC date and time on server


date = Date in yymmdd or yyyymmdd format
time = Time in hhmmss format


wildmat = Newsgroups of interest
date    = Date in yymmdd or yyyymmdd format
time    = Time in hhmmss format


wildmat = groups of interest


wildmat = groups of interest


wildmat = groups of interest


group  = name of newsgroup
number = estimated number of articles in the group
low    = reported low water mark
high   = reported high water mark
message-id = Article message-id
range      = Article(s) to return information for

header     = name of header, without the colon
range      = number(s) of articles
message-id = message-id of article


command-line = command EOL
command = article-command /
           body-command /
           date-command /
           group-command /
           hdr-command /
           head-command /
           help-command /
           ihave-command /
           last-command /
           list-active-command /
           list-active-times-command /
           list-distrib-pats-command /
           list-distributions-command /
           list-extensions-command /
           list-newsgroups-command /
           list-overview-fmt-command /
           listgroup-command /
           mode-reader-command /
           newgroups-command /
           newnews-command /
           next-command /
           over-command /
           post-command /
           quit-command /
           stat-command /
           x-command

article-command = "ARTICLE" [article-ref]
body-command = "BODY" [article-ref]
date-command = "DATE"
group-command = "GROUP" WS newsgroup-name
hdr-command = "HDR" WS header-meta-name [range-ref]
head-command = "HEAD" [article-ref]
help-command = "HELP"
ihave-command = "IHAVE" WS message-id
last-command = "LAST"
list-active-command = "LIST" [WS "ACTIVE" [WS wildmat]]
list-active-times-command = "LIST" WS "ACTIVE.TIMES" [WS wildmat]
list-distrib-pats-command = "LIST" WS "DISTRIB.PATS"
list-distributions-command = "LIST" WS "DISTRIBUTIONS"
list-extensions-command = "LIST" WS "EXTENSIONS"
list-newsgroups-command = "LIST" WS "NEWSGROUPS" [WS wildmat]
list-overview-fmt-command = "LIST" WS "OVERVIEW.FMT"
listgroup-command = "LISTGROUP" [WS newsgroup-name]
mode-reader-command = "MODE" WS "READER"
newgroups-command = "NEWGROUPS" WS date-time
newnews-command = "NEWNEWS" WS wildmat WS date-time
next-command = "NEXT"
over-command = "OVER" [WS range]
post-command = "POST"
quit-command = "QUIT"
stat-command = "STAT" [article-ref]
x-command = x-command-name *(WS x-argument)
         ; Each extension command is specified fully elsewhere

article-ref = WS (article-number / message-id)
article-number = 1*16DIGIT
date = [2DIGIT] 6DIGIT
date-time = date WS time [WS "GMT"]
header-meta-name = header-name / metadata-name
metadata-name = ":" 1*A-NOTCOLON
newsgroup-name = 1*wildmat-exact
range = article-number ["-" [article-number]]
range-ref = WS (range / message-id)
time = 6DIGIT
x-command-name = 3*12A-CHAR
x-argument = 1*P-CHAR

wildmat = wildmat-pattern *("," ["!"] wildmat-pattern)
wildmat-pattern = 1*wildmat-item
wildmat-item = wildmat-exact / wildmat-wild
wildmat-exact = %x21-29 / %x2B / %x2D-3E / %x40-5A / %x5E-7E /
          UTF8-non-ascii  ; exclude * , ? [ \ ]
wildmat-wild = "*" / "?"
response = simple-response / multiline-response
multiline-response = simple-response *content-line termination
termination = "." CRLF
content-line = [content-text] CRLF
content-text = (".." / B-NONDOT) *B-CHAR

simple-response = 3DIGIT parameters [ SP trailing-comment ] CRLF
trailing-comment = *U-CHAR
parameters = *( SP parameter ) ; How many depends on the response
parameter = 1*A-CHAR


article = 1*header CRLF body
header = header-name ":" [CRLF] SP header-content CRLF
header-content = *( P-CHAR / [CRLF] WS )
body = *(*B-CHAR CRLF)


header-name = 1*A-NOTCOLON
message-id = "<" 1*248A-NOTGT ">"

A-CHAR     = %x21-7E
A-NOTCOLON = %x21-39 / %x3B-7E  ; exclude ":"
A-NOTGT    = %x21-3D / %x3F-7E  ; exclude ">"
P-CHAR     = A-CHAR / UTF8-non-ascii
U-CHAR     = %x01-09 / %x0B-0C / %x0E-7F / UTF8-non-ascii
B-CHAR     = %x01-09 / %x0B-0C / %x0E-FF
B-NONDOT   = %x01-09 / %x0B-0C / %x0E-2D / %x2F-FF  ; exclude "."

CR = %x0D
CRLF = CR LF
DIGIT = %x30-39
EOL = *(SP / HT) CRLF
HT = %x09
LF = %x0A
SP = %x20
UTF8-non-ascii = UTF8-2 / UTF8-3 / UTF8-4
UTF8-2    = %xC2-DF UTF8-tail
UTF8-3    = %xE0 %xA0-BF UTF8-tail / %xE1-EC 2UTF8-tail /
                 %xED %x80-9F UTF8-tail / %xEE-EF 2UTF8-tail
UTF8-4    = %xF0 %x90-BF 2UTF8-tail / %xF1-F3 3UTF8-tail /
                 %xF4 %x80-8F 2UTF8-tail
UTF8-tail = %x80-BF
WS = 1*(SP / HT)

header = header-name ":" SP [header-content] CRLF
header-content = [WS] 1*P-CHAR *( [CRLF] WS 1*P-CHAR )


