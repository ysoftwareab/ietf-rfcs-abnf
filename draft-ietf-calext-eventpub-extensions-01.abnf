eventc     = "BEGIN" ":" "VEVENT" CRLF
               eventprop *alarmc *participantc
               "END" ":" "VEVENT" CRLF

eventprop =/ *(
                 ;
                 ; The following are OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 styleddescription / strucloc / strucres / sdataprop
                 ;
                 )

todoc      = "BEGIN" ":" "VTODO" CRLF
               todoprop *alarmc *participantc
               "END" ":" "VTODO" CRLF

todoprop =/ *(
                ;
                ; The following are OPTIONAL,
                ; and MAY occur more than once.
                ;
                styleddescription / strucloc / strucres / sdataprop
                ;
                )

journalc   = "BEGIN" ":" "VJOURNAL" CRLF
               jourprop *participantc
               "END" ":" "VJOURNAL" CRLF


jourprop =/ *(
                ;
                ; The following are OPTIONAL,
                ; and MAY occur more than once.
                ;
                styleddescription / sdataprop
                ;
                )
loctypeparam   = "LOCTYPE" "=" param-value

restypeparam   = "RESTYPE" "=" param-value

orderparam    = "ORDER" "=" integer ;Must be greater than or equal to 1

   Description:  The ORDER parameter is OPTIONAL and is used to indicate
      the relative ordering of the corresponding instance of a property.
      Its value MUST be an integer greater than or equal to 1 that
      quantifies the order.  Lower values correspond to a higher level
      of ordering, with 1 being the highest.

      When the parameter is absent, the default MUST be to interpret the
      property instance as being at the lowest level of ordering.

      Note that the value of this parameter is to be interpreted only in
      relation to values assigned to other corresponding instances of
      the same property in the same entity.  A given value, or the
      absence of a value, MUST NOT be interpreted on its own.

      This parameter MAY be applied to any property that allows multiple
      instances.

schemaparam    = "SCHEMA" "=" DQUOTE uri DQUOTE

SCHEMA="https://schema.org/FlightReservation";
ENCODING=BASE64;VALUE=BINARY:Zm9vYmFy

participanttype   = "PARTICIPANT-TYPE" "=" iana-token

VALUE=TEXT is used to provide rich-text variants of the plain-text
VALUE=URI is used to provide a link to rich-text content which is
styleddescription = "STYLED-DESCRIPTION" styleddescparam ":"
                       (
                         (
                           ";" "VALUE" "=" "URI"
                           ":" uri
                         )  /
                         (
                           ";" "VALUE" "=" "TEXT"
                           ":" text
                         )
                       )
                       CRLF

styleddescparam   = *(
                     ;
                     ; The following are OPTIONAL,
                     ; but MUST NOT occur more than once.
                     ;
                     (";" altrepparam) / (";" languageparam) /
                     (";" fmttypeparam) /
                     ;
                     ; the following is OPTIONAL
                     ; and MAY occur more than once
                     ;
                     (";" other-param)
                     )


strucloc        = "STRUCTURED-LOCATION" struclocparam
                       (
                         (
                           ";" "VALUE" "=" "URI"
                           ":" uri
                         )  /
                         (
                           ";" "VALUE" "=" "TEXT"
                           ":" text
                         )
                       )
                       CRLF

struclocparam   = *(
                     ;
                     ; the following are OPTIONAL
                     ; but MUST NOT occur more than once
                     ;
                     (";" fmttypeparam) /
                     (";" labelparam) /
                     (";" languageparam) /
                     (";" loctypeparam) /
                     ;
                     ; the following is OPTIONAL
                     ; and MAY occur more than once
                     ;
                     (";" other-param)
                     )
strucres       = "STRUCTURED-RESOURCE" strucresparam (":" uri)  /
                       (
                         ";" "VALUE" "=" "TEXT"
                         ":" text
                       )
                       CRLF

strucresparam  = *(
                     ;
                     ; the following are OPTIONAL
                     ; but MUST NOT occur more than once
                     ;
                     (";" fmttypeparam) /
                     (";" labelparam) /
                     (";" languageparam) /
                     (";" restypeparam) /
                     ;
                     ; the following is OPTIONAL
                     ; and MAY occur more than once
                     ;
                     (";" other-param)
                     )


source     = "SOURCE" sourceparam
                     (
                       (
                         ";" "VALUE" "=" "URI"
                         ":" uri
                       )  /
                       (
                         ";" "VALUE" "=" "TEXT"
                         ":" text
                       )
                     )
                     CRLF

sourceparam      = *(
                     ;
                     ; the following are OPTIONAL
                     ; but MUST NOT occur more than once
                     ;
                     (";" fmttypeparam) /
                     ;
                     ; the following is OPTIONAL
                     ; and MAY occur more than once
                     ;
                     (";" other-param)
                     ;
                     )

sdataprop   = "STRUCTURED-DATA" sdataparam
                       (":" text) /
                       (
                         ";" "ENCODING" "=" "BASE64"
                         ";" "VALUE" "=" "BINARY"
                         ":" binary
                       ) /
                       (
                         ";" "VALUE" "=" "URI"
                         ":" uri
                       )
                       CRLF

sdataparam  = *(
                      ;
                      ; The following is OPTIONAL for a URI value,
                      ; RECOMMENDED for a TEXT or BINARY value,
                      ; and MUST NOT occur more than once.
                      ;
                      (";" fmttypeparam) /
                      (";" schemaparam) /
                      ;
                      ; The following is OPTIONAL,
                      ; and MAY occur more than once.
                      ;
                      (";" other-param)
                      ;
                   )

SCHEMA="https://schema.org/SportsEvent";
VALUE=TEXT:{\n
        "@context": "http://schema.org"\,\n
        "@type": "SportsEvent"\,\n
        "homeTeam": "Pittsburgh Pirates"\,\n
        "awayTeam": "San Francisco Giants"\n
participantc  = "BEGIN" ":" "PARTICIPANT" CRLF
                     partprop *alarmc
                     "END" ":" "PARTICIPANT" CRLF

partprop      = *(
                     ;
                     ; The following are REQUIRED,
                     ; but MUST NOT occur more than once.
                     ;
                     dtstamp  / participanttype /
                     ;
                     ; The following are OPTIONAL,
                     ; but MUST NOT occur more than once.
                     ;
                     created / description / last-mod / seq /
                     source / status / summary / url /
                     ;
                     ; The following are OPTIONAL,
                     ; and MAY occur more than once.
                     ;
                     attach / categories / comment /
                     contact / rstatus / related /
                     resources / x-prop / iana-prop
                     ;
                     )


ORDER=1 could define the principal performer or soloist.

