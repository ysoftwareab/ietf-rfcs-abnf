capability      =/ "X-DRAFT-I02-NOTIFY"
                        ;; [[Note to RFC Editor: change the capability
                        ;; name before publication]]

command-auth    =/ notify

notify          = "NOTIFY" SP event-groups

idle            =/ "IDLE" SP "(FILTER" event-groups ")" CRLF
                        "DONE"
                        ;; Only if IDLE is also advertised

event-groups    = "(" 1*event-group ")"

event-group     = filter-mailboxes SP events

filter-mailboxes =  mbox-or-pat

events          = "(" event *(SP event) ")" / "*"
                        ;; "*" is used to denote all events.

event           = message-event SP "(" search-key ")"
                        / mailbox-event / user-event / event-ext
                        ;; As in draft-ietf-lemonade-msgevent-XX.txt

message-event   = ( "NewMessage" SP
                            "(" fetch-att *(SP fetch-att) ")" )
                        / "ExpungeMessage" / "OverQuota" / "UnderQuota"
                        / "FlagChange" / "AnnotationChange"
                        ;; "NewMessage" includes "AppendMessage".
                        ;; "FlagChange" is any of "ReadMessages",
                        ;; "TrashMessages", "SetFlags", "ClearFlags".

mailbox-event   = "CreateMailbox" / "DeleteMailbox" /
                        "RenameMailbox" /
                        "SubscriptionChange" / "MailboxMetadataChange"
                        / "QuotaChange" / "AdminMailbox"
                        ;; Should "{Create/Delete/Rename}Mailbox" be
                        ;; replaced with a single event?
                        ;; Is "AdminMailbox" (ACL change) needed?
                        ;; Add "LocationChange" - e.g. mailbox
                        ;; migration between servers?

user-event      = "OverQuota" / "UnderQuota"

event-ext       = atom
                        ;; For future extensions
