parameter     = attribute "=" value


attribute     = token
value         = token | quoted-string

quoted-string = <quoted-string, defined in [RFC2616], Section 2.2>
token         = <token, defined in [RFC2616], Section 2.2>

parameter     = reg-parameter | ext-parameter

reg-parameter = attribute "=" value
ext-parameter = attribute "*=" ext-value

ext-value     = charset  "'" [ language ] "'" value-chars

charset       = "UTF-8" | "ISO-8859-1" | ext-charset
                   ; NOTE: case-insensitive

ext-charset   = token ; see IANA charset registry
                   ; (<http://www.iana.org/assignments/character-sets>)

language      = <Language-Tag, defined in [RFC4646], Section 2.1>

value-chars   = *( pct-encoded | attr-char )

pct-encoded   = "%" HEXDIG HEXDIG

attr-char     = ALPHA | DIGIT
                   | "-" | "." | "_" | "~" | ":"
                   | "!" | "$" | "&" | "+"

ALPHA         = %x41-5A | %x61-7A
                   ; A-Z | a-z
DIGIT         = %x30-39
                   ; any US-ASCII digit "0".."9"
HEXDIG        = DIGIT | "A" | "B" | "C" | "D" | "E" | "F"
                   ; NOTE: case-insensitive

