product         = token ["/" product-version]
product-version = token

Method         = "OPTIONS"                ; Section 8.2
                      | "GET"                    ; Section 8.3
                      | "HEAD"                   ; Section 8.4
                      | "POST"                   ; Section 8.5
                      | "PUT"                    ; Section 8.6
                      | "DELETE"                 ; Section 8.7
                      | "TRACE"                  ; Section 8.8
                      | "CONNECT"                ; Section 8.9
                      | extension-method
extension-method = token

request-header = Accept                   ; [Part3], Section 5.1
                      | Accept-Charset           ; [Part3], Section 5.2
                      | Accept-Encoding          ; [Part3], Section 5.3
                      | Accept-Language          ; [Part3], Section 5.4
                      | Authorization            ; [Part7], Section 3.1
                      | Expect                   ; Section 10.2
                      | From                     ; Section 10.3
                      | Host                     ; [Part1], Section 8.4
                      | If-Match                 ; [Part4], Section 6.2
                      | If-Modified-Since        ; [Part4], Section 6.3
                      | If-None-Match            ; [Part4], Section 6.4
                      | If-Range                 ; [Part5], Section 5.3
                      | If-Unmodified-Since      ; [Part4], Section 6.5
                      | Max-Forwards             ; Section 10.5
                      | Proxy-Authorization      ; [Part7], Section 3.3
                      | Range                    ; [Part5], Section 5.4
                      | Referer                  ; Section 10.6
                      | TE                       ; [Part1], Section 8.8
                      | User-Agent               ; Section 10.9

Status-Code    =
            "100"  ; Section 9.1.1: Continue
          | "101"  ; Section 9.1.2: Switching Protocols
          | "200"  ; Section 9.2.1: OK
          | "201"  ; Section 9.2.2: Created
          | "202"  ; Section 9.2.3: Accepted
          | "203"  ; Section 9.2.4: Non-Authoritative Information
          | "204"  ; Section 9.2.5: No Content
          | "205"  ; Section 9.2.6: Reset Content
          | "206"  ; Section 9.2.7: Partial Content
          | "300"  ; Section 9.3.1: Multiple Choices
          | "301"  ; Section 9.3.2: Moved Permanently
          | "302"  ; Section 9.3.3: Found
          | "303"  ; Section 9.3.4: See Other
          | "304"  ; Section 9.3.5: Not Modified
          | "305"  ; Section 9.3.6: Use Proxy
          | "307"  ; Section 9.3.8: Temporary Redirect
          | "400"  ; Section 9.4.1: Bad Request
          | "401"  ; Section 9.4.2: Unauthorized
          | "402"  ; Section 9.4.3: Payment Required
          | "403"  ; Section 9.4.4: Forbidden
          | "404"  ; Section 9.4.5: Not Found
          | "405"  ; Section 9.4.6: Method Not Allowed
          | "406"  ; Section 9.4.7: Not Acceptable
          | "407"  ; Section 9.4.8: Proxy Authentication Required
          | "408"  ; Section 9.4.9: Request Time-out
          | "409"  ; Section 9.4.10: Conflict
          | "410"  ; Section 9.4.11: Gone
          | "411"  ; Section 9.4.12: Length Required
          | "412"  ; Section 9.4.13: Precondition Failed
          | "413"  ; Section 9.4.14: Request Entity Too Large
          | "414"  ; Section 9.4.15: Request-URI Too Large
          | "415"  ; Section 9.4.16: Unsupported Media Type
          | "416"  ; Section 9.4.17: Requested range not satisfiable
          | "417"  ; Section 9.4.18: Expectation Failed
          | "500"  ; Section 9.5.1: Internal Server Error
          | "501"  ; Section 9.5.2: Not Implemented
          | "502"  ; Section 9.5.3: Bad Gateway
          | "503"  ; Section 9.5.4: Service Unavailable
          | "504"  ; Section 9.5.5: Gateway Time-out
          | "505"  ; Section 9.5.6: HTTP Version not supported
          | extension-code

extension-code = 3DIGIT
Reason-Phrase  = *<TEXT, excluding CR, LF>

response-header = Accept-Ranges           ; [Part5], Section 5.1
                       | Age                     ; [Part6], Section 3.1
                       | ETag                    ; [Part4], Section 6.1
                       | Location                ; Section 10.4
                       | Proxy-Authenticate      ; [Part7], Section 3.2
                       | Retry-After             ; Section 10.7
                       | Server                  ; Section 10.8
                       | Vary                    ; [Part6], Section 3.5
                       | WWW-Authenticate        ; [Part7], Section 3.4

Allow   = "Allow" ":" #Method

Expect       =  "Expect" ":" 1#expectation

expectation  =  "100-continue" | expectation-extension
expectation-extension =  token [ "=" ( token | quoted-string )
                               *expect-params ]
expect-params =  ";" token [ "=" ( token | quoted-string ) ]
From   = "From" ":" mailbox

Location       = "Location" ":" absoluteURI

Max-Forwards   = "Max-Forwards" ":" 1*DIGIT

Referer        = "Referer" ":" ( absoluteURI | relativeURI )

Retry-After  = "Retry-After" ":" ( HTTP-date | delta-seconds )

Server         = "Server" ":" 1*( product | comment )

User-Agent     = "User-Agent" ":" 1*( product | comment )

