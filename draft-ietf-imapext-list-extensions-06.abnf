child-mbox-flag    = "\HasChildren" / "\HasNoChildren"
                        ; flags for Child Mailbox Extension, at most one
                        ; possible per LIST response

list               = "LIST" [SP list-options] SP mailbox SP list-mailbox

list-options       = "(" [option *(SP option)] ")"

mailbox-list       = "(" [mbx-list-flags] ")" SP
                        (DQUOTE QUOTED-CHAR DQUOTE / nil) SP mailbox
                        [SP mbox-list-extended]

mbox-list-extended = "(" [mbox-list-extended-item
                        *(SP mbox-list-extended-item)] ")"

mbox-list-extended-item = "(" mbox-list-extended-item-data ")"

mbox-list-extended-item-data =  mbox-list-extended-item-tag SP nstring-list

mbox-list-extended-item-tag  = vendor-tag / standard-tag
                        ; A tag registration template is described in section
                        ; 8.5 of this document.

vendor-tag         = "V-" atom
                        ; a vendor specific tag for extended list data

standard-tag         = atom
                        ; a tag for extended list data defined in a Standard
                        ; Track or Experimental RFC.

nstring-list       = nstring /
                        "(" [nstring-list *(SP nstring-list)] ")"
                        ;; a recursive list definition

mbox-list-oflag    = child-mbox-flag / "\NonExistent" / "\PlaceHolder"

option             = "SUBSCRIBED" / "CHILDREN" / "REMOTE" /
                        option-extension
                        ; An option registration template is described in section
                        ; 8.3 of this document.

option-extension   = option-vendor / option-public

option-vendor      = "V-" atom
                        ; a vendor specific option

option-public      = atom
                        ; an option defined in a Standard Track or
                        ; Experimental RFC


