SEPARATORS   = "(" / ")" / "<" / ">" / "@"
                  / "," / ";" / ":" / "\" / <">
                  / "/" / "[" / "]" / "?" / "="
                  / "{" / "}" / SP

       NOTE:  the range of allowed characters was determined by
       examination of HTTP and RFC822 header name formats and
       choosing the more resticted.  The intent is to allow CPIM
       headers to follow a syntax that is compatible with the
       allowed syntax for both RFC 822 [2] and HTTP [18]
       (including HTTP-derived protocols such as SIP).

Header       = Header-name *( ";" Parameter ) ": "
                    Header-value
                    CRLF

Header-name  = [ Name-prefix "." ] Name
Name-prefix  = Token
Name         = Token

Parameter    = Lang-param / Ext-param
Lang-param   = "lang=" Language-tag
Ext-param    = Param-name "=" Param-value
Param-name   = Name
Param-value  = Token / Number / String

Header-value = *UTF8-no-CTL

Token        = 1*TOKENCHAR
Number       = 1*DIGIT
String       = DQUOTE *( Str-char / Esc-pair ) DQUOTE
Str-char     = ( %x20-21 / %x23-5B / %x5D-7E / UTF8-multi )
Esc-pair     = "\" UTF8-no-CTL
Formal-name  = 1*( Token SP ) / String
URI          = <defined as absolute-URI by RFC 2396>
Language-tag = <defined by RFC 3066>
TOKENCHAR    = <Any character except CTLs or SEPARATORS or ".">
SEPARATORS   = "(" / ")" / "<" / ">" / "@"
                  / "," / ";" / ":" / "\" / <">
                  / "/" / "[" / "]" / "?" / "="
                  / "{" / "}" / SP
CTL          = <Defined by RFC 2234 -- %x0-%x17, %x7f>
CRLF         = <Defined by RFC 2234 -- CR, LF>
SP           = <defined by RFC 2234 -- %x20>
DIGIT        = <defined by RFC 2234 -- '0'-'9'>
DQUOTE       = <defined by RFC 2234 -- %x22>
UTF8-no-CTL  = %x20-7e / UTF8-multi
UTF8-multi   = %xC0-DF %x80-BF
                  / %xE0-EF %x80-BF %x80-BF
                  / %xF0-F7 %x80-BF %x80-BF %x80-BF
                  / %xF8-FB %x80-BF %x80-BF %x80-BF %x80-BF
                  / %xFC-FD %x80-BF %x80-BF %x80-BF %x80-BF %x80-BF


From-header = "From" ": " [ Formal-name ] "<" URI ">"

To-header = "To" ": " [ Formal-name ] "<" URI ">"

Cc-header   = "cc" ": " [ Formal-name ] "<" URI ">"

Date-header = "Date" ": " date-time

date-fullyear   = 4DIGIT
date-month      = 2DIGIT  ; 01-12
date-mday       = 2DIGIT  ; 01-28, 01-29, 01-30 or 01-31
time-hour       = 2DIGIT  ; 00-23
time-minute     = 2DIGIT  ; 00-59
time-second     = 2DIGIT  ; 00-59 or 00-60
time-secfrac    = "." 1*DIGIT
time-numoffset  = ("+" / "-") time-hour ":" time-minute
time-offset     = "Z" / time-numoffset

partial-time    = time-hour ":" time-minute ":" time-second
                       [time-secfrac]
full-date       = date-fullyear "-" date-month "-" date-mday
full-time       = partial-time time-offset
date-time       = full-date "T" full-time
Subject-header = "Subject" [ lang-param ] ": " *UTF8-no-CTL

NS-header = "NS" ": " [ Name-prefix ] "<" URI ">"

Require-header = "Require" ": " Header-name *( "," Header-name )

MDALG=SHA-1; type=application/pkcs
