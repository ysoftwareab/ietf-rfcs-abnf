bitstring = SQUOTE *binary-digit SQUOTE "B"

binary-digit = "0" / "1"

boolean = "TRUE" / "FALSE"

CountryString  = ALPHA ALPHA

delivery-value = pdm / ( WSP pdm SP DOLLAR SP delivery-value )

pdm = "any" / "mhs" / "physical" / "telex" / "teletex" /
                "g3fax" / "g4fax" / "ia5" / "videotex" / "telephone"

CN=Steve Kille,O=Isode Limited,C=GB

OU=Sales+CN=J. Smith,O=Widget Inc.,C=US

CN=L. Eagle,O=Sue\, Grabbit and Runn,C=GB

CN=Before\0DAfter,O=Test,C=GB
SN=Lu\C4\8Di\C4\87

EnhancedGuide = SP oid WSP SHARP WSP criteria WSP SHARP 
                      WSP subset

subset = "baseobject" / "oneLevel" / "wholeSubtree"

criteria = or-term / LPAREN or-term RPAREN

or-term = and-term *( "|" and-term )

and-term = not-term *( "&" not-term )

not-term = "!" not-term / 
                 attributetype DOLLAR match-type /
                 LPAREN or-term RPAREN /
                 "?true" / ; 
                 "?false" 

match-type = "EQ" / "SUBSTR" / "GE" / "LE" / "APPROX"

fax-number = printablestring [ "$" faxparameters ]  
                   ; telephone number, possibly followed by facsimile 
                   ; parameters

printablestring = 1*p

p = ALPHA / DIGIT / SQUOTE / LPAREN / RPAREN / PLUS / COMMA / 
          HYPHEN / DOT / EQUALS / SLASH / COLON / QUESTION / SPACE

faxparameters = faxparm / ( faxparm "$" faxparameters )

faxparm = "twoDimensional" / "fineResolution" / "unlimitedLength" 
         / "b4Length" / "a3Width" / "b4Width" / "uncompressed"

guide-value = [ object-class "#" ] criteria

object-class = SP oid

NameAndOptionalUID = DistinguishedName [ "#" bitstring ]

otherMailbox = mailbox-type DOLLAR mailbox

mailbox-type = printableString

mailbox = <an encoded IA5 String>

postal-address = dstring *( DOLLAR dstring )

presentation-address = [[[ psel "/" ] ssel "/" ] tsel "/" ] 
         network-address-list 

psel = selector
ssel = selector
tsel = selector

network-address-list = network-address USCORE 
         network-address-list / network-address

network-address = "NS" PLUS dothexstring
        / afi PLUS idi [ PLUS dsp ]
        / idp PLUS hexstring

selector = DQUOTE otherstring DQUOTE 
                / SHARP numericstring 
                / SQUOTE hexstring "'H"
                / "" 

idp = numericstring
dsp = "d" numericstring 
         / "x" dothexstring 
         / "l" otherstring 
         / "RFC-1006" PLUS prefix PLUS ip [ PLUS port [ PLUS tset ]]
         / "X.25(80)" PLUS prefix PLUS dte [ PLUS cudf-or-pid PLUS 
           hexstring ]
         / "ECMA-117-Binary" PLUS hexstring PLUS hexstring PLUS 
         hexstring / "ECMA-117-Decimal" PLUS numericstring PLUS
            numericstring PLUS numericstring

idi = numericstring

afi = "X121" / "DCC" / "TELEX" / "PSTN" / "ISDN" / "ICD" / "LOCAL"

prefix = DIGIT DIGIT

ip = numericstring
           ;  dotted decimal form (e.g., 10.0.0.6) or 
              domain (e.g., twg.com)

port = numericstring

tset = numericstring

dte = numericstring

cudf-or-pid = "CUDF" / "PID" 

other = keychar / PLUS / DOT

domainchar = keychar / DOT

hexoctet = HEX HEX

decimal-octet = 1*3DIGIT

otherstring = other otherstring / other

domainstring = domainchar otherstring / domainchar

hexstring = hexoctet hexstring / hexoctet

dotstring = decimaloctet DOT dotstring / 
         decimaloctet DOT decimaloctet

dothexstring = dotstring / hexstring
substring = [initial] any [final]

initial = value
 
any = "*" *(value "*")

final = value

teletex-id = ttx-term  0*("$" ttx-param)

ttx-term   = printablestring

ttx-param  = ttx-key ":" ttx-value

ttx-key    = "graphic" / "control" / "misc" / "page" / "private"

ttx-value  = octetstring

telex-number  = actual-number "$" country "$" answerback

actual-number = printablestring

country       = printablestring

answerback    = printablestring
