utf-8-type-addr     = "utf-8;" utf-8-enc-addr

utf-8-address       = Mailbox
     ; Mailbox as defined in [I-D.ietf-eai-rfc5336bis].

utf-8-enc-addr      = utf-8-addr-xtext /
                         utf-8-addr-unitext /
                         utf-8-address

utf-8-addr-xtext    = 1*(QCHAR / EmbeddedUnicodeChar)
                         ; 7bit form of utf-8-addr-unitext.
                         ; Safe for use in the ORCPT [RFC3461]
                         ; parameter even when UTF8SMTP SMTP
                         ; extension is not advertised.

utf-8-addr-unitext  = 1*(QUCHAR / EmbeddedUnicodeChar)
                       ; MUST follow utf-8-address ABNF when
                       ; dequoted.
                       ; Safe for using in the ORCPT [RFC3461]
                       ; parameter when UTF8SMTP SMTP extension
                       ; is also advertised.

QCHAR              = %x21-2a / %x2c-3c / %x3e-5b / %x5d-7e
                       ; US-ASCII printable characters except
                       ; CTLs, SP, '\', '+', '='.
QUCHAR              = QCHAR / UTF8-2 / UTF8-3 / UTF8-4
                       ; US-ASCII printable characters except
                       ; CTLs, SP, '\', '+' and '=', plus
                       ; other Unicode characters encoded in UTF-8

EmbeddedUnicodeChar =   %x5C.78 "{" HEXPOINT "}"
                       ; starts with "\x"

HEXPOINT = ( ( "0"/"1" ) %x31-39 ) / "10" / "20" /
              "2B" / "3D" / "7F" /         ; all xtext-specials
              "5C" / (HEXDIG8 HEXDIG) /    ; 2 digit forms
              ( NZHEXDIG 2(HEXDIG) ) /     ; 3 digit forms
              ( NZDHEXDIG 3(HEXDIG) ) /    ; 4 digit forms excluding
              ( "D" %x30-37 2(HEXDIG) ) /  ; ... surrogate
              ( NZHEXDIG 4(HEXDIG) ) /     ; 5 digit forms
              ( "10" 4*HEXDIG )            ; 6 digit forms
              ; represents either "\" or a Unicode code point outside
              ; the US-ASCII repertoire

HEXDIG8             = %x38-39 / "A" / "B" / "C" / "D" / "E" / "F"
                       ; HEXDIG excluding 0-7
NZHEXDIG            = %x31-39 / "A" / "B" / "C" / "D" / "E" / "F"
                       ; HEXDIG excluding "0"
NZDHEXDIG           = %x31-39 / "A" / "B" / "C" / "E" / "F"
                       ; HEXDIG excluding "0" and "D"

utf-8-delivery-status-content = per-message-fields
                         1*( CRLF utf-8-per-recipient-fields )
        ; "per-message-fields" remains unchanged from the definition
        ; in RFC 3464, except for the "extension-field"
        ; which is updated below.

utf-8-per-recipient-fields =
         [ original-recipient-field CRLF ]
         final-recipient-field CRLF
         action-field CRLF
         status-field CRLF
         [ remote-mta-field CRLF ]
         [ diagnostic-code-field CRLF
           *(localized-diagnostic-text-field CRLF) ]
         [ last-attempt-date-field CRLF ]
             [ final-log-id-field CRLF ]
         [ will-retry-until-field CRLF ]
         *( extension-field CRLF )
     ; All fields except for "original-recipient-field",
     ; "final-recipient-field", "diagnostic-code-field"
     ; and "extension-field" remain unchanged from
     ; the definition in RFC 3464.

generic-address =/ utf-8-enc-addr
     ; Only allowed with the "utf-8" address-type.
     ; Updates Section 3.2.3 of RFC3798
     ;
     ; This indirectly updates "original-recipient-field"
     ; and "final-recipient-field"

diagnostic-code-field =
        "Diagnostic-Code" ":" diagnostic-type ";" *text-fixed

localized-diagnostic-text-field =
        "Localized-Diagnostic" ":" Language-Tag ";" *utf8-text
     ; "Language-Tag" is a language tag as defined in [RFC5646].

extension-field =/ extension-field-name ":" *utf8-text
     ; Updates Section 7 of RFC3798

text-fixed = %d1-9 /      ; Any US-ASCII character except for NUL,
                %d11 /       ; CR and LF
                %d12 /       ; See note above about <text-fixed>
                %d14-127

utf8-text = text-fixed / UTF8-non-ascii

UTF8-non-ascii   = UTF8-2 / UTF8-3 / UTF8-4
failure-field = "Failure" ":" *utf8-text
     ; "utf8-text" is defined in Section 4 of this document.

error-field = "Error" ":" *utf8-text
     ; "utf8-text" is defined in Section 4 of this document.

warning-field = "Warning" ":" *utf8-text
     ; "utf8-text" is defined in Section 4 of this document.

