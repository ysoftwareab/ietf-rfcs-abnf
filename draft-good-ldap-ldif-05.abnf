ldif-file                = ldif-content / ldif-changes
ldif-content             = version-spec 1*(1*SEP ldif-attrval-record)

ldif-changes             = version-spec 1*(1*SEP ldif-change-record)

ldif-attrval-record      = dn-spec SEP 1*attrval-spec

ldif-change-record       = dn-spec SEP *control changerecord

version-spec             = "version:" FILL version-number

version-number           = 1*DIGIT
                              ; version-number MUST be "1" for the
                              ; LDIF format described in this document.

dn-spec                  = "dn:" (FILL distinguishedName /
                                     ":" FILL base64-distinguishedName)

distinguishedName        = SAFE-UTF8-STRING
                              ; a distinguished name, as defined in [3]

base64-distinguishedName = BASE64-UTF8-STRING
                              ; a distinguishedName which has been base64
                              ; encoded (see note 10, below)

rdn                      = SAFE-UTF8-STRING
                              ; a relative distinguished name, defined as
                              ; <name-component> in [3]

base64-rdn               = BASE64-UTF8-STRING
                              ; an rdn which has been base64 encoded (see
                              ; note 10, below)

control                  = "control:" FILL ldap-oid        ; controlType
                              0*1(1*SPACE ("true" / "false")) ; criticality
                              0*1(value-spec)                 ; controlValue
                              SEP
                              ; (See note 9, below)

ldap-oid                 = 1*DIGIT 0*1("." 1*DIGIT)
                              ; An LDAPOID, as defined in [4]

attrval-spec             = AttributeDescription value-spec SEP

value-spec               = ":" (    FILL 0*1(SAFE-STRING) /
                                   ":" FILL (BASE64-STRING) /
                                   "<" FILL url)
                              ; See notes 7 and 8, below
url                      = <a Uniform Resource Locator, as defined in [6]>
                                      ; (See Note 6, below)

AttributeDescription     = AttributeType [";" options]
                              ; Definition taken from [4]

AttributeType            = ldap-oid / (ALPHA *(attr-type-chars))

options                  = option / (option ";" options)

option                   = 1*opt-char

attr-type-chars          = ALPHA / DIGIT / "-"

opt-char                 = attr-type-chars

changerecord             = "changetype:" FILL
                              (change-add / change-delete /
                               change-modify / change-moddn)

change-add               = "add"                SEP 1*attrval-spec

change-delete            = "delete"             SEP

change-moddn             = ("modrdn" / "moddn") SEP
                               "newrdn:" (    FILL rdn /
                                          ":" FILL base64-rdn) SEP
                               "deleteoldrdn:" FILL ("0" / "1")  SEP
                               0*1("newsuperior:"
                               (    FILL distinguishedName /
                                ":" FILL base64-distinguishedName) SEP)

change-modify            = "modify"             SEP *mod-spec

mod-spec                 = ("add:" / "delete:" / "replace:")
                              FILL AttributeDescription SEP
                              *attrval-spec
                              "-" SEP

SPACE                    = %x20
                              ; ASCII SP, space

FILL                     = *SPACE

SEP                      = (CR LF / LF)

CR                       = %x0D
                              ; ASCII CR, carriage return
LF                       = %x0A
                              ; ASCII LF, line feed

ALPHA                    = %x41-5A / %x61-7A
                              ; A-Z / a-z

DIGIT                    = %x30-39
                              ; 0-9

UTF8-1                   = %x80-BF

UTF8-2                   = %xC0-DF UTF8-1

UTF8-3                   = %xE0-EF 2UTF8-1

UTF8-4                   = %xF0-F7 3UTF8-1

UTF8-5                   = %xF8-FB 4UTF8-1

UTF8-6                   = %xFC-FD 5UTF8-1

SAFE-CHAR                = %x01-09 / %x0B-0C / %x0E-7F
                              ; any value <= 127 decimal except NUL, LF, and CR

SAFE-INIT-CHAR           = %x01-09 / %x0B-0C / %x0E-1F /
                              %x21-39 / %x3B / %x3D-7F
                              ; any value <= 127 except NUL, LF, CR,
                              ; SPACE, colon (":", ASCII 58 decimal)
                              ; and less-than ("<" , ASCII 60 decimal)

SAFE-STRING              = [SAFE-INIT-CHAR *SAFE-CHAR]

SAFE-UTF8-CHAR           = SAFE-CHAR / UTF8-2 / UTF8-3 /
                              UTF8-4 / UTF8-5 / UTF8-6

SAFE-INIT-UTF8-CHAR      = SAFE-INIT-CHAR / UTF8-2 / UTF8-3 /
                              UTF8-4 / UTF8-5 / UTF8-6

SAFE-UTF8-STRING         = [SAFE-INIT-UTF8-CHAR *SAFE-UTF8-CHAR]

BASE64-UTF8-STRING       = BASE64-STRING
                              ; MUST be the base64 encoding of a valid
                              ; string of UTF-8 characters

BASE64-CHAR              = %x2B / %x2F / %x30-39 / %x3D / %x41-5A / %x61-7A
                              ; +, /, 0-9, =, A-Z, and a-z
                              ; as specified in [5]
BASE64-STRING            = [*(BASE64-CHAR)]


