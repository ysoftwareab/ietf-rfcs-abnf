fetch-att        =/ fetch-annotate
                      ; modifies original IMAP4 fetch-att

fetch-annotate   = "ANNOTATION" SP "(" entries SP attribs ")"
fetch-ann-resp   = "ANNOTATION" SP "(" entry-att *(SP entry-att) ")"


store-att-flags   =/ store-att-annotate
                       ; modifies original IMAP4 STORE command

store-att-annotate = "(" entry-att *(SP entry-att) ")"

search-key        =/ search-annotate
                       ; modifies original IMAP4 search-key

search-annotate   = "ANNOTATION" SP entry-match SP attrib-match
                       SP value

sort-key          =/ sort-annotate
                       ; modifies original
                       ; draft-crispin-imapext-sort-xx.txt sort-key

sort_annotate     = "ANNOTATION" SP entry SP attrib
entries           = entry-match /
                       "(" entry-match *(SP entry-match) ")"
attribs           = attrib-match /
                       "(" attrib-match *(SP attrib-match) ")"
entry-att         = entry SP "(" att-value *(SP att-value) ")
att-value         = attrib SP value

atom-slash        = any ATOM_CHAR except "/"
atom-dot          = any ATOM_CHAR except "."

entry             = DQUOTE 1*atom-slash *("/" 1*atom-slash) DQUOTE
entry-match       = DQUOTE 1*entry-match-atom
                       *("/" 1*entry-match-atom) DQUOTE
entry-match-atom  = 1*(list-wildcards / atom-slash)
                       *(list-wildcards / atom-slash)

attrib            = DQUOTE 1*atom-dot *("/" 1*atom-dot) DQUOTE
attrib-match      = DQUOTE 1*attrib-match-atom
                       *("/" 1*attrib-match-atom) DQUOTE
attrib-match-atom = 1*(list-wildcards / atom-dot)
                       *(list-wildcards / atom-dot)

value             = nstring
    
    
