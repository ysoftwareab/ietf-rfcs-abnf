ircURL   = type "://" location "/" [ channel ] [ options ]

type     = "irc" / "ircs"

location = [ authinfo "@" ] hostport
		     ; See Section 3.2.2 of [RFC2396] for
		     ; the definition of 'hostport'.

authinfo = [ nickname *2( "," nickname ) ] [ ":" password ]

nickname = *( escaped / unreserved )
		     ; Further restrictions may apply upon
		     ; connection, depending on the server.
		     ; Some common nickname characters must be
		     ; encoded, as per recommendations in Section
		     ; 2.4.3 in [RFC2396].

password = *( escaped / unreserved )

channel  = *( escaped / unreserved / safe / "=" )

options  = "?" option *( "&" option )

option   = optname [ "=" optvalue ]

optname  = *( ALPHA )

optvalue = optparam *( "," optparam )
		     ; Only some options have multiple parameters.

optparam = *( escaped / unreserved / safe / "?" )
		     ; Other characters (such as [Unicode]
		     ; characters in [UTF-8]) must be escaped
		     ; as per rules in Section 2.2.5 of [RFC2718].
		     ; See also Section 4 of this document.

safe     = "#" / "$" / "&" / "+" / "@" / ";" / ":"

value    = name [ "," key ]

