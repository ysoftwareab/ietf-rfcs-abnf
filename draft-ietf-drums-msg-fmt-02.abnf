CTL             =       %d1..%d31 /     ; Control characters, inc. delete
                        %d127

NO-WS-CTL       =       %d1..%d8 /      ; US-ASCII control characters 
                        %d11 /          ;  which do not include the
                        %d12 /          ;  carriage return, line feed,
                        %d14..%d31 /    ;  and whitespace characters
                        %d127

text            =       %d1..%d9 /      ; Characters excluding CR and LF
                        %d11..%d12 /
                        %d14..%d127 /
                        obs-text

specials        =       "(" / ")" /     ; Special characters used in other
                        "<" / ">" /     ;  parts of the syntax
                        "[" / "]" /
                        ":" / ";" /
                        <"> / "\" /
                        "," / "." /
                        "@"

quoted-pair     =       ("\" text) / obs-qp

WS              =       SPACE / HTAB            ; Whitespace characters
FWS             =       (*WS [CRLF WS] *WS) /   ; Folding white-space
                        obs-FWS

ctext           =       NO-WS-CTL /     ; Non-white-space controls

                        %d33..%d39 /    ; The rest of the US-ASCII
                        %d42..%d91 /    ;  characters not including "(",
                        %d93..%d127     ;  ")", or "\"

comment         =       "(" *(FWS (ctext / quoted-pair / comment)) ")"

CFWS            =       WS / comment / (FWS *(comment FWS))

atext           =       ALPHA / DIGIT / ; Any character except CTL,
                        "!" / "#" /     ;  SPACE, and specials.
                        "$" / "%" /     ;  Used for atoms
                        "&" / "'" /
                        "*" / "+" /
                        "-" / "/" /
                        "=" / "?" /
                        "^" / "_" /
                        "`" / "{" /
                        "|" / "}" /
                        "~"

atom            =       *CFWS 1*atext *CFWS

dot-atom        =       *CFWS 1*(atext ["." atext]) *CFWS

qtext           =       NO-WS-CTL /     ; Non-white-space controls

                        %d33 /          ; The rest of the US-ASCII
                        %d35..%d91 /    ;  characters not including "\"
                        %d93..%d127     ;  or the quote character

quoted-string   =       *CFWS <"> *(FWS (qtext / quoted-pair)) <"> *CFWS

word            =       atom / quoted-string

phrase          =       1*word / obs-phrase

unstructured    =       *(FWS text)

date-time       =       [ day-of-week "," ] date 1*CFWS time *CFWS

day-of-week     =       1*CFWS day-name 1*CFWS

day-name        =       "Mon" / "Tue" / "Wed" / "Thu" /
                        "Fri" / "Sat" / "Sun"

date            =       day month year

year            =       (*CFWS 4*DIGIT *CFWS) / obs-year

month           =       1*CFWS month-name 1*CFWS

month-name      =       "Jan" / "Feb" / "Mar" / "Apr" /
                        "May" / "Jun" / "Jul" / "Aug" /
                        "Sep" / "Oct" / "Nov" / "Dec"

day             =       *CFWS 1*2DIGIT *CFWS

time            =       time-of-day 1*CFWS zone

time-of-day     =       hour ":" minute [ ":" second ]

hour            =       *CFWS 2DIGIT *CFWS

minute          =       *CFWS 2DIGIT *CFWS

second          =       *CFWS 2DIGIT *CFWS

zone            =       (( "+" / "-" ) 4DIGIT) / obs-zone

address         =       mailbox / group

mailbox         =       phrase-addr / addr-spec / obs-mailbox

phrase-addr     =       [phrase] *CFWS "<" addr-spec ">" *CFWS

group           =       phrase ":" [mailbox-list] ";" *CFWS

mailbox-list    =       mailbox *("," mailbox)

address-list    =       address *("," address)

addr-spec       =       local-part "@" domain

local-part      =       dot-atom / quoted-string / obs-local-part

domain          =       dot-atom / domain-literal / obs-domain

domain-literal  =       *CFWS "[" *(FWS (dtext / quoted-pair)) "]" *CFWS

dtext           =       NO-WS-CTL /     ; Non-white-space controls

                        %d33..%d90 /    ; The rest of the US-ASCII
                        %d94..%d127     ;  characters not including "[",
                                        ;  "]", or "\"

message         =       (fields / obs-fields)
                        [CRLF body]

body            =       *(*998text CRLF) *998text

fields          =       *(trace
                          *(resent-date /
                           resent-from /
                           resent-sender /
                           resent-to /
                           resent-cc /
                           resent-bcc /
                           resent-id))
                        *(orig-date /
                        from /
                        sender /
                        reply-to /
                        to /
                        cc /
                        bcc /
                        identifier /
                        in-reply-to /
                        references /
                        subject /
                        comments /
                        keywords /
                        optional-field)

orig-date       =       "Date:" date-time CRLF

from            =       "From:" mailbox-list CRLF

sender          =       "Sender:" mailbox CRLF

reply-to        =       "Reply-To:" mailbox-list CRLF

to              =       "To:" address-list CRLF

cc              =       "Cc:" address-list CRLF

bcc             =       "Bcc:" address-list CRLF

message-id      =       "Message-ID:" identifier CRLF

in-reply-to     =       "In-Reply-To:" identifier CRLF

references      =       "References:" identifier *(*CFWS identifier) CRLF

identifier      =       "<" addr-spec ">"

subject         =       "Subject:" unstructured CRLF

comments        =       "Comments:" unstructured CRLF

keywords        =       "Keywords:" phrase *("," phrase) CRLF

resent-date     =       "Resent-Date:" date-time CRLF

resent-from     =       "Resent-From:" mailbox-list CRLF

resent-sender   =       "Resent-Sender:" mailbox CRLF

resent-to       =       "Resent-To:" address-list CRLF

resent-cc       =       "Resent-Cc:" address-list CRLF

resent-bcc      =       "Resent-Bcc:" address-list CRLF

resent-msg-id   =       "Resent-Message-ID:" identifier CRLF

trace           =       [return]
                        1*received

return          =       "Return-Path:" *(CFWS text) CRLF

received        =       "Received:" *(CFWS text) CRLF

optional-field  =       field-name ":" unstructured

field-name      =       1*ftext

ftext           =       %d33..%d57 /            ; Any character except CTL,
                        %d59..%d126             ;  SPACE, and ":".

obs-qp          =       "\" CHAR                ; CHAR is %d0..%d127

obs-text        =       *(obs-char /
                          (*CR obs-char) /
                          (*LF *CR obs-char))

obs-char        =       (%d0..%d9) /            ; %d0..%d127 except CR and LF
                        %d11 /
                        %d12 /
                        (%d14..%d127)

obs-phrase      =       word *(word / "." / CFWS)

obs-FWS         =       *(WS [CRLF WS])

obs-year        =       *CFWS 2*DIGIT *CFWS

obs-zone        =       "UT" / "GMT" /          ; Universal Time
                                                ; North American UT offsets
                        "EST" / "EDT" /         ; Eastern:  - 5/ - 4
                        "CST" / "CDT" /         ; Central:  - 6/ - 5
                        "MST" / "MDT" /         ; Mountain: - 7/ - 6
                        "PST" / "PDT" /         ; Pacific:  - 8/ - 7

                        %d65..%d73 /            ; Military zones - "A"
                        %d75..%d90 /            ; through "I" and "K" through
                        %d97..%d105 /           ; "Z", both upper and lower
                        %d107..%d122            ; case

obs-mailbox     =       addr-spec / [phrase] obs-route-addr

obs-route-addr  =       *CFWS "<" [obs-route] addr-spec ">" *CFWS

obs-route       =       *CFWS obs-domain-list ":" *CFWS

obs-domain-list =       "@" domain *("," *CFWS "@" domain)

obs-local-part  =       atom *("." atom)

obs-domain      =       atom *("." atom)

obs-fields      =       *(obs-return /
                        obs-received /
                        obs-orig-date /
                        obs-from /
                        obs-sender /
                        obs-reply-to /
                        obs-to /
                        obs-cc /
                        obs-bcc /
                        obs-message-id /
                        obs-in-reply-to /
                        obs-references /
                        obs-subject /
                        obs-comments /
                        obs-keywords /
                        obs-resent-from /
                        obs-resent-send /
                        obs-resent-rply /
                        obs-resent-to /
                        obs-resent-cc /
                        obs-resent-bcc /
                        obs-resent-mid /
                        obs-optional)

obs-orig-date   =       "Date" *CFWS ":" date-time CRLF

obs-from        =       "From" *CFWS ":" mailbox-list CRLF

obs-sender      =       "Sender" *CFWS ":" mailbox CRLF

obs-reply-to    =       "Reply-To" *CFWS ":" mailbox-list CRLF

obs-to          =       "To" *CFWS ":" address-list CRLF

obs-cc          =       "Cc" *CFWS ":" address-list CRLF

obs-bcc         =       "Bcc" *CFWS ":" address-list CRLF

obs-message-id  =       "Message-ID" *CFWS ":" identifier CRLF

obs-in-reply-to =       "In-Reply-To" *CFWS ":" *(phrase / identifier) CRLF

obs-references  =       "References" *CFWS ":" *(phrase / identifier) CRLF

obs-subject     =       "Subject" *CFWS ":" unstructured CRLF

obs-comments    =       "Comments" *CFWS ":" unstructured CRLF

obs-keywords    =       "Keywords" *CFWS ":" phrase *("," phrase) CRLF

obs-resent-from =       "Resent-From" *CFWS ":" mailbox-list CRLF

obs-resent-send =       "Resent-Sender" *CFWS ":" mailbox CRLF

obs-resent-date =       "Resent-Date" *CFWS ":" date-time CRLF

obs-resent-to   =       "Resent-To" *CFWS ":" address-list CRLF

obs-resent-cc   =       "Resent-Cc" *CFWS ":" address-list CRLF

obs-resent-bcc  =       "Resent-Bcc" *CFWS ":" address-list CRLF

obs-resent-mid  =       "Resent-Message-ID" *CFWS ":" address-list CRLF

obs-resent-rply =       "Resent-Reply-To" *CFWS ":" address-list CRLF

obs-return      =       "Return-Path" *CFWS ":" *(CFWS text) CRLF

obs-received    =       "Received" *CFWS ":" *(CFWS text) CRLF

obs-optional    =       field-name *CFWS ":" unstructured CRLF

