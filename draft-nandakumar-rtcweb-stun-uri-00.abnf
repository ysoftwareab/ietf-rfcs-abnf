stun-uri      = stun-scheme ":" [ userinfo "@" ] host [ ":" port ]
stun-scheme   = "stun"/"stuns"
userinfo      = user
user          = 1*(%x21-24 / %x26-39 / %x3B-3F / %x41-7F
                   / escaped)
                       ; The symbols "%", ":", "@", and symbols
                       ; with a character value below 0x21 may
                       ; be represented as escaped sequences.
host          = hostname / IPv4address / IPv6reference
hostname      = *( domainlabel "." ) toplabel [ "." ]
domainlabel   = alphanum / alphanum *( alphanum / "-" ) alphanum
toplabel      = ALPHA / ALPHA *( alphanum / "-" ) alphanum
IPv4address   = 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6reference = "[" IPv6address "]"
IPv6address   = hexpart [ ":" IPv4address ]
hexpart       = hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
hexseq        = hex4 *( ":" hex4 )
hex4          = 1*4HEXDIG
port          = 1*DIGIT
alphanum      = ALPHA / DIGIT
escaped       = "%" HEXDIG HEXDIG

