Language-Tag  = langtag
                 / privateuse             ; private use tag
                 / irregular              ; tags grandfathered by rule

langtag       = (language
                    ["-" script]
                    ["-" region]
                    *("-" variant)
                    *("-" extension)
                    ["-" privateuse])

language      = 2*3ALPHA               ; shortest ISO 639 code
                 / 4ALPHA                 ; reserved for future use
                 / 5*8ALPHA               ; registered language subtag

script        = 4ALPHA                 ; ISO 15924 code

region        = 2ALPHA                 ; ISO 3166-1 code
                 / 3DIGIT                 ; UN M.49 code

variant       = 5*8alphanum            ; registered variants
                 / (DIGIT 3alphanum)

extension     = singleton 1*("-" (2*8alphanum))

singleton     = %x41-57 / %x59-5A / %x61-77 / %x79-7A / DIGIT
                 ; "a"-"w" / "y"-"z" / "A"-"W" / "Y"-"Z" / "0"-"9"
                 ; Single alphanumerics
                 ; "x" is reserved for private use

privateuse    = "x" 1*("-" (1*8alphanum))

irregular     = "en-GB-oed" / "i-ami" / "i-bnn" / "i-default"
                 / "i-enochian" / "i-hak" / "i-klingon" / "i-lux"
                 / "i-mingo" / "i-navajo" / "i-pwn" / "i-tao"
                 / "i-tay" / "i-tsu" / "no-bok" / "no-nyn"
                 / "sgn-BE-FR" / "sgn-BE-NL" / "sgn-CH-DE" / "zh-cmn"
                 / "zh-cmn-Hans" / "zh-cmn-Hant" / "zh-gan"
                 / "zh-min" / "zh-min-nan" / "zh-wuu" / "zh-yue"

alphanum      = (ALPHA / DIGIT)       ; letters and numbers

                        Figure 1: Language Tag ABNF

obs-primary-language  = (2*3ALPHA [ extlang ]) ; shortest ISO 639 code
                / 4ALPHA                 ; reserved for future use
                / 5*8ALPHA               ; registered language subtag

extlang       = *3("-" 3ALPHA)         ; removed in this version

                     Figure 2: Obsolete Language ABNF

   Older language tag implementations sometimes reference [RFC3066].
   Again, all valid tags under that version also match this document's
   language tag ABNF.  However, a wider array of tags could be
   considered "well-formed" under that document.  The 'Language-Tag'
   production used in that document matches the following:

obs-language-tag = primary-subtag *( "-" subtag )
primary-subtag = 1*8ALPHA
subtag = 1*8(ALPHA / DIGIT)

                  Figure 3: RFC 3066 Language Tag Syntax

registry   = record *("%%" CRLF record)
record     = 1*( field-name *SP ":" *SP field-body CRLF )
field-name = (ALPHA / DIGIT) [*(ALPHA / DIGIT / "-") (ALPHA / DIGIT)]
field-body = *([[*SP CRLF] 1*SP] 1*CHARS)
CHARS      = (%x21-10FFFF)      ; Unicode code points

                      Figure 4: Registry Format ABNF

language      =  3 (ISO 639-2; ISO 639-1 requires 2)
script        =  5 (if not suppressed: see Section 4.1)
region        =  4 (UN M.49; ISO 3166-1 requires 3)
variant1      =  9 (needs 'language' as a prefix)
variant2      =  9 (needs 'language-variant1' as a prefix)

total         = 30 characters

              Figure 9: Derivation of the Limit on Tag Length

