
INTERNET-DRAFT                                  S.E. Kille, A.W. Young
                                                          October 1995
                                                 Expires:  April, 1996






              A string encoding of Presentation Address
                <draft-kille-presentationaddr-00.txt>



Status of this Memo

This document is an Internet Draft.  Internet Drafts are working
documents of the Internet Engineering Task Force (IETF), its Areas,
and its Working Groups.  Note that other groups may also distribute
working documents as Internet Drafts.

Internet Drafts are draft documents valid for a maximum of six months.
Internet Drafts may be updated, replaced, or obsoleted by other
documents at any time.  It is not appropriate to use Internet Drafts
as reference material or to cite them other than as a ``working
draft'' or ``work in progress.''

Please check the I-D abstract listing contained in each Internet Draft
directory to learn the current status of this or any other Internet
Draft.

Abstract

There are a number of environments where a simple string encoding of
Presentation Address is desirable.  This specification defines such a
representation.

This is an update to RFC1278 in particular to support NSAP addresses
for ITOT over IPv6.




INTERNET--DRAFT          String encoded P-Address         October 1995


1  Introduction

OSI Application Entities use presentation addresses to address other
Application Entities.  The model for this is defined in [ISO87b].
Presentation addresses are stored in the OSI Directory using an ASN.1
representation defined by the OSI Directory [CCI88].  Logically, a
presentation address consists of:

 o  A presentation selector

 o  A session selector

 o  A transport selector

 o  A set of network addresses

The selectors are all octet strings, but often have IA5 character
representations.  The format of network addresses is defined in
[ISO87a].

There is a need to represent presentation addresses as strings in a
number of different contexts.  This Internet Draft defines a format
for use on the Internet.  It is for display to human users, and its
use is recommended whenever this needs to be done.  Typically, this
will be for system managers rather than for end users.  It is not
intended for internal storage.

This Internet Draft was originally published as UCL Research Note
RN/89/14 [Kil89].  It was agreed as a unified syntax for the THORN and
ISODE projects.  It is used throughout ISODE.
Christian Huitema of Inria and Marshall Rose of PSI Inc.  gave much
useful input to this document.


2  Requirements

The main requirements are:


 o  Must be able to specify any legal value.

 o  Should be clean in the case of the presentation address containing
    network addresses and no selectors.


Kille, Young                            Expires:  April, 1996   Page 1




INTERNET--DRAFT          String encoded P-Address         October 1995


 o  Must deal with selectors in the following encodings:

    --  IA5

    --  Decimal digits encoded as IA5 (this is the most common syntax
        in Europe, as it is required by X.400(84) and should receive a
        straightforward encoding)

    --  Numeric encoded as a 16 bit unsigned integer (US GOSIP). This
        is mapped onto two octets, with the first octet being the high
        order byte of the integer.

    --  General Hexadecimal

 o  Should give special encodings for the ad hoc encoding proposed in
    ``An interim approach to use of Network Addresses'' [HK91].

    --  X.25(80) Networks

    --  TCP/IP Networks

 o  Should be extensible for additional forms.

 o  Should provide a reasonably compact representation .


3  Format

The_BNF_is_given_in_figure_1.__________________________________________


<digit> ::= [0-9]
<other> ::= [0-9a-zA-Z+-.]
<domainchar> ::= [0-9a-zA-Z-.]
<hexdigit> ::= [0-9a-fA-F]
<hexoctet> ::= <hexdigit> <hexdigit>
<decimaloctet> ::= <digit> | <digit> <digit>
                        | <digit> <digit> <digit>

<digitstring> ::= <digit> <digitstring>                             10
                        | <digit>
<otherstring> ::= <other> <otherstring>
                        | <other>


Kille, Young                            Expires:  April, 1996   Page 2




INTERNET--DRAFT          String encoded P-Address         October 1995


<domainstring> ::= <domainchar> <otherstring>
                        | <domainchar>
<hexstring> ::= <hexoctet> <hexstring> | <hexoctet>

<dotstring> ::= <decimaloctet> "." <dotstring>
                | <decimaloctet> "." <decimaloctet>
                                                                    20


<dothexstring> ::= <dotstring> | <hexstring>


<presentation-address> ::=
                [[[ <psel> "/" ] <ssel> "/" ] <tsel> "/" ]
                <network-address-list>

<network-address-list> ::= <network-address> "|" <network-address-list>30
                           | <network-address>

<psel> ::= <selector>
<ssel> ::= <selector>
<tsel> ::= <selector>

<selector>  ::= '"' <otherstring> '"'        -- IA5
                                             -- For chars not in this
                                             -- string use hex
                | "#" <digitstring>          -- US GOSIP            40
                | "'" <hexstring> "'H"       -- Hex
                | ""                         -- Empty but present

<network-address> ::=   "NS" "+" <dothexstring>
                                 -- Concrete Binary Representation
                                 -- This is the compact encoding
        | <afi> "+" <idi> [ "+" <dsp> ]
                                -- A user oriented form
        | <idp> "+" <hexstring>
                                -- ISO 8348 Compatability           50

<idp> ::= <digitstring> -

<dsp>  ::=
        | "d" <digitstring>          -- Abstract Decimal
        | "x" <dothexstring>            -- Abstract Binary
        | "l" <otherstring>             -- IA5:  local form only

Kille, Young                            Expires:  April, 1996   Page 3




INTERNET--DRAFT          String encoded P-Address         October 1995


        | "RFC-1006" "+" <prefix> "+" <ipv4>
           [ "+" <port> [ "+" <tset> ]]
        | "X.25(80)" "+" <prefix> "+" <dte>                         60
           [ "+" <cudf-or-pid> "+" <hexstring> ]
        | "ECMA-117-Binary" "+" <hexstring> "+" <hexstring>
           "+" <hexstring>
        | "ECMA-117-Decimal" "+" <digitstring> "+"
           <digitstring> "+" <digitstring>
        | "ITOT" "+" <ip> [ "+" <port-or-oid> ]

<idi> ::= <digitstring>
<afi> ::= "X121" | "DCC" | "TELEX" | "PSTN" | "ISDN"
                | "ICD" | "LOCAL"                                   70

<prefix> ::= <digit> <digit>

<ipv4> ::= <domainname> -- domain name (e.g., twg.com)
        |  <dottedquad> -- IPv4 dotted decimal form (e.g., 10.0.0.6)
<ip>   ::= <ipv4>
        |  <ipv6>       -- IPv6 conventional string representation
                        -- (e.g.  FEDC:BA98:7654:3210:FEDC:BA98:7654:3210)

<port> ::= <digitstring>                                            80
<tset> ::= <digitstring>
<port-or-oid> ::= <port>
        | <oid>
<oid> ::= <digitstring> "." <digitstringsequence>
<digitstringsequence> ::= <digitstring> "." <digitstringsequence>
        | <digitstring>

<dte> ::= <digitstring>
<cudf-or-pid> ::= "CUDF" | "PID"

________________________Figure_1:__String_BNF__________________________

Four examples:


"256"/NS+a433bb93c1|NS+aa3106

#63/#41/#12/X121+234219200300

'3a'H/TELEX+00728722+X.25(80)+02+00002340555+CUDF+"892796"


Kille, Young                             Expires: April, 1996   Page 4




INTERNET--DRAFT          String encoded P-Address         October 1995


TELEX+00728722+RFC-1006+03+10.0.0.6

ICD+0090+ITOT+FEDC:BA98:7654:3210:FEDC:BA98:7654:3210


Note that the RFC 1006 and ITOT encodings permits use of either a DNS
Domain Name or an IP address.  The former is primarily for ease of
entry.  If this DNS Domain Name maps onto multiple IP addresses, then
multiple network addresses should be generated.  The DNS Domain Name
form is for convenient input.  When mapping from an encoded address to
string form, the IP address form should always be used.


4  Encoding

Selectors are represented in a manner which can be easily encoded.  In
the NS notation, the concrete binary form of network address is given.
Otherwise, this string notation provides a mechanism for representing
the Abstract Syntax of a Network Address.  This must be encoded
according to Addendum 2 of ISO 8348 [ISO87a].


5  ITOT format


The encoding of the ITOT format is different in several ways:

1.  not all of the information which can be represented in the string
    encoding can be encoded in an NSAP address;

2.  the value of the encoded NSAP address includes a trailing octet
    which has the value '10000000'B, as specified in [ITOT].

3.  Only IDI types which permit a DSP with a binary abstract syntax
    and length at least 17 octets can be used.  At present these are
    the DCC and ICD formats.


The additional information in the string encoding which cannot be
represented in the NSAP address is the port or oid.  The port is the
TCP port number.  The oid is an OBJECT IDENTIFIER which identifies a
particular protocol stack and optional TCP port number.  If this
information is present then it must be encoded in protocol information
which accompanies the NSAP address (for example as a

Kille, Young                            Expires:  April, 1996   Page 5




INTERNET--DRAFT          String encoded P-Address         October 1995


protocol-information attribute in an X.500 Directory entry or as the
ProtocolInformation element of the AccessPoint type; see
[X.518/IS9594-4]) See [ITOT] for information on the encoding and
interpretation of this protocol information.


6  Macros

There are often common addresses, for which a cleaner representation
is desired.  This is achieved by use of Macros.  If a
<network-address> can be parsed as:

<otherstring> "=" *( any )


Then the leading string is taken as a Macro, which is substituted.
This may be applied recursively.  When presenting Network Address to
humans, the longest available substitution should be used.  For
example:

                       ________________________
                       |_Macro_|Value__________ |
                       | UK.AC |DCC+826+d110000 |
                       |_Leeds_|UK.AC=120______ |

Then ``Leeds=22'' would be expanded to ``DCC+826+d11000012022''.


7  Standard Macros

No Macros should ever be relied on.  However, the following are
suggested as standard.

           ________________________________________________
           |_Macro_____________|Value______________________ |
           | Int-X25(80)       |TELEX+00728722+X25(80)+01+  |
           | Janet-X25(80)     |TELEX+00728722+X25(80)+02+  |
           | Internet-RFC-1006 |TELEX+00728722+RFC-1006+03+ |
           | IXI               |TELEX+00728722+RFC-1006+06+ |
           |_Internet-ITOT_____|ICD+0090+ITOT+_____________ |





Kille, Young                            Expires:  April, 1996   Page 6




INTERNET--DRAFT          String encoded P-Address         October 1995


8  References

[CCI88]  The Directory --- overview of concepts, models and services,
         December 1988. CCITT X.500 Series Recommendations.

[HK91]   S.E. Hardcastle-Kille. Encoding network addresses to support
         operation over non-osi lower layers. Request for Comments
         RFC 1277, Department of Computer Science, University College
         London, November 1991.

[ISO87a] Information processing systems - data communications -
         network services definition:  Addendum 2 - network layer
         addressing, March 1987. ISO TC 97/SC 6.

[ISO87b] ISO DIS 7498-3 on naming and addressing, May 1987.
         ISO/IEC/JTC-1/SC 21.

[Kil89]  S.E. Kille. A string encoding of presentation address.
         Research Note RN/89/14, Department of Computer Science,
         University College London, February 1989.

[ITOT]   Y. Pouffary & A. Young ISO Transport Service on top of TCP

[X.518/IS9594-4] Information Technology - Open Systems
         Interconnection - The Directory:  Procedures for Distributed
         Operation ITU-T recommendation X.518(1993) / ISO/IEC 9594-4
         :  1993(E)


9  Security Considerations

Security considerations are not discussed in this memo.


10  Authors' Address

    Steve Kille, Alan Young         EMail: S.Kille@isode.com,
    ISODE Consortium                       A.Young@isode.com
    The Dome, The Square            Phone: +44 181 332 9091
    RICHMOND   TW9 1DT              Fax:   +44 181 332 9019
    U.K.


Kille, Young                            Expires:  April, 1996   Page 7
