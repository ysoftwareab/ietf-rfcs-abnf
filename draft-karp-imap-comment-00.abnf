append-ext       =/ "COMMENT" SP comment-value

capability       =/ "COMMENT"

comment-value    = "NIL" / comment-text

comment-text     = utf8-quoted / utf8-literal

fetch-att        =/ "COMMENT"

msg-att-dynamic  =/ "COMMENT" SP comment-value

resp-text-code   =/ "BADCOMMENT" SP ("NOTUTF8" / "TOOBIG") /
                       "COMMENT" SP number

search-key       =/ "COMMENT" string

select-param     =/ "COMMENT"

store-att-flags  =/ "COMMENT" [".SILENT"] SP comment-value

utf8-quoted      = DQUOTE 1*(QUOTED-CHAR) DQUOTE

utf8-literal     = "{" nz-number ["+"] "}" CRLF 1*(UTF8-char)
                         ; Number represents the octet count


