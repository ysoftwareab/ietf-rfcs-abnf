nai         =  username
nai         =/ "@" realm
nai         =/ username "@" realm

username    =  dot-string
dot-string  =  string
dot-string  =/ dot-string "." string
string      =  char
string      =/ string char
char        =  c
char        =/ "\" x

c           =  %x21    ; '!'              allowed
                          ; '"'              not allowed
c           =/ %x23    ; '#'              allowed
c           =/ %x24    ; '$'              allowed
c           =/ %x25    ; '%'              allowed
c           =/ %x26    ; '&'              allowed
c           =/ %x27    ; '''              allowed
                          ; '(', ')'         not allowed
c           =/ %x2a    ; '*'              allowed
c           =/ %x2b    ; '+'              allowed
                          ; ','              not allowed
c           =/ %x2d    ; '-'              allowed
                          ; '.'              not allowed
c           =/ %x2f    ; '/'              allowed
c           =/ %x30-39 ; '0'-'9'          allowed
                          ; ';', ':', '<'    not allowed
c           =/ %x3d    ; '='              allowed
                          ; '>'              not allowed
c           =/ %x3f    ; '?'              allowed
                          ; '@'              not allowed
c           =/ %x41-5a ; 'A'-'Z'          allowed
                          ; '[', '\', ']'    not allowed
c           =/ %x5e    ; '^'              allowed
c           =/ %x5f    ; '_'              allowed
c           =/ %x60    ; '`'              allowed
c           =/ %x61-7a ; 'a'-'z'          allowed
c           =/ %x7b    ; '{'              allowed
c           =/ %x7c    ; '|'              allowed
c           =/ %x7d    ; '}'              allowed
c           =/ %x7e    ; '~'              allowed
                          ; DEL              not allowed
c           =/ %x80-ff ; UTF-8            allowed (not in RFC 2486)
               ; c must also satisfy rules in Section 2.4
x           =  %x00-7F ; all 128 ASCII characters, no exception

realm       =  1*( label "." ) label
label       =  let-dig * (ldh-str)
ldh-str     =  *( alpha / digit / "-" ) let-dig
let-dig     =  alpha / digit
alpha       =  %x41-5A  ; 'A'-'Z'
alpha       =/ %x61-7A  ; 'a'-'z'
digit       =  %x30-39  ; '0'-'9'



fred=?#$&*+-/^smith@example.com
