newsgroups      =  "Newsgroups: " newsgroup-list CRLF

newsgroup-list  =  newsgroup-name *( "," [FWS] newsgroup-name )

newsgroup-name  =  component *( "." component ) ; 71 character max

component       =  plain-component / encoded-comp

plain-component =  component-start *29component-rest

component-start =  lowercase / DIGIT

lowercase       =  %x61-7A ;  a-z

component-rest  =  component-start / "+" / "-" / "_"

encoded-comp    =  ace-prefix 1*25ldh

ace-prefix      =  "zz--"

ldh             =  lowercase / DIGIT / "-"

path            =  "Path: " path-content CRLF

path-content    =  [FWS] *( path-host [FWS] path-delimiter [FWS] )
                      tail-entry [FWS]

path-host       =  ( ALPHA / DIGIT )
                      *( ALPHA / DIGIT / "-" / "." / ":" / "_" )

path-delimiter  =  "/" / "?" / "%" / "," / "!"

tail-entry      =  path-identity

followup-to     =  "Followup-To: " followup-value CRLF

followup-value  =  newsgroups-list / poster-text

poster-text     =  [FWS] %x70.6F.73.74.65.72 [FWS]
                      ; "poster" in lower-case

expires         =  "Expires: " date-time CRLF

