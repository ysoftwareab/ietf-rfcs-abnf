capability          =/ "CONTEXT" / "CONTEXT=" atom

command-select      =/ "FREECONTEXT" 1*(SP quoted)
       ;; <command-select> from [IMAP]

addto-position      = number
       ;; Number may be 0 for SEARCH result additions.
       ;; <number> from [IMAP]

modifier-context    = "CONTEXT"

modifier-partial    = "PARTIAL" SP seq-range
       ;; <seq-range> from [IMAP]

modifier-update     = "UPDATE"

search-return-opt   =/ modifier-context / modifier-partial /
                          modifier-update
       ;; All conform to <search-return-opt>, from [IMAP-ABNF]

resp-text-code      =/ "NOUPDATE" SP quoted
       ;; <resp-text-code> from [IMAP]

ret-data-addto      = "ADDTO"
                          SP "(" addto-position SP sequence-set
                          *(SP addto-position SP sequence-set)
                          ")"
       ;; <sequence-set> from [IMAP]

ret-data-partial    = "PARTIAL"
                         SP "(" seq-range SP partial-results ")"
       ;; <seq-range> is the requested range.
       ;; <seq-range> from [IMAP]

partial-results     = sequence-set / "NIL"
       ;; <sequence-set> from [IMAP]
       ;; NIL indicates no results correspond to the requested range.

ret-data-removefrom = "REMOVEFROM" SP sequence-set
       ;; <sequence-set> from [IMAP]

search-return-data  =/ ret-data-partial / ret-data-addto /
                          ret-data-removefrom
       ;; All conform to <search-return-data>, from [IMAP-ABNF]
