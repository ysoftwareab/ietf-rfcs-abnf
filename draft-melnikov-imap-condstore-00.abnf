store              = "STORE" SP set SP store-modifiers store-att-flags
   
store-modifiers    = [ "(" 1*store-modifier ")" ]
    
store-modifier     = "UNCHANGEDSINCE" time

fetch-att          =/ fetch-modtime
                         ; modifies original IMAP4 fetch-att

fetch-modtime      = "MODTIME" SP "(" 1*attr-name ")"

fetch-mod-resp     = "MODTIME" SP "(" 1*(attr-name SP counter) ")"

search-key         =/ search-modtime
                         ; modifies original IMAP4 search-key

search-modtime     = "MODTIME" SP attr-name SP counter

resp-text-code     =/ "MODIFIED" SP set

attr-name          = "/message/flags/system/" attr-flag 
                        ;; each system or user defined flag <flag> is mapped to
                        ;; "/message/flags/system/<flag>"


attr-flag          = "\\Answered" / "\\Flagged" / "\\Deleted" /
                        "\\Seen" / "\\Draft" / attr-flag-keyword / attr-flag-extension
                        ; Does not include "\Recent"

attr-flag-extension = "\\" atom
                       ; Future expansion.  Client implementations
                       ; MUST accept flag-extension flags.  Server
                       ; implementations MUST NOT generate
                       ; flag-extension flags except as defined by
                       ; future standard or standards-track
                       ; revisions of this specification.

attr-flag-keyword   = atom


time               = <"> time-year time-month time-day time-hour
                        time-minute time-second time-subsecond <">
                        ;; Timestamp in UTC

time-day           = 2DIGIT ;; 01-31

time-hour          = 2DIGIT ;; 00-23

time-minute        = 2DIGIT ;; 00-59

time-month         = 2DIGIT ;; 01-12

time-second        = 2DIGIT ;; 00-60

time-subsecond     = *DIGIT

time-year          = 4DIGIT


