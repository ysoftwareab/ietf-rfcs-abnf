policy-set      = 1*(idaddr conditions-sets ";")
idaddr          = ADDR
conditions-sets = 1*(conditions target-set)
conditions      = condition *("," condition)
condition       = NUMBER
target-set      = 1*(target rules)
target          = "local" / "any" / ADDR
rules           = 1*(flow action [lifetime])
flow            = ["proto" protocol] [srcaddr] [dstaddr] [match]
protocol        = [no] icmpv6 / [no] tcp / [no] udp / [no] NUMBER
icmpv6          = "icmpv6" icmptype
icmptype        = [no] type ["/" code]
type            = NUMBER
code            = NUMBER
tcp             = "tcp" [srcport] [dstport] [tcpflags]
udp             = "udp" [srcport] [dstport]
srcport         = "sport" [no] ports
dstport         = "dport" [no] ports
ports           =  port / port ":" port / ":" port / port ":"
port            = QSTRING / NUMBER
tcpflags        = "flags" flags ["/" flags]
flags           =  FLAG / flags "," FLAG
srcaddr         = "from" [no] addr
dstaddr         = "to" [no] addr
addr            = "any" / host [mask]
host            = ADDR
mask            = "/" NUMBER / "/" HEXNUM
match           = "match" 1*(matchitem)
matchitem       = "hoplimit" [no] hoplimit / "tclass" [no] tclass
                        / "ip6h" [no] ip6headers
hoplimit        = NUMBER / ":" NUMBER / NUMBER ":"
tclass          = NUMBER / HEXNUM
ip6headers      = ip6header *("," ip6header)
ip6header       = NUMBER
action          = "bid" NUMBER / "drop"
lifetime        = "lft" NUMBER
no              =  "!" / "not" / "no"

addr1           = 1*4HEXDIG ":" *(1*4HEXDIG":") 1*(":" 1*4HEXDIG)
addr2           = 1*4HEXDIG *6(":" 1*4HEXDIG) "::"
addr3           = 7*7(1*4HEXDIG ":") 1*4HEXDIG
ADDR            = addr1 / addr2 / addr3 / "::" / "::1"
QSTRING         = ALPHA *(ALPHA / DIGIT / "-")
NUMBER          = 1*DIGIT
HEXNUM          = "0x" 1*HEXDIG
FLAG            = "F" / "S" / "R" / "P" / "A" / "U"

