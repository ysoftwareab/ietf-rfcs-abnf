Foo-Example = sh-integer

sh-list       = list-member *( OWS "," OWS list-member )
list-member   = sh-item / inner-list

inner-list    = "(" OWS [ sh-item *( SP OWS sh-item ) OWS ] ")"
                   *parameter

parameter     = ";" OWS param-name [ "=" param-value ]
param-name    = key
key           = lcalpha *( lcalpha / DIGIT / "_" / "-" / "*" )
lcalpha       = %x61-7A ; a-z
param-value   = bare-item

sh-dictionary  = dict-member *( OWS "," OWS dict-member )
dict-member    = member-name "=" member-value
member-name    = key
member-value   = sh-item / inner-list
sh-item   = bare-item *parameter
bare-item = sh-integer / sh-float / sh-string / sh-token / sh-binary
               / sh-boolean

sh-integer = ["-"] 1*15DIGIT

sh-float    = ["-"] (1*9DIGIT "." 1*6DIGIT /
                         10DIGIT "." 1*5DIGIT /
                         11DIGIT "." 1*4DIGIT /
                         12DIGIT "." 1*3DIGIT /
                         13DIGIT "." 1*2DIGIT /
                         14DIGIT "." 1DIGIT )

sh-string = DQUOTE *(chr) DQUOTE
chr       = unescaped / escaped
unescaped = %x20-21 / %x23-5B / %x5D-7E
escaped   = "\" ( DQUOTE / "\" )

sh-token = ALPHA *( tchar / ":" / "/" )

sh-binary = "*" *(base64) "*"
base64    = ALPHA / DIGIT / "+" / "/" / "="

sh-boolean = "?" boolean
boolean    = "0" / "1"

