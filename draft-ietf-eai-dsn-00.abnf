utf-8-type-addr     = "utf-8;" utf-8-address

utf-8-address       = "<" Mailbox [ *WSP "<" Mailbox ">" ] ">"
          ; The first  occurrence of 'Mailbox' is defined in [utf8smtp]
          ; The second occurrence of 'Mailbox' is defined in RFC 2821

utf-8-enc-type-addr = "utf-8-enc;" utf-8-enc-addr

utf-8-enc-addr      = 1*(QUCHAR / QLOWCHAR / QMIDCHAR / QHIGHCHAR)
                       ; MUST follow utf-8-address ABNF when dequoted

QUCHAR              = %x21-24 / %x26-2a / %x2c-3c / %x3e-7e /
                         UTF8-2 / UTF8-3 / UTF8-4
                       ; Printable except CTLs SP, %, + and =
QLOWCHAR            = ("%0" NZHEXDIG) / ("%1" HEXDIG) / "%20" / "%25"
                       / "%2B" / "%3D" / "%7F"
                       ; Only permitted for CTLs, SPACE, %, + and =
QMIDCHAR            = "%" %x75 UCHAR-HEX-QUAD
                       ; %u#### excluding surrogates and US-ASCII
QHIGHCHAR           = "%" %x55 (UCHAR-HEX-5 / UCHAR-HEX-6)
                       ; %U######## excluding plane 1
UCHAR-HEX-QUAD      = UCHAR-HEX-2 / UCHAR-HEX-3
                       / UCHAR-HEX-4 / UCHAR-HEX-4D
UCHAR-HEX-2         = "00" HEXDIG8 HEXDIG
UCHAR-HEX-3         = "0" NZHEXDIG 2(HEXDIG)
UCHAR-HEX-4         = NZDHEXDIG 3(HEXDIG)
UCHAR-HEX-4D        = "D" %x30-37 2(HEXDIG)
UCHAR-HEX-5         = "000" NZHEXDIG 4(HEXDIG)
UCHAR-HEX-6         = "00" NZHEXDIG 5(HEXDIG)
HEXDIG8             = %x38-39 / "A" / "B" / "C" / "D" / "E" / "F"
                       ; HEXDIG excluding 0-7
NZHEXDIG            = %x31-39 / "A" / "B" / "C" / "D" / "E" / "F"
                       ; HEXDIG excluding "0"
NZDHEXDIG           = %x31-39 / "A" / "B" / "C" / "E" / "F"
                       ; HEXDIG excluding "0" and "D"


