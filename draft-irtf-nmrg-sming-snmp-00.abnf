snmpStatement           = snmpKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
                                 *(nodeStatement stmtsep)
                                 *(scalarsStatement stmtsep)
                                 *(tableStatement stmtsep)
                                 *(notificationStatement stmtsep)
                                 *(groupStatement stmtsep)
                                 *(complianceStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

nodeStatement           = nodeKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
   			      *1(representsStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 *1(descriptionStatement stmtsep)
                             "}" optsep ";"

representsStatement     = representsKeyword sep
   			      qucIdentifier optsep ";"

scalarsStatement        = scalarsKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
   			      1*(implementsStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

tableStatement          = tableKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
                                 anyIndexStatement stmtsep
                                 *1(createStatement stmtsep)
   			      1*(implementsStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

implementsStatement     = implementsKeyword sep qucIdentifier optsep
                                 "{" stmtsep
                                 1*(implObjectStatement stmtsep)
                             "}" optsep ";"

implObjectStatement     = objectKeyword sep
   			      lcIdentifier sep
   			      attrIdentifier optsep;

notificationStatement   = notificationKeyword sep lcIdentifier
                                 optsep "{" stmtsep
                                 oidStatement stmtsep
   			      signalsStatement stmtsep
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

signalsStatement        = signalsKeyword sep qattrIdentifier
   			      optsep "{" stmtsep
   			      *(signalsObjectStatement)
   			  "}" optsep ";"

signalsObjectStatement  = objectKeyword sep
   			      qattrIdentifier optsep ";"

groupStatement          = groupKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
                                 membersStatement stmtsep
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

complianceStatement     = complianceKeyword sep lcIdentifier optsep
                                 "{" stmtsep
                                 oidStatement stmtsep
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                                 *1(mandatoryStatement stmtsep)
                                 *(optionalStatement stmtsep)
                                 *(refineStatement stmtsep)
                             "}" optsep ";"

anyIndexStatement       = indexStatement /
                             augmentsStatement /
                             reordersStatement /
                             extendsStatement /
                             expandsStatement

indexStatement          = indexKeyword *1(sep impliedKeyword) optsep
                                 "(" optsep qlcIdentifierList
                                 optsep ")" optsep ";"

augmentsStatement       = augmentsKeyword sep qlcIdentifier
                                 optsep ";"

reordersStatement       = reordersKeyword sep qlcIdentifier
                                 *1(sep impliedKeyword)
                                 optsep "(" optsep
                                 qlcIdentifierList optsep ")"
                                 optsep ";"

extendsStatement        = extendsKeyword sep qlcIdentifier optsep ";"

expandsStatement        = expandsKeyword sep qlcIdentifier
                                 *1(sep impliedKeyword)
                                 optsep "(" optsep
                                 qlcIdentifierList optsep ")"
                                 optsep ";"

createStatement         = createKeyword optsep ";"

membersStatement        = membersKeyword optsep "(" optsep
                                 qlcIdentifierList optsep
                                 ")" optsep ";"

mandatoryStatement      = mandatoryKeyword optsep "(" optsep
                                 qlcIdentifierList optsep
                                 ")" optsep ";"

optionalStatement       = optionalKeyword sep qlcIdentifier optsep
                                 "{" descriptionStatement stmtsep
                             "}" optsep ";"

refineStatement         = refineKeyword sep qlcIdentifier optsep "{"
                                 *1(typeStatement stmtsep)
                                 *1(writetypeStatement stmtsep)
                                 *1(accessStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

typeStatement           = typeKeyword sep
                                 (refinedBaseType / refinedType)
                                 optsep ";"

writetypeStatement      = writetypeKeyword sep
                                 (refinedBaseType / refinedType)
                                 optsep ";"

oidStatement            = oidKeyword sep objectIdentifier optsep ";"

objectIdentifier        = (qlcIdentifier / subid) *127("." subid)

subid                   = decimalNumber

snmpKeyword             = "snmp"
nodeKeyword             = "node"
representsKeyword       = "represents"
scalarsKeyword          = "scalars"
tableKeyword            = "table"
implementsKeyword       = "implements"
objectKeyword           = "object"
notificationKeyword     = "notification"
signalsKeyword          = "signals"
oidKeyword              = "oid"
groupKeyword            = "group"
complianceKeyword       = "compliance"
impliedKeyword          = "implied"
indexKeyword            = "index"
augmentsKeyword         = "augments"
reordersKeyword         = "reorders"
extendsKeyword          = "extends"
expandsKeyword          = "expands"
createKeyword           = "create"
membersKeyword          = "members"
mandatoryKeyword        = "mandatory"
optionalKeyword         = "optional"
refineKeyword           = "refine"
writetypeKeyword        = "writetype"

