hex-octet = 2HEXDIGIT

octetarray = 1* (hex-octet [WSP])

unsigned = 1*DIGIT / '0x' 1*HEXDIG / '0b' 1*BIT

sign = "+" / "-"

signed = [sign] 1*DIGIT

sign = "+" / "-"

exponent = ( "e" / "E" ) [sign] 1*3DIGIT

right-decimal = "." 0*DIGIT

mantissa = 1*DIGIT [right-decimal]

num = [sign] mantissa [exponent]

naninf = "NaN" / sign "inf"

float = num / naninf

boolean-true = "true"

boolean-false = "false"

boolean = boolean-true / boolean-false

macaddress = hex-octet 5( ":" hex-octet )

date-fullyear   = 4DIGIT
date-month      = 2DIGIT  ; 01-12
date-mday       = 2DIGIT  ; 01-28, 01-29, 01-30, 01-31
time-hour       = 2DIGIT  ; 00-23
time-minute     = 2DIGIT  ; 00-59
time-second     = 2DIGIT  ; 00-58, 00-59, 00-60
time-msec       = "." 3*DIGIT
time-usec       = "." 6*DIGIT
time-nsec       = "." 9*DIGIT
partial-time    = time-hour ":" time-minute ":" time-second

datetimeseconds      = full-date "T" partial-time
datetimemilliseconds = full-date "T" partial-time "." time-msec
datetimemicroseconds = full-date "T" partial-time "." time-usec
datetimenanoseconds  = full-date "T" partial-time "." time-nsec

dec-octet   = DIGIT                 ; 0-9
               / %x31-39 DIGIT         ; 10-99
               / "1" 2DIGIT            ; 100-199
               / "2" %x30-34 DIGIT     ; 200-249
               / "25" %x30-35          ; 250-255

ipv4address = dec-octet 3( "." dec-octet )

ls32        = ( h16 ":" h16 ) / IPv4address
               ; least-significant 32 bits of address
h16         = 1*4HEXDIG
               ; 16 bits of address represented in hexadecimal
               ; zeroes to suppressed as in RFC 5952

ipv6address =                            6( h16 ":" ) ls32
               /                       "::" 5( h16 ":" ) ls32
               / [               h16 ] "::" 4( h16 ":" ) ls32
               / [ *1( h16 ":" ) h16 ] "::" 3( h16 ":" ) ls32
               / [ *2( h16 ":" ) h16 ] "::" 2( h16 ":" ) ls32
               / [ *3( h16 ":" ) h16 ] "::"    h16 ":"   ls32
               / [ *4( h16 ":" ) h16 ] "::"              ls32
               / [ *5( h16 ":" ) h16 ] "::"              h16
               / [ *6( h16 ":" ) h16 ] "::"

