PCHAR          = %x21-7e          ; a printing character, ! to ~
TCHAR          = PCHAR / SP / %x08   ; printing plus white space

error-response = error-code SP *TCHAR CRLF
error-code     = ("4" / "5") 2DIGIT

feat            = "Feat" CRLF

feat-response   = error-response / no-features / feature-listing
no-features     = "211" SP *TCHAR CRLF
feature-listing = "211-" *TCHAR CRLF
                          *( SP feature CRLF )
                          "211 End" CRLF
feature         = feature-label [ SP parms ]
feature-label   = 1*PCHAR
parms           = 1*TCHAR

opts             = opts-cmd SP command-name
                               [ SP command-options ] CRLF
opts-cmd         = "opts"
command-name     = <any FTP command which allows option setting>
command-options  = <format specified by individual FTP command>

opts-response    = opts-good / opts-bad
opts-good        = "200" SP response-message CRLF
opts-bad         = "451" SP response-message CRLF /
                           "501" SP response-message CRLF
response-message = *TCHAR

