JSON-text = array
 




begin-array     = ws %x5B ws  ; [ left square bracket

end-array       = ws %x5D ws  ; ] right square bracket

separator       = ws %x2C ws  ; , comma

ws = *(
              %x20 /              ; Space
              %x09 /              ; Horizontal tab
              %x0A /              ; Line feed or New line
              %x0D                ; Carriage return
            )

null  = %x6e.75.6c.6c      ; null

string = quotation-mark *char quotation-mark

char = unescaped /
             escape (
                     %x22 /          ; "    quotation mark  U+0022
                     %x5C /          ; \    reverse solidus U+005C
                     %x2F /          ; /    solidus         U+002F
                     %x62 /          ; b    backspace       U+0008
                     %x66 /          ; f    form feed       U+000C
                     %x6E /          ; n    line feed       U+000A
                     %x72 /          ; r    carriage return U+000D
                     %x74 /          ; t    tab             U+0009
                     %x75 4HEXDIG )  ; uXXXX                U+XXXX

escape = %x5C              ; \

quotation-mark = %x22      ; "

unescaped = %x20-21 / %x23-5B / %x5D-10FFFF

argument      = null / string

arguments     = begin-array 
                      [ argument *( separator argument ) ] 
                      end-array

function-name = null / string

function-call = begin-array 
                      function-name [ separator arguments ] 
                      end-array

batch         = begin-array 
                      [ function-call *( separator function-call ) ] 
                      end-array

 




