list-id              = <List-ID field per RFC 2919>

MIME-field           = <MIME fields, except MIME-Version>

obs-ws               = [FWS] ; FWS per RFC 2822, SHOULD parse, MUST
                          ; NOT generate

URI                  = <URI per corresponding specification>

br-URI               = "<" obs-ws URI obs-ws ">"

br-URI-list          = br-URI *([CFWS] "," [CFWS] br-URI)

list-help            = "List-Help:" [CFWS] br-URI-list [CFWS]

list-unsubscribe     = "List-Unsubscribe:" [CFWS] br-URI-list [CFWS]
list-subscribe       = "List-Subscribe:" [CFWS] br-URI-list [CFWS]

list-post            = "List-Subscribe:"    ; see RFC 2369 regarding
                          [CFWS] (br-URI-list  ; the unregistered "NO"
                          / "NO") [CFWS]       ; keyword

list-owner           = "List-Owner:" [CFWS] br-URI-list [CFWS]

list-archive         = "List-Archive:" [CFWS] br-URI-list [CFWS]

list-field           = list-help / list-unsubscribe / list-subscribe
                          / list-post / list-owner / list-archive /
                          list-id

original-return-path = "Original-Return-Path:" path ; path per RFC
                          ; 2822

message-list         = *(list-field / MIME-field / ; multipart-body
                          original-return-path) [CRLF ; per RFC 2046,
                          (multipart-body /           ; text per RFC
                          1*(*998text CRLF))]         ; 2822

