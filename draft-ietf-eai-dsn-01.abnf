utf-8-type-addr     = "utf-8;" utf-8-enc-addr

utf-8-address       = uMailbox [ *WSP "<" Mailbox ">" ]
    ; 'uMailbox' is defined in [I-D.ietf-eai-smtpext].
    ; 'Mailbox' is defined in [RFC2821].

utf-8-enc-addr      = utf-8-addr-xtext /
                        utf-8-addr-unitext /
                        utf-8-address

utf-8-addr-xtext    = xtext
                    ; xtext is defined in [RFC3461].
                    ; When xtext encoding is removed,
                    ; the syntax MUST conform to
                    ; 'utf-8-addr-unitext'.

utf-8-addr-unitext  = 1*(QUCHAR / EmbeddedUnicodeChar)
                      ; MUST follow 'utf-8-address' ABNF when
                      ; dequoted

QUCHAR              = %x21-2a / %x2c-3c / %x3e-7e /
                        UTF8-2 / UTF8-3 / UTF8-4
                      ; Printable except CTLs, SP, + and =

EmbeddedUnicodeChar =   %x5C.78 "{" HEXPOINT "}"
                      ; starts with "\x"

HEXPOINT = "5C" / ( NZHEXDIG 2*4HEXDIG ) / ( "10" 4*HEXDIG )
             ; represents either "\" or a Unicode code point outside the
             ; US-ASCII repertoire

NZHEXDIG            = %x31-39 / "A" / "B" / "C" / "D" / "E" / "F"
                      ; HEXDIG excluding "0"


