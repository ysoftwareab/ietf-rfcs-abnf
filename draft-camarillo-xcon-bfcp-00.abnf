FloorRequest =   (FIXED-HEADER)
                    (REQUEST-ID)
                  1*(FLOOR-ID)
                    (USER-ID)
                    (TOKEN)
                    [PRIORITY]
                    [USER-DISPLAY-NAME]
                    [USER-URI]
                    [HUMAN-READABLE-INFO]


FloorRelease =   (FIXED-HEADER)
                    (REQUEST-ID)
                  1*(FLOOR-ID)
                    (USER-ID)
                    (TOKEN)


FloorRequestStatus =   (FIXED-HEADER)
                          (REQUEST-ID)
                        1*(FLOOR-ID)
                          (USER-ID)
                          (REQUEST-STATUS)


FloorStatus        =     (FIXED-HEADER)
                            (FLOOR-ID)
                         *( (REQUEST-ID)
                            (USER-ID)
                            [PRIORITY]
                            [USER-DISPLAY-NAME]
                            [USER-URI]
                            [HUMAN-READABLE-INFO]
                            (REQUEST-STATUS)      )


ChairAction  =   (FIXED-HEADER)
                    (REQUEST-ID)
                    (USER-ID)
                    (TOKEN)
                    (FLOOR-ID)
                    (REQUEST-ID)
                    (USER-ID)
                    (REQUEST-STATUS)
                    [HUMAN-READABLE-INFO]


Ping         =   (FIXED-HEADER)
                    (REQUEST-ID)
                    (USER-ID)
                    (TOKEN)


PingAck      =   (FIXED-HEADER)
                    (REQUEST-ID)
                    (USER-ID)
                    [TOKEN]


Error              =   (FIXED-HEADER)
                          (REQUEST-ID)
                          (USER-ID)
                          (ERROR-CODE)
                          (HUMAN-READABLE-INFO)


bfcpURI           =  "bfcp://" server "/" conf-id [ uri-parameters ]
bfcpsURI          =  "bfcps://" server "/" conf-id [ uri-parameters ]

server            =  [ user "@" ] hostport
user              =  user-id [ ":" token ]
user-id           =  1*DIGIT
token             =  1*DIGIT

conf-id           =  1*DIGIT

uri-parameters    =  *( "?" uri-parameter)
uri-parameter     =  transport-param / other-param
transport-param   =  "transport="
                           ( "tcp" / other-transport )
other-transport   =  pvalue
other-param       =  pname [ "=" pvalue ]
pname             =  1*paramchar
pvalue            =  1*paramchar
paramchar         =  param-unreserved / unreserved / escaped
param-unreserved  =  "[" / "]" / "/" / ":" / "&" / "+" / "$"
unreserved        =  alphanum / mark
alphanum          =  ALPHA / DIGIT
mark              =  "-" / "_" / "." / "!" / "~" / "*" / "'"
                           / "(" / ")"
escaped           =  "%" HEXDIG HEXDIG
hostport         =  host [ ":" port ]

host             =  hostname / IPv4address / IPv6reference
hostname         =  *( domainlabel "." ) toplabel [ "." ]
domainlabel      =  alphanum
                          / alphanum *( alphanum / "-" ) alphanum
toplabel         =  ALPHA / ALPHA *( alphanum / "-" ) alphanum
IPv4address      =  1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6reference    =  "[" IPv6address "]"
IPv6address      =  hexpart [ ":" IPv4address ]
hexpart          =  hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
hexseq           =  hex4 *( ":" hex4)
hex4             =  1*4HEXDIG

port             =  1*DIGIT

