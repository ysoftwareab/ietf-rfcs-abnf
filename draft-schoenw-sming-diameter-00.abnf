diameterStatement       = diameterKeyword optsep
                                 "{" stmtsep
                                 *1(vendorStatement stmtsep)
                                 *(avpStatement stmtsep)
                                 *(msgStatement stmtsep)
                             "}" optsep ";"



vendorStatement         = vendorKeyword sep decimalNumber optsep ";"

avpStatement            = avpKeyword sep ucIdentifier optsep
                                 "{" stmtsep
                                 codeStatement stmtsep
                                 implementsStatement stmtsep
                                 *1(statusStatement stmtsep)
                             "}" optsep ";"

msgStatement            = msgKeyword sep ucIdentifier optsep
                                 "{" stmtsep
                                 codeStatement stmtsep
                                 *(includesStatement stmtsep)
                                 *1(statusStatement stmtsep)
                                 descriptionStatement stmtsep
                             "}" optsep ";"

codeStatement           = codeKeyword sep decimalNumber optsep ";"

implementsStatement     = implementsKeyword sep qucIdentifier optsep ";"

includesStatement       = includesKeyword sep qucIdentifier optsep ";"

diameterKeyword     =   %x64 %x69 %x61 %x6D %x65 %x74 %x65 %x72
vendorKeyword       =   %x76 %x65 %x6E %x64 %x6F %x72
avpKeyword          =   %x61 %x76 %x70
msgKeyword          =   %x6D %x73 %x67
codeKeyword         =   %x63 %x6F %x64 %x65
implementsKeyword   =   %x69 %x6D %x70 %x6C %x65 %x6D %x65 %x6E %x74 %x73
includesKeyword     =   %x69 %x6E %x63 %x6C %x75 %x64 %x65 %x73

