SEPARATORS   = "(" / ")" / "<" / ">" / "@"
                   / "," / ";" / ":" / "
                   / "/" / "[" / "]" / "?" / "="
                   / "{" / "}" / SP


         NOTE:  the range of allowed characters was determined by
         examination of HTTP and RFC822 header name formats and
         choosing the more resticted.  The intent is to allow CPIM
         headers to follow a syntax that is compatible with the
         allowed syntax for both RFC 822 [2] and HTTP [18]
         (including HTTP-derived protocols such as SIP).

Header       = Header-name ":" *( ";" Parameter ) SP
                     Header-value
                     CRLF

Header-name  = [ Name-prefix "." ] Name
Name-prefix  = Name

Parameter    = Lang-param / Ext-param
Lang-param   = "lang=" Language-tag
Ext-param    = Param-name "=" Param-value
Param-name   = Name
Param-value  = Token / Number / String

Header-value = *HEADERCHAR
Name         = 1*NAMECHAR
Token        = 1*TOKENCHAR
Number       = 1*DIGIT
String       = DQUOTE *( Str-char / Escape ) DQUOTE
Str-char     = %x20-21 / %x23-5B / %x5D-7E / UCS-high
Escape       = "\" ( "u" 4(HEXDIG)    ; UCS codepoint
                         / "b"              ; Backspace
                         / "t"              ; Tab
                         / "n"              ; Linefeed
                         / "r"              ; Return
                         / DQUOTE           ; Double quote
                         / "'"              ; Single quote
                         / "\" )            ; Backslash

Formal-name  = 1*( Token SP ) / String
URI          = <defined as absolute-URI by RFC 2396>
Language-tag = <defined by RFC 3066>

                   ; Any UCS character except CTLs, or escape
HEADERCHAR   = UCS-no-CTL / Escape

                   ; Any US-ASCII char except ".", CTLs or SEPARATORS:
NAMECHAR     = %21 / %23-26 / %2a-2b / %2d / %5e-60 / %7c / %7e
                   / ALPHA / DIGIT

                   ; Any UCS char except CTLs or SEPARATORS:
TOKENCHAR    = NAMECHAR / "." / UCS-high

SEPARATORS   = "(" / ")" / "<" / ">" / "@"    ; 28/29/3c/3e/40
                   / "," / ";" / ":" / "\" / <">    ; 2c/3b/3a/5c/22
                   / "/" / "[" / "]" / "?" / "="    ; 2f/5b/5d/3f/3d
                   / "{" / "}" / SP                 ; 7b/7d/20
CTL          = <Defined by RFC 2234 -- %x0-%x1f, %x7f>
CRLF         = <Defined by RFC 2234 -- CR, LF>
SP           = <defined by RFC 2234 -- %x20>
DIGIT        = <defined by RFC 2234 -- '0'-'9'>
HEXDIG       = <defined by RFC 2234 -- '0'-'9', 'A'-'F', 'a'-'f'>
ALPHA        = <defined by RFC 2234 -- 'A'-'Z', 'a'-'z'>
DQUOTE       = <defined by RFC 2234 -- %x22>

UCS-no-CTL   = %x20-7e / UCS-high
UCS-high     = %x80-ffffffff
UCS-no-CTL   = UTF8-no-CTL
UCS-high     = UTF8-multi
UTF8-no-CTL  = %x20-7e / UTF8-multi
UTF8-multi   = %xC0-DF %x80-BF
                   / %xE0-EF %x80-BF %x80-BF
                   / %xF0-F7 %x80-BF %x80-BF %x80-BF
                   / %xF8-FB %x80-BF %x80-BF %x80-BF %x80-BF
                   / %xFC-FD %x80-BF %x80-BF %x80-BF %x80-BF %x80-BF


From-header = "From" ": " [ Formal-name ] "<" URI ">"

To-header = "To" ": " [ Formal-name ] "<" URI ">"

Cc-header   = "cc" ": " [ Formal-name ] "<" URI ">"

DateTime-header = "DateTime" ": " date-time

Subject-header = "Subject" ":" [ lang-param ] SP *HEADERCHAR

NS-header = "NS" ": " [ Name-prefix ] "<" URI ">"

Require-header = "Require" ": " Header-name *( "," Header-name )

MDALG=SHA-1; type=application/pkcs
