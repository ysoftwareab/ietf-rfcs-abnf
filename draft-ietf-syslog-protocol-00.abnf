SYSLOG-MSG      = PRI HEADER MSG [TRAILER]

HEADER          = TIMESTAMP SP HOSTNAME SP TAG [SP]
TRAILER         = [CR] LF
PRI             = "<" PRIVALUE ">"
PRIVALUE        = (0..191) / (1*3DIGIT "," 0..7)
                        ; the alternate form is based on Albert Mietus comments...
HOSTNAME        = 1*64PRINTUSASCII  ; a FQDN,
                        ;adopt international domain names later (too political issue,
                        ; takes too long)?
TAG             = static-id  [full-dyn-id] [":"] ; 64 chars max
static-id       = 1*VISUAL
full-dyn-id     = "[" proc-id [thread-sep thread-id] "]"
proc-id         = 1*ALFANUM  ; recommended: number
thread-sep      = VISUAL / %d58     ; recommended: ",", or ':', or '.'
thread-id       = 1*ALFANUM  ; recommended: number
VISUAL          = (%d33-57/%d59-126) ; all but SP and ":"

TIMESTAMP       = TIMESTAMP-3164 / TIMESTAMP-3339
TIMESTAMP-3164  = MON-3164 SP DAY-3164 SP TIME-3164
MON-3164        = %d74.97.110  / ; "Jan"
                        %d70.101.97  / ; "Feb"
                        %d77.97.114  / ; "Mar"
                        %d65.112.114 / ; "Apr"
                        %d77.97.121  / ; "May"
                        %d74.117.110 / ; "Jun"
                        %d74.117.108 / ; "Jul"
                        %d65.117.103 / ; "Aug"
                        %d83.101.112 / ; "Sep"
                        %d79.99.116  / ; "Oct"
                        %d78.111.118 / ; "Nov"
                        %d68.101.99    ; "Dec"
DAY-3164        = (SP 1..9) / (10..31)
TIME-3164       = time-hour ":" time-minute ":" time-second-nl

TIMESTAMP-3339  = full-date "T" full-time
date-fullyear   = 4DIGIT
date-month      = 2DIGIT  ; 01-12
date-mday       = 2DIGIT  ; 01-28, 01-29, 01-30, 01-31 based on
                                ; month/year
time-hour       = 2DIGIT  ; 00-23
time-minute     = 2DIGIT  ; 00-59
time-second     = 2DIGIT  ; 00-58, 00-59, 00-60 based on leap
                                ; second rules
time-second-nl  = 2DIGIT  ; 00-59 no leap seconds!
time-secfrac    = "." 1*DIGIT
time-numoffset  = ("+" / "-") time-hour ":" time-minute
time-offset     = "Z" / time-numoffset

partial-time    = time-hour ":" time-minute ":" time-second
                        [time-secfrac]
full-date       = date-fullyear "-" date-month "-" date-mday
full-time       = partial-time time-offset

COOKIE          = "@#" (COOKIE-IANA / COOKIE-VENDOR / COOKIE-EXPER)
COOKIE-IANA     = COOKIE-ID ; IANA-Assigned
COOKIE-EXPER    = "X-" COOKIE-ID ; experimental
COOKIE-VENDOR   = "V-" VENDORURI "-" COOKIE-ID
VENDORURI       = 1*64PRINTUSASCII
                        ; a valid domain name owned by the vendor
COOKIE-ID       = 4*6PRINTUSASCII ; MUST NOT begin with "V-" or "X-"
MSG             = (COOKIE SP [COOKIE-PARAMS SP] MSG) / PAYLOAD
PAYLOAD         = *((%d32-126) / (%d128-254))
                        ; VALID UTF-8 String of PRINTABLE characters

COOKIE-PARAMS   = *(PRINTUSASCII / %d32)
                        ; parameters defined by the extension using the cookie

LF              = %d10
CR              = %d13
SP              = %d32
PRINTUSASCII    = %d33-126
ALFANUM         = %d48..57 / %d65..90 / %d97..122


Conveyer1=OK, Conveyer2=OK # %%

