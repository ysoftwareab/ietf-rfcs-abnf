telephone-url         = telephone-scheme ":" 
                            telephone-subscriber
telephone-scheme      = "telephone"
telephone-subscriber  = global-phone-number / local-phone-number
global-phone-number   = "+" 1*phonedigit [isdn-subaddress]
                            [post-dial]
local-phone-number    = 1*(phonedigit / dtmf-digit / 
                            pause-character) [isdn-subaddress] 
                            [post-dial]
isdn-subaddress       = ";isub=" 1*phonedigit
post-dial             = ";postd=" 1*(phonedigit / dtmf-digit
                            / pause-character)
phonedigit            = DIGIT / visual-separator
visual-separator      = "-" / "."
pause-character       = one-second-pause / wait-for-dial-tone
one-second-pause      = "p"
wait-for-dial-tone    = "w"
dtmf-digit            = "*" / "#" / "A" / "B" / "C" / "D"

fax-url               = fax-scheme ":" fax-subscriber 
fax-scheme            = "fax"
fax-subscriber        = fax-global-phone / fax-local-phone

fax-global-phone      = "+" 1*phonedigit [isdn-subaddress] 
                            [t33-subaddress] [post-dial]
fax-local-phone       = 1*(phonedigit / dtmf-digit / 
                            pause-character) [isdn-subaddress] 
                            [t33-subaddress] [post-dial]
t33-subaddress        = ";tsub=" 1*phonedigit

modem-url             = modem-scheme ":" remote-host
modem-scheme          = "modem"
remote-host           = telephone-subscriber *modem-params
modem-params          = ";type=" data-capabilities
data-bits             = "7" / "8"
parity                = "n" / "e" / "o" / "m" / "s"
stop-bits             = "1" / "2"

