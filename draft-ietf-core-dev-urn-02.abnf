devurn = "urn:dev:" body componentpart
body = macbody / owbody / orgbody / osbody / opsbody / otherbody
macbody = "mac:" hexstring
owbody = "ow:" hexstring
orgbody = "org:" number "-" identifier
osbody = "os:" number "-" serial
opsbody = "ops:" number "-" product "-" serial
otherbody = subtype ":" identifier
subtype = ALPHA *(DIGIT / ALPHA)
identifier = 1*unreservednout
product = identifier
serial = identifier
unreservednout = ALPHA / DIGIT / "_" / pct-encoding
componentpart = [ "_" component [ componentpart ]]
component = *1(DIGIT / ALPHA)
hexstring = hexbyte /
               hexbyte hexstring
hexbyte = hexdigit hexdigit
hexdigit = DIGIT / hexletter
hexletter = "a" / "b" / "c" / "d" / "e" / "f"
number = *1DIGIT

