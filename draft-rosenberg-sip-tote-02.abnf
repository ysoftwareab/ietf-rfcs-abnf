a=send-purp:pic image/jpg image/tiff
a=recv-purp:pic image/jpg
a=recv-purp:bizcard text/x-vcard text/html

send-purp       =    "send-purp" ":" purpose 1*(SP content-type)
recv-purp       =    "recv-purp" ":" purpose 1*(SP content-type)
purpose         =    global-purp / vendor-purp
global-purp     =    1*purp-char
vendor-purp     =    rev-hostname "." global-purp
rev-hostname    =    toplabel *( "." domainlabel  )
domainlabel     =    alphanum
                        / alphanum *( alphanum / "-" ) alphanum
toplabel        =    ALPHA / ALPHA *( alphanum / "-" ) alphanum
purp-char       =    purp-unreserved / pct-encoded / sub-delims
                        / ":" / "@"
                        ;pct-encoded from RFC 3986
                        ;sub-delims from RFC 3986
alphanum        =    ALPHA / DIGIT
                         ;DIGIT from RFC 4234
                         ;ALPHA from RFC 4234
purp-unreserved =    ALPHA / DIGIT / "-" / "_" / "~"

content-type    =    media-type
                         ;media-type from RFC 3261

tote-message    =    tote-header CRLF tote-body
tote-header     =    len-hdr CRLF
                        purp-hdr CRLF
                        type-hdr CRLF
                        *(ext-hdr CRLF)
len-hdr         =    "l" ":" 1*50DIGIT
purp-hdr        =    "p" ":" purpose
type-hdr        =    "t" ":" media-type
ext-hdr         =    hdr-name ":" hdr-value
hdr-name        =    token
                         ;token from RFC3261
hdr-value       =    *(TEXT-UTF8char / UTF8-CONT)
                         ;TEXT-UTF8char from RFC3261
                         ;UTF8-CONT from RFC3261
tote-body       =    *OCTET

