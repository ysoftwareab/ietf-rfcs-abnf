NONZERODIGIT        = %x31-39
      ; Digits one through nine.

NORMCHAR            = VCHAR / SP

NUL                 = %x00

FIN                 = %x66 %x69 %x6E %x69 %x73 %x68 %x65 %x64
      ; Lowercase "finished".

AUTH                = %x41 %x55 %x54 %x48

CAPS                = %x43 %x41 %x50 %x53

CLEARFLAG           = %x43 %x4C %x45 %x41 %x52 %x46 %x4C %x41 %x47

EXPUNGE             = %x45 %x58 %x50 %x55 %x4E %x47 %x45

GET                 = %x47 %x45 %x54

GETHDR              = %x47 %x45 %x54 %x48 %x44 %x52

LISTDIRS            = %x4C %x49 %x53 %x54 %x44 %x49 %x52 %x53

LISTMSGS            = %x4C %x49 %x53 %x54 %x4D %x53 %x47 %x53

MKDIR               = %x4D %x4B %x44 %x49 %x52

MKFOLDER            = %x4D %x4B %x46 %x4F %x4C %x44 %x45 %x52
MVFOLDER            = %x4D %x56 %x46 %x4F %x4C %x44 %x45 %x52

MVMSG               = %x4D %x56 %x4D %x53 %x47

PUT                 = %x50 %x55 %x54

QUIT                = %x51 %x55 %x49 %x54

RMDIR               = %x52 %x4D %x44 %x49 %x52

RMFOLDER            = %x52 %x4D %x46 %x4F %x4C %x44 %x45 %x52

RMMSG               = %x52 %x4D %x4D %x53 %x47

SEND                = %x53 %x45 %x4E %x44

SETFLAG             = %x53 %x45 %x54 %x46 %x4C %x41 %x47

STARTTLS            = %x53 %x54 %x41 %x52 %x54 %x54 %x4C %x53

ANONYMOUS           = %x41 %x4E %x4F %x4E %x59 %x4D %x4F %x55 %x53

DIGEST-MD5          = %x44 %x49 %x47 %x45 %x53
                         %x54 %x2D %x4D %x44 %x35

GSSAPI              = %x47 %x53 %x53 %x41 %x50 %x49

KERBEROS_V5         = %x4B %x45 %x52 %x42 %x45 %x52
                         %x4F %x53 %x5F %x56 %x35

PLAIN               = %x50 %x4C %x41 %x49 %x4E

SCRAM-SHA1          = %x53 %x43 %x52 %x41 %x4D
                         %x2D %x53 %x48 %x41 %x31

local-part-char     = %x21 / %x23-2B / %x2D-3F / %x41-5A / %x5E-7E
      ; See [[RFC3696][]].

domain-char         = %x2D / %x41-5A / %x61-7A
      ; See [[RFC3696][]].

envelope-spec       = 1*64( local-part-char ) %x40
                         1*255( domain-char )
      ; See [[RFC3696][]].

path-separator      = "/"
      ; May not occur in path components.
path-component-char = %x21-2E / %x30-7E
      ; All visible (printing) US-ASCII characters except
      ; <path-separator>.

path-component      = 1*path-component-char

directory-path      = path-component
                         *(path-separator path-component)

mail-folder         = path-component

folder-path         = *(path-component path-separator) mail-folder
      ; Complete path from mail store root to mail folder name.

auth-token          = 0*NORMCHAR
      ; No <CR> or <LF>, probably, but what about other nonprinting
      ; characters?

message-identifier  = path-component
      ; Unique within a mail folder, identifies a message.  Persistent
      ; as long as message remains in a given mail folder.

message-path        = folder-path path-separator message-identifier

directory-listing   = directory-path path-separator
      ; In LISTDIRS output, the trailing "/" indicates a directory
      ; instead of a mail folder.

flag                = %x44 / %x46 / %x4E / %x50 / %x52 / %x53 / %x54
      ; Current flags D, F, N, P, R, S, and T are uppercase only
      ; because of case-sensitivity, new flags can be added later
      ; using "r", etc.

flags               = [%x44] [%x46] [%x4E] [%x50] [%x52] [%x53]
                         [%x54]
      ; Flags are in any order but cannot be repeated.

content-size        =  NONZERODIGIT *DIGIT
      ; Positive nonzero decimal number with no leading zeroes.

content-block       = 1*OCTET
      ; Content-size octets of 8-bit data.

message-timestamp   = date-fullyear date-month date-mday %x54
                         time-hour time-minute time-second %x5A
      ; Example 20020304T134508Z.  The terms <date-fullyear>,
      ; <date-month>, <date-mday>, <time-hour>, <time-minute>,
      ; <time-second> are defined in [[RFC3339][]].
message-list-line   = message-identifier SP flags ":"
                         content-size ":" message-timestamp LF
      ; If a flag is present, that means it is set.  Note flags are
      ; optional and may not be present if all flags are cleared.

negative-code       = %x41 / %x45 / %x55 / %x58
      ; Single character negative response code A, E, U, or X.

response-code       = %x2B / %x4B / negative-code
      ; Single character response code +, K, A, E, U, or X.

response-modifier   = %x43
      ; Single character response modifier "C".  New modifiers may be
      ; added later.

response-modifiers  = [%x43]
      ; Modifiers in any order but cannot be repeated.

response-info       = *NORMCHAR
      ; Zero or more visible characters and spaces.

positive-response   = %x4B SP response-info LF

negative-response   = negative-code response-modifiers SP
                         response-info LF

one-line-response   = negative-response / positive-response

response-line       = response-code response-modifiers SP
                         response-info LF

continuation-line   = %x2B SP 1*NORMCHAR LF

multiline-response  = *continuation-line positive-response
      ; Zero or more continuation lines followed by a positive
      ; response.

command             = AUTH / CAPS / CLEARFLAG / EXPUNGE / GET /
                         GETHDR / LISTDIRS / LISTMSGS / MKDIR /
                         MKFOLDER / MVFOLDER / MVMSG / PUT / QUIT /
                         RMDIR / RMFOLDER / RMMSG / SEND / SETFLAG /
                         STARTTLS

auth-method         = ANONYMOUS / DIGEST-MD5 / GSSAPI /
                         KERBEROS_V5 / PLAIN / SCRAM-SHA1

starttls-command    = STARTTLS LF

starttls-response   = one-line-response

auth-command        = AUTH SP auth-method *( SP auth-token ) LF

token-query         = %x74 %x6F %x6B %x65 %x6E %x3F

auth-response       = negative-response
auth-response       =/ %x4B SP ( ( token-query ) /
                          ( %x6F %x6B ) / ( auth-token ) ) LF

auth-client-token   = auth-token LF

caps-command        = CAPS LF

caps-response       = negative-response
caps-response       =/ *continuation-line
                          %x2B SP 1*VCHAR SP 1*VCHAR LF
      ; "K Server-type Version"

quit-command        = QUIT LF

listdirs-command    = LISTDIRS LF

inbox               = %x69 %x6E %x62 %x6F %x78

listdirs-response   = negative-response
listdirs-response   =/ %x2B SP inbox LF *( %x2B SP
                          ( folder-path / directory-listing )
                          LF ) positive-response

mkdir-command       = MKDIR SP directory-path LF
mkdir-response      = one-line-response

rmdir-command       = RMDIR SP directory-path LF

rmdir-response      = one-line-response

mkfolder-command    = MKFOLDER SP folder-path LF

mkfolder-response   = one-line-response

rmfolder-command    = RMFOLDER SP folder-path LF

rmfolder-response   = one-line-response

mvfolder-command    = MVFOLDER SP folder-path SP folder-path LF

mvfolder-response   = one-line-response

listmsgs-command    = LISTMSGS SP folder-path LF

listmsgs-response   = negative-response
listmsgs-response   =/ *(%x2B SP message-list-line)
                          positive-response

get-command         = GET SP message-path LF

get-response        = negative-response
get-response        =/ %x4B SP content-size LF
                          content-block positive-response
      ; <content-block> is <content-size> octets of 8-bit data

gethdr-command      = GETHDR SP message-path LF

gethdr-response     = negative-response
gethdr-response     =/ %x4B SP content-size LF
                          content-block positive-response
      ; <content-block> is <content-size> octets of 8-bit data
      ; The header ends with <LF> <LF>.

put-command         = PUT SP folder-path SP content-size LF

put-cmd-response    = one-line-response

put-message         = content-block FIN LF
      ; <content-block> is <content-size> octets of 8-bit data
      ; Only used if server response is positive.

put-msg-response    = negative-response
put-msg-response    =/ %x4B SP message-identifier LF

rmmsg-command       = RMMSG SP message-path LF

rmmsg-response      = one-line-response

mvmsg-command       = MVMSG SP message-path SP folder-path LF

mvmsg-response      = negative-response
mvmsg-response      =/ %x4B SP message-identifier

setflag-command     = SETFLAG SP flag SP message-path LF

setflag-response    = one-line-response

clearflag-command   = CLEARFLAG SP flag SP message-path LF

clearflag-response  = one-line-response

expunge-command     = EXPUNGE [ SP folder-path ] LF

expunge-response    = one-line-response

send-command        = SEND SP message-path SP content-size LF

send-envelope       = 2*( envelope-spec NUL ) FIN LF
      ; Only used if server response to <send-command> is positive.

send-response       = one-line-response
      ; Response to either SEND or the envelope data.

