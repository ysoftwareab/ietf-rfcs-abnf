wildmat = wildmat-pattern *("," ["!"] wildmat-pattern)
wildmat-pattern = 1*wildmat-item
wildmat-item = wildmat-exact / wildmat-wild
wildmat-exact = %x21-29 / %x2B / %x2D-3E / %x40-5A / %x5E-7E /
         UTF8-non-ascii ; exclude * , ? [ \ ]
wildmat-wild = "*" / "?"


group  = name of newsgroup
number = estimated number of articles in the group
low    = reported low water mark
high   = reported high water mark
n          = article number
message-id = article message-id


n          = article number
message-id = article message-id


number     = Requested article number
n          = Returned article number
message-id = Article message-id


number     = Requested article number
n          = Returned article number
message-id = Article message-id


number     = Requested article number
n          = Returned article number
message-id = Article message-id


number     = Requested article number
n          = Returned article number
message-id = Article message-id


message-id = Article message-id


yyyymmddHHmmss = Current UTC date and time on server


date = Date in yymmdd or yyyymmdd format
time = Time in hhmmss format


wildmat = Newsgroups of interest
date    = Date in yymmdd or yyyymmdd format
time    = Time in hhmmss format
wildmat = groups of interest
wildmat = groups of interest


wildmat = groups of interest


group  = name of newsgroup
number = estimated number of articles in the group
low    = reported low water mark
high   = reported high water mark
range      = number(s) of articles
message-id = message-id of article


header     = name of header, without the colon
range      = number(s) of articles
message-id = message-id of article


MSGID = requests list for access by message-id
RANGE = requests list for access by range


command-line = command EOL
command = article-command /
           body-command /
           date-command /
           group-command /
           hdr-command /
           head-command /
           help-command /
           ihave-command /
           last-command /
           list-active-command /
           list-active-times-command /
           list-distrib-pats-command /
           list-distributions-command /
           list-extensions-command /
           list-headers-command /
           list-newsgroups-command /
           list-overview-fmt-command /
           listgroup-command /
           mode-reader-command /
           newgroups-command /
           newnews-command /
           next-command /
           over-command /
           post-command /
           quit-command /
           stat-command /
           x-command


article-command = "ARTICLE" [article-ref]
body-command = "BODY" [article-ref]
date-command = "DATE"
group-command = "GROUP" WS newsgroup-name
hdr-command = "HDR" WS header-meta-name [range-ref]
head-command = "HEAD" [article-ref]
help-command = "HELP"
ihave-command = "IHAVE" WS message-id
last-command = "LAST"
list-active-command = "LIST" [WS "ACTIVE" [WS wildmat]]
list-active-times-command = "LIST" WS "ACTIVE.TIMES" [WS wildmat]
list-distrib-pats-command = "LIST" WS "DISTRIB.PATS"
list-distributions-command = "LIST" WS "DISTRIBUTIONS"
list-extensions-command = "LIST" WS "EXTENSIONS"
list-headers-command = "LIST" WS "HEADERS" WS ["MSGID" / "RANGE"]
list-newsgroups-command = "LIST" WS "NEWSGROUPS" [WS wildmat]
list-overview-fmt-command = "LIST" WS "OVERVIEW.FMT"
listgroup-command = "LISTGROUP" [WS newsgroup-name]
mode-reader-command = "MODE" WS "READER"
newgroups-command = "NEWGROUPS" WS date-time
newnews-command = "NEWNEWS" WS wildmat WS date-time
next-command = "NEXT"
over-command = "OVER" [WS range-ref]
post-command = "POST"
quit-command = "QUIT"
stat-command = "STAT" [article-ref]
x-command = x-command-name *(WS x-argument)
         ; This is the generic syntax for an extension command.
         ; Each extension command is specified fully elsewhere


article-ref = WS (article-number / message-id)
date = date2y / date4y
date4y = 4DIGIT 2DIGIT 2DIGIT
date2y = 2DIGIT 2DIGIT 2DIGIT
date-time = date WS time [WS "GMT"]
header-meta-name = header-name / metadata-name
metadata-name = ":" 1*A-NOTCOLON
range = article-number ["-" [article-number]]
range-ref = WS (range / message-id)
time = 2DIGIT 2DIGIT 2DIGIT
x-command-name = 3*12A-CHAR
x-argument = 1*P-CHAR
command-continuation = ihave-continuation /
           post-continuation


ihave-continuation = encoded-article
post-continuation = encoded-article


encoded-article = content-lines termination
       ; after undoing the "byte-stuffing", this MUST match "article"



response = simple-response / multiline-response
multiline-response = simple-response content-lines termination


simple-response =
           simple-response-content [SP trailing-comment] CRLF
simple-response-content = 3DIGIT arguments
trailing-comment = *U-CHAR
arguments = *(SP argument) ; How many depends on the response
argument = 1*A-CHAR
simple-response-content =/ response-111-content
           response-211-content
           response-22x-content
           response-401-content


response-111-content = "111" SP date4y time
response-211-content = "211" 3(SP article-number) SP newsgroup-name
response-22x-content = ("220" / "221" / "222" / "223")
           SP article-number SP message-id
response-401-content = "401" SP extension-label



multiline-response-content = article-response /
           body-response /
           hdr-response /
           head-response /
           help-response /
           list-active-response /
           list-active-times-response /
           list-distrib-pats-response /
           list-distributions-response /
           list-extensions-response /
           list-headers-response /
           list-newsgroups-response /
           list-overview-fmt-response /
           listgroup-response /
           newgroups-response /
           newnews-response /
           over-response


article-response = article
body-response = body
hdr-response = *(article-number SP hdr-content CRLF)
head-response = 1*header
help-response = *(*B-CHAR CRLF)
list-active-response = *(newsgroup-name SPA article-number
           SPA article-number SPA newsgroup-status CRLF)
list-active-times-response =
           *(newsgroup-name SPA 1*DIGIT SPA newsgroup-creator CRLF)
list-distrib-pats-response =
           *(1*DIGIT ":" wildmat ":" distribution CRLF)
list-distributions-response =
           *(distribution SPA distribution-description CRLF)
list-extensions-response =
           *(extension-descriptor CRLF)
list-headers-response = *(header-meta-name CRLF) /
           *((metadata-name / ":") CRLF)
list-newsgroups-response =
           *(newsgroup-name WS newsgroup-description CRLF)
list-overview-fmt-response = list-overview-fmt-text
listgroup-response = *(article-number CRLF)
newgroups-response = list-active-response
newnews-response = *(message-id CRLF)
over-response = *(article-number over-content CRLF)


list-overview-fmt-text =
           "Subject:" CRLF
           "From:" CRLF
           "Date:" CRLF
           "Message-ID:" CRLF
           "References:" CRLF
           ( ":bytes" CRLF ":lines" / "Bytes:" CRLF "Lines:") CRLF
           *((header-name ":full" / metadata-name) CRLF)


distribution = 1*P-CHAR
distribution-description = U-TEXT
hdr-content = *S-NONTAB
hdr-n-content = [(header-name ":" / metadata-name) SP hdr-content]
newsgroup-creator = U-TEXT
newsgroup-description = S-TEXT
newsgroup-status = %x79 / %x6E / %x6D / private-status
over-content = 1*6(TAB hdr-content) /
           7(TAB hdr-content) *(TAB hdr-n-content)
private-status = 1*P-CHAR ; except the values in newsgroup-status
extension-argument = 1*P-CHAR
extension-descriptor = extension-generic-descriptor
extension-generic-descriptor =
           extension-label *(SPA extension-argument)
extension-label = 1*12UPPER


extension-descriptor =/ hdr-extension /
           listgroup-extension /
           over-extension


hdr-extension = %x48.44.52                         ; "HDR"
listgroup-extension = %x4C.49.53.54.47.52.4F.55.50 ; "LISTGROUP"
over-extension = %x4F.56.45.52 [SPA "MSGID"]       ; "OVER"



article = 1*header CRLF body
header = header-name ":" [CRLF] SP header-content CRLF
header-content = *(S-CHAR / [CRLF] WS)
body = *(*B-CHAR CRLF)



article-number = 1*16DIGIT
content-lines = *([content-text] CRLF)
content-text = (".." / B-NONDOT) *B-CHAR
header-name = 1*A-NOTCOLON
message-id = "<" 1*248A-NOTGT ">"
newsgroup-name = 1*wildmat-exact
termination = "." CRLF
wildmat = wildmat-pattern *("," ["!"] wildmat-pattern)
wildmat-pattern = 1*wildmat-item
wildmat-item = wildmat-exact / wildmat-wild
wildmat-exact = %x21-29 / %x2B / %x2D-3E / %x40-5A / %x5E-7E /
          UTF8-non-ascii  ; exclude * , ? [ \ ]
wildmat-wild = "*" / "?"


base64 = *(4base64-char) [base64-terminal]
base64-char = UPPER / LOWER / DIGIT / "+" / "/"
base64-terminal = 2base64-char "==" / 3base64-char "="


A-CHAR     = %x21-7E
A-NOTCOLON = %x21-39 / %x3B-7E  ; exclude ":"
A-NOTGT    = %x21-3D / %x3F-7E  ; exclude ">"
P-CHAR     = A-CHAR / UTF8-non-ascii
U-CHAR     = CTRL / TAB / SP / A-CHAR / UTF8-non-ascii
U-NONTAB   = CTRL /       SP / A-CHAR / UTF8-non-ascii
U-TEXT     = P-CHAR *U-CHAR
B-CHAR     = CTRL / TAB / SP / %x21-FF
B-NONDOT   = CTRL / TAB / SP / %x21-2D / %x2F-FF  ; exclude "."


ALPHA = UPPER / LOWER   ; use only when case-insensitive
CR = %x0D
CRLF = CR LF
CTRL = %x01-08 / %x0B-0C / %x0E-1F
DIGIT = %x30-39
EOL = *(SP / TAB) CRLF
LF = %x0A
LOWER = %x61-7A
SP = %x20
SPA = 1*SP
TAB = %x09
UPPER = %x41-5A
UTF8-non-ascii = UTF8-2 / UTF8-3 / UTF8-4
UTF8-2    = %xC2-DF UTF8-tail
UTF8-3    = %xE0 %xA0-BF UTF8-tail / %xE1-EC 2UTF8-tail /
                 %xED %x80-9F UTF8-tail / %xEE-EF 2UTF8-tail
UTF8-4    = %xF0 %x90-BF 2UTF8-tail / %xF1-F3 3UTF8-tail /
                 %xF4 %x80-8F 2UTF8-tail
UTF8-tail = %x80-BF
WS = 1*(SP / TAB)
S-CHAR   = %x21-FF
S-NONTAB = CTRL / SP / S-CHAR
S-TEXT   = (CTRL / S-CHAR) *B-CHAR


S-CHAR   = P-CHAR
S-NONTAB = U-NONTAB
S-TEXT   = U-TEXT
header = header-name ":" SP [header-content] CRLF
header-content = [WS] 1*P-CHAR *( [CRLF] WS 1*P-CHAR )



