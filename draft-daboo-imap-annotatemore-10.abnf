annotate-data     = "METADATA" SP entry-list

att-select        = "value" / "value.priv" / "value.shared"
                          ; the only attributes that can be selected

att-value         = attrib SP value

attrib            = astring
                           ; dot-separated attribute name
                           ; MUST NOT contain "*" or "%"

attribs           = attrib / "(" attrib *(SP attrib) ")"
                           ; one or more attribute specifiers

capability        =/ "METADATA"
                           ; defines the capability for this extension

command-auth      =/ setmetadata / getmetadata
                           ; adds to original IMAP command

entries           = entry-match /
                           "(" entry-match *(SP entry-match) ")"
                           ; entry specifiers that can include wildcards

entry             = astring
                           ; slash-separated path to entry
                           ; MUST NOT contain "*" or "%"

entry-att         = entry SP "(" att-value *(SP att-value) ")"

entry-list        = entry-att *(SP entry-att) /
                           "(" entry *(SP entry) ")"
                           ; entry attribute-value pairs list for
                           ; GETMETADATA response, or
                           ; parenthesised entry list for unsolicited
                           ; notification of annotation changes

entry-match       = list-mailbox
                           ; slash-separated path to entry
                           ; MAY contain "*" or "%" for use as wildcards

getmetadata       = "GETMETADATA" SP entries SP attribs

list-select-base-opt =/ "METADATA" SP
                               "(" list-select-metadata
                                    *(SP list-select-metadata) ")"
                           ; adds a new selection option type to
                           ; LISTEXT. When evaluating multiple entry,
                           ; attribute, value combinations, a match to
                           ; a mailbox must occur when all items match.


list-select-metadata = entry-match SP att-select SP value
                           ; value set to the empty string means match
                           ; if that entry and attribute exist.
                           ; value set to NIL means match if that entry
                           ; and attribute do not exist.

response-payload  =/ annotate-data
                           ; adds to original IMAP data responses

resp-text-code    =/ "METADATA" SP ("TOOBIG" / "TOOMANY" /
                                           "NOPRIVATE")
                           ; new response codes for SETMETADATA
                           ; failures

setmetadata       = "SETMETADATA" SP list-mailbox
                                         SP setentryatt
                           ; empty string for list-mailbox implies
                           ; server annotation.

setentryatt       = entry-att / "(" entry-att *(SP entry-att) ")"

value             = nstring / literal8


