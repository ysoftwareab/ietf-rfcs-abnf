XMPP-URI      = "xmpp:" jid [ "/" resource]
jid           = [ node "@" ] host
node          = *( alphanum / escaped /
                   "-" / "_" / "." / "!" / "~" / "*" / "(" / ")" )
host          = hostname / IPv4address / IPv6reference
hostname      = *( domainlabel "." ) toplabel [ "." ]
domainlabel   = alphanum / alphanum *( alphanum / "-" ) alphanum
toplabel      = alpha / alpha *( alphanum / "-" ) alphanum
IPv6reference = "[" IPv6address "]"
IPv6address   = hexpart [ ":" IPv4address ]
IPv4address   = 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
hexpart       = hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
hexseq        = hex4 *( ":" hex4)
hex4          = 1*4HEXDIG
resource      = *( unreserved / escaped )
reserved      = ";" / "/" / "?" / ":" / "@" / "&" / "=" / "+" /
                   "$" / "," / "[" / "]"


