local-part     = local-quoted / local-unquoted

local-quoted   = <"> primary-q ["+" subaddress-q] <">

local-unquoted = [primary-c] ["+" [subaddress-c]]

primary        = *(SAFEQ-CHAR / QUOTE-CHAR)
                      ;; This is the syntax for an unquoted primary
                      ;; address. If it fits canonical form (primary-c)
                      ;; it is used directly in a local-part.  Otherwise
                      ;; QUOTE-CHARs are escaped to fit quoted form.

primary-c      = primc-word *("." primc-word)

primary-q      = *(SAFEQ-CHAR / quoted)

primc-word     = 1*SAFE-CHAR

quoted         = "\" QUOTE-CHAR

subaddress     = *(SAFEQ-CHAR / QUOTE-CHAR / "+")
                      ;; This is the syntax for an unquoted subaddress.
                      ;; If it fits canonical form (subaddress-c) it is
                      ;; used directly in a local-part.  Otherwise
                      ;; QUOTE-CHARs are escaped to fit quoted form.

subaddress-c   = subc-word *("." subc-word)

subaddress-q   = *(SAFEQ-CHAR / "+" / quoted)

subc-word      = 1*(SAFE-CHAR / "+")
QUOTE-CHAR     =  <"> / "\"

SAFE-CHAR      =  %21 / %23..27 / %2A / %2D / %2F..39 / %3D /
                       %3F / %41..5A / %5E..7E
                       ;; All printable US-ASCII characters except
                       ;; space, plus "+" and <specials> as defined
                       ;; in [IMAIL]

SAFEQ-CHAR     =  %20..21 / %23..2A / %2C..5B / %5D..7E
                       ;; All printable US-ASCII characters except
                       ;; plus "+", quote <"> and backslash "\"


