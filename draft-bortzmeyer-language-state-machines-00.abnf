state-machine = 1*(statement / (*comment-wsp))

statement = (declaration / transition / assignment)
               *comment-wsp ";" *comment-wsp

colon = *comment-wsp ":" *comment-wsp
comma = *comment-wsp "," *comment-wsp
equal = *comment-wsp "=" *comment-wsp
arrow = *comment-wsp "->" *comment-wsp

declaration = names colon value

assignment = name equal value

names = name *(comma name)

name = quoted-name / regular-identifier

regular-identifier = ALPHA / (ALPHA *(ALPHA / DIGIT / "-") ALPHA)

transition = message colon
                current-state arrow next-state
                [colon action]

message = name
current-state = name
next-state = name
action = name

value = regular-identifier
identifier-chars = %x41-5A / %x61-7A / DIGIT /
                      "-" / "_" / "'" / "," / ";" / SP
                     ; All letters and digits and some chars
quoted-name = DQUOTE 1*(identifier-chars) DQUOTE

comment        =  "#" *(WSP / VCHAR) CRLF

comment-nl           =  comment / CRLF

comment-wsp          =  *(WSP / comment-nl)
