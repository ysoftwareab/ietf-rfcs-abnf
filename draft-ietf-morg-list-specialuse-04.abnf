create-param   =/  "USE" SP "(" [use-flag *(SP use-flag)] ")"
                    ; Extends "create-param" from RFC 4466 [RFC4466]

mbx-list-oflag =/  use-flag
                    ; Extends "mbx-list-oflag" from IMAP base [RFC3501]

list-select-independent-opt =/  "SPECIAL-USE"
                    ; Extends "list-select-independent-opt" from
                    ; LIST-extended [RFC5258]

return-option =/ "SPECIAL-USE"
                    ; Extends "return-option" from
                    ; LIST-extended [RFC5258]

resp-text-code =/  "USEFLAG"
                    ; Extends "resp-text-code" from
                    ; IMAP [RFC3501]

use-flag        =  "\All" / "\Archive" / "\Drafts" / "\Flagged" /
                    "\Junk" / "\Sent" / "\Trash" / use-flag-ext

use-flag-ext    =  "\" atom
                    ; Reserved for future extensions.  Clients
                    ; MUST ignore list flags they do not understand
                    ; Server implementations MUST NOT generate
                    ; extension flags except as defined by
                    ; future standards-track revisions of or
                    ; extensions to this specification.


