cnNldD11dGYtOA=="
ZDRiYmQ3NjBhMTUyMzIxZjIxNDNhZjcscW9wPWF1dGg="
mZmZA==")

cnNldD11dGYtOA==
ZDRiYmQ3NjBhMTUyMzIxZjIxNDNhZjcscW9wPWF1dGg="
sieveurl = sieveurl-server / sieveurl-list-scripts /
                    sieveurl-script

sieveurl-server = "sieve://" authority

sieveurl-list-scripts = "sieve://" [ authority ] ["/"]

sieveurl-script = "sieve://" [ authority ] "/" scriptname

scriptname = 1*pchar

SAFE-CHAR             = %x01-09 / %x0B-0C / %x0E-21 / %x23-5B /
                            %x5D-7F
                            ;; any TEXT-CHAR except QUOTED-SPECIALS

QUOTED-CHAR           = SAFE-UTF8-CHAR / DQUOTE QUOTED-SPECIALS

QUOTED-SPECIALS       = DQUOTE / "\"

SAFE-UTF8-CHAR        = SAFE-CHAR / UTF8-2 / UTF8-3 / UTF8-4
                            ;; <UTF8-2>, <UTF8-3> and <UTF8-4>
                            ;; are defined in [UTF-8]

ATOM-CHAR             = "!" / %x23-27 / %x2A-5B / %x5D-7A / %x7C-7E
                            ;; Any CHAR except ATOM-SPECIALS

ATOM-SPECIALS         = "(" / ")" / "{" / SP / CTL /
                             QUOTED-SPECIALS

QUOTED-SPECIALS       = <"> / "\"
atom                  = 1*1024ATOM-CHAR

iana-token            = atom
                            ;; MUST be registered with IANA

auth-type             = DQUOTE auth-type-name DQUOTE

auth-type-name        = iana-token
                            ;; as defined in SASL [SASL]

command               = command-authenticate / command-logout /
                            command-getscript / command-setactive /
                            command-listscripts / command-deletescript /
                            command-putscript / command-capability /
                            command-havespace / command-starttls /
                            command-renamescript / command-noop

command-authenticate  = "AUTHENTICATE" SP auth-type [SP string]
                            *(CRLF string) CRLF

command-capability    = "CAPABILITY" CRLF

command-deletescript  = "DELETESCRIPT" SP sieve-name CRLF

command-getscript     = "GETSCRIPT" SP sieve-name CRLF

command-havespace     = "HAVESPACE" SP sieve-name SP number CRLF

command-listscripts   = "LISTSCRIPTS" CRLF

command-noop          = "NOOP" [SP string] CRLF

command-logout        = "LOGOUT" CRLF

command-putscript     = "PUTSCRIPT" SP sieve-name SP sieve-script
                             CRLF

sieve-script          = string

command-renamescript  = "RENAMESCRIPT" SP old-sieve-name SP
                            new-sieve-name CRLF

old-sieve-name        = sieve-name

new-sieve-name        = sieve-name

command-setactive     = "SETACTIVE" SP sieve-name CRLF
command-starttls      = "STARTTLS" CRLF

extend-token          = atom
                            ;; MUST be defined by a standards track or
                            ;; IESG approved experimental protocol
                            ;; extension

extension-data        = extension-item *(SP extension-item)

extension-item        = extend-token / string / number /
                            "(" [extension-data] ")"

literal-c2s           = "{" number  "+}" CRLF *OCTET
                            ;; The number represents the number of
                            ;; octets.
                            ;; This type of literal can only be sent
                            ;; from the client to the server.

literal-s2c           = "{" number  "}" CRLF *OCTET
                            ;; Almost identical to literal-c2s,
                            ;; but with no '+' character.
                            ;; The number represents the number of
                            ;; octets.
                            ;; This type of literal can only be sent
                            ;; from the server to the client.

number                = 1*DIGIT
                            ;; A 32-bit unsigned number.
                            ;; (0 <= n < 4,294,967,296)

quoted                = DQUOTE *1024QUOTED-CHAR DQUOTE
                            ;; limited to 1024 octets between the <">s

resp-code             = "AUTH-TOO-WEAK" / "ENCRYPT-NEEDED" /
                            "QUOTA" / resp-code-sasl /
                            resp-code-referral /
                            "TRANSITION-NEEDED" / "TRYLATER" /
                            "ACTIVE" / "NONEXISTENT" /
                            "ALREADYEXISTS" /
                            "TAG" SP string /
                            resp-code-ext

resp-code-referral    = "REFERRAL" SP sieveurl

resp-code-sasl        = "SASL" SP string

resp-code-name        = iana-token
                            ;; The response code name is hierarchical,
                            ;; separated by '/'.
                            ;; The response code name MUST NOT start
                            ;; with '/'.

resp-code-ext         = resp-code-name [SP extension-data]
                            ;; unknown response codes MUST be tolerated
                            ;; by the client.

response              = response-authenticate /
                            response-logout /
                            response-getscript /
                            response-setactive /
                            response-listscripts /
                            response-deletescript /
                            response-putscript /
                            response-capability /
                            response-havespace /
                            response-starttls /
                            response-renamescript /
                            response-noop

response-authenticate = *(string CRLF)
                            ((response-ok [response-capability]) /
                             response-nobye)
                            ;; <response-capability> is REQUIRED if a
                            ;; SASL security layer was negotiated and
                            ;; MUST be omitted otherwise.

response-capability   = *(single-capability) response-oknobye

single-capability     = capability-name [SP string] CRLF

capability-name       = string
                            ;; Note that literal-s2c is allowed.

initial-capabilities  = DQUOTE "IMPLEMENTATION" DQUOTE SP string /
                            DQUOTE "SASL" DQUOTE SP sasl-mechs /
                            DQUOTE "SIEVE" DQUOTE SP sieve-extensions /
                            DQUOTE "NOTIFY" DQUOTE SP notify-mechs /
                            DQUOTE "STARTTLS" DQUOTE /
                            DQUOTE "LANGUAGE" DQUOTE SP language /
                            DQUOTE "RENAME" DQUOTE /
                            DQUOTE "NOOP" DQUOTE
                            ;; Each capability conforms to
                            ;; the syntax for single-capability.
                            ;; Also note that the capability name
                            ;; can be returned as either literal-s2c
                            ;; or quoted, even though only "quoted"
                            ;; string is shown above.

sasl-mechs = string
                 ; space separated list of SASL mechanisms,
                 ; each SASL mechanism name complies with rules
                 ; specified in [SASL].
                 ; Can be empty.

sieve-extensions = string
                 ; space separated list of supported SIEVE extensions,
                 ; can be empty.

language     = string
                 ; Contains <Language-Tag> from [RFC4646].

notify-mechs = string
                 ; space separated list of URI schema parts
                 ; for supported notification [NOTIFY] methods.
                 ; MUST NOT be empty.

response-deletescript = response-oknobye

response-getscript    = (sieve-script CRLF response-ok) /
                            response-nobye

response-havespace    = response-oknobye

response-listscripts  = *(sieve-name [SP "ACTIVE"] CRLF)
                            response-oknobye
                            ;; ACTIVE may only occur with one sieve-name

response-logout       = response-oknobye

response-ok           = "OK" [SP "(" resp-code ")"]
                            [SP string] CRLF
                            ;; The string contains human readable text
                            ;; encoded as UTF-8.

response-nobye        = ("NO" / "BYE") [SP "(" resp-code ")"]
                            [SP string] CRLF
                            ;; The string contains human readable text
                            ;; encoded as UTF-8.

response-oknobye      = response-ok / response-nobye

response-noop         = response-ok

response-putscript    = response-oknobye
response-renamescript = response-oknobye

response-setactive    = response-oknobye

response-starttls     = (response-ok response-capability) /
                            response-nobye

sieve-name            = string
                            ;; See Section 1.6 for the full list of
                            ;; prohibited characters.

string                = quoted / literal-c2s / literal-s2c
                            ;; literal-c2s is only allowed when sent
                            ;; from the client to the server.
                            ;; literal-s2c is only allowed when sent
                            ;; from the server to the client.
                            ;; quoted is allowed in either direction.


