capability          =/ "CONDSTORE" / "QRESYNC"

status-att          =/ "HIGHESTMODSEQ"
                             ;; extends non-terminal defined in [RFC3501].

status-att-val      =/ "HIGHESTMODSEQ" SP mod-sequence-valzer
                             ;; extends non-terminal defined in [RFC4466].
                             ;; Value 0 denotes that the mailbox doesn't
                             ;; support persistent mod-sequences
                             ;; as described in Section 3.1.1.2
                             ;; [[RFC Editor: Please check the above reference]]

store-modifier      =/ "UNCHANGEDSINCE" SP mod-sequence-valzer





                             ;; Only a single "UNCHANGEDSINCE" may be
                             ;; specified in a STORE operation

fetch-modifier      =/ chgsince-fetch-mod
                             ;; conforms to the generic "fetch-modifier"
                             ;; syntax defined in [RFC4466].

chgsince-fetch-mod  = "CHANGEDSINCE" SP mod-sequence-value
                             ;; CHANGEDSINCE FETCH modifier conforms to
                             ;; the fetch-modifier syntax

fetch-att           =/ fetch-mod-sequence
                             ;; modifies original IMAP4 fetch-att

fetch-mod-sequence  = "MODSEQ"

fetch-mod-resp      = "MODSEQ" SP "(" permsg-modsequence ")"

msg-att-dynamic     =/ fetch-mod-resp

search-key          =/ search-modsequence
                             ;; modifies original IMAP4 search-key
                             ;;
                             ;; This change applies to all commands
                             ;; referencing this non-terminal, in
                             ;; particular SEARCH, SORT and THREAD.

search-modsequence  = "MODSEQ" [search-modseq-ext] SP
                            mod-sequence-valzer

search-modseq-ext   = SP entry-name SP entry-type-req

resp-text-code      =/ "HIGHESTMODSEQ" SP mod-sequence-value /
                             "NOMODSEQ" /
                             "MODIFIED" SP sequence-set

entry-name          = entry-flag-name

entry-flag-name     = DQUOTE "/flags/" attr-flag DQUOTE
                             ;; each system or user defined flag <flag>
                             ;; is mapped to "/flags/<flag>".
                             ;;
                             ;; <entry-flag-name> follows the escape rules
                             ;; used by "quoted" string as described in
                             ;; Section 4.3 of [RFC3501], e.g., for the flag
                             ;; \Seen the corresponding <entry-name> is
                             ;; "/flags/\\seen", and for the flag
                             ;; $MDNSent, the corresponding <entry-name>





                             ;; is "/flags/$mdnsent".

entry-type-resp     = "priv" / "shared"
                             ;; metadata item type

entry-type-req      = entry-type-resp / "all"
                             ;; perform SEARCH operation on private
                             ;; metadata item, shared metadata item or both

permsg-modsequence  = mod-sequence-value
                             ;; per message mod-sequence

mod-sequence-value  = 1*DIGIT
                             ;; Positive unsigned 64-bit integer
                             ;; (mod-sequence)
                             ;; (1 <= n < 18,446,744,073,709,551,615)

mod-sequence-valzer = "0" / mod-sequence-value

search-sort-mod-seq = "(" "MODSEQ" SP mod-sequence-value ")"

select-param        =/ condstore-param
                             ;; conforms to the generic "select-param"
                             ;; non-terminal syntax defined in [RFC4466].

condstore-param     = "CONDSTORE"

mailbox-data        =/ "SEARCH" [1*(SP nz-number) SP
                             search-sort-mod-seq]

sort-data           = "SORT" [1*(SP nz-number) SP
                             search-sort-mod-seq]
                             ; Updates SORT response from RFC 5256

attr-flag           = "\\Answered" / "\\Flagged" / "\\Deleted" /
                            "\\Seen" / "\\Draft" / attr-flag-keyword /
                            attr-flag-extension
                             ;; Does not include "\\Recent"

attr-flag-extension = "\\" atom
                             ;; Future expansion.  Client implementations
                             ;; MUST accept flag-extension flags.  Server
                             ;; implementations MUST NOT generate
                             ;; flag-extension flags except as defined by
                             ;; future standard or standards-track
                             ;; revisions of [RFC3501].

attr-flag-keyword   = atom





select-param        =  "QRESYNC" SP "(" uidvalidity SP
                          mod-sequence-value [SP known-uids]
                          [SP seq-match-data] ")"
                          ;; conforms to the generic select-param
                          ;; syntax defined in [RFC4466]

seq-match-data      =  "(" known-sequence-set SP known-uid-set ")"

uidvalidity         =  nz-number

known-uids          =  sequence-set
                          ;; sequence of UIDs, "*" is not allowed

known-sequence-set  =  sequence-set
                          ;; set of message numbers corresponding to
                          ;; the UIDs in known-uid-set, in ascending order.
                          ;; * is not allowed.

known-uid-set       =  sequence-set
                          ;; set of UIDs corresponding to the messages in
                          ;; known-sequence-set, in ascending order.
                          ;; * is not allowed.

message-data        =/ expunged-resp

expunged-resp       =  "VANISHED" [SP "(EARLIER)"] SP known-uids

rexpunges-fetch-mod =  "VANISHED"
                          ;; VANISHED UID FETCH modifier conforms
                          ;; to the fetch-modifier syntax
                          ;; defined in [RFC4466].  It is only
                          ;; allowed in the UID FETCH command.

resp-text-code      =/ "CLOSED"

