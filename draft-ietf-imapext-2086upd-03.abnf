LOWER_ALPHA     =  %x61-7A   ;; a-z

acl_data        = "ACL" SP mailbox *(SP identifier SP
                        rights)

deleteacl       = "DELETEACL" SP mailbox SP identifier

getacl          = "GETACL" SP mailbox

identifier      = astring

listrights      = "LISTRIGHTS" SP mailbox SP identifier

listrights_data = "LISTRIGHTS" SP mailbox SP identifier
                           SP rights *(SP rights)

mod_rights      = astring
                      ;; +rights to add, -rights to remove
                      ;; rights to replace

myrights        = "MYRIGHTS" SP mailbox

myrights_data   = "MYRIGHTS" SP mailbox SP rights

new_rights      = 1*LOWER_ALPHA
                      ;; MUST include "t", "e", "x" and "k".
                      ;; MUST NOT include standard rights listed
                      ;; in section 3.2

rights          = astring
                      ;; only lowercase ASCII letters and digits
                      ;; are allowed.

rights_capa     = "RIGHTS=" new_rights
                      ;; RIGHTS=... capability

setacl          = "SETACL" SP mailbox SP identifier
                       SP mod_rights


