p3p-header       = `P3P: ` p3p-header-field *(`,` p3p-header-field)

p3p-header-field = policy-ref-field | compact-policy-field | extension-field

policy-ref-field = `policyref="` URI `"`

extension-field  = token [`=` (token | quoted-string) ]

compact-policy-field  = `CP="` compact-policy `"`

compact-policy        =  compact-access
                            [" " compact-disputes]
                            [*(" " compact-remedies)]
                            [" " compact-non-identifiable]
                            [1*(" " compact-purpose)]
                            [1*(" "compact-recipient)]
                            1*(" " compact-retention)
                            [*(" " compact-category)]
                            [compact-test]

compact-access        = "NOI" | "ALL" | "CAO" | "IDC" | "OTI" | "NON"

compact-disputes      = "DSP"

compact-remedies      = "COR" | "MON" | "LAW"

compact-non-identifiable = "NID"

compact-purpose       = "CUR"        | "ADM" [creq] | "DEV" [creq] | "TAI" [creq] |
                           "PSA" [creq] | "PSD" [creq] | "IVA" [creq] | "IVD" [creq] |
                           "CON" [creq] | "HIS" [creq] | "TEL" [creq] | "OTP" [creq]

creq                  = "a" | "i" | "o"

compact-recipient     = "OUR" | "DEL" [creq] | "SAM" [creq] | "UNR" [creq] |
                           "PUB" [creq] | "OTR" [creq]

compact-retention     = "NOR" | "STP" | "LEG" | "BUS" | "IND"

compact-category      = "PHY" | "ONL" | "UNI" | "PUR" | "FIN" | "COM" |
                           "NAV" | "INT" | "DEM" | "CNT" | "STA" | "POL" |
                           "HEA" | "PRE" | "LOC" | "GOV" | "OTC"

compact-test          = "TST"
