smb-URI        = ( smb-absURI / smb-relURI )
smb-absURI     = scheme "://" smb-service [ "?" [ nbt-context ] ]
smb-relURI     = ( path-absolute / path-rootless )
                       [ "?" [ nbt-context ] ]

scheme         = "smb" / "cifs"
smb-service    = ( smb-wrkgrp / smb-net-path )

smb-wrkgrp     = [ smb-userinfo "@" ] [ smb-srv-name ]
                         [ ":" port ] [ "/" ]
smb-net-path   = smb-server [ path-absolute ]
smb-server     = [ smb-userinfo "@" ] smb-srv-name [ ":" port ]

smb-srv-name   = nbt-name / host
nbt-name       = netbiosname [ "." scope-id ]
netbiosname    =  netbiosnamec 1*14( netbiosnamec / "*" )
netbiosnamec   = ( ALPHA / DIGIT / pct-encoded
                         / "-" / "_" / "~"
                         / "!" / "$" / "&" / "'" / "(" / ")"
                         / "+" / "," / ";" / "="
                       )

scope-id       = [ scope-label *( "." scope-label ) ]
scope-label    = 1*63( scope-char )
scope-char     = ALPHA / DIGIT / "-" / "_" / "~"
                       / sub-delims / pct-encoded

smb-userinfo   = [ auth-domain ";" ] userinfo-nosem
auth-domain    = smb-srv-name
userinfo-nosem = *( unreserved / pct-encoded
                          / "!" / "$" / "&" / "'" / "(" / ")"
                          / "*" / "+" / "," / "=" / ":"
                        )

nbt-context   = nbt-param *(";" nbt-param )
nbt-param     = ( "BROADCAST=" IPv4address [ ":" port ]
                        / "CALLED=" netbiosname
                        / "CALLING=" netbiosname
                        / ( "NBNS=" / "WINS=" ) host [ ":" port ]
                        / "NODETYPE=" ("B" / "P" / "M" / "H")
                        / ( "SCOPEID=" / "SCOPE=" ) scope-id
                      )
