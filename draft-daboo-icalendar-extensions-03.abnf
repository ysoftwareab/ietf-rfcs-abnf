calname       = "NAME" calnameparam ":" text CRLF

calnameparam  = *(
                    ;
                    ; The following are OPTIONAL,
                    ; but MUST NOT occur more than once.
                    ;
                    (";" altrepparam) / (";" languageparam) /
                    ;
                    ; The following is OPTIONAL,
                    ; and MAY occur more than once.
                    ;
                    (";" other-param)
                    ;
                    )

calrefesh      = "REFRESH-INTERVAL" calrefreshparam
                    ":" dur-value CRLF
                    ;consisting of a positive duration of time.

calrefeshparam = *(";" other-param)

color          = "COLOR" colorparam ":" colorvalue CRLF

colorparam     = *(";" other-param)

colorvalue     = integer ";" integer ";" integer
                    ; Red, green, and blue values in the range
                    ; 0 - 255.
image      = "IMAGE" imageparam ( ":" uri ) /
                (
                  ";" "ENCODING" "=" "BASE64"
                  ";" "VALUE" "=" "BINARY"
                  ":" binary
                )
                CRLF

imageparam = *(
                 ;
                 ; The following is OPTIONAL for a URI value,
                 ; RECOMMENDED for a BINARY value,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" fmttypeparam) /
                 ;
                 ; The following are OPTIONAL,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" alturiparam) / (";" displayparam) /
                 ;
                 ; The following is OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
                 )

broadcast = "BROADCAST" broadparam  ":" uri CRLF

broadparam  = *(
                  ;
                  ; The following are OPTIONAL,
                  ; and MUST NOT occur more than once.
                  ;
                  (";" featureparam) / (";" infoparam) /
                  (";" languageparam) / (";" regionparam) /
                  ;
                  ; The following is OPTIONAL,
                  ; and MAY occur more than once.
                  ;
                  (";" other-param)
                  ;
                  )

conference = "CONFERENCE" confparam  ":" uri CRLF

confparam  = *(
                 ;
                 ; The following are OPTIONAL,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" featureparam) / (";" infoparam) /
                 (";" languageparam) / (";" regionparam) /
                 ;
                 ; The following is OPTIONAL,
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
                 )

alturiparam = "ALTURI" "=" DQUOTE uri DQUOTE

displayparam = "DISPLAY" "="
                  ("BADGE" /       ; A small "badge" image
                   "BACKGROUND" /  ; Use as a background image
                   "OVERLAY" /     ; Use as an overlay image
                   "BANNER" /      ; Use as a "banner" across the top
                   x-name /        ; Experimental type
                   iana-token)     ; Other IANA registered type
                                   ;
                                   ; Default is BADGE

featureparam = "FEATURE" "=" featuretext *("," featuretext)
featuretext  =  ("AUDIO" /      ; Audio capability
                    "CHAT" /       ; Chat or instanst messaging
                    "FEED" /       ; Blog or Atom feed
                    "SCREEN" /     ; Screen sharing
                    "VIDEO" /      ; Video capability
                    x-name /       ; Experimental type
                    iana-token)    ; Other IANA registered type
infoparam = "INFO" "=" paramtext

regionparam = "REGION" "=" regiontext *("," regiontext)
regiontext  = paramtext ; an ISO3166-1 or ISO3166-2 code

