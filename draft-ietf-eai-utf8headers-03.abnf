UTF8-xtra-char  =   UTF8-2 / UTF8-3 / UTF8-4

UTF8-2          =   %xC2-DF UTF8-tail

UTF8-3          =   %xE0 %xA0-BF UTF8-tail /
                       %xE1-EC 2(UTF8-tail) /
                       %xED %x80-9F UTF8-tail /
                       %xEE-EF 2(UTF8-tail)

UTF8-4          =   %xF0 %x90-BF 2(UTF8-tail) /
                       %xF1-F7 3(UTF8-tail)

UTF8-tail       =   %x80-BF

ctext   =  NO-WS-CTL /     ; all of <text> except
              %d33-39 /       ; SP, HTAB, "(", ")"
              %d42-91 /       ; and "\"
              %d93-126 /
              UTF8-xtra-char

utext   =  NO-WS-CTL /     ; Non white space controls
              %d33-126 /      ; The rest of US-ASCII
              UTF8-xtra-char

comment = "(" *([FWS] utf8-ccontent) [FWS] ")"

word    = utf8-atom / utf8-quoted-string

utf8-text   =  %d1-9 /         ; all UTF-8 characters except
              %d11-12 /       ; US-ASCII NUL, CR and LF
              %d14-127 /
              UTF8-xtra-char

utf8-quoted-pair   = ("\" utf8-text) / obs-qp

utf8-qcontent      = utf8-qtext / utf8-quoted-pair

utf8-quoted-string = [CFWS]
                           DQUOTE *([FWS] utf8-qcontent) [FWS] DQUOTE
                           [CFWS]

utf8-ccontent      = ctext / utf8-quoted-pair / comment

utf8-qtext  =   NO-WS-CTL /     ; all of <text> except
              %d33 /               ; The rest of the US-ASCII
              %d35-91 /        ; characters not including "\"
              %d93-126 /       ; or the quote character
              UTF8-xtra-char

utf8-atext   =  ALPHA / DIGIT /
                   "!" / "#" /     ; Any character except
                   "$" / "%" /     ; controls, SP, and specials.
                   "&" / "'" /     ; Used for atoms
                   "*" / "+" /
                   "-" / "/" /
                   "=" / "?" /
                   "^" / "_" /
                   "`" / "{" /
                   "|" / "}" /
                   "~" /
                   UTF8-xtra-char

utf8-atom     = [CFWS] 1*utf8-atext [CFWS]

utf8-dot-atom = [CFWS] utf8-dot-atom-text [CFWS]

utf8-dot-atom-text = 1*utf8-atext *("." 1*utf8-atext)

mailbox        =  name-addr / addr-spec / utf8-addr-spec

angle-addr     =  [CFWS] "<" utf8-addr-spec [alt-address] ">" [CFWS]

utf8-addr-spec =  utf8-local-part "@" utf8-domain

utf8-local-part=  utf8-dot-atom / utf8-quoted-string / obs-local-part

utf8-domain    =  utf8-dot-atom / domain-literal / obs-domain

alt-address    =  [CFWS] "<" addr-spec ">" [CFWS]

value   =       token / quoted-string

qcontent = utf8-qtext / quoted-pair

