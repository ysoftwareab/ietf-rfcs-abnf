CTL             =       1..31 / 127     ; Control characters, inc. delete

NO-WS-CTL       =       1..8 /          ; US-ASCII control characters 
                        11 /            ;  which do not include the
                        12 /            ;  carriage return, linefeed
                        14..31 / 127    ;  space, and tab characters

text            =       1..9 / 11..12 / ; Characters excluding CR and LF
                        14..127

specials        =       "(" / ")" /     ; Special characters used in other
                        "<" / ">" /     ;  parts of the syntax
                        "[" / "]" /
                        ":" / ";" /
                        <"> / "\" /
                        "," / "." /
                        "@"

quoted-pair     =       "\" text / obs-qp

WS              =       SPACE / HTAB
FWS             =       1*([CRLF] WS)   ; Folding white-space

ctext           =       NO-WS-CTL /     ; Non-white-space controls

                        33..39 /        ; The rest of the US-ASCII
                        42..91 /        ;  characters not including "(",
                        93..127         ;  ")", or "\"

comment         =       "(" *(ctext / FWS /quoted-pair / comment) ")"

CFWS            =       comment / FWS

atext           =       ALPHA / DIGIT / ; Any character except CTL,
                        "!" / "#" /     ;  SPACE, and specials.
                        "$" / "%" /     ;  Used for atoms
                        "&" / "'" /
                        "*" / "+" /
                        "-" / "/" /
                        "=" / "?" /
                        "^" / "_" /
                        "`" / "{" /
                        "|" / "}" /
                        "~"

atom            =       *CFWS 1*atext *CFWS

dot-atom        =       *CFWS 1*(atext ["." atext]) *CFWS

qtext           =       NO-WS-CTL /     ; Non-white-space controls

                        33 /            ; The rest of the US-ASCII
                        35..91 /        ;  characters not including "\"
                        93..127         ;  or the quote character

quoted-string   =       *CFWS <"> *(qtext / FWS /quoted-pair) <"> *CFWS

dtext           =       NO-WS-CTL /     ; Non-white-space controls

                        33..90 /        ; The rest of the US-ASCII
                        94..127         ;  characters not including "[",
                                        ;  "]", or "\"

domain-literal  =       *CFWS "[" *(dtext / FWS /quoted-pair) "]" *CFWS

word            =       atom / quoted-string

phrase          =       1*word

unstructured    =       *(text / FWS) / obs-unstruct

date-time       =       *CFWS [ day "," SPACE ] date SPACE time *CFWS /
                        obs-date-time

day-of-week     =       "Mon" / "Tue" / "Wed" / "Thu" /
                        "Fri" / "Sat" / "Sun"

date            =       1*2DIGIT SPACE month SPACE 4DIGIT

month           =       "Jan" / "Feb" / "Mar" / "Apr" /
                        "May" / "Jun" / "Jul" / "Aug" /
                        "Sep" / "Oct" / "Nov" / "Dec"

time            =       hour SPACE zone

hour            =       2DIGIT ":" 2DIGIT [ ":" 2DIGIT ]

zone            =       ( "+" / "-" ) 4DIGIT

address         =       mailbox / group

mailbox         =       [phrase] *CFWS "<" addr-spec ">" *CFWS /
                        addr-spec /
                        obs-mailbox

group           =       phrase ":" #mailbox ";" *CFWS

addr-spec       =       local-part "@" domain

local-part      =       dot-atom / quoted-string / obs-local-part

domain          =       dot-atom / domain-literal / obs-domain

message         =       (fields / obs-fields)
                        [CRLF body]

body            =       *(*998text CRLF) *998text / obs-body

fields          =       [trace]
                        {orig-date
                        originator
                        [identifier]
                        [informational]
                        [destination]
                        *resent
                        *optional-field}

orig-date       =       "Date:" WS date-time CRLF

originator      =       {(from / sender)
                         [reply-to]}

reply-to        =       "Reply-To:" WS 1#mailbox CRLF

sender          =       {"Sender:" WS mailbox CRLF
                         "From:" WS 1#mailbox CRLF}

from            =       "From:" WS mailbox CRLF

destination     =       {["To:" WS 1#address CRLF]
                         ["Cc:" WS 1#address CRLF]
                         ["Bcc:" WS 1#address CRLF]}

identifier      =       {["Message-ID:" WS msg-id CRLF]
                         ["In-Reply-To:" WS msg-id CRLF]
                         ["References:" WS msg-id *(*CFWS msg-id) CRLF]}

msg-id          =       "<" addr-spec ">"

informational   =       {["Subject:" WS unstructured CRLF]
                         ["Comments:" WS unstructured CRLF]
                         ["Keywords:" WS 1#phrase CRLF]}

trace           =       [return]
                        1*received

return          =       "Return-Path:" WS "<" [addr-spec] ">" CRLF

received        =       "Received:" *CFWS
                        [WS "from" WS domain]
                        [WS "by" WS domain ]
                        [WS "via" WS atom]
                        *(WS "with" WS atom) 
                        [WS "id" WS atom [WS "@" WS domain]]
                        [WS "for" WS addr-spec]
                        WS ";" WS date-time CRLF

resent          =       {resent-orig CRLF
                         "Resent-Date:" WS date-time CRLF
                         ["Resent-To:" WS 1#address CRLF]
                         ["Resent-Cc:" WS 1#address CRLF]
                         ["Resent-Bcc:" WS 1#address CRLF]
                         ["Resent-Message-ID:" WS 1#address CRLF]

resent-orig     =       {("Resent-From:" WS mailbox CRLF /
                          {"Resent-Sender:" WS mailbox CRLF
                           "Resent-From:" WS 1#mailbox CRLF})
                         ["Resent-Reply-To:" WS 1#address CRLF]}

optional-field  =       field-name ":" WS unstructured

field-name      =       1*ftext

ftext           =       33..57 / 58..126        ; Any character except CTL,
                                                ;  SPACE, and ":".

obs-qp          =       "\" CHAR                ; CHAR is 0..127

obs-body        =       *(*998obs-text CRLF) *998obs-text

obs-unstruct    =       *(FWS / obs-text)

obs-text        =       obs-chars /
                        (obs-chars CR obs-chars) /
                        (obs-chars LF obs-chars)

obs-chars       =       text / 0                ; 0..127 except CR and LF

obs-date-time   =       [obs-day ","] 1*CFWS obs-date 1*CFWS obs-time

obs-day         =       *CFWS day *CFWS

obs-date        =       1*2DIGIT 1*CFWS month 1*CFWS 2*4DIGIT

obs-time        =       *CFWS (hour / obs-hour) 1*CFWS
                        (zone / obs-zone) *CFWS

obs-hour        =       2DIGIT *CFWS ":" *CFWS 2DIGIT *CFWS
                        [":" *CFWS 2DIGIT *CFWS]

obs-zone        =       "UT" / "GMT" /          ; Universal Time
                                                ; North American : UT
                        "EST" / "EDT" /         ; Eastern:  - 5/ - 4
                        "CST" / "CDT" /         ; Central:  - 6/ - 5
                        "MST" / "MDT" /         ; Mountain: - 7/ - 6
                        "PST" / "PDT" /         ; Pacific:  - 8/ - 7
                        ("A".."I" / "K".."Z")   ; Military zone

obs-mailbox     =       obs-addr-spec / [obs-phrase] route-addr

route-addr      =       *CFWS "<" [route] obs-addr-spec ">" *CFWS

route           =       *CFWS 1#("@" obs-domain) ":" *CFWS

obs-addr-spec   =       obs-local-part "@" obs-domain

obs-local-part  =       quoted-string / atom *("." atom)

obs-domain      =       atom *("." atom) / domain-literal

obs-fields      =       {[obs-trace]
                        obs-orig-date
                        obs-originator
                        *obs-destination
                        *obs-identifier
                        *obs-information
                        *obs-resent
                        *obs-optional}

obs-trace       =       {[obs-return CRLF]
                        1*obs-received}

obs-return      =       RETURN-PATH *CFWS ":" route-addr CRLF

obs-received    =       RECEIVED *CFWS ":"
                        ["from" 1*CFWS obs-domain]
                        ["by" 1*CFWS obs-domain]
                        ["via" 1*CFWS atom]
                        *("with" 1*CFWS atom) 
                        ["id" 1*CFWS atom ["@" obs-domain]]
                        ["for" 1*CFWS obs-addr-spec]
                        ";" obs-date-time CRLF

obs-orig-date   =       DATE *CFWS ":" obs-date-time CRLF

obs-originator  =       {(obs-from / obs sender)
                         [obs-reply-to]

obs-from        =       FROM *CFWS ":" obs-mailbox CRLF

obs-sender      =       {SENDER *CFWS ":" obs-mailbox CRLF
                         FROM *CFWS ":" 1#obs-mailbox CRLF}

obs-reply-to    =       REPLY-TO *CFWS ":" 1#obs-mailbox CRLF

obs-destination =       {[TO *CFWS ":" 1#address CRLF]
                         [CC *CFWS ":" 1#address CRLF]
                         [BCC *CFWS ":" 1#address CRLF]}

obs-identifier  =       {[MESSAGE-ID *CFWS ":" obs-msg-id CRLF]
                         [IN-REPLY-TO *CFWS ":" *(phrase / obs-msg-id) CRLF]
                         [REFERENCES *CFWS ":" *(phrase / obs-msg-id) CRLF]}

obs-msg-id      =       *CFWS "<" obs-addr-spec ">" *CFWS

obs-information =       {[SUBJECT *CFWS ":" obs-unstruct CRLF]
                         [COMMENTS *CFWS ":" obs-unstruct CRLF]
                         [KEYWORDS *CFWS ":" 1#phrase CRLF]}

obs-resent      =       {obs-resent-orig CRLF
                         RESENT-DATE *CFWS ":" obs-date-time CRLF
                         [RESENT-TO *CFWS ":" 1#address CRLF]
                         [RESENT-CC *CFWS ":" 1#address CRLF]
                         [RESENT-BCC *CFWS ":" 1#address CRLF]
                         [RESENT-MSG-ID *CFWS ":" 1#address CRLF]}

obs-resent-orig =       {(RESENT-FROM *CFWS ":" obs-mailbox CRLF /
                          {RESENT-SENDER *CFWS ":" obs-mailbox CRLF
                           RESENT-FROM *CFWS ":" 1#mailbox CRLF})
                         [RESENT-REPLY-TO *CFWS ":" 1#address CRLF]}

obs-optional    =       field-name *CFWS ":" obs-unstruct CRLF

DATE            =       ("D"/"d") ("A"/"a") ("T"/"t") ("E"/"e")

SENDER          =       ("S"/"s") ("E"/"e") ("N"/"n") ("D"/"d") ("E"/"e")
                        (/"R"/"r")

FROM            =       ("F"/"f") ("R"/"r") ("O"/"o") ("M"/"m")

REPLY-TO        =       ("R"/"r") ("E"/"e") ("P"/"p") ("L"/"l") ("Y"/"y")
                        "-" ("T"/"t") ("O"/"o")

TO              =       ("T"/"t") ("O"/"o")

CC              =       ("C"/"c") ("C"/"c")

BCC             =       ("B"/"b") ("C"/"c") ("C"/"c")

MESSAGE-ID      =       ("M"/"m") ("E"/"e") ("S"/"s") ("S"/"s") ("A"/"a")
                        ("G"/"g") ("E"/"e") "-" ("I"/"i") ("D"/"d")

IN-REPLY-TO     =       ("I"/"i") ("N"/"n") "-" ("R"/"r") ("E"/"e") ("P"/"p")
                        ("L"/"l") ("Y"/"y") "-" ("T"/"t") ("O"/"o")

REFERENCES      =       ("R"/"r") ("E"/"e") ("F"/"f") ("E"/"e") ("R"/"r")
                        ("E"/"e") ("N"/"n") ("C"/"c") ("E"/"e") ("S"/"s")

SUBJECT         =       ("S"/"s") ("U"/"u") ("B"/"b") ("J"/"j") ("E"/"e")
                        ("C"/"c") ("T"/"t")

COMMENTS        =       ("C"/"c") ("O"/"o") ("M"/"m") ("M"/"m") ("E"/"e")
                        ("N"/"n") ("T"/"t") ("S"/"s")

KEYWORDS        =       ("K"/"k") ("E"/"e") ("Y"/"y") ("W"/"w") ("O"/"o")
                        ("R"/"r") ("D"/"d") ("S"/"s")

RETURN-PATH     =       ("R"/"r") ("E"/"e") ("T"/"t") ("U"/"u") ("R"/"r")
                        ("N"/"n") "-" ("P"/"p") ("A"/"a") ("T"/"t") ("H"/"h")

RECEIVED        =       ("R"/"r") ("E"/"e") ("C"/"c") ("E"/"e") ("I"/"i")
                        ("V"/"v") ("E"/"e") ("D"/"d")

RESENT-DATE     =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("D"/"d") ("A"/"a") ("T"/"t") ("E"/"e")

RESENT-TO       =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("T"/"t") ("O"/"o")

RESENT-CC       =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("C"/"c") ("C"/"c")

RESENT-BCC      =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("B"/"b") ("C"/"c") ("C"/"c")

RESENT-MSG-ID   =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("M"/"m") ("E"/"e") ("S"/"s") ("S"/"s")
                        ("A"/"a") ("G"/"g") ("E"/"e") "-" ("I"/"i") ("D"/"d")

RESENT-SENDER   =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("S"/"s") ("E"/"e") ("N"/"n") ("D"/"d")
                        ("E"/"e") (/"R"/"r")

RESENT-FROM     =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("F"/"f") ("R"/"r") ("O"/"o") ("M"/"m")

RESENT-REPLY-TO =       ("R"/"r") ("E"/"e") ("S"/"s") ("E"/"e") ("N"/"n")
                        ("T"/"t") "-" ("R"/"r") ("E"/"e") ("P"/"p") ("L"/"l")
                        ("Y"/"y") "-" ("T"/"t") ("O"/"o")

