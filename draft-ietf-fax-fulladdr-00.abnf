gstn-phone = ( global-phone / local-phone )

pstn-mbox = service-selector "=" gstn-phone
                 [ sub-addr-spec ] [post-sep post-dial]

local-phone = [ exit-code ] [ dial-number ]

exit-code = phone-string
                  ; this include anything needed to enable dialling, like
                  ; the digit to access outside line, the long distance
                  ; carrier access code, the access password to the service,
                  ; etc...


dial-number = phone-string
                  ; this is in many cases composed of different elements
                  ; like the local phone number, the area code (if needed),
                  ; the international country code (id needed), etc...
      
phone-string = 1*( DTMF / pause / tonewait / written-sep )

DTMF = ( DIGIT / "#" / "*" / "A" / "B" / "C" / "D" )
                     ; special DTMF codes like "*", "#", "A", "B",
                     ; "C", "D" are defined in [6]
                     ; Important Note: this is NOT the alpha to digit
                     ; convention in use in some countries.

pause = ( "p" / "P" )

tonewait = ( "w" / "W" )


sub-addr-spec = [ isdn-sep sub-addr ]

isdn-sep = "/ISUB="
                 ; note that "/ISUB=" is case INSENSITIVE

sub-addr = 1*( DIGIT / written-sep )


post-sep =  "/POSTD="
                  ; note that "/POSTD=" is case INSENSITIVE

post-dial = phone-string

pstn-recipient = [ recipient-name ]
                       [ 1*( recipient-qualifier ) ]

recipient-name = ( personal-type1 / personal-type2 )

personal-type1 = "/PN="
                       [ givenname "." ]
                       [ initials "." ]
                       surname
                       ; this is compatible with ITU F.401 [9] [10]

personal-type2 = [ "/G=" givenname ]
                       [ "/I=" initials ]
                       "/S=" surname
                       ; this is compatible with ITU F.401 [9] [10]


surname = printablestring

givenname = 1*( DIGIT /  ALPHA / SP / "'" / "+" /
                    "," / "-" / "/" / ":" / "=" / "?" )

initials = 1*ALPHA


recipient-qualifier = ( qualif-type1 / qualif-type2 )

qualif-type2 = "/" x400-label "=" printablestring
      
x400-label = ( "GQ" / "O" / "OU1" / "OU2" / "OU3" / "OU4" /
                     "P" / "A" / "C" / "PD-PN" / "PD-EA" / "PD-ED" /
                     "PD-OFN" / "PD-OF" / "PD-O" / "PD-S" / "PD-A" /
                     "PD-U" / "PD-L" / "PD-R" / "PD-B" / "PD-PC" /
                     "PD-SN" / "PD-C" / "X121" / "E164" / "PSAP" /
                     "N-ID" / "T-ID" / "T-TY" / "DD." dd-key )
                    ; this is the full list of existing text labels
                    ; according to ITU F.401 [10] [11]. "PD-x" labels 
                    ; provide a set of predefined attributes for physical
                    ; delivery which can be also useful for message
                    ; recipient identification.

dd-key = 1*( DIGIT / ALPHA / "-" )
               ; note that SP (space) is not allowed in dd-key

string = PCHAR
               ; note that printable characters are %x20-7E

printablestring = 1*( DIGIT / ALPHA / SP / 
                            "'" / "(" / ")" / "+" / "," / "-" / 
                            "." / "/" / ":" / "=" / "?" )
                        ; this definition comes from ITU F.401 [9]
                        ; and MIXER [3]

FAX=0103940226338

XYZ=+49.81.7856345/ISUB=1234

FAX=+1-202-455-7622/T33S=8745/PostD=p1w7005393w373

FAX=003940226338/Isub=9823/T33S=4312

FAX=9p040p22.63.38/t33s=4312

XYZ=+1.202.344-5723


FAX=0p0134782289/T33s=3345

FAX=/postd=w6743w99p51


FAX=+12023445723

XYZ=+3940226338/PN=Mark.Collins

FAX=9p040p22.63.38/t33s=4312/PD-ON=T2-33A/OU1=Q-C


FAX=+12023445723@faxgw

FAX=+39-40-226338/PN=Mark.Collins@faxgw

FAX=9p040p226338/T33S=4312/PD-ON=T2-33A/OU1=Q-C@faxgw

FAX=+3940226338/PN=Mark.Collins/@faxgw

S=Allocchio;G=Claudio;
