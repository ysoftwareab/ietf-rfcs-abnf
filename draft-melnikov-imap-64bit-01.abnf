body-extension  =/ number64
             ; Alexey: I am not sure if this change is absolutely needed!

body-fld-lines  = number64

body-fld-octets = number64

fetch-att       =/ "BODY" section ["<" number64 "." nz-number64 ">"] /
                     "BODY.PEEK" section ["<" number64 "." nz-number64 ">"]

literal         = "{" number64 ["+"] "}" CRLF *CHAR8
                    ; number64 represents the number of CHAR8s.
                    ; NOTE: "+" can only present when LITERAL+/LITERAL-
                    ; is also advertised

literal8        = "~{" number64 ["+"] "}" CRLF *OCTET
                    ;; Updating RFC 4466 version.
                    ;; A string that might contain NULs.
                    ;; <number> represents the number of OCTETs
                    ;; in the response string.
                    ;; The "+" is only allowed when both LITERAL+/LITERAL-
                    ;; and BINARY extensions are supported by the server
                    ;; [RFC2088] - needs to be updated on LITERAL-
                    ;; publication

msg-att-static  =/ "RFC822.SIZE" SP number64

search-key      =/ "LARGER" SP number64 / "SMALLER" SP number64

number64    = 1*DIGIT
                ; Unsigned 63-bit integer
                ; (0 <= n <= 9,223,372,036,854,775,807)

nz-number64 = digit-nz *DIGIT
                ; Unsigned 63-bit integer
                ; (0 < n <= 9,223,372,036,854,775,807)


CHAR8   = <defined in RFC 3501>

