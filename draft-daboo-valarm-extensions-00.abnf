alarmcext  = "BEGIN" ":" "VALARM" CRLF
                     alarmprop
                     "END" ":" "VALARM" CRLF

alarmprop  = *(

                   ; the following are REQUIRED,
                   ; but MUST NOT occur more than once

                   action / trigger /

                   ; one set of action properties MUST be
                   ; present and MUST match the action specified
                   ; in the ACTION property

                   actionprops /

                   ; the following is OPTIONAL,
                   ; and MAY occur more than once

                   x-prop / iana-prop

                   )

actionprops = audiopropext / disppropext / emailpropext

audiopropext  = *(

                   ; 'duration' and 'repeat' are both OPTIONAL,
                   ; and MUST NOT occur more than once each,
                   ; but if one occurs, so MUST the other

                   duration / repeat /

                   ; the following is OPTIONAL,
                   ; but MUST NOT occur more than once

                   attach

                   )

disppropext = *(
                   ; the following are REQUIRED,
                   ; but MUST NOT occur more than once

                   description /

                   ; 'duration' and 'repeat' are both OPTIONAL,
                   ; and MUST NOT occur more than once each,
                   ; but if one occurs, so MUST the other

                   duration / repeat

                   )

emailpropext = *(

                   ; the following are all REQUIRED,
                   ; but MUST NOT occur more than once

                   description / summary /

                   ; the following is REQUIRED,
                   ; and MAY occur more than once

                   attendee /

                   ; 'duration' and 'repeat' are both OPTIONAL,
                   ; and MUST NOT occur more than once each,
                   ; but if one occurs, so MUST the other

                   duration / repeat

                   )



uriprop     = *(

                       ; the following are REQUIRED,
                       ; but MUST NOT occur more than once

                       description / uri /

                       ; 'duration' and 'repeat' are both OPTIONAL,
                       ; and MUST NOT occur more than once each,
                       ; but if one occurs, so MUST the other

                       duration / repeat

                       )
alarm-agent     = "ALARM-AGENT" alarmagentparam ":"
                        alarmagentvalue CRLF

alarmagentparam = *(

                        ; the following is OPTIONAL,
                        ; but MUST NOT occur more than once

                        (";" uriparam) /

                        ; the following is OPTIONAL,
                        ; and MAY occur more than once

                        (";" other-param)

                        )

alarmagentvalue = "SERVER" / "CLIENT" / "BOTH" / "NONE"
                        / iana-token / x-name

uriparam   = "URI" "=" DQUOTE uri DQUOTE

statvalue-alarm = "ACTIVE"    ; Indicates alarm is active and
                                         ; waiting to be triggered.
                           / "CANCELLED" ; Indicates alarm is inactive
                                         ; and won't be triggered.
                           / "COMPLETED" ; Indicates alarm has triggered
                                         ; and been dismissed.
                           / "SNOOZED"   ; Indicates alarm has triggered
                                         ; and been snoozed.
last-triggered = "LAST-TRIGGERED" lasttrigparam ":" datetime CRLF

lasttrigparam  = *(

                        ; the following is OPTIONAL,
                        ; and MAY occur more than once

                        (";" other-param)
                        )

snooze-until      = "SNOOZE-UNTIL" snoozeuntilparam ":"
                            datetime CRLF

snoozeuntilparam  = *(

                        ; the following is OPTIONAL,
                        ; and MAY occur more than once

                        (";" other-param)
                        )

uid-ref       = "UID-REFERENCE" uidrefparam ":" text CRLF

uidrefparam   = *(";" other-param)

