ldif-file            = 0,1*(version-spec SEP) ldif-record *(SEP ldif-record)

version-spec         = "version:" *SPACE version-number
version-number       = 1*DIGIT  ; version-number must be "1" for the
                                   ; LDIF format described in this document.
ldif-record          = dn-spec SEP ldif-content SEP

dn-spec              = "dn:" *SPACE dn
dn                   = <a distinguished name, as defined in RFC 1779 [3]>
rdn                  = <a relative distinguished name, as defined in RFC
                          1779 [3]>

ldif-content         = 1*(attrval-spec SEP) / (changerecord SEP)
attrval-spec         = attrname (":" *SPACE value) /
                          ("::" *SPACE base64-value) /
                          (":<" *SPACE file-spec)
file-spec            = <a file name, as defined by local operating
                           system conventions>
attrname             = <an attribute name, as defined in
                           draft-ietf-asid-ldapv3-attributes-03.txt [4]
                           Attribute names may not contain a colon ":">
value                = 1*safe-initval *safe
safe                 = <ASCII values 040 - 0176 octal (32 - 126 decimal)>
safe-initval         = <ASCII values 040 - 0176 octal (32 - 126 decimal),
                          excluding colon (":", ASCII 58 decimal), SPACE, and
                          less-than ("<" , ASCII 60 decimal)>
base64-value         = <base-64-encoded value, as defined in RFC 1521 [5]>

changerecord         = change-add / change-delete / change-modify /
                          change-moddn
change-add           = "changetype:" *SPACE "add" 1*(SEP attrval-spec)
change-delete        = "changetype:" *SPACE "delete"
change-moddn         = "changetype:" *SPACE ("modrdn" / "moddn") SEP
                          "newrdn:" *SPACE rdn SEP
                          "deleteoldrdn:" *SPACE ("0" / "1")
                          0,1*(SEP "newsuperior:" *SPACE dn)
change-modify        = "changetype:" *SPACE "modify" 1*(SEP mod-spec)
mod-spec             = mod-add-spec / mod-delete-spec / mod-replace-spec
mod-add-spec         = "add:" *SPACE attrname 1*(SEP attrval-spec) SEP "-"
mod-delete-spec      = "delete:" *SPACE attrname *(SEP attrval-spec) SEP "-"
mod-replace-spec     = "replace:" *SPACE attrname *(SEP attrval-spec) SEP
                          "-"
SPACE                = <ASCII SP, space>
SEP                  = (CR LF / LF)
CR                   = <ASCII CR, carriage return>
LF                   = <ASCII LF, line feed>
DIGIT                = <any ASCII decimal digit (60 - 71 decimal) >


