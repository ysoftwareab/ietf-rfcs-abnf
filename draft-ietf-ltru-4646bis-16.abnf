Language-Tag  = langtag             ; normal language tags
               / privateuse          ; private use tag
               / grandfathered       ; grandfathered tags

langtag       = language
                 ["-" script]
                 ["-" region]
                 *("-" variant)
                 *("-" extension)
                 ["-" privateuse]

language      = 2*3ALPHA            ; shortest ISO 639 code
                 ["-" extlang]       ; sometimes followed by
                                     ;   extended language subtags
               / 4ALPHA              ; or reserved for future use
               / 5*8ALPHA            ; or registered language subtag
extlang       = 3ALPHA              ; selected ISO 639 codes
                 *2("-" 3ALPHA)      ; permanently reserved

script        = 4ALPHA              ; ISO 15924 code

region        = 2ALPHA              ; ISO 3166-1 code
               / 3DIGIT              ; UN M.49 code

variant       = 5*8alphanum         ; registered variants
               / (DIGIT 3alphanum)

extension     = singleton 1*("-" (2*8alphanum))

                                     ; Single alphanumerics
                                     ; "x" reserved for private use
singleton     = %x41-57             ; a - w
               / %x59-5A             ; y - z
               / %x61-77             ; A - W
               / %x79-7A             ; Y - Z
               / DIGIT               ; 0 - 9


privateuse    = "x" 1*("-" (1*8alphanum))

grandfathered = irregular           ; non-redundant tags registered
               / regular             ;   during the RFC 3066 era


irregular     = "en-GB-oed"         ; irregular tags do not match
               / "i-ami"             ; the 'langtag' production and
               / "i-bnn"             ; would not otherwise be
               / "i-default"         ; considered 'well-formed'
               / "i-enochian"        ; These tags are all valid,
               / "i-hak"             ; but most are deprecated
               / "i-klingon"         ; in favor of more modern
               / "i-lux"             ; subtags or subtag
               / "i-mingo"           ; combination
               / "i-navajo"
               / "i-pwn"
               / "i-tao"
               / "i-tay"
               / "i-tsu"
               / "sgn-BE-FR"
               / "sgn-BE-NL"
               / "sgn-CH-DE"
regular       = "art-lojban"        ; these tags match the 'langtag'
               / "cel-gaulish"       ; production, but their subtags
               / "no-bok"            ; are not extended language
               / "no-nyn"            ; or variant subtags: their meaning
               / "zh-guoyu"          ; is defined by their registration
               / "zh-hakka"          ; and all of these are deprecated
               / "zh-min"            ; in favor of a more modern
               / "zh-min-nan"        ; subtag or sequence of subtags
               / "zh-xiang"

alphanum      = (ALPHA / DIGIT)     ; letters and numbers

                        Figure 1: Language Tag ABNF

   All subtags have a maximum length of eight characters and whitespace
   is not permitted in a language tag.  There is a subtlety in the ABNF
   production 'variant': variants starting with a digit MAY be four
   characters long, while those starting with a letter MUST be at least
   five characters long.  For examples of language tags, see Appendix B.

   Although [RFC5234] refers to octets, the language tags described in
   this document are sequences of characters from the US-ASCII [ISO646]
   repertoire.  Language tags MAY be used in documents and applications
   that use other encodings, so long as these encompass the relevant
   part of the US-ASCII repertoire.  An example of this would be an XML
   document that uses the UTF-16LE [RFC2781] encoding of [Unicode].

obs-language-tag = primary-subtag *( "-" subtag )
primary-subtag   = 1*8ALPHA
subtag           = 1*8(ALPHA / DIGIT)

                  Figure 2: RFC 3066 Language Tag Syntax

registry   = record *("%%" CRLF record)
record     = 1*( field-name *SP ":" *SP field-body CRLF )
field-name = (ALPHA / DIGIT) [*(ALPHA / DIGIT / "-") (ALPHA / DIGIT)]
field-body = *([[*SP CRLF] 1*SP] 1*CHARS)
CHARS      = (%x21-10FFFF)      ; Unicode code points

                      Figure 3: Registry Format ABNF

language      =  8 (longest registered value)
script        =  5 (if not suppressed: see Section 4.1)
region        =  4 (UN M.49; ISO 3166-1 requires 3)
variant1      =  9 (needs 'language' as a prefix)
variant2      =  9 (needs 'language-variant1' as a prefix)

total         = 35 characters

              Figure 7: Derivation of the Limit on Tag Length

