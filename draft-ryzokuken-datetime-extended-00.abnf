date-year      = 4DIGIT / ("+" / "-") 6DIGIT
date-month     = 2DIGIT ; 01-12
date-mday      = 2DIGIT ; 01-28, 01-29, 01-30, 01-31 based on month/year
date-full      = date-year "-" date-month "-" date-mday

time-hour      = 2DIGIT ; 00-23
time-minute    = 2DIGIT ; 00-59
time-second    = 2DIGIT ; 00-58, 00-59, 00-60 based on leap second rules
time-secfrac   = "." 1*DIGIT
time-partial   = time-hour ":" time-minute ":" time-second [time-secfrac]
time-numoffset = ("+" / "-") time-partial
time-offset    = "Z" / time-numoffset
time-full      = time-partial time-offset

time-zone-char = ALPHA / "." / "_"
time-zone-part = time-zone-char *13(time-zone-char / DIGIT / "-" / "+") ; but not "." or ".."
time-zone-id   = time-zone-part *("/" time-zone-part)
time-zone      = "[" time-zone-id "]"

calendar-part  = 3*8(ALPHA / DIGIT)
calendar-id    = calendar-part *("-" calendar-part)
calendar       = "[c=" calendar-id "]"

suffix-part    = "[" 1*ALPHA "=" 1*VCHAR "]"
suffix         = [time-zone] [calendar] *suffix-part

date-time      = date-full "T" time-full suffix

                                  Figure 1

      |  NOTE 1: Per [RFC2234] and ISO8601, the "T" and "Z" characters
      |  in this syntax may alternatively be lower case "t" or "z"
      |  respectively.

cent = year / 100;
