jid          = [ localpart "@" ] domainpart [ "/" resourcepart ]
localpart    = 1*1023(localbyte)
                     ;
                     ; a "localbyte" is a byte used to represent a
                     ; UTF-8 encoded Unicode code point that can be
                     ; contained in a string that conforms to the
                     ; "LocalpartIdentifierClass" profile of the
                     ; PRECIS IdentifierClass
                     ;
domainpart   = IP-literal / IPv4address / ifqdn
                     ;
                     ; the "IPv4address" and "IP-literal" rules are
                     ; defined in RFC 3986, and the first-match-wins
                     ; (a.k.a. "greedy") algorithm described therein
                     ; applies to the matching process
                     ;
                     ; note well that reuse of the IP-literal rule from
                     ; RFC 3986 implies that IPv6 addresses are enclosed
                     ; in square brackets (i.e., beginning with '[' and
                     ; ending with ']')
                     ;
ifqdn        = 1*1023(domainbyte)
                     ;
                     ; a "domainbyte" is a byte used to represent a
                     ; UTF-8 encoded Unicode code point that can be
                     ; contained in a string that conforms to RFC 5890
                     ;
resourcepart = 1*1023(resourcebyte)
                     ;
                     ; a "resourcebyte" is a byte used to represent a
                     ; UTF-8 encoded Unicode code point that can be
                     ; contained in a string that conforms to the
                     ; "ResourcepartFreeformClass" profile of the
                     ; PRECIS FreeformClass
                     ;

