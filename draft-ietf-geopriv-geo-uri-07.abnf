geo-URI       = geo-scheme ":" geo-path
geo-scheme    = "geo"
geo-path      = coordinates p
coordinates   = coord-a "," coord-b [ "," coord-c ]

coord-a       = num
coord-b       = num
coord-c       = num

p             = [ crsp ] [ uncp ] *parameter
crsp          = ";crs=" crslabel
crslabel      = "wgs84" / labeltext
uncp          = ";u=" uval
uval          = pnum
parameter     = ";" pname [ "=" pvalue ]
pname         = labeltext
pvalue        = 1*paramchar
paramchar     = p-unreserved / unreserved / pct-encoded

labeltext     = 1*( alphanum / "-" )
pnum          = 1*DIGIT [ "." 1*DIGIT ]
num           = [ "-" ] pnum
unreserved    = alphanum / mark
mark          = "-" / "_" / "." / "!" / "~" / "*" /
                             "'" / "(" / ")"
pct-encoded   = "%" HEXDIG HEXDIG
p-unreserved  = "[" / "]" / ":" / "&" / "+" / "$"
alphanum      = ALPHA / DIGIT

coord-a        = latitude
coord-b        = longitude
coord-c        = altitude

latitude       = [ "-" ] 1*2DIGIT [ "." 1*DIGIT ]
longitude      = [ "-" ] 1*3DIGIT [ "." 1*DIGIT ]
altitude       = [ "-" ] 1*DIGIT [ "." 1*DIGIT ]

xmlns="http://www.opengis.net/gml">
xmlns="http://www.opengis.net/gml">
