action = toss / fileinto / forward / bounce / reply / stop

address = string
             ;; any legal [IMAIL] address

any-of = "any-of" test-list

all-of = "all-of" test-list

block = "{" [WSP] commands [WSP] "}"
             ;; C-style block

bounce = bounce WSP string
             ;; string is the reason contained in the bounce message.

control-structure = if

command = ( action ";" ) / block / control-structure

commands = *([WSP] command [WSP])

comment = "#" *CHAR CRLF

comparator = octet

fileinto = "fileinto" WSP string
             ;; string is a mailbox; semantics are defined by the
             ;; underlying mail system

forward = "forward" WSP address

if = "if" WSP test WSP command [ else command ]
             ;; Commands are typically blocks.

header = "header" WSP string-list WSP match-keyword WSP string-list

match-keyword = ("contains" / "matches" / "is") ["-" comparator]

multi-line = "text:" [WSP] CRLF "." CRLF
             ;; Note when used,
             ;; a leading ".." on a line is mapped to ".".

number = 1*DIGIT [QUANTIFIER]
             ;; quantifier is a multiplier (or bit shift)
QUANTIFIER = "K" / "M" / "G"
             ;; K == 2^10; M == 2^20; G = 2^30

quoted-string = <"> *CHAR <">
             ;; \" inside a string maps to "
             ;; \\ inside a string maps to \
             ;; All other escape sequences must be preserved.
             ;; Note that newlines and other weird characters
             ;; are all allowed strings.

size = "size" WSP ( "over" / "under" ) WSP number

stop = "stop"

string = quoted-string / multi-line

string-list = "(" [WSP] *(string WSP) string [WSP] ")" / string
             ;; if there is only a single string, the parens are optional

test = [WSP] (any-of / all-of / exists / false / header /
         not / size) [WSP]

test-list = [WSP] "(" [WSP] *(condition [WSP] "," [WSP])
         condition [WSP] ")" [WSP]

WSP = 1*(SPACE / CRLF / tab) / comment
             ;; just whitespace.  anyplace this is allowed, a comment is
             ;; as well


