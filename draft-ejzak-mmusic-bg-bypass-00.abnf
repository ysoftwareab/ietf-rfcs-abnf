visited-realm         = "visited-realm" ":" realm-number SP 
                           realm SP 
                           nettype SP                ;from RFC 4566 
                           addrtype SP               ;from RFC 4566 
                           connection-address SP     ;from RFC 4566 
                           port                      ;from RFC 4566 
                           [SP rtcp-port [SP rtcp-address]] 
                           [SP coordinates] 
                           [SP delay] 
                           [SP loss] 
                           [SP temp-gruu] 
                           [SP credentials] 
                           *(SP extension-att-name SP 
                                extension-att-value) 
    
secondary-realm       = "secondary-realm" ":" realm-number SP 
                           realm SP 
                           nettype SP                ;from RFC 4566 
                           addrtype SP               ;from RFC 4566 
                           connection-address SP     ;from RFC 4566 
                           port                      ;from RFC 4566 
                           [SP rtcp-port [SP rtcp-address]] 
                           [SP coordinates] 
                           [SP delay] 
                           [SP loss] 
                           [SP temp-gruu] 
                           [SP credentials] 
                           *(SP extension-att-name SP 
                                extension-att-value) 
    
realm-number          = 1*DIGIT 
realm                 = non-ws-string             ;from RFC 4566 
rtcp-port             = "rtcp-port" SP port 
rtcp-address          = "rtcp-address" SP connection-address 
coordinates           = "coordinates" SP latitude "," longitude 
latitude              = [ "-" ] 1*2DIGIT [ "." *DIGIT ] 
longitude             = [ "-" ] 1*3DIGIT [ "." *DIGIT ] 
delay                 = "delay" SP delay-value 
delay-value           = 1*DIGIT 
loss                  = "loss" SP loss-value 
loss-value            = "-" 1*DIGIT ["." 1*DIGIT] 
temp-gruu             = "temp-gruu" SP SIP-URI    ;from RFC 3261 
credentials           = "credentials" SP credentials-value 
credentials-value     = non-ws-string 
extension-att-name    = token 
extension-att-value   = non-ws-string 
    
    

 
 
 
 
 
