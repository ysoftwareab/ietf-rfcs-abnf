presence-id     = word-pres ":" local-part "@" domain
im-id           = word-im ":" local-part "@" domain
local-part      = 1*( unreserved / escaped )
unreserved      = ALPHA / DIGIT / "!" / "$" / "&" / "'" / "*"
                        / "." / "+" / "-" / "/" / "=" / "?" / "_" / "~"
escaped         = "%" hex-char hex-char
hex-char        = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
                        / "a" / "b" / "c" / "d" / "e" / "f"
domain          = 1*domain-label *("." 1*domain-label)
domain-label    = 1*( unreserved / escaped )
word-pres       = %x70.72.65.73     ; "pres"
word-im         = %x69.6D           ; "im"
decimal-byte    = 1*3DIGIT
ALPHA           = <defined by RFC 2234 -- 'A'-'Z' / 'a'-'z'>
DIGIT           = <defined by RFC 2234 -- '0'-'9'>

hex4            = 1*4hex-char
hexseq          = hex4 *(":" hex4)
ip6-address     = hexseq / hexseq "::" [ hexseq ] / "::" [ hexseq ]
ip4-address     = "::" 1*1decimal-byte 3*3("." 1*1decimal-byte)
PRIM-command = request / response

generic-command = start-line
                             *command-header
                             CRLF
                             [ command-body ]

start-line = request-line / response-line

request-line = method
                            SP version
                            SP request-identifier
                            SP content-length
                            CRLF

method = "LOGIN"               ; Section 10.1
                          / "STARTTLS"            ; Section 10.2
                          / "LOGOUT"              ; Section 10.3
                          / "PING"                ; Section 10.4
                          / "VERIFYSERVER"        ; Section 10.5
                          / "SETACL"              ; Section 10.6.1
                          / "GETACL"              ; Section 10.6.2
                          / "SUBSCRIBE"           ; Section 11.1.1
                          / "UNSUBSCRIBE"         ; Section 11.1.2
                          / "CANCELSUBSCRIPTION"  ; Section 11.1.3
                          / "FETCH"               ; Section 11.1.4
                          / "PUBLISH"             ; Section 11.2.1
                          / "REMOVE"              ; Section 11.2.2
                          / "NOTIFY"              ; Section 11.3
                          / "SETCLASSTABLE"       ; Section 11.4.2
                          / "GETCLASSTABLE"       ; Section 11.4.3
                          / "STARTWATCHERNOTIFY"  ; Section 11.4.4
                          / "STOPWATCHERNOTIFY"   ; Section 11.4.5
                          / "WATCHERNOTIFY"       ; Section 11.4.6
                          / "LISTEN"              ; Section 12.1.1
                          / "SILENCE"             ; Section 12.1.2
                          / "SEND"                ; Section 12.2

version = "PP/1.0" / "IMP/1.0"

request-identifier = 1*[ALPHA / DIGIT] / "-"

content-length = 1*DIGIT


response-line = version
                            SP request-identifier
                            SP content-length
                            SP status-code
                            SP response-phrase
                            CRLF

command-header = (common-header
                              / presence-header
                              / im-header
                              )
                             CRLF

common-header = (from-header
                              / to-header
                              / auth-state-header
                              / SASL-mechanism-header
                              / redirect-header
                              / content-type-header
                              / server-address-header
                              / astrength-header
                              / user-agent-id-header
                              / max-content-length-header
                              / date-header
                              )
presence-header = (class-header
                              / tuple-id-header
                              / duration-header
                              / pi-type-header
                              / watcher-type-header
                              )

im-header = (message-id-header
                              / conversation-id-header
                              / reply-to-header
                              )


from-header = "From: " ( presence-id / im-id )

to-header = "To: " ( presence-id / im-id )

auth-state-header = "Auth-State: "
                               ( "init"
                               / "continue"
                               / "abort" )

SASL-mechanism-header = "SASL-Mech: " mechanisms
mechanisms = mechanism [ *(SP mechanism) ]
mechanism = 1*20(ALPHA / DIGIT / "-" / "_")

redirect-header = "Redirect: " address SP port
address = domain / ip4-address / ip6-address
port = 1*DIGIT

charset=UTF-8" for Instant Messaging commands, and
server-address-header = "Server-Address: "
                             ( ip4-address / ip6-address )

astrength-header = "AStrength: " astrength
astrength = "strong" / "medium" / "weak" / "none"

user-agent-id-header = "User-Agent-ID: " 1*(unreserved / escaped)

max-content-length-header = "Max-Content-Length: " 1*DIGIT

date-header = "Date: " date-time
                                     ; as defined in Section 15.5

class-header = "Class: " class [ SP class ]
class = 1*(unreserved / escaped)

tuple-id-header = "Tuple-ID: " 1*(unreserved / escaped)

duration-header = "Duration: " 1*DIGIT

pi-type-header = "PI-Type: "
                              ( "leased"
                              / "permanent"
                              / "renew"
                              / "revert")
watcher-type-header = "Watcher-Type: " ("fetch" / "subscribe")


message-id-header = "Message-ID" 1*(DIGIT / ALPHA) ": " im-id

conversation-id-header = "Conversation-ID: " 1*(unreserved / escaped)

reply-to-header = "Reply-To: " im-id


date-time = [day ","] date time
day = "Mon" / "Tue" / "Wed" / "Thu" /  "Fri"
                   / "Sat" / "Sun"
date = 1*2DIGIT month 4DIGIT
month = "Jan" / "Feb" / "Mar" / "Apr" / "May"
                   / "Jun" / "Jul" / "Aug" / "Sep" / "Oct"
                   / "Nov" / "Dec"
time = hour zone
hour = 2DIGIT ":" 2DIGIT [":" 2DIGIT]
zone = ("+" / "-") 4DIGIT ; hours+min (HHMM)

date="Thu, 5 Sep 2000 15:45:45 -0500">
date="Thu, 5 Sep 2000 16:30:28 -0500">
