feedback-report = *( feedback-type / user-agent / version )
                   opt-fields-once
                   *( opt-fields-many )
                   *( ext-field )

feedback-type = "Feedback-Type:" [CFWS] token [CFWS] CRLF
     ; the "token" must be a registered feedback type as
     ; described elsewhere in this document

user-agent = "User-Agent:" [CFWS] product *( CFWS product ) [CFWS] CRLF

version = "Version:" [CFWS] %x31-%x39 *DIGIT [CFWS] CRLF
     ; as described above

opt-fields-once = [ arrival-date ]
                   [ incidents ]
                   [ original-envelope-id ]
                   [ original-mail-from ]
                   [ reporting-mta ]
                   [ source-ip ]

arrival-date = "Arrival-Date:" [CFWS] date-time CRLF

incidents = "Incidents:" [CFWS] 1*DIGIT [CFWS] CRLF
           ; must be a 32-bit unsigned integer

original-envelope-id = "Original-Envelope-Id:" [CFWS]
                        envelope-id [CFWS] CRLF

original-mail-from = "Original-Mail-From:" [CFWS]
                      reverse-path [CFWS] CRLF

reporting-mta = "Reporting-MTA:" [CFWS] mta-name-type [CFWS] ";" [CFWS]
                 mta-name [CFWS] CRLF

source-ip = "Source-IP:" [CFWS]
             ( IPv4-address-literal /
               IPv6-address-literal ) [CFWS] CRLF

opt-fields-many = [ authres-header ]
                   [ original-rcpt-to ]
                   [ reported-domain ]
                   [ reported-uri ]

original-rcpt-to = "Original-Rcpt-To:" [CFWS]
                    forward-path [CFWS] CRLF

reported-domain = "Reported-Domain:" [CFWS]
                   domain [CFWS] CRLF

reported-uri = "Reported-URI:" [CFWS] URI [CFWS] CRLF

ext-field = field-name ":" unstructured

   A set of fields satisfying this ABNF may appear in the transmitted
   message in any order.

   "CRLF" and "DIGIT" are imported from [ABNF].

   "token" is imported from [MIME].
   "product" is imported from [HTTP].

   "field-name", "unstructured", "CFWS", "date-time" and "domain" are
   imported from [MAIL].

   "envelope-id", "mta-name-type" and "mta-name" are imported from
   [DSN].

   "reverse-path", "forward-path", "local-part", "IPv4-address-literal"
   and "IPv6-address-literal" are imported from [SMTP].

   "URI" is imported from [URI].

   "authres-header" is imported from [AUTH-RESULTS].

   "ext-field" refers to extension fields, which are discussed in
   Section 5.
boundary="part1_13d.2e68ed54_boundary"

boundary="part1_13d.2e68ed54_boundary"

spf=fail smtp.mail=somespammer@example.com
