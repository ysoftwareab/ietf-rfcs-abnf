authres = 1*( version ":" authserv-id ":" methodspec
                            ":" propspec )
              ; relays a single unit of authentication results
              ; information


version = 1*DIGIT
           ; indicates which version of this specification is in use;
           ; this specification is version "1"; the absence of a version
           ; implies this version of the specification


authserv-id = dot-atom-text
                 ; see below for a description of this element;
                 ; "dot-atom-text" is defined in section 3.2.4 of [MAIL]


methodspec = method "=" result
                 ; indicates which authentication method was evaluated


propspec = ptype "." property "=" pvalue
               ; an indication of which property of the message
               ; was evaluated by the authentication scheme being
               ; applied to yield the reported result


method = token [ "/" version ]
             ; a method indicates which method's result is
             ; is represented by "result", and is one of the methods
             ; explicitly defined as valid in this document
             ; or is an extension method as defined below


result = token
             ; an indication of the results of the attempt to
             ; authenticate the message; see below for details
ptype = "smtp" / "header" / "body" / "policy"
            ; indicates whether the property being evaluated was
            ; a parameter to an [SMTP] command, or was a value taken
            ; from a message header field, or was some property of
            ; the message body, or some other property evaluated by
            ; the receiving MTA


property = token
              ; if "ptype" is "smtp", this indicates which [SMTP]
              ; command provided the value which was evaluated by the
              ; authentication scheme being applied; if "ptype" is
              ; "header", this indicates from which header field the
              ; value being evaluated was extracted; if "ptype" is
              ; "body", this indicates the offset into the body at which
              ; content of interest was detected; if "ptype" is "policy"
              ; then this indicates the name of the policy which caused
              ; this header field to be added (see below)


pvalue = token / addr-spec
            ; the value extracted from the message property defined
            ; by the "ptype.property" construction; if the value
            ; identifies a mailbox, then it is an "addr-spec"
            ; as defined in section 3.4 of [MAIL];

