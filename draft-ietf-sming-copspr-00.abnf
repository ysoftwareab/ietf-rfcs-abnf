copsprStatement         = copsprKeyword *1(sep lcIdentifier) optsep 
                             "{" stmtsep 
                                 *1(oidStatement stmtsep) 
                                 *1(clienttypeStatement stmtsep) 
                                 *(nodeStatement stmtsep) 
                                 *(prcStatement stmtsep) 
                                 *(groupStatement stmtsep) 
                                 *(complianceStatement stmtsep) 
  
 
 
                                 *1(statusStatement stmtsep) 
                                 descriptionStatement stmtsep 
                                 *1(referenceStatement stmtsep) 
                             "}" optsep ";" 
    
oidStatement            = oidKeyword sep objectIdentifier optsep ";" 
    
objectIdentifier        = (qlcIdentifier / subid) *127("." subid) 
    
subid                   = decimalNumber 
    
clienttypeStatement     = clienttypeKeyword optsep 
                             "{" stmtsep 
                                 allKeyword / 
                                 categoryId *("," categoryId) 
                             "}" optsep ";" 
    
categoryId              = identifier optsep "(" number ")" 
    
nodeStatement           = nodeKeyword sep lcIdentifier optsep 
                             "{" stmtsep 
                                 oidStatement stmtsep 
                                 *1(representsStatement stmtsep) 
                                 *1(statusStatement stmtsep) 
                                 *1(descriptionStatement stmtsep) 
                                 *1(referenceStatement stmtsep) 
                             "}" optsep ";" 
    
representsStatement     = representsKeyword sep 
                             qucIdentifier optsep ";" 
    
prcStatement            = prcKeyword sep lcIdentifier optsep 
                             "{" stmtsep 
                                 oidStatement stmtsep 
                                 1*(implementsStatement stmtsep) 
                                 *1(statusStatement stmtsep) 
                                 descriptionStatement stmtsep 
                                 *1(referenceStatement stmtsep) 
                                 pibaccessStatement stmtsep 
                                 *1(piberrorsStatement stmtsep) 
                                 *1(anyIndexStatement stmtsep) 
                             "}" optsep ";" 
 
    
implementsStatement     = implementsKeyword sep identifier optsep 
                             "{" stmtsep 
                                 1*(implobjectStatement stmtsep) 
                             "}" optsep ";" 
    
implobjectStatement     = objectKeyword sep  
                             attributeIdentifier  
                             *1( optsep 
                                 "{" optsep 
  
 
 
                                     pibreferenceStatement optsep / 
                                     pibtagStatement optsep 
                                 "}" 
                             )  
                             optsep ";" 
    
pibreferenceStatement   = pibreferenceKeyword sep  
                             lcIdentifier optsep ";" 
    
pibtagStatement         = pibtagKeyword sep  
                             lcIdentifier optsep ";" 
    
pibaccessStatement      = pibaccessKeyword sep  
                             pibaccess optsep *1("," number) 
                             optsep ";" 
    
pibaccess               = installonlyKeyword / 
                             notifyonlyKeyword / 
                             installnotifyKeyword / 
                             reportonlyKeyword 
    
piberrorsStatement      = installerrorsKeyword sep 
                             "{" stmtsep 
                                 piberror optsep *("," piberror) 
                             "}" optsep ";" 
    
piberror                = identifier optsep "(" number ")" 
    
anyIndexStatement       = pibindexStatement / 
                             augmentsStatement / 
                             extendsStatement 
    
pibindexStatement       = pibindexKeyword sep qlcIdentifier 
                             optsep ";" 
    
augmentsStatement       = augmentsKeyword sep qlcIdentifier 
                             optsep ";" 
    
extendsStatement        = extendsKeyword sep qlcIdentifier 
                             optsep ";" 
    
groupStatement          = groupKeyword sep lcIdentifier optsep 
                             "{" stmtsep 
                                 oidStatement stmtsep 
                                 membersStatement stmtsep 
                                 *1(statusStatement stmtsep) 
                                 descriptionStatement stmtsep 
                                 *1(referenceStatement stmtsep) 
                             "}" optsep ";" 
    
membersStatement        = membersKeyword optsep "(" optsep 
                             qlcIdentifierList optsep 
                             ")" optsep ";" 
  
 
 
    
complianceStatement     = complianceKeyword sep lcIdentifier optsep 
                             "{" stmtsep 
                                 oidStatement stmtsep 
                                 *1(statusStatement stmtsep) 
                                 descriptionStatement stmtsep 
                                 *1(referenceStatement stmtsep) 
                                 *1(mandatoryStatement stmtsep) 
                                 *(optionalStatement stmtsep) 
                                 *(refineStatement stmtsep) 
                             "}" optsep ";" 
    
mandatoryStatement      = mandatoryKeyword optsep "(" optsep 
                             qlcIdentifierList optsep 
                             ")" optsep ";" 
    
optionalStatement       = optionalKeyword sep qlcIdentifier optsep 
                             "{" stmtsep 
                                 descriptionStatement stmtsep 
                             "}" optsep ";" 
    
refineStatement         = refineKeyword sep qlcIdentifier optsep "{" 
                                 *1(installtypeStatement stmtsep) 
                                 *1(pibminaccessStatement stmtsep) 
                                 descriptionStatement stmtsep 
                             "}" optsep ";" 
    
installtypeStatement    = installtypeKeyword sep 
                             (refinedBaseType / refinedType) 
                             optsep ";" 
    
pibminaccessStatement   = pibminaccessKeyword sep pibminaccess 
                             optsep ";" 
    
pibminaccess            = noaccessKeyword / 
                             installKeyword / 
                             notifyKeyword / 
                             installnotifyKeyword 
copsprKeyword       =  %x63 %x6f %x70 %x73 %x70 %x72 
oidKeyword          =  %x6f %x69 %x64 
clienttypeKeyword   =  %x63 %x6c %x69 %x65 %x6e %x74 %x74 %x79 %x70  
                          %x65 
allKeyWord          =  %x63 %x6c %x6c 
nodeKeyword         =  %x6e %x6f %x64 %x65 
representsKeyword   =  %x72 %x65 %x70 %x72 %x65 %x73 %x65 %x6e %x74 
                          %x73 
prcKeyword          =  %x70 %x72 %x63 
implementsKeyword   =  %x69 %x6d %x70 %x6c %x65 %x6d %x65 %x6e %x74 
                          %x73 
  
 
 
objectKeyword       =  %x6f %x62 %x6a %x65 %x63 %x74 
pibreferenceKeyword =  %x70 %x69 %x62 %x72 %x65 %x66 %x65 %x72  %x65 
                          %x6E %x63 %x65 
pibtagKeyword       =  %x70 %x69 %x62 %x74 %x61 %x67  
pibaccessKeyword    =  %x70 %x69 %x62 %x61 %x63 %x63 %x65 %x73 %x73 
installonlyKeyword  =  %x69 %x6E %x73 %x74 %x61 %x6C %x6C %x6F %x6E 
                          %x6C %x79 
notifyonlyKeyword   =  %x6E %x6F %x74 %x69 %x66 %x79 %x6F %x6E %x6C 
                          %x79 
installnotifyKeyword=  %x69 %x6E %x73 %x74 %x61 %x6C %x6C %x6E %x6F 
                          %x74 %x69 %x66 %x79 
reportonlyKeyword   =  %x72 %x65 %x70 %x6F %x72 %x74 %x6F %x6E %x6C 
                          %x79 
installerrorsKeyword=  %x69 %x6E %x73 %x74 %x61 %x6C %x6C %x65 %x72 
                          %x72 %x6F %x72 %x73 
pibindexKeyword     =  %x70 %x69 %x62 %x69 %x6e %x64 %x65 %x78 
augmentsKeyword     =  %x61 %x75 %x67 %x6d %x65 %x6e %x74 %x73 
extendsKeyword      =  %x65 %x78 %x74 %x65 %x6e %x64 %x73 
groupKeyword        =  %x67 %x72 %x6f %x75 %x70 
membersKeyword      =  %x6d %x65 %x6d %x62 %x65 %x72 %x73 
complianceKeyword   =  %x63 %x6f %x6d %x70 %x6c %x69 %x61 %x6e %x63 
                          %x65 
mandatoryKeyword    =  %x6d %x61 %x6e %x64 %x61 %x74 %x6f %x72 %x79 
optionalKeyword     =  %x6f %x70 %x74 %x69 %x6f %x6e %x61 %x6c 
refineKeyword       =  %x72 %x65 %x66 %x69 %x6e %x65 
installtypeKeyword  =  %x69 %x6E %x73 %x74 %x61 %x6C %x6C %x74 %x79 
                          %x70 %x65 
pibminacessKeyword  =  %x70 %x69 %x62 %x6d %x69 %x6E %x61 %x63 %x63  
                          %x65 %x73 %x73 
noaccessKeyword     =  %x6E %x6f %x61 %x63 %x63 %x65 %x73 %x73 
installKeyword      =  %x69 %x6E %x73 %x74 %x61 %x6C %x6C 
notifyKeyword       =  %x6E %x6F %x74 %x69 %x66 %x79 
    
    
